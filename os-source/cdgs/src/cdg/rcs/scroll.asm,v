head     1.13;
branch   ;
access   ;
symbols  ;
locks    ; strict;
comment  @* @;


1.13
date     92.04.20.13.50.30;  author darren;  state Exp;
branches ;
next     1.12;

1.12
date     92.04.20.13.22.53;  author darren;  state Exp;
branches ;
next     1.11;

1.11
date     92.04.15.08.42.37;  author darren;  state Exp;
branches ;
next     1.10;

1.10
date     92.04.10.17.01.46;  author darren;  state Exp;
branches ;
next     1.9;

1.9
date     92.03.11.12.22.56;  author darren;  state Exp;
branches ;
next     1.8;

1.8
date     92.03.04.14.50.20;  author darren;  state Exp;
branches ;
next     1.7;

1.7
date     92.03.04.14.48.50;  author darren;  state Exp;
branches ;
next     1.6;

1.6
date     92.03.02.13.38.17;  author darren;  state Exp;
branches ;
next     1.5;

1.5
date     92.02.18.16.41.04;  author darren;  state Exp;
branches ;
next     1.4;

1.4
date     92.02.17.14.35.14;  author darren;  state Exp;
branches ;
next     1.3;

1.3
date     92.01.31.11.31.43;  author darren;  state Exp;
branches ;
next     1.2;

1.2
date     92.01.30.16.46.51;  author darren;  state Exp;
branches ;
next     1.1;

1.1
date     92.01.30.16.05.23;  author darren;  state Exp;
branches ;
next     ;


desc
@Initial check-in of ALPHA cdg.library for CDTV-CR
@


1.13
log
@Change debug message
@
text
@**
**	$Id: scroll.asm,v 1.12 92/04/20 13:22:53 darren Exp Locker: darren $
**
**	CDTV CD+G -- preset.asm (scroll with preset)
**
**	(C) Copyright 1992 Commodore-Amiga, Inc.
**	    All Rights Reserved
**
**	
**

** Includes

		INCLUDE	"cdgbase.i"
		INCLUDE	"cdgprefs.i"
		INCLUDE	"debug.i"

** Exports
		SECTION	cdg

		XDEF	_TVScrollPreset
		XDEF	_TVScrollCopy
** Imports
		XREF	_custom			;amiga.lib

** Equates

DISPLAY_BYTES	EQU	(DISPLAY_WIDTH/8)
DISPLAY_WORDS	EQU	(DISPLAY_BYTES/2)
SCREEN_ROWS	EQU	(DISPLAY_ROWS*FONT_HEIGHT)
SCREEN_END	EQU	((SCREEN_ROWS)*DISPLAY_BYTES)
DONTPRESET	EQU	-1

** Structures

	; structure for storing blitter scroll set-up info
	; MUST be 32 bytes long for quick look-up

 STRUCTURE	BLIT_CONSTANTS,0
	UWORD	bcc_bltcon0
	UWORD	bcc_bltcon1
	UWORD	bcc_modulo
	UWORD	bcc_bltsize
	UWORD	bcc_presetcol
	UWORD	bcc_copycol
	ULONG	bcc_aflwm
	ULONG	bcc_aoffset
	ULONG	bcc_doffset
	ULONG	bcc_presetrow
	ULONG	bcc_copyrow
	LABEL	BLIT_CONSTANTS_SIZEOF


** Assumptions

	IFNE	cdd_BitPlane1_1-cdd_BitPlane1_0-4
	FAIL	"cdd_BitPlane1_1 does not follow cdd_BitPlane1_0; recode!"
	ENDC

	IFNE	cdd_BitPlane1_2-cdd_BitPlane1_1-4
	FAIL	"cdd_BitPlane1_2 does not follow cdd_BitPlane1_1; recode!"
	ENDC

	IFNE	cdd_BitPlane1_3-cdd_BitPlane1_2-4
	FAIL	"cdd_BitPlane1_3 does not follow cdd_BitPlane1_2; recode!"
	ENDC

	IFNE	cdd_BitPlane2_1-cdd_BitPlane2_0-4
	FAIL	"cdd_BitPlane2_1 does not follow cdd_BitPlane2_0; recode!"
	ENDC

	IFNE	cdd_BitPlane2_2-cdd_BitPlane2_1-4
	FAIL	"cdd_BitPlane2_2 does not follow cdd_BitPlane2_1; recode!"
	ENDC

	IFNE	cdd_BitPlane2_3-cdd_BitPlane2_2-4
	FAIL	"cdd_BitPlane2_3 does not follow cdd_BitPlane2_2; recode!"
	ENDC

	IFNE	cdd_ExtraFlags2-cdd_MOVEMPTRS-1
	FAIL	"cdd_ExtraFlags2 does not follow cdd_MOVEMPTRS; recode!"
	ENDC

	IFNE	cdd_ExtraFlags3-cdd_ExtraFlags2-1
	FAIL	"cdd_ExtraFlags3 does not follow cdd_ExtraFlags2; recode!"
	ENDC

	IFNE	cdd_VBlankFlags-cdd_ExtraFlags3-1
	FAIL	"cdd_VBlankFlags does not follow cdd_ExtraFlags3; recode!"
	ENDC

	IFNE	cdd_ActivePlanes-cdd_InactivePlanes-4
	FAIL	"cdd_ActivePlanes does not follow cdd_InactivePlanes; recode!"
	ENDC

	IFNE	cdd_InactivePlanes-cdd_VBlankFlags-1
	FAIL	"cdd_InactivePlanes does not follow cdd_VBlankFlags; recode!"
	ENDC

	IFNE	cdd_NegPlane-cdd_FontPlane-4
	FAIL	"cdd_NegPlane does not follow cdd_FontPlane; recode!"
	ENDC

	IFNE	cdd_0sPlane-cdd_1sPlane-4
	FAIL	"cdd_0sPlane does not follow cdd_1sPlane; recode!"
	ENDC

	IFNE	bltcon1-bltcon0-2
	FAIL	"bltcon1 does not follow bltcon0; recode!"
	ENDC

	IFNE	bltafwm-bltcon1-2
	FAIL	"bltafwm does not follow bltcon1; recode!"
	ENDC

	IFNE	bltalwm-bltafwm-2
	FAIL	"bltalwm does not follow bltafwm; recode!"
	ENDC

	IFNE	bltcpt-bltalwm-2
	FAIL	"bltcpt does not follow bltalwm; recode!"
	ENDC

	IFNE	bltbpt-bltcpt-4
	FAIL	"bltbpt does not follow bltcpt; recode!"
	ENDC

	IFNE	bltapt-bltbpt-4
	FAIL	"bltapt does not follow bltbpt; recode!"
	ENDC

	IFNE	bltdpt-bltapt-4
	FAIL	"bltdpt does not follow bltapt; recode!"
	ENDC

	IFNE	bltbmod-bltcmod-2
	FAIL	"bltbmod does not follow bltcmod; recode!"
	ENDC

	IFNE	bltamod-bltbmod-2
	FAIL	"bltamod does not follow bltbmod; recode!"
	ENDC

	IFNE	bltdmod-bltamod-2
	FAIL	"bltdmod does not follow bltamod; recode!"
	ENDC

	IFNE	BLIT_CONSTANTS_SIZEOF-32
	FAIL	"BLIT_CONSTANTS_SIZEOF != 32; recode!"
	ENDC

*****i* cdg.library/_TVScrollCopy *************************************
*
*   NAME
*	_TVScrollCopy -- CD+G Scroll with COPY
*   
*   SYNOPSIS
*	VOID _TVScrollCopy( data, pack )
*	                      A6    A4
*
*	VOID _TVScrollCopy( struct CDGData *, struct CDGPack * );
*
*   FUNCTION
*	Perform simple, or hard scroll with COPY.  COPY means
*	that the vacated area of the screen is filled with a COPY
*	of the data which is about to be scrolled off.
*
*	See TVScrollPreset() for more info.
*
*   RETURNS
*	N/A
*
*   NOTES
*
***********************************************************************

_TVScrollCopy:

	PRINTF	DBG_ENTRY,<'CDG--_TVScrollCopy($%lx,$%lx)'>,A6,A4

 		lea	cdp_Data+1(a4),a0

		movem.l	d2-d6,-(sp)
		movem.l	a2-a5,-(sp)

	; obtain COPH & PH

		moveq	#00,d0
		move.b	(a0)+,d0
		lsl.b	#2,d0			;6->8 bits

		lea	TVSCR_HTable(pc),a1
		move.l	0(a1,d0.w),d2
		bmi	TVSCC_Exit

	; obtain COPV & PV

		move.b	(a0)+,d0
		lsl.b	#2,d0			;6->8 bits

		lea	TVSCR_VTable(pc),a1
		move.l	0(a1,d0.w),d3
		bmi	TVSCC_Exit

	; We now have VALID COPH, COPV, PH, and PV values
	;
	; We can worry about PH, and PV later.  All we really
	; care about right now is do we need to do a scroll
	; which requires memory copy
	; 

		move.w	d3,d5

		lsl.b	#2,d5
		or.b	d2,d5
		beq	TVSCR_FastScroll

                lsl.w	#5,d5			;* size of struct(BLIT_CONSTANTS)


	; A memory copy is required.  Own blitter, and use
	; an optimized copy routine.

		move.l	a6,a2
		move.l	cdd_GfxLib(a2),a6

		JSRLIB	OwnBlitter
		JSRLIB	WaitBlit

	; clear bitplane swap bit (so VBLANK won't try to swap
	; planes while we are in the process of blitting

		lea	_custom,a1

		bclr	#VBFB_NEWPLANES,cdd_VBlankFlags(a2)

		movem.l	cdd_InactivePlanes(a2),a3-a4

		moveq	#03,d6				;loop * 4

		lea	TVSCR_Blits(pc),a5
		add.w	d5,a5

		move.l	bcc_bltcon0(a5),bltcon0(a1)	;+ bltcon1

		move.l	bcc_aflwm(a5),bltafwm(a1)	;+ lwm

		move.w	bcc_modulo(a5),d0

		move.w	d0,bltamod(a1)
		move.w	d0,bltdmod(a1)

TVSCC_Blit4:
		move.l	(a4)+,d0

		move.l	d0,d4

		add.l	bcc_aoffset(a5),d0

		move.l	d0,bltapt(a1)

		move.l	(a3)+,d0

		move.l	d0,d5

		add.l	bcc_doffset(a5),d0

		move.l	d0,bltdpt(a1)

		move.w	bcc_bltsize(a5),bltsize(a1)

	; Peform memory COPY of ROW while this blit is running

		move.l	bcc_presetrow(a5),d1
		bmi.s	TVSCC_DontCopyRow

		tst.b	d2				;combo scroll?
		bne	TVSCC_ComboScroll

		move.l	d1,a1
		add.l	d5,a1

		move.l	d4,a0
		add.l	bcc_copyrow(a5),a0

		move.w	#(((FONT_HEIGHT*DISPLAY_BYTES)/4)-1),d1

TVSCC_DoCopyRow:
		move.l	(a0)+,(a1)+
		dbf	d1,TVSCC_DoCopyRow

TVSCC_DontCopyRow:

	; Perform memory COPY of COLUMN while bit is running

		move.l	d5,a1
		move.w	bcc_presetcol(a5),d1
		bmi.s	TVSCC_DontCopyCol

		add.w	d1,a1

		move.l	d4,a0
		move.w	bcc_copycol(a5),d0
		add.w	d0,a0

		moveq	#(DISPLAY_BYTES),d5
		moveq	#10,d4			;shift

		cmp.w	d0,d1
		blt.s	TVSCC_DoCopyLeft

		move.w	#((FONT_HEIGHT*DISPLAY_ROWS)-1),d1

TVSCC_DoCopyRight:
		move.w	(a0),d0
		lsl.w	d4,d0
		move.w	d0,(a1)
		add.w	d5,a0
		add.w	d5,a1
		dbf	d1,TVSCC_DoCopyRight
		bra.s	TVSCC_DontCopyCol

TVSCC_DoCopyLeft:
		move.w	#((FONT_HEIGHT*DISPLAY_ROWS)-1),d1

TVSCC_DoCopyLeftI:
		move.w	(a0),d0
		lsr.w	d4,d0
		move.w	d0,(a1)
		add.w	d5,a0
		add.w	d5,a1
		dbf	d1,TVSCC_DoCopyLeftI

TVSCC_DontCopyCol:

		lea	_custom,a1

	;
	; Wait for this blit, and the last blit to be done.  This way
	; we don't swap bitplanes until we are visually ready
	;
		JSRLIB	WaitBlit

		dbf	d6,TVSCC_Blit4

TVSCC_DidLastRow:

		JSRLIB	DisownBlitter

	; And swap Active/Inactive pointers

		movem.l	cdd_InactivePlanes(a2),d0/a0
		exg	d0,a0
		movem.l	d0/a0,cdd_InactivePlanes(a2)

		move.l	a2,a6			;restore A6

	; Change PH & PV values; update screen during VBLANK

TVSCC_FastScroll:


	; Calculate new PH, and PV

		swap	d3
		add.w	d3,d3
		lea	TVScroll_PVTable(pc),a0
		move.w	0(a0,d3.w),d3

		swap	d2
		move.w	cdd_CopperList1+cdc_BPLCON1_DEF(a6),d0
		move.w	d0,d1
		sub.b	d2,d0
		and.b	#PF_FINE_SCROLL_MASK,d0
		cmp.b	d1,d0
		bls.s	TVSCC_ValidScroll

		addq.w	#2,d3

TVSCC_ValidScroll:
		move.w	d0,d1
		lsl.w	#PFB_FINE_SCROLL_SHIFT,d1
		or.w	d1,d0			;odd/even planes

		move.w	d3,cdd_PHPV+2(a6)
		move.w	d0,cdd_PHPV(a6)


	; Tell VBLANK to modify screen pointers

		bset	#VBFB_NEWPLANES,cdd_VBlankFlags(a6)
		
TVSCC_Exit:

		movem.l	(sp)+,a2-a5
		movem.l	(sp)+,d2-d6

	PRINTF	DBG_EXIT,<'CDG--Exit _TVScrollCopy()'>

		rts

TVSCC_ComboScroll:

	; combo scroll required; copy COLUMN while blit is running
	; with up/down shift

		movem.l	d4-d5,-(sp)		;cache A & D

		move.l	d4,a0
		add.w	bcc_copycol(a5),a0

		move.l	d5,a1
		add.w	bcc_presetcol(a5),a1

		movem.l	a0-a1,-(sp)

		cmp.b	#1,d3			;scroll down?
		beq.s	TVSCC_CColDwn

		add.w	#(FONT_HEIGHT*DISPLAY_BYTES),a0	;adjust src
		bra.s	TVSCC_CCol
TVSCC_CColDwn:
		add.w	#(FONT_HEIGHT*DISPLAY_BYTES),a1	;adjust dst

TVSCC_CCol:

		moveq	#(DISPLAY_BYTES),d5
		moveq	#10,d4			;shift

		move.w	#((FONT_HEIGHT*(DISPLAY_ROWS-1))-1),d1

		cmp.b	#1,d2			;copy right?  column -> left
		beq.s	TVSCC_SlowLeft


	; copy right

TVSCC_SlowRight
		move.w	(a0),d0
		lsl.w	d4,d0
		move.w	d0,(a1)
		add.w	d5,a0
		add.w	d5,a1
		dbf	d1,TVSCC_SlowRight


		movem.l	(sp)+,a0-a1
		add.l	bcc_copyrow(a5),a0
		add.l	bcc_presetrow(a5),a1

		moveq	#(FONT_HEIGHT-1),d1

	; copy corner

TVSCC_CornerRight:
		move.w	(a0),d0
		lsl.w	d4,d0
		move.w	d0,(a1)
		add.w	d5,a0
		add.w	d5,a1
		dbf	d1,TVSCC_CornerRight

	; calc row adjustment for LEFT (forward) copy

		moveq	#02,d0					;adjust A
		moveq	#00,d1					;adjust D

		bra.s	TVSCC_SlowRows


	; copy left

TVSCC_SlowLeft:
		move.w	(a0),d0
		lsr.w	d4,d0
		move.w	d0,(a1)
		add.w	d5,a0
		add.w	d5,a1
		dbf	d1,TVSCC_SlowLeft

		movem.l	(sp)+,a0-a1
		add.l	bcc_copyrow(a5),a0
		add.l	bcc_presetrow(a5),a1

		moveq	#(FONT_HEIGHT-1),d1

TVSCC_CornerLeft:

		move.w	(a0),d0
		lsr.w	d4,d0
		move.w	d0,(a1)
		add.w	d5,a0
		add.w	d5,a1
		dbf	d1,TVSCC_CornerLeft

	; calc row adjustment for RIGHT (reverse) copy

		move.l	#((FONT_HEIGHT*DISPLAY_BYTES)-4),d0	;adjust A
		move.l	#((FONT_HEIGHT*DISPLAY_BYTES)-2),d1	;adjust D

	; setup, and wait for blit to finish before doing row

TVSCC_SlowRows:

		movem.l	(sp)+,d4-d5	;A & D

		add.l	bcc_copyrow(a5),d4
		add.l	bcc_presetrow(a5),d5

		add.l	d0,d4
		add.l	d1,d5

		move.w	#((FONT_HEIGHT<<HSIZEBITS)!DISPLAY_WORDS-1),d1

		lea	_custom,a1

		JSRLIB	WaitBlit

	; start blit of row copy (with l/r shift)

		move.l	d4,bltapt(a1)
		move.l	d5,bltdpt(a1)
		move.w	d1,bltsize(a1)

		JSRLIB	WaitBlit

		dbf	d6,TVSCC_Blit4
		bra	TVSCC_DidLastRow


*****i* cdg.library/_TVScrollPreset ***********************************
*
*   NAME
*	_TVScrollPreset -- CD+G Scroll with PRESET
*   
*   SYNOPSIS
*	VOID _TVScrollPreset( data, pack )
*	                      A6    A4
*
*	VOID _TVScrollPreset( struct CDGData *, struct CDGPack * );
*
*   FUNCTION
*	Perform simple, or hard scroll with PRESET.  PRESET means
*	that the vacated area of the screen is filled with a PRESET
*	color encoded in the PACK data.  
*
*	There is a neat trick which is taken advantage of by this
*	routine.  It turns out that the first, and last columns
*	of the display fit within their own 16 bit words using a
*	320 pixel wide bitplane.
*
*	48 visible columns * 6 pixels wide == 288 visible pixels
*
*	16 pixels (for column 0) + 16 pixels for (column 1) == 32 pixels.
*
*	288 pixels + 32 pixels == 320 pixels.
*
*	Because a double buffered display is used, it is possible
*	to perform the memory copy such that the vacated area can be
*	filled with the CPU in parallel with the blitter copy.  Since
*	the blitter need only do a simple A->D copy, the copy can be
*	fairly efficient.  Turns out the same is true when doing
*	a scroll with COPY.  The destination column can be filled with
*	the CPU using source data which is only being READ by the
*	blitter.
*
*	The caller must first open the cdg.library, and have called
*	CDGBegin() to start CD+G.
*
*   RETURNS
*	N/A
*
*   NOTES
*
***********************************************************************

_TVScrollPreset:

	PRINTF	DBG_ENTRY,<'CDG--_TVScrollPreset($%lx,$%lx)'>,A6,A4

	DEBUG_TRAP	12

 		lea	cdp_Data(a4),a0

		movem.l	d2-d6,-(sp)
		movem.l	a2-a5,-(sp)

		move.b	(a0)+,d4		;color
		andi.b	#(SYMF_T!SYMF_U!SYMF_V!SYMF_W),d4

	; obtain COPH & PH

		moveq	#00,d0
		move.b	(a0)+,d0
		lsl.b	#2,d0			;6->8 bits

		lea	TVSCR_HTable(pc),a1
		move.l	0(a1,d0.w),d2
		bmi	TVSCR_Exit

	; obtain COPV & PV

		move.b	(a0)+,d0
		lsl.b	#2,d0			;6->8 bits

		lea	TVSCR_VTable(pc),a1
		move.l	0(a1,d0.w),d3
		bmi	TVSCR_Exit


	; We now have VALID COPH, COPV, PH, and PV values
	;
	; We can worry about PH, and PV later.  All we really
	; care about right now is do we need to do a scroll
	; which requires memory copy
	; 

		move.w	d3,d5

		lsl.b	#2,d5
		or.b	d2,d5
		beq	TVSCR_FastScroll

                lsl.w	#5,d5			;* size of struct(BLIT_CONSTANTS)


	; A memory copy is required.  Own blitter, and use
	; an optimized copy routine.

		move.l	a6,a2
		move.l	cdd_GfxLib(a2),a6

		JSRLIB	OwnBlitter
		JSRLIB	WaitBlit

	; clear bitplane swap bit (so VBLANK won't try to swap
	; planes while we are in the process of blitting

		bclr	#VBFB_NEWPLANES,cdd_VBlankFlags(a2)

		lea	_custom,a1

		movem.l	cdd_InactivePlanes(a2),a3-a4

		moveq	#03,d6				;loop * 4

		lea	TVSCR_Blits(pc),a5
		add.w	d5,a5

		move.l	bcc_bltcon0(a5),bltcon0(a1)	;+ bltcon1

		move.l	bcc_aflwm(a5),bltafwm(a1)	;+ lwm

		move.w	bcc_modulo(a5),d0

		move.w	d0,bltamod(a1)
		move.w	d0,bltdmod(a1)

TVSCR_Blit4:
		move.l	(a4)+,d0

		add.l	bcc_aoffset(a5),d0

		move.l	d0,bltapt(a1)

		move.l	(a3)+,d0

		move.l	d0,d5

		add.l	bcc_doffset(a5),d0

		move.l	d0,bltdpt(a1)

		move.w	bcc_bltsize(a5),bltsize(a1)

	; Peform memory PRESET of ROW while this blit is running

		moveq	#00,d0
		lsr.b	#1,d4			;obtain 1's, or 0's for this
		bcc.s	TVSCR_PresetColor	;plane from PRESET PEN #
		moveq	#-1,d0
TVSCR_PresetColor:

		move.l	bcc_presetrow(a5),d1
		bmi.s	TVSCR_DontPresetRow
		move.l	d1,a0
		add.l	d5,a0

		move.w	#(((FONT_HEIGHT*DISPLAY_BYTES)/4)-1),d1

TVSCR_DoPresetRow:
		move.l	d0,(a0)+
		dbf	d1,TVSCR_DoPresetRow

TVSCR_DontPresetRow:

	; Perform memory PRESET of COLUMN while bit is running

		move.l	d5,a0
		move.w	bcc_presetcol(a5),d1
		bmi.s	TVSCR_DontPresetCol

		add.w	d1,a0

		move.w	#((FONT_HEIGHT*DISPLAY_ROWS)-1),d1

		moveq	#(DISPLAY_BYTES),d5
TVSCR_DoPresetCol:
		move.w	d0,(a0)
		add.w	d5,a0
		dbf	d1,TVSCR_DoPresetCol

TVSCR_DontPresetCol:

	;
	; Wait for this blit, and the last blit to be done.  This way
	; we don't swap bitplanes until we are visually ready
	;
		JSRLIB	WaitBlit

		dbf	d6,TVSCR_Blit4

		JSRLIB	DisownBlitter

	; And swap Active/Inactive pointers

		movem.l	cdd_InactivePlanes(a2),d0/a0
		exg	d0,a0
		movem.l	d0/a0,cdd_InactivePlanes(a2)

		move.l	a2,a6			;restore A6

	; Change PH & PV values; update screen during VBLANK

TVSCR_FastScroll:

	; Calculate new PH, and PV

		swap	d3
		add.w	d3,d3
		lea	TVScroll_PVTable(pc),a0
		move.w	0(a0,d3.w),d3

		swap	d2
		move.w	cdd_CopperList1+cdc_BPLCON1_DEF(a6),d0
		move.w	d0,d1
		sub.b	d2,d0
		and.b	#PF_FINE_SCROLL_MASK,d0
		cmp.b	d1,d0
		bls.s	TVSCR_ValidScroll

		addq.w	#2,d3

TVSCR_ValidScroll:
		move.w	d0,d1
		lsl.w	#PFB_FINE_SCROLL_SHIFT,d1
		or.w	d1,d0			;odd/even planes

		move.w	d3,cdd_PHPV+2(a6)
		move.w	d0,cdd_PHPV(a6)

	; Tell VBLANK to modify screen pointers

		bset	#VBFB_NEWPLANES,cdd_VBlankFlags(a6)
		
TVSCR_Exit:

		movem.l	(sp)+,a2-a5
		movem.l	(sp)+,d2-d6

	PRINTF	DBG_EXIT,<'CDG--Exit _TVScrollPreset()'>

		rts

		SECTION DATA

**
** Data tables for blits.  Illegal values for COPH, and COPV have
** already been caught above, so this is not a complete table
**
** COPV<<2|COPH
**

TVSCR_Blits:

	; COPV == 0, COPH == 0 - INVALID

		dc.w	0
		dc.w	0

		dc.w	0
		dc.w	0

		dc.w	0
		dc.w	0

		dc.l	0
		dc.l	0
		dc.l	0
		dc.l	0
		dc.l	0

	; COPV == 0, COPH == 1 -- Copy RIGHT

		dc.w	((10<<ASHIFTSHIFT)!SRCA!DEST!ABC!ABNC!ANBC!ANBNC)
		dc.w	BLITREVERSE

		dc.w	2
		dc.w	((SCREEN_ROWS<<HSIZEBITS)!(DISPLAY_WORDS-1))

		dc.w	0
		dc.w	DISPLAY_BYTES-2

		dc.l	$FFFF003F

		dc.l	SCREEN_END-4
		dc.l	SCREEN_END-2

		dc.l	DONTPRESET
		dc.l	DONTPRESET

	; COPV == 0, COPH == 2 -- Copy LEFT

		dc.w	((10<<ASHIFTSHIFT)!SRCA!DEST!ABC!ABNC!ANBC!ANBNC)
		dc.w	0

		dc.w	2
		dc.w	((SCREEN_ROWS<<HSIZEBITS)!(DISPLAY_WORDS-1))

		dc.w	DISPLAY_BYTES-2
		dc.w	0

		dc.l	$FFFFFC00

		dc.l	2
		dc.l	0

		dc.l	DONTPRESET
		dc.l	DONTPRESET

	; COPV == 0, COPH == 3 -- INVALID

		dc.w	0
		dc.w	0

		dc.w	0
		dc.w	0

		dc.w	0
		dc.w	0

		dc.l	0
		dc.l	0
		dc.l	0
		dc.l	0
		dc.l	0

	; COPV == 1, COPH == 0 -- Copy DOWN

		dc.w	(SRCA!DEST!ABC!ABNC!ANBC!ANBNC)
		dc.w	0

		dc.w	0
		dc.w	(((SCREEN_ROWS-FONT_HEIGHT)<<HSIZEBITS)!DISPLAY_WORDS)

		dc.w	DONTPRESET
		dc.w	DONTPRESET

		dc.l	$003FFC00

		dc.l	0
		dc.l	(FONT_HEIGHT*DISPLAY_BYTES)

		dc.l	0
		dc.l	((SCREEN_ROWS-FONT_HEIGHT)*DISPLAY_BYTES)

	; COPV == 1, COPH == 1 -- Copy DOWN, and RIGHT


		dc.w	((10<<ASHIFTSHIFT)!SRCA!DEST!ABC!ABNC!ANBC!ANBNC)
		dc.w	BLITREVERSE

		dc.w	2
		dc.w	(((SCREEN_ROWS-FONT_HEIGHT)<<HSIZEBITS)!DISPLAY_WORDS-1)

		dc.w	0
		dc.w	DISPLAY_BYTES-2

		dc.l	$FFFF003F

		dc.l	SCREEN_END-4-(FONT_HEIGHT*DISPLAY_BYTES)
		dc.l	SCREEN_END-2

		dc.l	0
		dc.l	((SCREEN_ROWS-FONT_HEIGHT)*DISPLAY_BYTES)

	; COPV == 1, COPH == 2 -- Copy DOWN, and LEFT

		dc.w	((10<<ASHIFTSHIFT)!SRCA!DEST!ABC!ABNC!ANBC!ANBNC)
		dc.w	0

		dc.w	2
		dc.w	(((SCREEN_ROWS-FONT_HEIGHT)<<HSIZEBITS)!DISPLAY_WORDS-1)

		dc.w	DISPLAY_BYTES-2
		dc.w	0

		dc.l	$FFFFFC00

		dc.l	2
		dc.l	(FONT_HEIGHT*DISPLAY_BYTES)

		dc.l	0
		dc.l	((SCREEN_ROWS-FONT_HEIGHT)*DISPLAY_BYTES)

	; COPV == 1, COPH == 3 -- INVALID

		dc.w	0
		dc.w	0

		dc.w	0
		dc.w	0

		dc.w	0
		dc.w	0

		dc.l	0
		dc.l	0
		dc.l	0
		dc.l	0
		dc.l	0

	; COPV == 2, COPH == 0 -- Copy UP

		dc.w	(SRCA!DEST!ABC!ABNC!ANBC!ANBNC)
		dc.w	0

		dc.w	0
		dc.w	(((SCREEN_ROWS-FONT_HEIGHT)<<HSIZEBITS)!DISPLAY_WORDS)

		dc.w	DONTPRESET
		dc.w	DONTPRESET

		dc.l	$003FFC00

		dc.l	(FONT_HEIGHT*DISPLAY_BYTES)
		dc.l	0

		dc.l	((SCREEN_ROWS-FONT_HEIGHT)*DISPLAY_BYTES)
		dc.l	0

	; COPV == 2, COPH == 1 -- Copy UP, and RIGHT


		dc.w	((10<<ASHIFTSHIFT)!SRCA!DEST!ABC!ABNC!ANBC!ANBNC)
		dc.w	BLITREVERSE

		dc.w	2
		dc.w	(((SCREEN_ROWS-FONT_HEIGHT)<<HSIZEBITS)!DISPLAY_WORDS-1)

		dc.w	0
		dc.w	DISPLAY_BYTES-2

		dc.l	$FFFF003F

		dc.l	SCREEN_END-4
		dc.l	SCREEN_END-2-(FONT_HEIGHT*DISPLAY_BYTES)

		dc.l	((SCREEN_ROWS-FONT_HEIGHT)*DISPLAY_BYTES)
		dc.l	0

	; COPV == 2, COPH == 2 -- Copy UP, and LEFT

		dc.w	((10<<ASHIFTSHIFT)!SRCA!DEST!ABC!ABNC!ANBC!ANBNC)
		dc.w	0

		dc.w	2
		dc.w	(((SCREEN_ROWS-FONT_HEIGHT)<<HSIZEBITS)!DISPLAY_WORDS-1)

		dc.w	DISPLAY_BYTES-2
		dc.w	0

		dc.l	$FFFFFC00

		dc.l	2+(FONT_HEIGHT*DISPLAY_BYTES)
		dc.l	0

		dc.l	((SCREEN_ROWS-FONT_HEIGHT)*DISPLAY_BYTES)
		dc.l	0

	; Table end - all else is invalid, and already screened


**
** Pre calculated look-up table for COPH/PH.  Converts the
** COPH/PH byte into a LONG word consisting of COPH in the lower
** word, and a new PH value in the upper word.  Invalid values
** are also caught as part of the table.
**

INVALID_SCROLL	EQU	-1

SCROLLH		MACRO	;Table entry

		; Note that the AND masks SYMF_T, which is suppose
		; to be 0.  The masking allows it to be set in some
		; future CD+G standard in a upwardly compatable way.
		; The problem is, its not clear if the SYMF_T bit would
		; ever be used, and if so, if it would be used in a
		; backwards compatable fashion.  So I assume that
		; any decent designer will realize the need for backwards
		; compatability, and select that kind of solution.
		;

PHTMP		SET	(\1&(SYMF_U!SYMF_V!SYMF_W))
COPHTMP		SET	((\1&(SYMF_R!SYMF_S))>>4)

HRZTMP		SET	((PHTMP<<16)!COPHTMP)

		IFGT	PHTMP-5		; PH must be 0-5
HRZTMP		SET	INVALID_SCROLL
		ENDC

		IFGT	COPHTMP-2	; COPH must be 0-2
HRZTMP		SET	INVALID_SCROLL
		ENDC

		dc.l	HRZTMP

		ENDM

TVSCR_HTable:
		SCROLLH	00
		SCROLLH	01
		SCROLLH	02
		SCROLLH	03
		SCROLLH	04
		SCROLLH	05
		SCROLLH	06
		SCROLLH	07
		SCROLLH	08
		SCROLLH	09
		SCROLLH	10
		SCROLLH	11
		SCROLLH	12
		SCROLLH	13
		SCROLLH	14
		SCROLLH	15
		SCROLLH	16
		SCROLLH	17
		SCROLLH	18
		SCROLLH	19
		SCROLLH	20
		SCROLLH	21
		SCROLLH	22
		SCROLLH	23
		SCROLLH	24
		SCROLLH	25
		SCROLLH	26
		SCROLLH	27
		SCROLLH	28
		SCROLLH	29
		SCROLLH	30
		SCROLLH	31
		SCROLLH	32
		SCROLLH	33
		SCROLLH	34
		SCROLLH	35
		SCROLLH	36
		SCROLLH	37
		SCROLLH	38
		SCROLLH	39
		SCROLLH	40
		SCROLLH	41
		SCROLLH	42
		SCROLLH	43
		SCROLLH	44
		SCROLLH	45
		SCROLLH	46
		SCROLLH	47
		SCROLLH	48
		SCROLLH	49
		SCROLLH	50
		SCROLLH	51
		SCROLLH	52
		SCROLLH	53
		SCROLLH	54
		SCROLLH	55
		SCROLLH	56
		SCROLLH	57
		SCROLLH	58
		SCROLLH	59
		SCROLLH	60
		SCROLLH	61
		SCROLLH	62
		SCROLLH	63


**
** Pre calculated look-up table for COPV/PV.  Converts the
** COPV/PV byte into a LONG word consisting of COPV in the lower
** word, and a new PV value in the upper word.  Invalid values
** are also caught as part of the table.
**


SCROLLV		MACRO	;Table entry

PVTMP		SET	(\1&(SYMF_T!SYMF_U!SYMF_V!SYMF_W))
COPVTMP		SET	((\1&(SYMF_R!SYMF_S))>>4)

VERTMP		SET	((PVTMP<<16)!COPVTMP)

		IFGT	PVTMP-11	; PV must be 0-11
VERTMP		SET	INVALID_SCROLL
		ENDC

		IFGT	COPVTMP-2	; COPV must be 0-2
VERTMP		SET	INVALID_SCROLL
		ENDC

		dc.l	VERTMP

		ENDM

TVSCR_VTable:
		SCROLLV	00
		SCROLLV	01
		SCROLLV	02
		SCROLLV	03
		SCROLLV	04
		SCROLLV	05
		SCROLLV	06
		SCROLLV	07
		SCROLLV	08
		SCROLLV	09
		SCROLLV	10
		SCROLLV	11
		SCROLLV	12
		SCROLLV	13
		SCROLLV	14
		SCROLLV	15
		SCROLLV	16
		SCROLLV	17
		SCROLLV	18
		SCROLLV	19
		SCROLLV	20
		SCROLLV	21
		SCROLLV	22
		SCROLLV	23
		SCROLLV	24
		SCROLLV	25
		SCROLLV	26
		SCROLLV	27
		SCROLLV	28
		SCROLLV	29
		SCROLLV	30
		SCROLLV	31
		SCROLLV	32
		SCROLLV	33
		SCROLLV	34
		SCROLLV	35
		SCROLLV	36
		SCROLLV	37
		SCROLLV	38
		SCROLLV	39
		SCROLLV	40
		SCROLLV	41
		SCROLLV	42
		SCROLLV	43
		SCROLLV	44
		SCROLLV	45
		SCROLLV	46
		SCROLLV	47
		SCROLLV	48
		SCROLLV	49
		SCROLLV	50
		SCROLLV	51
		SCROLLV	52
		SCROLLV	53
		SCROLLV	54
		SCROLLV	55
		SCROLLV	56
		SCROLLV	57
		SCROLLV	58
		SCROLLV	59
		SCROLLV	60
		SCROLLV	61
		SCROLLV	62
		SCROLLV	63


**
** Precalculated PV lookup table.  Is essentially a Y offset which includes
** the first hidden row, plus N lines of the next row, plus 2 so we skip the
** first hidden COLUMN
**
TVScroll_PVTable:
		dc.w	((((DISPLAY_WIDTH/8)*0)+0)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))
		dc.w	((((DISPLAY_WIDTH/8)*1)+0)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))
		dc.w	((((DISPLAY_WIDTH/8)*2)+0)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))
		dc.w	((((DISPLAY_WIDTH/8)*3)+0)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))
		dc.w	((((DISPLAY_WIDTH/8)*4)+0)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))
		dc.w	((((DISPLAY_WIDTH/8)*5)+0)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))
		dc.w	((((DISPLAY_WIDTH/8)*6)+0)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))
		dc.w	((((DISPLAY_WIDTH/8)*7)+0)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))
		dc.w	((((DISPLAY_WIDTH/8)*8)+0)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))
		dc.w	((((DISPLAY_WIDTH/8)*9)+0)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))
		dc.w	((((DISPLAY_WIDTH/8)*10)+0)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))
		dc.w	((((DISPLAY_WIDTH/8)*11)+0)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))


**
		END



**
** About the scroll routines -
**
**	The scroll routines used double buffering to avoid screen
**	trash during the blits.  This also makes it possible to
**	use a simple A->D blitter copy (perhaps with SHIFT, which
**	is free for the blitter), and also allows us to interleave
**	PRESET or COPY operations with the blits.
**
**	The bitplanes are layed out such that the first FONT column
**	is the last 6 bits of the first WORD, and the last FONT column
**	is the first 6 bits of the last WORD.  Like so -
**
**	111111           111111           111111          
**	5432109876543210 5432109876543210 5432109876543210
**	----------------+----------------+----------------
**                ^    ^                  ^    ^
**                |    |                  |    |
**	1st FONT -------                  ------- last FONT
**
**
**	Therefore to COPY LEFT we have -
**
**	111111           111111           111111           <-src
**	5432109876543210 5432109876543210 5432109876543210
**	----------------+----------------+----------------
**                      /
**                     /
**                    /
**                   /
**		    /
**                 /
**	111111           111111           111111           <-dest
**	5432109876543210 5432109876543210 5432109876543210
**	----------------+----------------+----------------
**
**	A = SRC + 1 WORD
**
**	D = DEST
**
**	Right SHIFT = 10
**
**	ASCENDING MODE
**
**	MODULO = 1 WORD (2 BYTES)
**
**
**
**	Therefore to COPY RIGHT we have -
**
**	111111           111111           111111           <-src
**	5432109876543210 5432109876543210 5432109876543210
**	----------------+----------------+----------------
**                \      
**                 \     
**                  \ 
**                   \
**		      \
**                     \
**	111111           111111           111111           <-dest
**	5432109876543210 5432109876543210 5432109876543210
**	----------------+----------------+----------------
**
**	A = Last Line - 2 WORDS
**
**	D = Last Line - 1 WORD
**
**	Left Shift = 10
**
**	DESCENDING MODE
**
**	MODULO = 1 WORD (2 BYTES)
**
**
**
**	COPY UP/DOWN is as easy as adjusting the SRC (A), and DEST (D)
**	pointers, and combinations are simply combinations.  During
**	COPY UP/DOWN, we copy one less font row.
**
**
**	PRESET --
**
**	Because the first, and last FONTs are contained within their own
**	WORDS, the CPU can PRESET a COLUMN during the BLIT.  ROWS
**	likewise, because this is memory that is not written to (via the
**	blitter during the memory copy.
**
**	COPY --
**
**	Copy is like PRESET, except we fetch the original data from
**	the opposite column/row during the blits.  The data to be copied
**	is in the SRC bitplanes, and is copied to the opposing
**	ROW/COLUMN while the blit (one bitplane at a time) is running.
**
**	This works because the DES memory is not being written to by
**	the blitter.  When doing column copy, the SRC->DEST is shifted
**	L/R 10 pixels (also handled by the CPU).
**
**	Even though this seems complex, the value is that the PRESET and/or
**	COPY portions of the scrolls are free since they are done while
**	the blitter is busy moving the bulk of the data. [Infact these
**	operations are done well before the blitter is done if the CPU
**	is available for use!]
**
**	OPTIMIZATIONS -
**
**	Another optimization is a table look-up scheme to decode PH/COPH,
**	PV/COPV.  While somewhat larger than using just code, it is
**	faster, and simpler.  The tables also mark invalid combinations
**	as -1, and can be easily caught with a BMI test.
**
**	PH/PV values are stored as actual bitplane offset, and scroll
**	register values so these do not need to be looked up during VBLANK
**	when the copper list is updated.
**@


1.12
log
@New optimized code for combo scrolls - nifty; takes <200us overhead
to do a combo scroll; lets us keep up with 60 hard scrolls per
second
@
text
@d2 1
a2 1
**	$Id: scroll.asm,v 1.11 92/04/15 08:42:37 darren Exp Locker: darren $
d398 1
a398 1
	PRINTF	DBG_EXIT,<'CDG--Exit _TVSCCollPreset()'>
@


1.11
log
@calc PH (bplcon1) so we can support arbitrary x display position
of screen
@
text
@d2 1
a2 1
**	$Id: scroll.asm,v 1.10 92/04/10 17:01:46 darren Exp Locker: darren $
d183 1
a183 1
		movem.l	d2-d7,-(sp)
a204 1

a211 2
		moveq	#00,d7			;assume not combo

a212 4
		beq.s	TVSCC_NotCombo

		tst.w	d2
		beq.s	TVSCC_NotCombo
a213 4
		moveq	#00,d5			;defer up/down scroll
		moveq	#01,d7			;is combo scroll

TVSCC_NotCombo:
d216 1
a216 1
		beq	TVSCC_FastScroll
a243 2
TVSCC_SlowRow:

d276 4
a287 1

a337 5
	; see if we are combo scroll, if so, scroll up/down painfully

		tst.w	d7
		bne.s	TVSCC_SlowUpDown

d396 1
a396 1
		movem.l	(sp)+,d2-d7
d402 14
a415 1
TVSCC_SlowUpDown:
d417 7
a423 1
		JSRLIB	WaitBlit
d425 1
a425 1
		move.l	#$FFFFFFFF,bltafwm(a1)	;and lwm
d427 2
a428 5
		moveq	#00,d0
		move.w	d0,bltamod(a1)
		move.w	d0,bltdmod(a1)
		move.w	d0,bltcon1(a1)
		move.w	#(SRCA!DEST!ABC!ABNC!ANBC!ANBNC),bltcon0(a1)
d430 1
a430 20
		move.l	-(a4),a1	;refetch A (use as D)
		move.l	-(a3),a0	;refetch D (use as A)

		cmp.b	#2,d3
		beq.s	TVSCC_SlowUp

	; slow copy down

		add.w	#((SCREEN_ROWS-FONT_HEIGHT)*DISPLAY_BYTES),a0
		bsr.s	TVSCC_CPUCopyRow

		move.w	#BLITREVERSE,bltcon1(a1)

		move.l	(a3),a0

		adda.w	#(SCREEN_END-2-(FONT_HEIGHT*DISPLAY_BYTES)),a0
		move.l	a0,bltapt(a1)

		adda.w	#(FONT_HEIGHT*DISPLAY_BYTES),a0
		move.l	a0,bltdpt(a1)
d432 2
a433 1
		move.w	#(((SCREEN_ROWS-FONT_HEIGHT)<<HSIZEBITS)!DISPLAY_WORDS),bltsize(a1)
a434 1
		JSRLIB	WaitBlit
d436 1
a436 1
	; restore copied row
d438 7
a444 2
		move.l	(a4)+,a0
		move.l	(a3)+,a1
a445 1
		bsr.s	TVSCC_CPUCopyRow
d447 3
a449 2
		dbf	d6,TVSCC_SlowRow
		bra	TVSCC_DidLastRow
d451 1
a451 2
TVSCC_SlowUp:
		bsr.s	TVSCC_CPUCopyRow
d453 1
a453 5
		move.l	(a3),a0
		move.l	a0,d0
		add.w	#(FONT_HEIGHT*DISPLAY_BYTES),a0
		move.l	a0,bltapt(a1)
		move.l	d0,bltdpt(a1)
d455 7
a461 1
		move.w	#(((SCREEN_ROWS-FONT_HEIGHT)<<HSIZEBITS)!DISPLAY_WORDS),bltsize(a1)
d463 1
a463 1
		JSRLIB	WaitBlit
d465 4
a468 1
	; restore copied row
a469 2
		move.l	(a4)+,a0
		move.l	(a3)+,a1
d471 1
a471 2
		add.w	#((SCREEN_ROWS-FONT_HEIGHT)*DISPLAY_BYTES),a1
		bsr.s	TVSCC_CPUCopyRow
d473 7
a479 2
		dbf	d6,TVSCC_SlowRow
		bra	TVSCC_DidLastRow
d481 3
a483 2
	; cache row which will be destroyed (but has been shifted!)
	; D is visible bitplanes, but in the invisible first row
d485 1
a485 1
TVSCC_CPUCopyRow:
d487 1
a487 1
		move.w	#(((FONT_HEIGHT*DISPLAY_BYTES)/32)-1),d1
d489 6
a494 1
TVSCC_CPUCopyRowL:
d496 1
d498 14
a511 8
		move.l	(a0)+,(a1)+
		move.l	(a0)+,(a1)+
		move.l	(a0)+,(a1)+
		move.l	(a0)+,(a1)+
		move.l	(a0)+,(a1)+
		move.l	(a0)+,(a1)+
		move.l	(a0)+,(a1)+
		move.l	(a0)+,(a1)+
d513 1
a513 1
		dbf	d1,TVSCC_CPUCopyRowL
d517 13
a529 1
		rts
@


1.10
log
@Ack, additional code so combo scroll with copy works like other players.
The spec gives no example, but having finally obtained the test disk
(also old), I see everyone else is interpreting combo scroll as
2 separate scroll commands.  Yikes, this new code is not terribly
fast (extra blit per plane), but will suffice for now.
@
text
@d2 1
a2 1
**	$Id: scroll.asm,v 1.9 92/03/11 12:22:56 darren Exp Locker: darren $
d383 1
a383 1
		move.w	0(a0,d3.w),d1
d386 6
a391 4
		add.w	d2,d2
		lea	TVScroll_PHTable(pc),a0
		move.w	0(a0,d2.w),d0
		bne.s	TVSCC_ValidScroll
d393 1
a393 2
		subq.w	#2,d1
	;	moveq	#00,d0
d396 5
a400 1
		move.w	d1,cdd_PHPV+2(a6)
d724 1
a724 1
		move.w	0(a0,d3.w),d1
d727 6
a732 4
		add.w	d2,d2
		lea	TVScroll_PHTable(pc),a0
		move.w	0(a0,d2.w),d0
		bne.s	TVSCR_ValidScroll
d734 1
a734 2
		subq.w	#2,d1
	;	moveq	#00,d0
d737 5
a741 1
		move.w	d1,cdd_PHPV+2(a6)
d1181 12
a1192 23
		dc.w	((((DISPLAY_WIDTH/8)*0)+2)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))
		dc.w	((((DISPLAY_WIDTH/8)*1)+2)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))
		dc.w	((((DISPLAY_WIDTH/8)*2)+2)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))
		dc.w	((((DISPLAY_WIDTH/8)*3)+2)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))
		dc.w	((((DISPLAY_WIDTH/8)*4)+2)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))
		dc.w	((((DISPLAY_WIDTH/8)*5)+2)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))
		dc.w	((((DISPLAY_WIDTH/8)*6)+2)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))
		dc.w	((((DISPLAY_WIDTH/8)*7)+2)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))
		dc.w	((((DISPLAY_WIDTH/8)*8)+2)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))
		dc.w	((((DISPLAY_WIDTH/8)*9)+2)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))
		dc.w	((((DISPLAY_WIDTH/8)*10)+2)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))
		dc.w	((((DISPLAY_WIDTH/8)*11)+2)+((DISPLAY_WIDTH/8)*FONT_HEIGHT))


**
** Precalculated PH lookup table.  Fine scrolling is adjusted by playing
** with bplcon1 in the copper list
**

FINEPHSCROLL	MACRO
		dc.w	(((15-\1)<<PFB_FINE_SCROLL_SHIFT)!(15-\1))
		ENDM
TVScroll_PHTable:
d1195 1
a1195 7
		FINEPHSCROLL	15	;0 means backup 1 word, and delay is 0
		FINEPHSCROLL	0
		FINEPHSCROLL	1
		FINEPHSCROLL	2
		FINEPHSCROLL	3
		FINEPHSCROLL	4

@


1.9
log
@Fix lookup table for left/up scroll combo.  Comment out 2 needless
moveq #00,d0 instructions.
@
text
@d2 1
a2 1
**	$Id: scroll.asm,v 1.8 92/03/04 14:50:20 darren Exp Locker: darren $
d53 1
d183 1
a183 1
		movem.l	d2-d6,-(sp)
d213 2
d216 7
d224 1
d244 2
a247 2
		lea	_custom,a1

d255 2
d297 1
d348 5
d361 2
d406 1
a406 1
		movem.l	(sp)+,d2-d6
d411 97
@


1.8
log
@Remove comments regarding scrolling all 4 planes at once - not
TRUE; there would have been more blitter time required to do so than
single plane blits (because an overlapping space is needed for u/d
scrolls between planes, and that would have may the total blit time
slower).
@
text
@d2 1
a2 1
**	$Id: scroll.asm,v 1.7 92/03/04 14:48:50 darren Exp Locker: darren $
d371 1
a371 1
		moveq	#00,d0
d610 1
a610 1
		moveq	#00,d0
d837 1
a837 1
		dc.l	$FFFFFFC0
d839 1
a839 1
		dc.l	2+(FONT_HEIGHT+DISPLAY_BYTES)
@


1.7
log
@Additional comments regarding scrolling for future reference
@
text
@d2 1
a2 1
**	$Id: scroll.asm,v 1.6 92/03/02 13:38:17 darren Exp Locker: darren $
a172 4
*	The 4 bit-planes which are used for drawing the 16 color fonts
*	in are allocated out of contiguous memory, hence a single
*	blit can be used for scrolling.  There is a 5th bitplane which
*	is part of the display, but is not drawn in.
a432 4
*	The 4 bit-planes which are used for drawing the 16 color fonts
*	in are allocated out of contiguous memory, hence a single
*	blit can be used for scrolling.  There is a 5th bitplane which
*	is part of the display, but is not drawn in.
@


1.6
log
@Moved code which marks as +G outside of drawing functions.  Lets me
light the +G light but do no drawing.
@
text
@d2 1
a2 1
**	$Id: scroll.asm,v 1.5 92/02/18 16:41:04 darren Exp Locker: darren $
d1100 5
d1173 2
a1174 1
**	pointers, and combinations are simply combinations.
d1187 24
a1210 3
**	the opposite column/row during the blits.  Because this data
**	is only being READ by the blitter, there is no problem doing
**	both blit, and copy at the same time.
@


1.5
log
@Change calc of PH so that we get the 1st pixel of the first visible
column when PH == 0.
@
text
@d2 1
a2 1
**	$Id: scroll.asm,v 1.4 92/02/17 14:35:14 darren Exp Locker: darren $
a385 4
	; Mark - is graphics

		bset	#CDGB_GRAPHICS,cdd_TypeLights(a6)

a627 4
	; Mark - is graphics

		bset	#CDGB_GRAPHICS,cdd_TypeLights(a6)

@


1.4
log
@Fix up scroll tables for combo scrolls.
@
text
@d2 1
a2 1
**	$Id: scroll.asm,v 1.3 92/01/31 11:31:43 darren Exp Locker: darren $
d363 5
d371 5
a375 1
		move.w	0(a0,d2.w),cdd_PHPV(a6)
d377 3
a379 4
		swap	d3
		add.w	d3,d3
		lea	TVScroll_PVTable(pc),a0
		move.w	0(a0,d3.w),cdd_PHPV+2(a6)
d610 5
d618 2
a619 1
		move.w	0(a0,d2.w),cdd_PHPV(a6)
d621 2
a622 4
		swap	d3
		add.w	d3,d3
		lea	TVScroll_PVTable(pc),a0
		move.w	0(a0,d3.w),cdd_PHPV+2(a6)
d624 3
d1094 1
a1099 1
		FINEPHSCROLL	5
@


1.3
log
@Mark as graphics so we can enable +G light
@
text
@d2 1
a2 1
**	$Id: scroll.asm,v 1.2 92/01/30 16:46:51 darren Exp Locker: darren $
d749 2
a750 2
		dc.l	DONTPRESET
		dc.l	DONTPRESET
d765 3
d769 1
a769 4
		dc.l	2+(FONT_HEIGHT*DISPLAY_BYTES)

		dc.l	DONTPRESET
		dc.l	DONTPRESET
d816 1
a817 1
		dc.w	0
d825 1
a825 1
		dc.l	DONTPRESET
d835 1
a836 1
		dc.w	DISPLAY_BYTES-2
d840 1
a841 1
		dc.l	2+(FONT_HEIGHT+DISPLAY_BYTES)
d844 1
a844 1
		dc.l	DONTPRESET
d1049 3
a1051 1
** Precalculated PV lookup table
d1069 2
a1070 1
** Precalculated PH lookup table
@


1.2
log
@debug off
@
text
@d2 1
a2 1
**	$Id: scroll.asm,v 1.1 92/01/30 16:05:23 darren Exp Locker: darren $
d15 1
d378 4
d617 4
@


1.1
log
@Initial revision
@
text
@d2 1
a2 1
**	$Id: preset.asm,v 1.1 92/1/2 08:00:00 darren Exp $
d16 1
a16 1
DEBUG_DETAIL	SET	DBG_BASIC
@

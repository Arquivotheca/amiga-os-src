head     1.15;
branch   ;
access   ;
symbols  ;
locks    ; strict;
comment  @* @;


1.15
date     93.02.03.16.18.22;  author darren;  state Exp;
branches ;
next     1.14;

1.14
date     93.02.03.15.37.56;  author darren;  state Exp;
branches ;
next     1.13;

1.13
date     93.02.03.11.44.40;  author darren;  state Exp;
branches ;
next     1.12;

1.12
date     92.04.03.09.43.05;  author darren;  state Exp;
branches ;
next     1.11;

1.11
date     92.04.02.17.07.56;  author darren;  state Exp;
branches ;
next     1.10;

1.10
date     92.03.06.13.02.27;  author darren;  state Exp;
branches ;
next     1.9;

1.9
date     92.03.06.09.28.19;  author darren;  state Exp;
branches ;
next     1.8;

1.8
date     92.03.05.15.48.04;  author darren;  state Exp;
branches ;
next     1.7;

1.7
date     92.03.02.12.16.44;  author darren;  state Exp;
branches ;
next     1.6;

1.6
date     92.02.27.10.55.20;  author darren;  state Exp;
branches ;
next     1.5;

1.5
date     92.02.18.11.53.07;  author darren;  state Exp;
branches ;
next     1.4;

1.4
date     92.02.17.11.40.12;  author darren;  state Exp;
branches ;
next     1.3;

1.3
date     92.02.14.17.58.11;  author darren;  state Exp;
branches ;
next     1.2;

1.2
date     92.02.14.12.02.02;  author darren;  state Exp;
branches ;
next     1.1;

1.1
date     92.01.30.16.40.18;  author darren;  state Exp;
branches ;
next     ;


desc
@Initial check in of ALPHA CDTV-CR cdg.library
@


1.15
log
@change branch to word size
@
text
@**
**	$Id: interrupts.asm,v 1.14 93/02/03 15:37:56 darren Exp Locker: darren $
**
**	CDTV CD+G -- interrupts.asm (interrupt server code)
**
**	(C) Copyright 1992 Commodore-Amiga, Inc.
**	    All Rights Reserved
**
**	
**

** Includes

		INCLUDE	"cdgbase.i"
		INCLUDE	"cdgprefs.i"
		INCLUDE	"cdgs_hw.i"
		INCLUDE	"debug.i"

		SECTION	cdg
** Imports
		XREF	_custom		;amiga.lib

** Assumptions

	IFNE	cdc_Color0
	FAIL	"cdc_Color0 not 0; recode!"
	ENDC

	IFNE	cdd_ActivePlanes-cdd_InactivePlanes-4
	FAIL	"cdd_ActivePlanes does not follow cdd_InactivePlanes; recode!"
	ENDC

	IFNE	cdd_SpriteDelay-cdd_StartSprite-2
	FAIL	"cdd_SpriteDelay does not follow cdd_StartSprite; recode!"
	ENDC

	IFNE	cdgp_ChannelSprites
	FAIL	"cdgp_ChannelSprites not 0; recode!"
	ENDC

	IFNE	cdgp_PAUSESprite-cdgp_ChannelSprites-(64*4)
	FAIL	"cdgp_PAUSESprite ! cdgp_ChannelSprites+(64*4); recode!"
	ENDC

	IFNE	cdgp_NTrackSprite-cdgp_PAUSESprite-(4*4)
	FAIL	"cdgp_NTrackSprite ! cdgp_PAUSESprite-(4*4); recode!"
	ENDC

	IFNE	cdd_BitPlane1_1-cdd_BitPlane1_0-4
	FAIL	"cdd_BitPlane1_1 does not follow cdd_BitPlane1_0; recode!"
	ENDC

	IFNE	cdd_BitPlane1_2-cdd_BitPlane1_1-4
	FAIL	"cdd_BitPlane1_2 does not follow cdd_BitPlane1_1; recode!"
	ENDC

	IFNE	cdd_BitPlane1_3-cdd_BitPlane1_2-4
	FAIL	"cdd_BitPlane1_3 does not follow cdd_BitPlane1_2; recode!"
	ENDC

	IFNE	cdd_BitPlane1_Extra-cdd_BitPlane1_3-4
	FAIL	"cdd_Bitplane1_Extra does not follow cdd_Bitplane1_3; recode!"
	ENDC

	IFNE	cdd_BitPlane2_1-cdd_BitPlane2_0-4
	FAIL	"cdd_BitPlane2_1 does not follow cdd_BitPlane2_0; recode!"
	ENDC

	IFNE	cdd_BitPlane2_2-cdd_BitPlane2_1-4
	FAIL	"cdd_BitPlane2_2 does not follow cdd_BitPlane2_1; recode!"
	ENDC

	IFNE	cdd_BitPlane2_3-cdd_BitPlane2_2-4
	FAIL	"cdd_BitPlane2_3 does not follow cdd_BitPlane2_2; recode!"
	ENDC

	IFNE	cdd_BitPlane2_Extra-cdd_BitPlane2_3-4
	FAIL	"cdd_Bitplane2_Extra does not follow cdd_Bitplane2_3; recode!"
	ENDC

	IFNE	cdd_CDIO
	FAIL	"cdd_CDIO not first in CDGData struct; recode!"
	ENDC


** Exports

		XDEF	CDGPackInt
		XDEF	CDGVertInt

** Equates

		CNOP	0,4

*****i* cdg/CDGPackInt ************************************************
*
*   NAME
*	CDGPackInt -- Interrupt for copying new packets
*   
*   SYNOPSIS
*	VOID CDGPackInt( data )
*			  A1
*
*	VOID CDGPackInt( struct CDGData * );
*
*   FUNCTION
*	Interrupt code for copying packet data to circular buffer.
*	The interrupt is routed through the CD.DEVICE via the FRAMECALL
*	command.  We are set-up to get every FRAME.
*
*   RETURNS
*	N/A
*
*   NOTES
*	There are 4 PACKS per PACKET, and these are buffered
*	by the 4510 in INTERLEAVED data order.  We get roughly
*	75 PACKETS per second - efficiency throughout is therefore
*	critical.
*
*	This routines only responsibility is to make a copy of the
*	PACKET, and store it in a circular buffer.  If the buffer
*	is FULL - ARGGHH!!!  Data is then lost, however this should
*	rarely (if ever happen) unless the main CD+G task is not given
*	CPU time for some reason.  In the world of CDTV-CR, its a
*	closed system when CD+G is running, and the system software
*	needs to be structured to guarantee the CD+G task gets most CPU
*	time (except for interrupts).
*
*	!!!NOTE!!!
*
*	Optimization of this code is IMPORTANT!!!  For the sake of
*	efficiency, if data is lost, it is overwritten with the most
*	recent data - tolerable, particularly when one considers it
*	doesn't matter much that we lost the last moment of data, or
*	the next moment of data.  Lost is lost.
*
*	!!!NEW!!!
*
*	The CDGS system is faster than the CDTV-CR, but gack, we
*	have to use a much slower interrupt copy routine here
*	because we lack the 4510 to do the P&Q bit stripping.
*
***********************************************************************

CDGPackInt:


	; The following flag allows us to ignore subcodes if
	; the cdg.library thinks the disk is stopped, or otherwise
	; not in CDGPlay() mode.  This allows CDGDraw() to exit
	; quickly, and if Signal() is being used, to never
	; Signal().  Hence the application could do something
	; else while the cdg.library thinks the disk is not
	; playing.
	;
	;
		btst	#EF2B_IGNORE,cdd_ExtraFlags2(a1)
		BNE_S	CDGPackInt_Ignore

	; Periodically reset +G/+MIDI pack validity counters

		addq.b	#1,cdd_FRollCount(a1)
		bne.s	CDPackInt_Count

		move.b	#GPACKCOUNT,cdd_GPackCount(a1)
		move.b	#MPACKCOUNT,cdd_MPackCount(a1)

CDPackInt_Count:

	; have we exceeded our max buffer space?

		move.l	cdd_PutPacket(a1),a0
		cmp.l	cdd_MaxPacket(a1),a0
		bne.s	CDGPackInt_Copy

	; if so, wrap PutPacket

		lea	cdd_PacketData(a1),a0

CDGPackInt_Copy:

		movem.l	d2/a2,-(sp)
		move.l	a1,a2

		move.l	cdd_SubCodeBank(a2),a1

		cmp.b	#$80,(CDHARDWARE+CDSUBINX)
		bcs.s	CDGPackInt_CopyPacks

		lea	$80(a1),a1	;plus $80 hex

CDGPackInt_CopyPacks:


	; use inline move for speed here

		move.l	#$3F3F3F3F,d1	;strip P & Q

		moveq	#(24-1),d2	;Move 4 * 24 == 96 bytes

	; the 020 has a pretty decent cache, so what the heck, a loop
	; is pretty much free

CDGPackInt_CopyPack:

		move.l	(a1)+,d0	;Move 4 bytes at a time
		and.l	d1,d0
		move.l	d0,(a0)+

		dbf	d2,CDGPackInt_CopyPack

		move.l	a0,cdd_PutPacket(a2)

	;;
	;; See if we need to signal task

		move.l	cdd_SigMask(a2),d0
		beq.s	CDGPackInt_Ending

		move.l	a6,-(sp)
		move.l	cdd_SigTask(a2),a1
		move.l	cdd_ExecLib(a2),a6
		JSRLIB	Signal
		move.l	(sp)+,a6

CDGPackInt_Ending:

		movem.l	(sp)+,d2/a2

CDGPackInt_Ignore:
		moveq	#00,d0		; Return 0 for future compatability

		rts

*****i* cdg/CDGVertInt *************************************************
*
*   NAME
*	CDGVertInt -- Vertical blank interrupt
*   
*   SYNOPSIS
*	VOID CDGVertInt( data )
*			  A1
*
*	VOID CDGVertInt( struct CDGData * );
*
*   FUNCTION
*	Vertical blank interrupt.  Used for timing purposes when
*	sprites are displayed (e.g., channel number selection), and
*	for reloading color registers in the active copper list.
*
*	This function also swaps the bitplane pointers around for
*	double buffered stuff.
*
*	Because this stuff is handled during VBLANK, no display
*	glitches!
*
*   RETURNS
*	N/A
*
*   NOTES
*	Timing is adjusted for VBlank RATE.
*
*	Sprites for user feedback-
*
*	This interrupt fetches a LONG WORD starting at cdd_StartSprite
*	which makes it possible for the cdg.library to stash a sprite
*	offset, and time delay value using an ATOMIC MOVE.L instruction -
*	hence, no DISABLE is needed to move both values at task time.
*
*	Sprite data is copied quickly, or cleared quickly, hence
*	it is possible to use data stored in ROM to save RAM
*	space.  While more expensive than poking copper list
*	pointers, the later does not allow sprite data to be in ROM.
*	Since there is so much sprite data, and a real need for
*	CHIP ram when PlayerPrefs, and CD+G is up ...
*
*	Also note that the displaying of sprites is a rare event
*	in the overall scheme of things; the extra CPU cycles really
*	don't hurt us here.  Because the copy is done at VBLANK time,
*	no visual sprite glitchies!
*
*	Color changes -
*
*	The 16 colors can be moved into the copper list at
*	VBLANK time.  In addition, the border color is also updated
*	within this routine, and must be one of the PEN colors.
*
*	NOTE!!!
*
*	Cheating like a slime here (performance needed on a 68000
*	machine), but more importantly is the need to batch up color
*	changes for the next frame, and update all colors atomically -
*	can't be done with LoadRGB4 or SetRGB4 because they are not
*	safe from interrupts.  Worse, we want a solid top border, and
*	screen color change (no slipping in between copinit color change
*	0, and the screen's color change 0).  
*
*	Double buffering -
*
*	The bitplane pointers can be swapped in the copper list
*	during VBLANK, hence no visual glitches.
*
***********************************************************************

		CNOP	0,4

CDGVertInt:

		move.l	a1,a5

	; See if we need to turn off sprite DMA for the Player program

		bclr	#VBFB_RESETDMA,cdd_VBlankFlags(a5)
		beq.s	CDGVertInt_NoResetDMA

		move.w	cdd_CacheDMACONR(a5),d0

	; Since I turn on SPRITE DMA, I'm not going to turn it ON again
	; if it was already on when CDGFront() was called

		and.w	#DMAF_SPRITE,d0
		bne.s	CDGVertInt_NoResetDMA

		move.w	#DMAF_SPRITE,_custom+dmacon

CDGVertInt_NoResetDMA:

	; See if there are any new colors to be loaded into copper list

		bclr	#VBFB_NEWCOLORS,cdd_VBlankFlags(a5)
		beq	CDVertInt_NoNewColors

	PRINTF	(DBG_INTERRUPT+DBG_FLOW),<'CDG--Copy New Colors'>

		move.l	cdd_BorderPen(a5),a0
		move.w	(a0),d0			;obtain color

		lea	cdd_CLUTTable(a5),a0

	; set graphics copinit color 0 so our top border looks right

		btst	#VBFB_BACKSCREEN,cdd_VBlankFlags(a5)
		bne.s	CDGV_SkipCopInit

		move.l	cdd_CopperList1+cdc_COPINITColor0(a5),a1
		move.w	d0,2(a1)

		move.l	cdd_CopperList1+cdc_COPINITColor0b(a5),a1
		move.w	d0,2(a1)

CDGV_SkipCopInit:

		move.l	a2,d1

		move.l	cdd_CopperList1+cdc_Color0(a5),a1
		move.l	cdd_CopperList1+cdc_Color0b(a5),a2

	; set new border color

		move.w	d0,2(a1)		;change border color
		move.w	d0,2(a2)		;change border color

	; update colors 0..7 mapped to hardware colors 8-15
	; 1 LONG WORD per instruction, so 8*4 = 32, plus 2 for instruction

		move.w	(a0),34(a1)		;maybe same or second bank in AA
		move.w	(a0)+,34(a2)		;primary first then secondary
						;just in case we are late
		move.w	(a0),38(a1)
		move.w	(a0)+,38(a2)

		move.w	(a0),42(a1)
		move.w	(a0)+,42(a2)

		move.w	(a0),46(a1)
		move.w	(a0)+,46(a2)

		move.w	(a0),50(a1)
		move.w	(a0)+,50(a2)

		move.w	(a0),54(a1)
		move.w	(a0)+,54(a2)

		move.w	(a0),58(a1)
		move.w	(a0)+,58(a2)

		move.w	(a0),62(a1)
		move.w	(a0)+,62(a2)

	; update colors 8..15 mapped to hardware 24-31
	; 1 LONG WORD per instruction, so 24*4 = 96, plus 2 for instruction

		move.w	(a0),098(a1)
		move.w	(a0)+,098(a2)

		move.w	(a0),102(a1)
		move.w	(a0)+,102(a2)

		move.w	(a0),106(a1)
		move.w	(a0)+,106(a2)

		move.w	(a0),110(a1)
		move.w	(a0)+,110(a2)

		move.w	(a0),114(a1)
		move.w	(a0)+,114(a2)

		move.w	(a0),118(a1)
		move.w	(a0)+,118(a2)

		move.w	(a0),122(a1)
		move.w	(a0)+,122(a2)

		move.w	(a0),126(a1)
		move.w	(a0)+,126(a2)

		move.l	d1,a2			;restore

CDVertInt_NoNewColors

	; see if we need to swap bitplane pointers

		bclr	#VBFB_NEWPLANES,cdd_VBlankFlags(a5)
		beq.s	CDVertInt_NoNewPlanes

	PRINTF	(DBG_INTERRUPT+DBG_FLOW),<'CDG--Swap bitplanes'>

		moveq	#00,d1

		IFEQ	SHOWBORDER


	; set horizontal scroll; PH is pre-calced

		move.l	cdd_CopperList1+cdc_BPLCON1(a5),a0
		move.w	cdd_PHPV(a5),2(a0)

		move.w	cdd_PHPV+2(a5),d1	;PV (pre-calced)

		ENDC

		move.l	cdd_ActivePlanes(a5),a0
		move.l	cdd_CopperList1+cdc_BPL0(a5),a1

	; Assumes bitplane pointer copper lists are stored in contiguous
	; memory!!

		; plane 0

		move.l	(a0)+,d0
		add.l	d1,d0
		move.w	d0,6(a1)		;store low word
		swap	d0
		move.w	d0,2(a1)		;store high word

		; plane 1

		move.l	(a0)+,d0
		add.l	d1,d0			;skip first word
		move.w	d0,14(a1)		;store low word
		swap	d0
		move.w	d0,10(a1)		;store high word

		; plane 2

		move.l	(a0)+,d0
		add.l	d1,d0			;skip first word
		move.w	d0,22(a1)		;store low word
		swap	d0
		move.w	d0,18(a1)		;store high word

		; plane 3 (mapped to plane 4)

		move.l	(a0)+,d0
		add.l	d1,d0			;skip first word
		move.w	d0,38(a1)		;store low word
		swap	d0
		move.w	d0,34(a1)		;store high word

		; plane 4 (mapped to plane 3)

		move.l	(a0)+,d0
		add.l	d1,d0			;skip first word
		move.w	d0,30(a1)		;store low word
		swap	d0
		move.w	d0,26(a1)		;store high word


CDVertInt_NoNewPlanes:

	; See if we need to put up a new sprite

		move.l	cdd_StartSprite(a5),d0
		bne.s	CDGVertInt_newsprite	;typical case is ==
		lea	_custom,a0		;compatability!
	;;;	moveq	#00,d0			;CC == EQ

		rts

	; check for new sprite before decrementing delay
	; counter.  allows us to display a new sprite before
	; the previous one has been turned off

CDGVertInt_newsprite:

		move.l	a5,a1

	; swap cdd_StartSprite into lower word, and check if <>

		swap	d0
		tst.w	d0

		BEQ_S	CDGVertInt_checkdelay

	PRINTF	(DBG_INTERRUPT+DBG_FLOW),<'CDG--Copy Sprite'>

		move.w	d2,-(sp)

	; Don't bother copying sprite data if the screen is in the back

		btst	#VBFB_BACKSCREEN,cdd_VBlankFlags(a1)
		bne.s	CDGV_backscreen

	; Copy sprite data quickly during VBLANK - no display glitchies,
	; and no copper list poking.
	;
	; This takes longer than poking graphics private copper list,
	; but is faster than ChangeSprite() (which really thinks it
	; runs on TASK time), and it makes it possible to share sprite
	; images (e.g., the caller can provide pointers to the same NULL
	; sprite if they do not want to draw all of the possible
	; sprites).

		move.l	cdd_CDGPrefs(a1),a6

		move.w	cdgp_SpriteHeight(a6),d2
		subq.w	#1,d2

		add.w	d0,a6		;add preadjusted offset

		move.l	(a6),a5		;sprite data ptr

		addq.w	#4,a5		;skip posctrl

		move.l	cdd_4SpriteData(a1),a0

		addq.w	#4,a0		;skip posctrl

		
                move.w	d2,d1

CDGV_copyleft1:

		move.l	(a5)+,(a0)+
		dbf	d1,CDGV_copyleft1

		move.l	4(a6),a5	;get next pointer
		addq.w	#4,a5		;skip posctrl

		addq.w	#8,a0		;skip terminator, and posctrl

                move.w	d2,d1

CDGV_copyleft2:
		move.l	(a5)+,(a0)+
		dbf	d1,CDGV_copyleft2

		move.l	8(a6),a5	;get next pointer
		addq.w	#4,a5		;skip posctrl

		addq.w	#8,a0		;skip terminator, and posctrl

                move.w	d2,d1

CDGV_copyright1:
		move.l	(a5)+,(a0)+
		dbf	d1,CDGV_copyright1

		move.l	12(a6),a5	;get next pointer
		addq.w	#4,a5		;skip posctrl

		addq.w	#8,a0		;skip terminator, and posctrl

CDGV_copyright2:
		move.l	(a5)+,(a0)+
		dbf	d2,CDGV_copyright2

CDGV_backscreen:

	; indicate sprite has been copied - don't do it again

		clr.w	cdd_StartSprite(a1)

		move.w	(sp)+,d2

	PRINTF	(DBG_INTERRUPT+DBG_EXIT),<'CDG--VBL XIT'>

		lea	_custom,a0		;compatability!
		moveq	#00,d0
		rts

	; check if we need to decrement our delay counter

CDGVertInt_checkdelay

		swap	d0
		tst.w	d0

	; this counter portion will be 0 if nothing to decrement,
	; NEGATIVE means no decrement (e.g., PAUSE sprite is displayed),
	; otherwise the counter is decremented until 0, at which time
	; the sprite is blanked.

		beq.s	CDGVertInt_Exit
		bmi.s	CDGVertInt_Exit

		subq.w	#1,d0
		move.w	d0,cdd_SpriteDelay(a1)
		bne.s	CDGVertInt_Exit		;0 yet?

	PRINTF	(DBG_INTERRUPT+DBG_FLOW),<'CDG--Clear Sprite'>

	; clear sprite imagery for all 4 sprites

		move.w	d2,-(sp)

		move.l	cdd_CDGPrefs(a1),a0
		move.w	cdgp_SpriteHeight(a0),d2
		subq.w	#1,d2

		move.l	cdd_4SpriteData(a1),a0

		moveq	#00,d0

		addq.w	#4,a0		;skip posctrl

		move.w	d2,d1

CDGV_clearleft1:
		move.l	d0,(a0)+
		dbf	d1,CDGV_clearleft1

		addq.w	#8,a0		;skip posctrl, and terminator

		move.w	d2,d1

CDGV_clearleft2:
		move.l	d0,(a0)+
		dbf	d1,CDGV_clearleft2

		addq.w	#8,a0		;skip posctrl, and terminator

		move.w	d2,d1

CDGV_clearright1:
		move.l	d0,(a0)+
		dbf	d1,CDGV_clearright1

		addq.w	#8,a0		;skip posctrl, and terminator

CDGV_clearright2:
		move.l	d0,(a0)+
		dbf	d2,CDGV_clearright2

		move.w	(sp)+,d2
		
CDGVertInt_Exit:

		lea	_custom,a0		;compatability!
		moveq	#00,d0			;CC == EQ
		rts


		END@


1.14
log
@Add AA copper list sniffing (gack)
@
text
@d2 1
a2 1
**	$Id: interrupts.asm,v 1.12 92/04/03 09:43:05 darren Exp Locker: darren $
d331 1
a331 1
		BEQ_S	CDVertInt_NoNewColors
@


1.13
log
@First pass at mods for AA CD Game system
@
text
@d288 10
d346 1
d348 1
d353 2
d356 1
d361 1
d366 20
a385 8
		move.w	(a0)+,34(a1)
		move.w	(a0)+,38(a1)
		move.w	(a0)+,42(a1)
		move.w	(a0)+,46(a1)
		move.w	(a0)+,50(a1)
		move.w	(a0)+,54(a1)
		move.w	(a0)+,58(a1)
		move.w	(a0)+,62(a1)
d387 3
d393 25
a417 8
		move.w	(a0)+,098(a1)
		move.w	(a0)+,102(a1)
		move.w	(a0)+,106(a1)
		move.w	(a0)+,110(a1)
		move.w	(a0)+,114(a1)
		move.w	(a0)+,118(a1)
		move.w	(a0)+,122(a1)
		move.w	(a0)+,126(a1)
@


1.12
log
@Roll a counter; every 3 secs (approx) reset valid counters, so we need
to get a burst of +MIDI/+G before enabling the lights
@
text
@d2 1
a2 1
**	$Id: interrupts.asm,v 1.11 92/04/02 17:07:56 darren Exp Locker: darren $
d16 1
a16 1
		INCLUDE	"cdtv_cr_hw.i"
a91 1
SYNCPATTERN	EQU	$1F3D
d102 1
a102 1
*			  A2
d137 6
a144 1

a155 4
	; Infact the 4510 is suppose to actually stop interrupting
	; the device driver when the disk is not actually playing, but
	; the use of this flag allows us to put the cdg.library in
	; a state which may be desirable for some applications.
d157 2
a159 3
		btst	#EF2B_IGNORE,cdd_ExtraFlags2(a2)
		bne.s	CDGPackInt_Ignore

d162 1
a162 1
		addq.b	#1,cdd_FRollCount(a2)
d165 2
a166 2
		move.b	#GPACKCOUNT,cdd_GPackCount(a2)
		move.b	#MPACKCOUNT,cdd_MPackCount(a2)
d172 2
a173 2
		move.l	cdd_PutPacket(a2),a0
		cmp.l	cdd_MaxPacket(a2),a0
d178 1
a178 1
		lea	cdd_PacketData(a2),a0
d182 4
d187 2
a188 1
		lea	(CDTVHARDWARE+CDSK_SUBCODE),a1
d190 1
a190 2
		tst.b	(CDTVHARDWARE+CDSK_SUBCODEBANK)
		bne.s	CDGPackInt_CopyPacks
a191 1
		lea	CDGPacket_SIZEOF(a1),a1
d194 2
a195 12
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;
	;; This code actually works better when I don't check for
	;; frame-sync.  Need Jerry to look into why I occassionally
	;; get a bad framesync, yet valid data.  Throwing this
	;; PACKET away results in at least 8 more bad packs (ARGG!!)
	;; because of the de-interleaving process, so rather I now
	;; rely on the error correction code to catch bad data, and
	;; don't check here.
	;;
	;;	cmpi.w	#SYNCPATTERN,cdk_SYNCSX-cdk_STARTPACKET(a1)
	;;	bne.s	CDGPackInt_Ignore
d197 1
a197 1
	; use MOVEM for speed - 24*4 total bytes
d199 10
a208 20
		movem.l	d2-d6,-(sp)

		moveq	#CDGPack_SIZEOF,d6


		movem.l	(a1)+,d0-d5	;move a PACK at a time
		movem.l	d0-d5,(a0)
		adda.l	d6,a0

		movem.l	(a1)+,d0-d5
		movem.l	d0-d5,(a0)
		adda.l	d6,a0

		movem.l	(a1)+,d0-d5
		movem.l	d0-d5,(a0)
		adda.l	d6,a0

		movem.l	(a1)+,d0-d5
		movem.l	d0-d5,(a0)
		adda.l	d6,a0
d210 1
a210 1
		movem.l	(sp)+,d2-d6
d218 1
a218 1
		beq.s	CDGPackInt_Done
d226 4
a230 1

a231 1
CDGPackInt_Done:
@


1.11
log
@Change CD_ -> CDSK_
@
text
@d2 1
a2 1
**	$Id: interrupts.asm,v 1.10 92/03/06 13:02:27 darren Exp Locker: darren $
d161 10
d248 1
@


1.10
log
@Disable SPRITE DMA within interrupt for safety.
@
text
@d2 1
a2 1
**	$Id: interrupts.asm,v 1.9 92/03/06 09:28:19 darren Exp Locker: darren $
d174 1
a174 1
		lea	(CDTVHARDWARE+CD_SUBCODE),a1
d176 1
a176 1
		tst.b	(CDTVHARDWARE+CD_SUBCODEBANK)
@


1.9
log
@Now pokes gfx copinit with border color IF my screen is the front
most screen
@
text
@d2 1
a2 1
**	$Id: interrupts.asm,v 1.8 92/03/05 15:48:04 darren Exp Locker: darren $
d304 19
a323 2

		move.l	a1,a5
@


1.8
log
@Added code to signal task in packet collection
@
text
@d2 1
a2 1
**	$Id: interrupts.asm,v 1.7 92/03/02 12:16:44 darren Exp Locker: darren $
d317 13
a329 1
		move.l	cdd_CopperList1(a5),a1
@


1.7
log
@Stop throwing away PACKETS if frame sync doesn't match.  Actually
works better (less data lost) since I now have error correction code
to check/fix bad packets.
@
text
@d2 1
a2 1
**	$Id: interrupts.asm,v 1.6 92/02/27 10:55:20 darren Exp Locker: darren $
d222 12
d237 1
@


1.6
log
@Remove code which modified PackCount in FRAMECALL interrupt.  This
doesn't work right since CDGDraw() uses a register variable to
track it.  Ok, we we get 8 junk packs; not really any different
than having the code in there.
@
text
@d2 1
a2 1
**	$Id: interrupts.asm,v 1.5 92/02/18 11:53:07 darren Exp Locker: darren $
d182 12
a193 2
		cmpi.w	#SYNCPATTERN,cdk_SYNCSX-cdk_STARTPACKET(a1)
		bne.s	CDGPackInt_Ignore
@


1.5
log
@Remove extra RTS, and DEBUG_TRAP macros.
@
text
@d2 1
a2 1
**	$Id: interrupts.asm,v 1.4 92/02/17 11:40:12 darren Exp Locker: darren $
d183 1
a183 1
		bne.s	CDGPackInt_FrameError
a214 10
		rts

	; On frame error, reset PACK count, and we need 8 fresh PACKS
	; before we get 1 de-interleaved PACK
	;
CDGPackInt_FrameError:

		move.l	a0,cdd_PutPacket(a2)
		moveq	#00,d0
		move.w	d0,cdd_PackCount(a2)
@


1.4
log
@Add ignore sub-code flag.
@
text
@d2 1
a2 1
**	$Id: interrupts.asm,v 1.3 92/02/14 17:58:11 darren Exp Locker: darren $
a142 1
	DEBUG_TRAP	8
d221 1
a221 1
	DEBUG_TRAP	$C
a226 2
		rts

a296 2

	DEBUG_TRAP	13
@


1.3
log
@Change cdtv_CR.. to cdtv_cr
@
text
@d2 1
a2 1
**	$Id: interrupts.asm,v 1.2 92/02/14 12:02:02 darren Exp Locker: darren $
d145 17
d212 2
@


1.2
log
@Do not display sprites if screen is in back.
@
text
@d2 1
a2 1
**	$Id: interrupts.asm,v 1.1 92/01/30 16:40:18 darren Exp Locker: darren $
d16 1
a16 1
		INCLUDE	"cdtv:include/internal/cdtv_CR_hw.i"
@


1.1
log
@Initial revision
@
text
@d2 1
a2 1
**	$Id: interrupts.asm,v 1.1 92/1/8 08:00:00 darren Exp $
d420 5
d488 2
@

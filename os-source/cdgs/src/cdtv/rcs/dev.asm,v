head     1.17;
branch   ;
access   ;
symbols  ;
locks    ; strict;
comment  @* @;


1.17
date     93.05.06.15.10.57;  author jerryh;  state Exp;
branches ;
next     1.16;

1.16
date     93.04.20.10.04.14;  author jerryh;  state Exp;
branches ;
next     1.15;

1.15
date     93.04.08.10.16.21;  author jerryh;  state Exp;
branches ;
next     1.14;

1.14
date     93.04.02.10.57.37;  author jerryh;  state Exp;
branches ;
next     1.13;

1.13
date     93.04.01.13.46.08;  author jerryh;  state Exp;
branches ;
next     1.12;

1.12
date     93.04.01.09.15.40;  author jerryh;  state Exp;
branches ;
next     1.11;

1.11
date     93.03.30.14.57.59;  author jerryh;  state Exp;
branches ;
next     1.10;

1.10
date     93.03.26.10.56.56;  author jerryh;  state Exp;
branches ;
next     1.9;

1.9
date     93.03.23.11.01.02;  author jerryh;  state Exp;
branches ;
next     1.8;

1.8
date     93.03.22.10.19.06;  author jerryh;  state Exp;
branches ;
next     1.7;

1.7
date     93.03.18.14.46.36;  author jerryh;  state Exp;
branches ;
next     1.6;

1.6
date     93.03.01.14.47.40;  author jerryh;  state Exp;
branches ;
next     1.5;

1.5
date     93.02.25.11.52.10;  author jerryh;  state Exp;
branches ;
next     1.4;

1.4
date     93.02.24.16.24.48;  author jerryh;  state Exp;
branches ;
next     1.3;

1.3
date     93.01.15.15.48.07;  author jerryh;  state Exp;
branches ;
next     1.2;

1.2
date     93.01.11.16.43.49;  author jerryh;  state Exp;
branches ;
next     1.1;

1.1
date     92.11.20.16.23.39;  author jerryh;  state Exp;
branches ;
next     ;


desc
@cdtv.device driver main service routines.
@


1.17
log
@removed addintserver call to caused interrupt.
@
text
@************************************************************************
***                                                                  ***
***                    -= CDTV DEVICE DRIVER =-                      ***
***                                                                  ***
************************************************************************
***                                                                  ***
***     CONFIDENTIAL and PROPRIETARY                                 ***
***     Copyright (c) 1990 by Commodore-Amiga, Inc.                  ***
***     Created by Carl Sassenrath, Sassenrath Research, Ukiah, CA   ***
***                                                                  ***
***     Modified 6/1/91: Jerry Horanoff                              ***
***                                                                  ***
************************************************************************

        INCLUDE "include:exec/types.i"
        INCLUDE "include:exec/nodes.i"
        INCLUDE "include:exec/lists.i"
        INCLUDE "include:exec/ports.i"
        INCLUDE "include:exec/libraries.i"
        INCLUDE "include:exec/tasks.i"
        INCLUDE "include:exec/devices.i"
        INCLUDE "include:exec/memory.i"
        INCLUDE "include:exec/interrupts.i"
        INCLUDE "include:exec/resident.i"
        INCLUDE "include:exec/io.i"
        INCLUDE "include:exec/errors.i"
        INCLUDE "include:exec/execbase.i"
        INCLUDE "include:exec/semaphores.i"
        INCLUDE "include:exec/ables.i"
        INCLUDE "include:utility/tagitem.i"
        INCLUDE "include:hardware/intbits.i"
        INCLUDE "include:devices/trackdisk.i"
        INCLUDE "include:devices/timer.i"
        INCLUDE "include:libraries/configregs.i"
        INCLUDE "include:libraries/configvars.i"

        INCLUDE "gs:cd/cd.i"
        INCLUDE "cdtv.i"
        INCLUDE "defs.i"
        INCLUDE "cddev.i"
        INCLUDE "cdtv_rev.i"
        INCLUDE "macros.i"

        OPT     p=68020

*
************************************************************************
***
***  External References
***
************************************************************************

        XREF    DevFuncs
        XREF    CmdOpts
        XREF    CmdAborts
        XREF    IntrProc
        XREF    GenStatus
        XREF    GetClass1Error
        XREF    GetClass2Error
        XREF    GetROMTrack
        XREF    PutHex
        XREF    DoCmd
        XREF    CmdReset
        XREF    ResetConfig
        XREF    AbortPlay
        XREF    AbortCmd
        XREF    WimpAllocCD
        XREF    FreeCD
        XREF    NextSeg
        XREF    CDBeginIO

        XREF    PutStr

        XDEF    DevName
        XDEF    Disable
        XDEF    Enable

        INT_ABLES

*
************************************************************************
***
***  Preliminaries
***
***********************************************************************/

                rts

                dc.b    'CDTV Device Driver',0
                dc.b    'Copyright (c) 1991 Commodore Electronics Ltd.',0
                dc.b    'All Rights Reserved.',0
                ds.w    0
                dc.b    'Patent Pending.',0
                dc.b    'Written by Carl Sassenrath, Ukiah, CA',0
                dc.b    'Modified by Jerry Horanoff',0
                ds.w    0

                VERSTAG
                ds.w    0


************************************************************************
***
***  Resident Tag
***
************************************************************************

        XDEF    ResTag
        XREF    EndCode

ResTag:         dc.w    RTC_MATCHWORD
                dc.l    ResTag
                dc.l    EndCode
                dc.b    RTF_COLDSTART
                dc.b    VERSION
                dc.b    NT_DEVICE
                dc.b    8                                               ; - priority
                dc.l    DevName
                dc.l    ModIdent
                dc.l    InitDevice

************************************************************************
***
***  Module Strings
***
************************************************************************

ModIdent:       VSTRING
                ds.w    0
cdDevName:      dc.b    'cd.device',0
DevName:        dc.b    'cdtv.device',0
DevTask:        dc.b    'CDTVTask',0
ExpanName:      dc.b    'expansion.library',0
                ds.w    0

ManuProd        dc.l    $202,$3                                         ; ConfigDev Manufacturer/Product


TagListSingle:
                dc.l    TAGCD_READSPEED,75
                dc.l    TAG_END,0

TagListDouble:
                dc.l    TAGCD_READSPEED,150
                dc.l    TAG_END,0


*
************************************************************************
***
***  DEVICE DRIVER INITIALIZATION
***
***     Create and initialize all data structures and hardware.
***
************************************************************************

InitDevice:
                movem.l d1-d7/a0-a6,-(sp)

                move.l  #db_SizeOf,d0                                   ; Create device (library) node
                clear   d1
                lea     DevFuncs(pc),a0
                move.l  d1,a1
                move.l  d1,a2
                exec    MakeLibrary
                tst.l   d0
                beq     4$
                move.l  d0,db
                move.b  #NT_DEVICE,LN_TYPE(db)
                lea     DevName(pc),a0
                move.l  a0,LN_NAME(db)
                move.b  #LIBF_SUMUSED|LIBF_CHANGED,LIB_FLAGS(db)
                move.w  #VERSION,LIB_VERSION(db)
                move.w  #REVISION,LIB_REVISION(db)
                lea     ModIdent(pc),a0
                move.l  a0,LIB_IDSTRING(db)

                clr.w   db_Openned(db)
                move.l  #2048,db_BlockSize(db)

                move.l  db,a1                                           ; Add to device list
                exec    AddDevice
4$
                move.l  db,d0
                movem.l (sp)+,d1-d7/a0-a6
                rts






*
************************************************************************
*                                                                      *
*   CD DEVICE DRIVER TASK                                              *
*                                                                      *
*       This is the task that runs the device driver.  Its main        *
*       body is a loop that reads requests from two message ports,     *
*       performs commands, and replies to the requests.                *
*                                                                      *
*       Two message ports are used to catagorize the requests into     *
*       two different classes:  Requests that directly access the      *
*       CD, and requests that don't or that only modify the behavior   *
*       of requests that do.  A PAUSE command for example modifies     *
*       the behavior of a PLAY command, but it doesn't effect the      *
*       request itself (aside from making the request finish later).   *
*                                                                      *
************************************************************************

CDTask:
                move.l  4(sp),db                                        ; Get device base arg and init hardware

                exec    CreateMsgPort                                   ; Create msg port D
                beq     open_fail
                move.l  d0,db_PortD(db)
                
                move.l  db_PortD(db),a0                                 ; Create I/O request D
                move.l  #IOSTD_SIZE,d0
                exec    CreateIORequest
                beq     open_fail
                move.l  d0,db_IORD(db)

                lea     cdDevName(pc),a0                                ; Open cd.device
                clr.l   d0
                move.l  db_IORD(db),a1
                clr.l   d1
                exec    OpenDevice

                move.l  db_IORD(db),a1                                  ; Setpatch cd.device BeginIO
                move.l  IO_DEVICE(a1),a0
                exec    Forbid
                move.l  -30+2(a0),db_CDBeginIO(db)
                lea     CDBeginIO(pc),a1
                move.l  a1,-30+2(a0)
                exec    Permit
TaskLoop:
                lea     db_DiskPort(db),a0                              ; Get a message
                exec    GetMsg
                tst.l   d0
                bne     1$

                move.l  #SIGF_DISKPORT,d0                               ; Wait for something to happen
                exec    Wait
                bra     TaskLoop
1$
                bclr    #0,db_abortedflags(db)                          ;should be cleared often -
                                                                        ;only set if CMD_READ is aborting an improgress IO
                                                                        ;request
                move.l  d0,ior
                cmp.w   #CDTV_READXL,IO_COMMAND(ior)
                bne     NotReadXL

                clr.l   d4                                              ; Transfer's IO_ACTUAL

                move.l  db_IORD(db),a1                                  ; Just do a regular READ
                move.w  #CD_READ,IO_COMMAND(a1)

                move.l  IO_LENGTH(ior),d3                               ; D3 = MaxTransfer
                move.l  db_BlockSize(db),d1
                mulu.l  d1,d3

                move.l  IO_OFFSET(ior),d0                               ; Correct offset
                mulu.l  d1,d0
                move.l  d0,IO_OFFSET(a1)

                clr.b   IO_FLAGS(a1)                                    ; Clear out flags

                move.l  IO_DATA(ior),a2                                 ; Get XL node
                FIRSTNODE a2

NextNode:
                tst.l   d3                                              ; is IO_OFFSET exceeded?
                ble     EndOfXL

                clr.l   XL_ACTUAL(a2)

                move.l  XL_LENGTH(a2),d2
                beq     5$                                              ; skip XL node?
1$
                move.l  XL_BUFFER(a2),d0
                bne     2$

                lea     db_Buff(db),a0                                  ; use skip buffer if no buf provided (seek)
                move.l  a0,d0

                move.l  #2048,d1
                cmp.l   d1,d2
                bls     2$

                move.l  d1,d2
2$
                move.l  d0,IO_DATA(a1)

                cmp.l   d2,d3                                           ; bound to IO_OFFSET
                bhs     3$
                move.l  d3,d2
3$
                move.l  d2,IO_LENGTH(a1)

                exec    DoIO                                            ; Do the read

                move.l  db_IORD(db),a1
                tst.b   IO_ERROR(a1)
                bne     EndOfXL

                add.l   d2,IO_OFFSET(a1)                                ; increment offset for next read
                add.l   d2,XL_ACTUAL(a2)
                add.l   d2,d4                                           ; add to total
                sub.l   d2,d3                                           ; subtract for max
                ble     4$                                              ; if exhausted, we are done
                                                                        ; regardless of XL node info
                move.l  XL_LENGTH(a2),d2
                sub.l   XL_ACTUAL(a2),d2
                bne     1$              
4$
                move.l  XL_DONECODE(a2),a0                              ; Call DoneCode function
                cmp.l   #0,a0
                beq     5$
                jsr     (a0)
                move.l  db_IORD(db),a1
5$
                move.l  LN_SUCC(a2),a2
                tst.l   (a2)
                bne     NextNode
                
EndOfXL:
                move.l  d4,IO_ACTUAL(ior)                               ; report error status
                move.b  IO_ERROR(a1),IO_ERROR(ior)
                bra     Reply
NotReadXL:
                move.l  db_IORD(db),a1
                exec    DoIO

                move.l  db_IORD(db),a1
                move.l  IO_ACTUAL(a1),IO_ACTUAL(ior)
                move.b  IO_ERROR(a1),d0
                move.b  d0,IO_ERROR(ior)

                bclr    #0,db_abortedflags(db)
                beq     Reply
                cmp.b   #CDERR_ABORTED,d0                               ; if aborted due to a CMD_READ
                beq     TaskLoop                                        ; no reply desired
Reply:
                move.l  ior,a1
                exec    ReplyMsg
                bra     TaskLoop



************************************************************************
***
***  OPEN DEVICE
***
***     Open access to the device.  Setup the unit field of the
***     I/O request to identify successful open.
***
***     The system calls this function with:
***             D0 == Unit Number (0-3)
***             A1 -> IO Request Structure
***             A6 -> Device Node
***
***     Errors are reported in the IO_ERROR field of IORequest
***
************************************************************************

 FUNCTION Open
                save    d1-d7/a0-a6
                save    a2/ior
                tst.l   d0                                              ; Only allow unit zero requests
                bne     open_fail

                move.l  a1,ior

                bclr.b  #LIBB_DELEXP,LIB_FLAGS(db)                      ; Clear possible pending delayed expunge

                addq.w  #1,LIB_OPENCNT(db)                              ; Bump unit and device open counters

                lea     DevTask(pc),a1
                exec    FindTask
                tst.l   d0
                bne     1$

                move.l  #STACK_SIZE,d0                                  ; Create I/O task stack
                clear   d1
                exec    AllocMem
                tst.l   d0
                beq     open_fail
                move.l  d0,a0
                lea     db_Task(db),a1
                move.l  a0,TC_SPLOWER(a1)
                add     #STACK_SIZE,a0
                move.l  a0,TC_SPUPPER(a1)
                move.l  db,-(a0)
                move.l  a0,TC_SPREG(a1)

                moveq   #SIGB_DISKPORT,d0
                lea     db_DiskPort(db),a0
                bsr     InitPort

                lea     db_Task(db),a1                                  ; Create I/O Task
                move.b  #NT_TASK,LN_TYPE(a1)
                move.b  #100,LN_PRI(a1)
                lea     DevTask(pc),a0
                move.l  a0,LN_NAME(a1)
                lea     CDTask(pc),a2
                sub.l   a3,a3
                exec    AddTask
1$
                move.l  #UNIT_SizeOF,d0
                move.l  #MEMF_PUBLIC,d1
                exec    AllocMem
                move.l  d0,IO_UNIT(ior)
                move.l  d0,a2

                exec    CreateMsgPort                                   ; Create msg port A
                beq     open_fail
                move.l  d0,UNIT_PortA(a2)
                
                move.l  UNIT_PortA(a2),a0                                 ; Create I/O request A
                move.l  #IOSTD_SIZE,d0
                exec    CreateIORequest
                beq     open_fail
                move.l  d0,UNIT_IORA(a2)

                exec    CreateMsgPort                                   ; Create msg port Chg
                beq     open_fail
                move.l  d0,UNIT_PortChg(a2)
                
                move.l  UNIT_PortChg(a2),a0                               ; Create I/O request Chg
                move.l  #IOSTD_SIZE,d0
                exec    CreateIORequest
                beq     open_fail
                move.l  d0,UNIT_IORChg(a2)

                lea     cdDevName(pc),a0                                ; Open cd.device
                clr.l   d0
                move.l  UNIT_IORA(a2),a1
                clr.l   d1
                exec    OpenDevice
                tst.b   d0
                bne     open_fail

                move.l  UNIT_IORA(a2),a0                                  ; Duplicate IORequest
                move.l  UNIT_IORChg(a2),a1
                move.l  IO_DEVICE(a0),IO_DEVICE(a1)
                move.l  IO_UNIT(a0),IO_UNIT(a1)

                clear   d0                                              ; Open ok
open_exit:
                restore a2/ior
                restore d1-d7/a0-a6
                move.b  d0,IO_ERROR(a1)                                 ; Return Open status
                rts

open_fail:      moveq.l #IOERR_OPENFAIL,d0                              ; Open failed
                bra     open_exit


************************************************************************
***
***  CLOSE DEVICE
***
***     Conclude access to a particular device unit.
***
***     The system calls this function with:
***             A1 -> IO Request Structure
***             A6 -> Device Node
***
***     Return zero to prevent any kind of expunge.
***
************************************************************************

 FUNCTION Close
                save    a2/ior
                move.l  a1,ior

                move.l  IO_UNIT(ior),a2

                moveq.l #-1,d0                                          ; Invalidate various I/O Request fields
                move.l  d0,IO_UNIT(ior)
                move.l  d0,IO_DEVICE(ior)

                subq.w  #1,LIB_OPENCNT(db)                              ; Decrement unit and device open counters

                move.l  UNIT_IORA(a2),a1
                exec    CloseDevice

                move.l  UNIT_IORChg(a2),a0
                exec    DeleteIORequest
                move.l  UNIT_PortChg(a2),a0
                exec    DeleteMsgPort

                move.l  UNIT_IORA(a2),a0
                exec    DeleteIORequest
                move.l  UNIT_PortA(a2),a0
                exec    DeleteMsgPort

                move.l  a2,a1
                move.l  #UNIT_SizeOF,d0
                exec    FreeMem

close_exit:
                restore a2/ior
                clear   d0                                              ; Prevent expunge
                rts

*
************************************************************************
***
***  EXPUNGE DEVICE
***
***     Never expunge.  Driver doesn't take much space.
***
************************************************************************

 FUNCTION Expunge
 FUNCTION Freserved
                clear   d0                                              ; Never expunge
                rts


*
************************************************************************
***
*** LSNtoMSFPOS
***
*** in:         D0 = (long)LSN
***
*** out:        D0 = (long)0x00MMSSFF
***
************************************************************************

 FUNCTION LSNtoMSFPOS

        save    d1

        add.l   #150,d0                                                 ; Add on 00:02:00

        move.l  d0,d1                                                   ; Calculate Frame (Block)
        clr.l   d0
        divu    #75,d1
        swap    d1
        move.b  d1,d0

        clr     d1                                                      ; Calculate Minute and Second
        swap    d1
        divu    #60,d1
        swap    d1
        lsl     #8,d1
        or.l    d1,d0

        restore d1
        rts


************************************************************************
***
*** MSFtoLSNPOS
***
*** in:         D0 = (long)0x00MMSSFF
***
*** out:        D0 = (long)LSN
***
************************************************************************

 FUNCTION MSFtoLSNPOS

        save    d1/d2

        swap    d0                                                      ; Calculate sector from minute
        move    d0,d1
        mulu    #60*75,d1

        swap    d0                                                      ; Add in sector from frame
        clr.l   d2
        move.b  d0,d2
        add.l   d2,d1

        lsr     #8,d0                                                   ; Add in sector from second
        mulu    #75,d0
        add.l   d1,d0

        sub.l   #150,d0                                                 ; Subtract off 00:02:00

        restore d1/d2
        rts


*
************************************************************************
***
*** LSNtoMSF
***
*** in:         D0 = (long)LSN
***
*** out:        D0 = (long)0x00MMSSFF
***
************************************************************************

 FUNCTION LSNtoMSF

        save    d1

        move.l  d0,d1                                                   ; Calculate Frame (Block)
        clr.l   d0
        divu    #75,d1
        swap    d1
        move.b  d1,d0

        clr.w   d1                                                      ; Calculate Minute and Second
        swap    d1
        divu    #60,d1
        swap    d1
        lsl     #8,d1
        or.l    d1,d0

        restore d1
        rts


************************************************************************
***
*** MSFtoLSN
***
*** in:         D0 = (long)0x00MMSSFF
***
*** out:        D0 = (long)LSN
***
************************************************************************

 FUNCTION MSFtoLSN

        save    d1/d2

        swap    d0                                                      ; Calculate sector from minute
        move.w  d0,d1
        mulu    #60*75,d1

        swap    d0                                                      ; Add in sector from frame
        clr.l   d2
        move.b  d0,d2
        add.l   d2,d1

        lsr.w   #8,d0                                                   ; Add in sector from second
        mulu    #75,d0
        add.l   d1,d0

        restore d1/d2
        rts


*
************************************************************************
***
*** BCDtoBIN
***
*** in:
***
***     D0 = (byte)BCD
***
*** out:
***
***     D0 = (byte)BIN
***
***
************************************************************************

 FUNCTION BCDtoBIN

        save    d1

        move    d0,d1                                                   ; Isolate one's digit
        and     #$000f,d1

        lsr     #4,d0                                                   ; Isolate ten's digit
        and     #$000f,d0

        mulu    #10,d0                                                  ; Multiply ten's digit

        add     d1,d0                                                   ; Add one's digit

        restore d1
        rts


*
************************************************************************
***
***  InitIntr
***
***     in:     D1 = Priority
***             DB = Data
***             A0 = Code
***             A1 = Node
***
************************************************************************

 FUNCTION InitIntr
                move.b  #NT_INTERRUPT,LN_TYPE(a1)                       ; Set up interrupt node
                move.b  d1,LN_PRI(a1)
                move.l  db,IS_DATA(a1)
                move.l  a0,IS_CODE(a1)  
                lea     DevName(pc),a0
                move.l  a0,LN_NAME(a1)
                rts


************************************************************************
***
***  InitPort
***
***     in:     A0 = Message port
***             D0 = SignalBit
***
************************************************************************

 FUNCTION InitPort
                clr.b   MP_FLAGS(a0)                                    ; Initialize message port
                move.b  d0,MP_SIGBIT(a0)
                lea     db_Task(db),a1
                move.l  a1,MP_SIGTASK(a0)
                lea     MP_MSGLIST(a0),a1
                NEWLIST a1
                rts


************************************************************************
***
***  Disable
***
************************************************************************

 FUNCTION Disable
                save    a6                                              ; Disable interrupts
                DISABLE a6
                restore a6
                rts


************************************************************************
***
***  Enable
***
************************************************************************

 FUNCTION Enable
                save    a6                                              ; Enable interrupts
                ENABLE  a6
                restore a6
                rts


@


1.16
log
@tst.b D0 after opendevice of cd.device was added.
@
text
@a705 2

                exec    AddIntServer                                    ; Add interrupt to server chain
@


1.15
log
@Removed speed changes for READXL command.
@
text
@d441 1
@


1.14
log
@Fixed trashing of A1 in XL callback routine.
@
text
@a255 10
                move.l  db_IORD(db),a1                                  ; Single speed
                move.w  #CD_CONFIG,IO_COMMAND(a1)
                clr.l   IO_LENGTH(a1)
                lea     TagListSingle(pc),a0
                move.l  a0,IO_DATA(a1)
                exec    DoIO
                move.l  db_IORD(db),a1
                tst.b   IO_ERROR(a1)
                bne     EndOfXL

a329 11
1$
                move.l  db_IORD(db),a1                                  ; Double speed
                move.w  #CD_CONFIG,IO_COMMAND(a1)                
                clr.l   IO_LENGTH(a1)
                lea     TagListDouble(pc),a0
                move.l  a0,IO_DATA(a1)
                exec    DoIO
                move.l  db_IORD(db),a1
                tst.b   IO_ERROR(a1)
                bne     1$

@


1.13
log
@Darren made some changes to make the skip buffer work properly.
@
text
@d331 1
@


1.12
log
@Fixed AbortIO of ReadXL.
@
text
@d281 1
d283 4
a286 6
                move.l  XL_LENGTH(a2),d2                                ; Set proper length
                beq     EndOfXL
                cmp.l   d2,d3
                bhs     1$
                move.l  d3,d2
1$              move.l  d2,IO_LENGTH(a1)
d288 4
a291 1
                move.l  XL_BUFFER(a2),d0                                ; Set the data pointer
d293 2
a294 1
                lea     db_Buff(db),a0
d297 7
a303 2
                cmp.l   #2048,IO_LENGTH(a1)
                bls.s   2$
d305 4
a308 1
                move.l  #2048,d2
d311 1
a311 1
2$              move.l  d0,IO_DATA(a1)
a312 1
                exec    DoIO                                            ; Do the read
d317 10
a326 3
                move.l  d2,XL_ACTUAL(a2)                                ; Report node's IO_ACTUAL
                add.l   d2,d4

d329 1
a329 1
                beq     3$
d331 1
a331 3
3$
                move.l  db_IORD(db),a1                                  ; Next Node
                add.l   d2,IO_OFFSET(a1)
a333 3
                beq     EndOfXL

                sub.l   d2,d3                                           ; End of transfer?
d335 1
d368 1
@


1.11
log
@ReadXL command can now be aborted.
@
text
@d282 7
d290 1
a290 1
                bne     1$
a292 1
1$              move.l  d0,IO_DATA(a1)
d294 7
a300 6
                move.l  XL_LENGTH(a2),d2                                ; Set proper length
                beq     EndOfXL
                cmp.l   d2,d3
                bhs     2$
                move.l  d3,d2
2$              move.l  d2,IO_LENGTH(a1)
d409 1
a409 1
                move.b  #127,LN_PRI(a1)
@


1.10
log
@Clears IO_LENGTH when doing config command of ReadXL simulation.
@
text
@d237 2
a238 2
1$
                lea     db_DiskPort(db),a0
d241 2
a242 2
                bne     3$
2$
d245 2
a246 2
                bra     1$
3$
d252 1
a252 1
                bne     11$
d254 2
d262 3
d281 1
a281 3

                clr.l   d4                                              ; Transfer's IO_ACTUAL
4$
d283 1
a283 1
                bne     5$
d286 1
a286 1
5$              move.l  d0,IO_DATA(a1)
d289 1
a289 1
                beq     10$
d291 1
a291 1
                bhs     6$
d293 1
a293 1
6$              move.l  d2,IO_LENGTH(a1)
d296 3
d305 1
a305 1
                beq     7$
d307 1
a307 1
7$
d312 1
a312 1
                beq     10$
d315 2
a316 2
                bne     4$
10$
d319 1
a319 1

d326 3
d330 2
a331 2
                bra     99$
11$
d341 1
a341 1
                beq     99$
d343 2
a344 2
                beq     1$                                              ; no reply desired
99$
d347 1
a347 1
                bra     1$
@


1.9
log
@NULL XL_BUFFER pointer fix.
Read abort play fix.
@
text
@d247 1
a247 1
                bclr    #0,db_abortedflags(db)                   ;should be cleared often -
d255 2
a256 1
                move.w  #CD_CONFIG,IO_COMMAND(a1)                
d316 1
@


1.8
log
@Detects tail of CDXL sequence correctly.
@
text
@d247 3
d278 5
a282 1
                move.l  XL_BUFFER(a2),IO_DATA(a1)                       ; Set the data pointer
d287 1
a287 1
                bhs     5$
d289 1
a289 1
5$              move.l  d2,IO_LENGTH(a1)
d298 1
a298 1
                beq     6$
d300 1
a300 1
6$
d326 7
a332 1
                move.b  IO_ERROR(a1),IO_ERROR(ior)
@


1.7
log
@Fixed some CDXL stuff.
@
text
@d297 1
a297 6
                cmp.l   #0,a2
                beq     10$

                move.l  IO_DATA(ior),a0
                lea     LH_TAIL(a0),a0
                cmp.l   a0,a2
@


1.6
log
@Implemented CDTV_READXL command.
@
text
@d44 1
d179 1
d261 2
a262 2
                lsl.l   #8,d3
                lsl.l   #3,d3
d265 1
a265 2
                lsl.l   #8,d0
                lsl.l   #3,d0
d268 1
a268 1
                clr.b   IO_FLAGS(a2)                                    ; Clear out flags
d270 2
a271 2
                move.l  IO_DATA(ior),a0                                 ; Get XL node
                move.l  LN_SUCC(a0),a2
d383 1
a383 1
                move.b  #20,LN_PRI(a1)
@


1.5
log
@uses cdtv_rev.i now.
@
text
@d30 1
d37 2
a40 1
        INCLUDE "cdtv.i"
d44 1
d137 10
d245 70
a314 1
                move.l  d0,a2
d316 2
d322 4
a325 4
                move.l  IO_ACTUAL(a1),IO_ACTUAL(a2)
                move.b  IO_ERROR(a1),IO_ERROR(a2)

                move.l  a2,a1
d382 1
a382 1
                move.b  #0,LN_PRI(a1)
d504 1
a504 1
*** LSNtoMSF
d512 1
a512 1
 FUNCTION LSNtoMSF
d537 1
a537 1
*** MSFtoLSN
d545 1
a545 1
 FUNCTION MSFtoLSN
d563 63
@


1.4
log
@Fixed TOC stuff (not completely).
@
text
@d39 1
a39 1
        INCLUDE "version.i"
d95 2
@


1.3
log
@task is added at Open now.
@
text
@d32 1
a194 2
                PRINT   51$,52$,"CDTVTaskRunning"

a212 2
                PRINT   53$,54$,"DEVICE_Openned"

a267 2
                PRINT   4$,5$,"OPEN"

a276 2
                PRINT   51$,52$,"AddingTask"

a328 10
                exec    CreateMsgPort                                   ; Create msg port Chg
                beq     open_fail
                move.l  d0,UNIT_PortFrm(a2)
                
                move.l  UNIT_PortFrm(a2),a0                               ; Create I/O request Chg
                move.l  #IOSTD_SIZE,d0
                exec    CreateIORequest
                beq     open_fail
                move.l  d0,UNIT_IORFrm(a2)

a339 6
                move.l  UNIT_IORFrm(a2),a1
                move.l  IO_DEVICE(a0),IO_DEVICE(a1)
                move.l  IO_UNIT(a0),IO_UNIT(a1)


                PRINT   53$,54$,"SUCCESS"
a369 2
                PRINT   1$,2$,"Close"

a380 5
                move.l  UNIT_IORFrm(a2),a0
                exec    DeleteIORequest
                move.l  UNIT_PortFrm(a2),a0
                exec    DeleteMsgPort

d528 1
a528 1
InitIntr:
d545 1
d549 1
a549 1
InitPort:
@


1.2
log
@added beginio setpatch to make read abort plays.
@
text
@d125 1
a162 26
                move.l  #STACK_SIZE,d0                                  ; Create I/O task stack
                clear   d1
                exec    AllocMem
                tst.l   d0
                beq     4$
                move.l  d0,a0
                lea     db_Task(db),a1
                move.l  a0,TC_SPLOWER(a1)
                add     #STACK_SIZE,a0
                move.l  a0,TC_SPUPPER(a1)
                move.l  db,-(a0)
                move.l  a0,TC_SPREG(a1)

                moveq   #SIGB_DISKPORT,d0
                lea     db_DiskPort(db),a0
                bsr     InitPort

                lea     db_Task(db),a1                                  ; Create I/O Task
                move.b  #NT_TASK,LN_TYPE(a1)
                move.b  #0,LN_PRI(a1)
                lea     DevName(pc),a0
                move.l  a0,LN_NAME(a1)
                lea     CDTask(pc),a2
                sub.l   a3,a3
                exec    AddTask

a200 2
                move.l  d0,a1
                exec    AddPort
d214 2
a234 2
                PRINT   55$,56$,"GotMSG"

a241 2
                PRINT   57$,58$,"CDTV_ReplyMsg"

d264 1
d271 1
a271 1
                PRINT   4$,5$,"Open"
d277 6
a282 1
                PRINT   2$,3$,"Creating_Unit"
d284 26
a318 2
                move.l  d0,a1
                exec    AddPort
a328 2
                move.l  d0,a1
                exec    AddPort
a338 2
                move.l  d0,a1
                exec    AddPort
d360 4
a363 1
1$
d367 1
a401 2
                tst.w   LIB_OPENCNT(db)
                bne     close_exit
a407 2
                move.l  UNIT_PortFrm(a2),a1
                exec    RemPort
a412 2
                move.l  UNIT_PortChg(a2),a1
                exec    RemPort
a417 2
                move.l  UNIT_PortA(a2),a1
                exec    RemPort
d424 1
@


1.1
log
@Initial revision
@
text
@d65 1
d69 1
d162 26
d197 79
d305 1
a305 1
                PRINT   2$,3$,"Creating"
a324 12
                exec    CreateMsgPort                                   ; Create msg port D
                beq     open_fail
                move.l  d0,UNIT_PortD(a2)
                move.l  d0,a1
                exec    AddPort
                
                move.l  UNIT_PortD(a2),a0                                 ; Create I/O request D
                move.l  #IOSTD_SIZE,d0
                exec    CreateIORequest
                beq     open_fail
                move.l  d0,UNIT_IORD(a2)

d357 1
a357 1
                move.l  UNIT_IORD(a2),a1
d360 1
a360 1
                move.l  UNIT_IORChg(a2),a1
d370 1
a370 1
open_fail:      moveq.l #CDERR_OpenFail,d0                              ; Open failed
a420 7
                move.l  UNIT_IORD(a2),a0
                exec    DeleteIORequest
                move.l  UNIT_PortD(a2),a1
                exec    RemPort
                move.l  UNIT_PortD(a2),a0
                exec    DeleteMsgPort

d446 1
a446 1
 FUNCTION Reserved
@

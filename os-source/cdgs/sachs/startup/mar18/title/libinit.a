; :ts=8
*
*	libinit.a
*
*	William A. Ware			D317
*
*************************************************************************
*   This information is CONFIDENTIAL and PROPRIETARY			*
*   Copyright 1993, Silent Software, Incorporated.			*
*   All Rights Reserved.						*
*************************************************************************


		include "exec/types.i"
		include "exec/libraries.i"

		section "initlib",code

		xdef __Init_Lib

		xref	_InitData
		xref	_FuncTable
		xref	_LVOOpenLibrary
		xref	_LVOMakeLibrary
		xref	_LVOAddLibrary
		xref	_LVOCloseLibrary
		xref	_LVORemove
		xref	_LVOFreeMem
		
	; a4 = titlebase
	; a5 = segments
	; a6 = sysbase

__Init_Lib
		movem.l	d2/a2/a4-a6,-(sp)

		movea.l	(4).w,a6	; Load the system base

		lea	_FuncTable,a0
		lea	_InitData,a1
		moveq	#0,d1
		movea.l	d1,a2
		moveq	#LIB_SIZE,d0
		jsr	_LVOMakeLibrary(a6)
		tst.l	d0
		beq.s	il_err
		movea.l	d0,a4

		movea.l	a4,a1
		jsr	_LVOAddLibrary(a6)
		
		move.l	a4,d0
il_exit		movem.l	(sp)+,d2/a2/a4-a6
		rts

il_err		moveq	#0,d0
		bra.s	il_exit

;********************************************************************

		xdef	__Open_Lib

__Open_Lib:	addq.w	#1,LIB_OPENCNT(a6)
		bclr.b	#LIBB_DELEXP,LIB_FLAGS(a6)
		move.l	a6,d0
		rts

;********************************************************************
		xdef	__Close_Lib
		
__Close_Lib	move.w	LIB_OPENCNT(a6),d0
		beq.s	cl_exit
		subq.w	#1,LIB_OPENCNT(a6)

cl_exit		moveq	#0,d0
		rts

 
;***********************************************************************
		xdef __Expunge_Lib

__Expunge_Lib:
 		moveq	#0,d0
		rts
  		END

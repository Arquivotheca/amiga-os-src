;
; assembly language support for rs485.device
; 
			
	SECTION	text,CODE

;
; listener task startup code.  Referenced in task.c
; 
	xdef	_taskent
	xdef	@taskent
	xref	_listener
	xref	_getlibbase

_taskent:
@taskent:	; initialize a6, start listener task
	jsr	_getlibbase(pc)
	move.l	d0,a6
	jmp	_listener(pc)


;
; interrupt stub - referenced in interupts.c
; 
	xdef	_interrupt
	xdef	@interrupt
	xref	_interruptC

@interrupt:
_interrupt:	; init a6, call interrupt function
	movem.l	a2-a6/d2-d7,-(sp)
	move.l	14(a1),a6		; __a6 = up->libbase (a1 is unit pntr)
	jsr	_interruptC(pc)
	movem.l	(sp)+,a2-a6/d2-d7
	moveq	#0,d0
	rts
			
; 
; copy data out of board into ram buffer.  referenced in recv.c
;
	xdef	_copyin

_copyin:	; copy data from board to buffer
	bra.s	e
c:	move.b	(a0),(a1)+
e:	dbf	d0,c
	rts

;
; copy data out from ram into board.  referenced in startxmit.c
;
	xdef	_copyout

_copyout:	; copy from buffer to board
	bra.s	e1
c1:	move.b	(a0)+,(a1)
e1:	dbf	d0,c1
	rts

	end


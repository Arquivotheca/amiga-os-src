head     1.4;
branch   ;
access   ;
symbols  ;
locks    ; strict;
comment  @* @;


1.4
date     93.12.14.13.48.26;  author kcd;  state Exp;
branches ;
next     1.3;

1.3
date     92.01.21.15.39.49;  author gregm;  state Exp;
branches ;
next     1.2;

1.2
date     91.11.07.11.49.21;  author gregm;  state Exp;
branches ;
next     1.1;

1.1
date     91.10.24.11.01.11;  author gregm;  state Exp;
branches ;
next     ;


desc
@First verison of New A2065 driver
@


1.4
log
@Fixed file header, updated to work better with Ameristar ethernet boards.
@
text
@**
** A2065 SANA-II Device Driver source
** Copyright (c) 1992 by Commodore-Amiga, Inc.
**
** All rights Reserved.
**



        include "includes.asm"
        include "globals.i"
        include "a2065_defs.i"

        include "libraries/configregs.i"
        include "libraries/configvars.i"

        list

        XDEF    FindBds,RemBds
        XREF    DName,TheSoftInt

;
;       FindBds
;
;       On Entry:
;               A5 contains pointer to device
;
;       Register Usage:
;               D4      Counter of boards found
;               A3      Pointer to current ConfigDev structure
;               A2      Pointer To Newly Allocated Unit Structure
;
;       OUTPUT: D0      Count of the number of boards found and configured.

FindBds movem.l d1-d4/a0-A3/a6,-(sp)
        moveq   #0,D4
        move.l  D4,A3

10$     moveq.l #0,d3
        move.l  A3,A0
        move.l  #-1,D0
        moveq.l #-1,D1
        move.l  ExpansionBase(A5),A6
        CALL    FindConfigDev
        tst.l   D0
        beq     99$
        move.l  d0,a3

        move.w  cd_Rom+er_Manufacturer(a3),d1
        cmp.w   #MANUFACTURER,d1                    * C= A2065 board
        bne     17$
        move.b  cd_Rom+er_Product(a3),d1
        cmp.b   #PRODUCT,d1
        beq     150$

17$     move.w  cd_Rom+er_Manufacturer(a3),d1
        cmp.w   #MAN_AMERISTAR,d1
        bne     10$                                 * Ameristar
        move.b  cd_Rom+er_Product(a3),d1
        cmp.b   #PROD_AMERISTAR,d1
        bne     10$

150$
        btst    #CDB_CONFIGME,cd_Flags(a3)   * If already configured, don't do it again.
        beq     99$
        bclr    #CDB_CONFIGME,cd_Flags(a3)

        ; Board matching our requirements has been found.

        move.l  #UNIT_SIZE_,D0
        move.l  #MEMF_PUBLIC|MEMF_CLEAR,D1
        move.l  SysBase(A5),A6
        CALL    AllocMem
        tst.l   d0
        beq     99$

        ; A Unit data structure has been allocated. Now initialize it.

        move.l  D0,A2


        tst.b   d3
        beq     603$
        move.l  #MEMORY_OFFSET_500,MemoryOffset(a2)
        move.l  #CHIP_OFFSET_500,ChipOffset(a2)
        bra     604$
603$    move.l  #MEMORY_OFFSET,MemoryOffset(a2)
        move.l  #CHIP_OFFSET,ChipOffset(a2)
604$

        clr.l   UnitStats+S2DS_RECONFIGURATIONS(A2)

        move.l  A3,UnitConfigDev(a2)
        move.l  cd_BoardAddr(A3),A1
        move.l  A1,UnitBoard(A2)
        move.l  A1,A0

*        lea     CHIP_OFFSET(A0),A0
        add.l   ChipOffset(a2),a0
        move.l  A0,UnitChip(A2)

        add.l   MemoryOffset(a2),a1
*        add.l   #MEMORY_OFFSET,A1
        move.l  A1,UnitMemory(A2)

        move.l  A1,A0
        lea     LANCE_TX_BUFFER(A0),A0
        move.l  A0,UnitTxBuffer(A2)

        move.l  A1,A0
        lea     LANCE_RX_BUFFER(A0),A0
        move.l  A0,UnitRxBuffer(A2)

        move.l  A1,A0
        lea     LANCE_TX_RING(A0),A0
        move.l  A0,UnitTxDescriptor(A2)

        move.l  A1,A0
        lea     LANCE_RX_RING(A0),A0
        move.l  A0,UnitRxDescriptor(A2)

        ; Construct the default Ethernet address for this unit.

        move.w  cd_Rom+er_Manufacturer(a3),d0
        cmp.w   #MAN_AMERISTAR,d0
        bne     439$
        move.l  #AMERISTAR,UnitDefaultAddress(a2)       ; first three bytes
        bra     440$
439$    move.l  #COMMODORE,UnitDefaultAddress(A2)       ; first three bytes
440$    lea     UnitDefaultAddress+3(A2),A1
        lea     cd_Rom+er_SerialNumber+1(A3),A0
        move.b  (A0)+,(A1)+                             ; fourth byte
        move.b  (A0)+,(A1)+                             ; fifth byte
        move.b  (A0)+,(A1)+                             ; sixth byte

        ; Initialize This Units SoftInterrupt

        lea     UnitSoftInt(A2),A0
        move.b  #0,LN_PRI(A0)
        move.b  #NT_INTERRUPT,LN_TYPE(A0)
        move.l  #DName,LN_NAME(A0)
        clr.l   IS_DATA(A0)
        move.l  #TheSoftInt,IS_CODE(A0)

        ; Initialize The Protocol Nexus For 802 Style Packets and Orphans

        lea     Unit802PN(A2),A0
        NLIST   PN_RXQ(A0)

        lea     UnitOrfPN(A2),A0
        NLIST   PN_RXQ(A0)

        ; Initialize the MultiCast Address List

        NLIST   UnitMultiCastList(A2)

        ; Initialize the Transmission List

        NLIST   UnitTxList(A2)
        NLIST   UnitTxRunningList(A2)

        ; Initialize the OnEvent List

        NLIST    UnitEventList(A2)

        ; Initialize the ConfigIOB List

        NLIST   UnitConfigList(A2)

        ; Initialize the Protocol Hash Table

        bsr.s   InitPH

        ; Place this unit into the Unit Array

        lea     UnitArray(A5),A0
        move.l  D4,D0
        asl.l   #2,D0
        move.l  A2,0(A0,D0.l)

        ; Increment the count of the boards found

        addq.l  #1,d4
        cmp.l   #MAX_BOARDS,D4

        ; And loop again if we haven't found a box full of Ethernet boards.

        bne     10$

99$     move.l  D4,D0
        move.l  D4,UnitCount(a5)
        movem.l (sp)+,a0-A3/a6/d1-d4
        rts

;---------------------------------------------------------------------------
; InitPH - Initializes a Unit's ProtocolHashTable.
;---------------------------------------------------------------------------
; INPUT:        A2      Pointer To A Newly Allocated Unit Structure
;
;---------------------------------------------------------------------------

InitPH  movem.l A2/D2,-(SP)
        lea     UnitProtocolHashTable(A2),A2
        move.w  #HASH_SIZE-1,D2
100$    NEWLIST A2
        lea     MLH_SIZE(A2),A2
        dbf     D2,100$
        movem.l (SP)+,A2/D2
        rts

RemBds  movem.l D2/A2,-(sp)

        lea     UnitArray(A5),A2
        move.l  SysBase(A5),A6
        moveq.l #MAX_BOARDS,D2

0$      tst.l   (A2)
        beq.s   99$

        move.l  (a2),a1
        move.l  UnitConfigDev(a1),a1

        bset    #CDB_CONFIGME,cd_Flags(a1)     * Allow others to config it

        move.l  (A2),A1
        move.l  #UNIT_SIZE_,d0
        CALL    FreeMem

        clr.l   (A2)+
        subq.l  #1,D2
        bne.s   0$

99$     movem.l (sp)+,D2/A2
        rts

        end
@


1.3
log
@Added use of the cd_Flags CONFIGME bit in the ConfigDev struct, due to
complaints that it wasn't being done.
@
text
@d1 6
a6 1
;DEBUG_MODE     equ     1
a7 1
        nolist
a8 5
;
;         Created Under Contract To Commodore Business Machines By:
;
;                   Perry Kivolowitz - ASDG Incorporated
;
d12 1
a12 1
        include "a2065.i"
d39 4
a42 3
10$     move.l  A3,A0
        move.l  #MANUFACTURER,D0
        moveq.l #PRODUCT,D1
d46 2
a47 1
        bne     150$
d49 13
a61 6
        move.l  a3,a0
        move.l  #MAN_AMERISTAR,d0         * look for ameristar stuff too
        moveq.l #PROD_AMERISTAR,d1
        CALL    FindConfigDev
        tst.l   D0
        beq     99$
a63 2
        move.l  D0,A3

d81 10
d98 2
a99 1
        lea     CHIP_OFFSET(A0),A0
d102 2
a103 1
        add.l   #MEMORY_OFFSET,A1
d124 7
a130 2
        move.l  #COMMODORE,UnitDefaultAddress(A2)       ; first three bytes
        lea     UnitDefaultAddress+3(A2),A1
d220 4
a223 2
        move.l  UnitConfigDev(a2),a1
        bset    #CDB_CONFIGME,(a1)     * Allow others to config it
@


1.2
log
@Changes too numerous to mention!
@
text
@d46 7
d55 1
d58 4
d77 1
d196 3
@


1.1
log
@Initial revision
@
text
@a90 3
        KPRN2   BMsg,UnitChip(A2),UnitMemory(A2)
        KPRN2   CMsg,UnitTxBuffer(A2),UnitRxBuffer(A2)

d124 1
d128 1
a128 10
        movem.l D2/A2,-(SP)
        lea     UnitErrorErrList(A2),A2
        moveq.l #S2EVENT_SOFTWARE,D2

100$    move.l  A2,A0
        NEWLIST A0
        lea     MLH_SIZE(A2),A2
        dbf     D2,100$

        movem.l (SP)+,A2/D2
a193 5

        IFD     DEBUG_MODE
BMsg    dc.b    'Board Found. Chip: 0x%lx Memory: 0x%lx ',0
CMsg    dc.b    'TXB: 0x%lx RXB: 0x%lx',13,10,0
        ENDC
@

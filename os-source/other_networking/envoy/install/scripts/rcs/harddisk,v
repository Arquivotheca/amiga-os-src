head     1.8;
branch   ;
access   ;
symbols  ;
locks    ; strict;
comment  @# @;


1.8
date     93.12.23.20.35.59;  author jesup;  state Exp;
branches ;
next     1.7;

1.7
date     93.12.23.20.05.41;  author jesup;  state Exp;
branches ;
next     1.6;

1.6
date     93.12.23.19.15.46;  author jesup;  state Exp;
branches ;
next     1.5;

1.5
date     93.11.03.00.04.57;  author jesup;  state Exp;
branches ;
next     1.4;

1.4
date     93.10.06.18.17.11;  author jesup;  state Exp;
branches ;
next     1.3;

1.3
date     93.10.06.18.01.18;  author jesup;  state Exp;
branches ;
next     1.2;

1.2
date     93.10.06.17.22.37;  author jesup;  state Exp;
branches ;
next     1.1;

1.1
date     93.10.06.16.57.44;  author jesup;  state Exp;
branches ;
next     ;


desc
@@


1.8
log
@I wish the installer was documented...

@
text
@
;
; Envoy HARD DISK Installation script
; Copyright (c) 1993, Commodore-Amiga, Inc.

; $Id: harddisk,v 1.7 93/12/23 20:05:41 jesup Exp Locker: jesup $
;
;  Make sure that we are running under 2.04 or better
;
(if (< (/ (getversion) 65536) 37)
        (abort "You must be using Kickstart 2.04 or higher to install and use Envoy.")
)
(if (exists "inet:libs/inet.library" (noreq))
        (if (< (/ (getversion "inet:libs/inet.library") 65536) 5.0)
                (abort
                        "If using AS225, you must be using inet.library 5.0"
                        "or higher to install Envoy."
                )
        )
)

(set serverMode FALSE)
(if     (= (expandpath (pathonly @@icon)) "SoftServe:Public/NewEnvoy")
        (
            (set serverMode TRUE)
            (makeassign "EnvoyInstall" "SoftServe:Public/NewEnvoy")
        )
)

(if     (= (expandpath (pathonly @@icon)) "SoftPublic:NewEnvoy")
        (
            (set serverMode TRUE)
            (makeassign "EnvoyInstall" "SoftPublic:NewEnvoy")
        )
)

;
;  Figure out where to put it if installing to a hard disk.
;
(run "setenv envoydev SYS:" (safe))
(run "envoyinstall:guessenvoydev >env:envoydev" (safe))
(set @@default-dest (getenv "envoydev"))

(set envoy_dest
        (tackon
                (askdir
                        (prompt
                                "\nIn which disk (or drawer) should the Envoy "
                                "drawer be installed or updated?"
                        )
                        (help @@askdir-help)
                        (default @@default-dest)
                )
                "Envoy"
        )
)
(set is_update 0)
(if (exists envoy_dest)
        (if (askbool
                (prompt
                        "\nDo you wish to update your current installation? "
                )
                (help
                        "\nThe Installer has determined that you may already "
                        "have Envoy installed on your "
                        "system. If this is incorrect or if you want to "
                        "completely re-install Envoy, select 'Re-Install.' "
                        "Otherwise, select 'Update.'"
                )
                (choices "Update" "Re-Install")
                (default 1)
            )
        (set is_update 1)       ; if user wants in same place
        )
)

(if (NOT (exists envoy_dest))
        (makedir envoy_dest
                (infos)
        )
)

;
;
;  Now that we know where it goes and whether we are overwriting it:
;
(if (NOT @@pretend)
        (set @@default-dest envoy_dest)
)
;
;  do the main copying.  Because pattern and all are mutually exclusive,
;  I have to copy each directory separately!!!  Copylib is used
;  for each library individually so that no smaller version is overwritten
;  by a higher version.  Last time I checked though, this feature was broken.
;
(copyfiles (source "EnvoyInstall:libs")
           (dest "libs:")
           (all)
)

(copyfiles (prompt "Copy Envoy Configuration Editors.")
           (source "EnvoyInstall:Configuration")
           (dest (tackon envoy_dest "Configuration"))
           (all)
           (infos)
           (help
                   "\nConfiguration editors for NIPC, Services, EFS"
           )
)

; Now comes the somthing really fun. Depending on which version of the
; OS the user has, we may or may not need to update some of the commands
; in the C: directory.

(copylib (prompt "Updating C: commands.")
         (source "EnvoyInstall:c/mount")
         (dest "C:")
)

(copylib
        (source "EnvoyInstall:C/protect")
        (dest "C:")
)

(copylib
        (source "EnvoyInstall:C/list")
        (dest "C:")
)

; needed to get the fix for V37/38/39 dos exall emulation (drops files at
; buffer boundaries).  I do hope copylib works...
; can't use the V40 setpatch under pre-V38 WB
;
(set n 0)
(if     (= WBVersion 37)
	(set n 1)
)
(copyfiles
	(source (select n       "EnvoyInstall:c/SetPatch"
				"EnvoyInstall:c/SetPatch.V37"
		)
	)
	(dest   "C:")
	(newname "SetPatch")
)

(copyfiles
        (source "EnvoyInstall:C/owner")
        (dest "C:")
)

(copyfiles
        (source "EnvoyInstall:C/group")
        (dest "C:")
)

(copylib
        (prompt "Installing the Envoy Network File System.")
        (source "EnvoyInstall:L/EnvoyFileSystem")
        (dest "L:")
)

(if (NOT (exists "env:Envoy"))
        (makedir "env:Envoy")
)

(if (NOT (exists "envarc:Envoy"))
        (makedir "envarc:Envoy")
)

(if (NOT (exists "devs:Networks"))
        (makedir "devs:Networks")
)

(copyfiles
        (prompt "Copy Envoy services.")
        (source "EnvoyInstall:Services")
        (dest (tackon envoy_dest "Services"))
        (all)
        (infos)
        (optional)
)
(copyfiles
        (source "EnvoyInstall:Accounts")
        (dest (tackon envoy_dest "Accounts"))
        (all)
        (infos)
)
(tooltype
        (dest (tackon envoy_dest "Services/Services Manager"))
        (settooltype "DONOTWAIT" "TRUE")
)
(tooltype
        (dest (tackon envoy_dest "Accounts/Accounts Manager"))
        (settooltype "UserDB" (tackon envoy_dest "Accounts/UserData"))
        (settooltype "GroupDB" (tackon envoy_dest "Accounts/GroupData"))
        (settooltype "DONOTWAIT" "TRUE")
)
(set svcs   (askoptions
                    (prompt         "Please select which Envoy services you would like "
                                    "activated when Services Manager is run. "
                    )
                    (choices        "Printer"
                                    "File System"
                    )
                    (default        2)
                    (help           "\nPrinter\n"
                                    "\n"
                                    "Select this option to enable this machine to export a printer "
                                    "to other machines on the network. Deselect this option "
                                    "to disable printer exporting.\n"
                                    "\n"
                                    "File System\n"
                                    "\n"
                                    "Select this option to enable this machine to export file systems "
                                    "to other machines on the network. Deselect this option "
                                    "to disable file exporting.\n")
            )
)
(textfile
        (dest   "T:servicesconfig.txt")
        (append (cat    "SERVICE \"Print Spooler\" "
                        (tackon envoy_dest "Services/printspool.service")
                )
        )
        (if (in svcs 0)
            (append (cat    " Active\n"
                    )
            )
            (append "\n")
        )
        (append (cat    "SERVICE \"Filesystem\" "
                        (tackon envoy_dest "Services/filesystem.service")
                )
        )
        (if (in svcs 1)
            (append (cat    " Active\n"
                    )
            )
            (append "\n")
        )
)
; if either is set copy services and accounts to wbstartup
(if (in svcs 0 1)
 (
        (copyfiles
                (source (tackon envoy_dest "Services/Services Manager"))
                (dest   "SYS:WBStartup")
                (infos)
        )
        (copyfiles
                (source (tackon envoy_dest "Accounts/Accounts Manager"))
                (dest   "SYS:WBStartup")
                (infos)
        )
 )
)
(run "EnvoyInstall:C/BuildSMPrefs t:servicesconfig.txt ENVARC:Envoy/Services.prefs")
(run "EnvoyInstall:C/BuildSMPrefs t:servicesconfig.txt ENV:Envoy/Services.prefs")
(set UserDBPath (tackon envoy_dest "Accounts/UserData"))
(set GroupDBPath (tackon envoy_dest "Accounts/GroupData"))

;
;   Need to delete old drivers located in devs: rather than devs:Networks.
;   Need to ask which SANA-II drivers to install
;   Note that this also copies mount examples to devs:DOSDrivers
;

(copylib
        (source "EnvoyInstall:DEVS/envoyprint.device")
        (dest "DEVS:")
)

(copylib
        (source "EnvoyInstall:DEVS/printer.device")
        (dest "DEVS:")
)

(copylib
        (source "EnvoyInstall:L/port-handler")
        (dest "L:")
)

(copylib (source "EnvoyInstall:Prefs/Printer")
        (dest "SYS:Prefs")
        (infos)
)

(if (= (/ (getversion) 65536) 37)
        (startup "NetworkPrinting"
                (prompt "Adding an ASSIGN to your S:User-startup.")
                (command
                        ("ASSIGN PRINTERS: DEVS:Printers")
                )
                (help
                        "\nIn order for network printing to work on Release 2.04 systems, "
                        "an assign must be made for PRINTERS:. "
                )
        )
)

(if is_update (exit))

(set NetworkType
        (askchoice
                (prompt "Please select what type of network configuration you are using." )
                (choices "Simple Network"
                         "Complex Network"
                )
                (help
                        "\nSimple Network\n"
                        "\n"
                        "Select this option if your Amiga will not be on a network that "
                        "is connected to the Internet.\n"
                        "\n"
                        "Complex Network\n"
                        "\n"
                        "Select this option if your Amiga will be "
                        "on a network that contains any network routers, or is "
                        "connected to the Internet."
                       "See your "
                        "Envoy documentation for more information on these options."
                )
        )
)

;  No Internet:
;
;  Hostname
;  Address (1-254)
;  SANA-II Device  (no need to support multiple devices if not internet)
;               (don't need subnet mask)

(if (= NetworkType 0)
        (
                (set HostNumber "137.239.1.")

                (set OwnerName
                        (askstring
                                (prompt "Please enter the name of this Amiga's Owner.")
                                (default "None")
                                (help
                                        "\nEnvoy allows you to enter the name of a person "
                                        "as the machine's Owner. You may leave this set to "
                                        "None if you wish."
                                )
                        )
                )
                (set HostName
                        (askstring
                                (prompt "Please enter a name for your Amiga.")
                                (default "Amiga")
                                (help
                                        "\nEach machine on a network must have a unique "
                                        "name and number. Machine names can have a maximum of "
                                        "31 characters, and may not contain a colon (:). "
                                )
                        )
                )
                (set HostNumber
                        (cat    HostNumber
                                (asknumber
                                        (prompt "Please enter a network address for your Amiga.")
                                        (default "1")
                                        (help
                                                "\nEach machine on a network must have a unique "
                                                "name and number. Machine numbers must be in the "
                                                "range of 1 to 254."
                                        )
                                        (range 1 254)
                                )
                        )
                )
                (set DeviceDriver
                        (askchoice
                                (prompt "Please select what type of network interface hardware you will be using.")
                                (choices        "Commodore Ethernet Board (A2065)"
                                                "Commodore ARCNET Board   (A2060 or A560)"
                                                "Other"
                                )
                                (default 0)
                                (help
                                        "\nCommodore Ethernet Board (A2065)\n"
                                        "\n"
                                        "Select this option if you will be using the Commodore A2065 "
                                        "Ethernet board with Envoy. This option can also be used if you will be using the "
                                        "Ameristar Ethernet board.\n"
                                        "\n"
                                        "Commodore ARCNET Board (A2060 or A560)\n"
                                        "\n"
                                        "Select this option if you will be using the Commodore A2060 "
                                        "or A560 ARCNET boards with Envoy.\n"
                                        "\n\n"
                                        "Other\n"
                                        "\n"
                                        "Select this option if you will be using a third-party network "
                                        "interface with Envoy. Please have the documentation supplied "
                                        "with your interface ready for the next few questions.\n"
                                        "\n"

                                )
                        )
                )
                (if (= DeviceDriver 2)
                        (
                                (message
                                       "\nIf you will be using a SANA-II device driver that has not yet been "
                                       "copied to DEVS:Networks, copy it there now manually. Come back to "
                                       "the Installer when you are done."
                                )
                                (set DeviceName
                                        (askfile
                                                (default "DEVS:Networks")
                                                (prompt "Please select the SANA-II driver for your network interface. ")
                                                (help
                                                        "\nEach network interface must have an associated software driver "
                                                        "to work with Envoy. This driver should be placed in DEVS:Networks "
                                                        "along with any other SANA-II device drivers. The documentation "
                                                        "supplied with your network interface should provide the "
                                                        "name of the device driver for the interface. "
                                                )
                                        )
                                )
                                (set IPType
                                        (asknumber
                                                (default 2048)
                                                (prompt "Please enter the packet type number for IP packets "
                                                        "for your network interface."
                                                )
                                                (help
                                                        "\nEnter the decimal number for the IP packet type used "
                                                        "with your network interface. Enter the value supplied by "
                                                        "the documentation for your network interface if the value isn't "
                                                        "listed below.\n"
                                                        "\n"
                                                        "Ethernet Interfaces:      2048\n"
                                                        "ARCNET Interfaces:        240\n"

                                                )
                                        )
                                )
                                (set ARPType
                                        (asknumber
                                                (default 2054)
                                                (prompt "Please enter the packet type number for ARP packets "
                                                        "for your network interface."
                                                )
                                                (help
                                                        "\nEnter the decimal number for the ARP packet type used "
                                                        "with your network interface. Enter the value supplied by "
                                                        "the documentation for your network interface if the value isn't "
                                                        "listed below.\n"
                                                        "\n"
                                                        "Ethernet Interfaces:      2054\n"
                                                        "ARCNET Interfaces:        241\n"

                                                )
                                        )
                                )
                        )
                )

                (if (= DeviceDriver 0)
                        (
                                (set DeviceName "DEVS:Networks/a2065.device")
                                (copylib
                                        (source "EnvoyInstall:DEVS/Networks/a2065.device")
                                        (dest   "DEVS:Networks")
                                )
                                (set IPType 2048)
                                (set ARPType 2054)
                        )
                        (if (= DeviceDriver 1)
                                (
                                        (set DeviceName "DEVS:Networks/a2060.device")
                                        (copylib
                                                (source "EnvoyInstall:DEVS/Networks/a2060.device")
                                                (dest   "DEVS:Networks")
                                        )
                                        (set IPType 240)
                                        (set ARPType 241)
                                )
                        )
                )
                (set HostInfo
                        (cat    "HOSTINFO "
                                HostName
                                " OWNER \""
                                OwnerName
                                "\"\n"
                        )
                )
                (set DevInfo
                        (cat    "DEVINFO "
                                DeviceName
                                " "
                                HostNumber
                                " "
                                ("%ld " IPType)
                                ("%ld\n" ARPType)

                        )
                )
                (textfile
                        (dest   "T:nipcconfig.txt")
                        (append HostInfo)
                        (append DevInfo)
                )
                (run "EnvoyInstall:C/BuildNIPCPrefs t:nipcconfig.txt ENVARC:Envoy/NIPC.prefs")
                (run "EnvoyInstall:C/BuildNIPCPrefs t:nipcconfig.txt ENV:Envoy/NIPC.prefs")
        )

;  Internet:
;
;  Hostname
;  Address (full IP number)
;  SANA-II Devices (with all the fixin's)
;  Realm name
;  Realm Server
;  Realms
;  Gateway config
;
        (
                (set HostNumber "137.239.1.")
                (set OwnerName
                        (askstring
                                (prompt "Please enter the name of this Amiga's Owner.")
                                (default "None")
                                (help
                                        "\nEnvoy allows you to enter the name of a person "
                                        "as the machine's Owner. You may leave this set to "
                                        "None if you wish."
                                )
                        )
                )
                (set HostName
                        (askstring
                                (prompt "Please enter a name for your Amiga.")
                                (default "Amiga")
                                (help
                                        "\nEach machine on a network must have a unique "
                                        "name and number. Machine names can have a maximum of "
                                        "31 characters, and may not contain a colon (:). "
                                )
                        )
                )
                (set HostIP
                        (askstring
                                (prompt "Please enter a network address for your Amiga.")
                                (default "137.239.1.1")
                                (help
                                        "\nEach machine on a network must have a unique "
                                        "network address. \n\nSee your "
                                        "Envoy documentation for more information, or contact "
                                        "your Network Administrator."
                                )
                        )
                )
                (if     (askbool
                                (prompt "Is your network using a default subnet mask?")
                                (default 0)
                                (help
                                        "\nSubnet masks are used to break a large network into smaller "
                                        "subnetworks. \n\nIf you are unsure whether subnet masks "
                                        "are in use, contact your Network Administrator, or see "
                                        "your Envoy documentation."
;**This help message needs to tell the user something about the consequences of this choice.
; Will our docs really be able to help them with this particular question?**
                                )
                        )
                        (set SubnetMask "none")
                        (set SubnetMask
                                (askstring
                                        (prompt "Please enter the subnet mask being used on your network.")
; ** If only one per network is possible, should be "...whether a subnet mask
; is in use..." in the preceding Help.  If more than one is possible, then
; this prompt needs further qualification. **
                                        (default "255.255.255.0")
                                        (help
                                                "\nThe subnet mask determines what portion of a network "
                                                "address is used to specify the network, and what portion "
                                                "is used to specify a host. \n\nFor more information, see "
                                                "your Envoy documentation, or contact "
                                                "your Network Administrator."

                                        )
                                )
                        )
                )
                (if     (askbool
                                (prompt "Do you want to select the network address for a "
                                        "default router?"
                                )
                                (default 1)
                                (help
                                        "\nRouters are machines that direct network traffic from one network "
                                        "to another. Usually each physical network has one router that "
                                        "is designated as the 'default' router. \n\nIf you are not sure "
                                        "whether you have a default router, contact your Network "
                                        "Administrator."
                                )
                        )
                        (set DefaultRouter
                                (askstring
                                        (prompt "Please enter the network address of the default router.")
                                        (help
                                                "\nAs with other hosts on a network, routers must also have "
                                                "a network address, usually one for each network to which they "
                                                "are connected. \n\nFor more information, see "
                                                "your Envoy documentation, or contact your Network Administrator."
; **The second part of the first sentence is confusing.  Is there one default
; router address, or several? **
                                        )
                                )
                        )
                        (set DefaultRouter "none")

                )
                (set RealmName
                        (askstring
                                (prompt "Please enter the name of your Amiga's Realm.")
                                (default "Realm1")
                                (help
                                        "\nAn Envoy Realm is a logical grouping of physical networks. "
                                        "Realms are typically used to organize large numbers of "
                                        "machines into manageable groups."
; ** This needs a little more: how about max. characters, other restrictions/suggestions, whether the
; realm must already exist, etc.  Also, Is it a grouping of networks, or of hosts?  What's the difference
; between a Realm and a subnet defined by a subnet mask?**
                                )
                        )
                )
                (set RealmIP
                        (askstring
                                (prompt "Please enter the network address of your Realm Server.")
                                (default "137.239.1.254")
                                (help
                                        "\nA Realm Server is a machine that knows about all of the Realms "
                                        "that exist on your network. Enter the network address of "
                                        "that machine. \n\nFor more information, see "
                                        "your Envoy documentation, or contact your Network Administrator."
                                )
                        )
                )
                (set DeviceDriver
                        (askchoice
                                (prompt "Please select what type of network interface you will be using.")
                                (choices        "Commodore Ethernet Board (A2065)"
                                                "Commodore ARCNET Board   (A2060)"
                                                "Other"
                                )
                                (default 0)
                                (help
                                        "\nCommodore Ethernet Board (A2065)\n"
                                        "\n"
                                        "Select this option if you will be using the Commodore A2065 "
                                        "Ethernet board with Envoy. This option can also be used if you will be using the "
                                        "Ameristar Ethernet board.\n"
                                        "\n"
                                        "Commodore ARCNET Board (A2060 or A560)\n"
                                        "\n"
                                        "Select this option if you will be using the Commodore A2060 "
                                        "or A560 ARCNET boards with Envoy.\n"
                                        "\n\n"
                                        "Other\n"
                                        "\n"
                                        "Select this option if you will be using a third-party network "
                                        "interface with Envoy. Please have the documentation supplied "
                                        "with your interface ready for the next few questions.\n"
                                        "\n"
                                )
                        )
                )
                (if (= DeviceDriver 2)
                        (
                                (set DeviceName
                                        (askfile
                                                (default "DEVS:Networks")
                                                (prompt "Please select the SANA-II driver for your network interface.")
                                                (help
                                                        "\nEach network interface must have an associated software driver "
                                                        "to work with Envoy. This driver should be placed in DEVS:Networks "
                                                        "along with any other SANA-II device drivers. \n\nThe documentation "
                                                        "supplied with your network interface should provide the "
                                                        "name of the device driver for the interface. "
                                                )
                                        )
                                )
                                (set IPType
                                        (asknumber
                                                (default 2048)
                                                (prompt "Please enter the packet type number for IP packets "
                                                        "for your network interface."
                                                )
                                                (help
                                                        "\nEnter the decimal number for the IP packet type used "
                                                        "with your network interface. Enter the value supplied by "
                                                        "the documentation for your network interface if the value isn't "
                                                        "listed below.\n"
                                                        "\n"
                                                        "Ethernet Interfaces:      2048\n"
                                                        "ARCNET Interfaces:        240\n"
                                                )
                                        )
                                )
                                (set ARPType
                                        (asknumber
                                                (default 2054)
                                                (prompt "Please enter the packet type number for ARP packets "
                                                        "for your network interface."
                                                )
                                                (help
                                                        "\nEnter the decimal number for the ARP packet type used "
                                                        "with your network interface. Enter the value supplied by "
                                                        "the documentation for your network interface if the value isn't "
                                                        "listed below.\n"
                                                        "\n"
                                                        "Ethernet Interfaces:      2054\n"
                                                        "ARCNET Interfaces:        241\n"
                                                )
                                        )
                                )
                        )
                )
                (if (= DeviceDriver 0)
                        (
                                (set DeviceName "DEVS:Networks/a2065.device")
                                (copylib
                                        (source "EnvoyInstall:DEVS/Networks/a2065.device")
                                        (dest   "DEVS:Networks")
                                )
                                (set IPType 2048)
                                (set ARPType 2054)
                        )
                        (if (= DeviceDriver 1)
                                (
                                        (set DeviceName "DEVS:Networks/a2060.device")
                                        (copylib
                                                (source "EnvoyInstall:DEVS/Networks/a2060.device")
                                                (dest   "DEVS:Networks")
                                        )
                                        (set IPType 240)
                                        (set ARPType 241)
                                )
                        )
                )
                (set HostInfo
                        (cat    "HOSTINFO "
                                HostName
                                " REALMNAME "
                                RealmName
                                " REALMIP "
                                RealmIP
                                " OWNER \""
                                OwnerName
                                "\"\n"
                        )
                )
                (set DevInfo
                        (cat    "DEVINFO "
                                DeviceName
                                " "
                                HostIP
                                " "
                                ("%ld " IPType)
                                ("%ld " ARPType)

                        )
                )
                (if (<> "none" SubnetMask)
                        (set DevInfo
                                (cat    DevInfo
                                        " SUBMASK "
                                        SubnetMask
                                )
                        )
                )
                (set DevInfo
                        (cat    Devinfo
                                "\n"
                        )
                )
                (textfile
                        (dest   "T:nipcconfig.txt")
                        (append HostInfo)
                        (append DevInfo)
                        (if (<> "none" DefaultRouter)
                                (append
                                        (cat    "ROUTEINFO "
                                                "0.0.0.0 "
                                                DefaultRouter
                                                " 1\n"
                                        )
                                )
                        )
                )
                (run "EnvoyInstall:C/BuildNIPCPrefs t:nipcconfig.txt ENVARC:Envoy/NIPC.prefs")
                (run "EnvoyInstall:C/BuildNIPCPrefs t:nipcconfig.txt ENV:Envoy/NIPC.prefs")
        )
)
(
    (run ("\"%s\" %s %s \"%s\" %s \"%s\" " (tackon envoy_dest "Accounts/Accounts Manager")
                              "InitDB"
                              "UserDB"
                              UserDBPath
                              "GroupDB"
                              GroupDBPath))

)
(copyfiles
        (source "EnvoyInstall:C/Reboot")
        (dest   "ram:")

)

(message "\nThe installation of Envoy is now complete.\n\n"
         "To enable Envoy, you must reboot your Amiga.\n\n"
         "Remove the \"Envoy Install\" disk from the floppy drive "
         "and select the \"Proceed\" gadget to reboot your Amiga."
)
(run "ram:Reboot")

@


1.7
log
@missing closing quote
@
text
@d6 1
a6 1
; $Id: harddisk,v 1.6 93/12/23 19:15:46 jesup Exp Locker: jesup $
d133 1
d138 2
a139 3
(copylib
	(source (select n
                                "EnvoyInstall:c/SetPatch"
d143 2
a144 1
	(dest   "C:SetPatch")
@


1.6
log
@added v37 setpatch to install scripts
@
text
@d6 1
a6 1
; $Id: harddisk,v 1.5 93/11/03 00:04:57 jesup Exp Locker: jesup $
d143 1
a143 1
	(dest   "C:SetPatch)
@


1.5
log
@added Setpatch, id string
@
text
@d6 1
a6 1
; $Id: $
d132 5
d138 6
a143 2
        (source "EnvoyInstall:C/SetPatch")
        (dest "C:")
@


1.4
log
@argh
@
text
@d6 1
d127 7
@


1.3
log
@*** empty log message ***
@
text
@d224 1
a224 1

d226 13
a238 12
	(if (BITAND svcs 3)
                (copyfiles
                        (source (tackon envoy_dest "Services/Services Manager"))
                        (dest   "SYS:WBStartup")
                        (infos)
                )
                (copyfiles
                        (source (tackon envoy_dest "Accounts/Accounts Manager"))
                        (dest   "SYS:WBStartup")
                        (infos)
                )
	)
@


1.2
log
@don't install services and accounts in wbstartup unless a service is
going to be started.
@
text
@a226 1
	 (
a236 1
	 )
@


1.1
log
@Initial revision
@
text
@d91 1
a91 1
;  I have to copy each freaking directory separately!!!  Copylib is used
d225 15
a245 10
(copyfiles
        (source (tackon envoy_dest "Services/Services Manager"))
        (dest   "SYS:WBStartup")
        (infos)
)
(copyfiles
        (source (tackon envoy_dest "Accounts/Accounts Manager"))
        (dest   "SYS:WBStartup")
        (infos)
)
@

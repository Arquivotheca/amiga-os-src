; VARIABLES:
;       envoy_dest   - where to copy it all to.  We default to inet:, if assigned
;                 (but only if assigned, not if a device has that name.)
;

;
;  Make sure that we are running under 2.04 or better
;
(if (< (/ (getversion) 65536) 37)
        (abort "You must be using Kickstart 2.04 or higher to install and use Envoy.")
)
(if (exists "inet:libs/inet.library" (noreq))
        (if (< (/ (getversion "inet:libs/inet.library") 65536) 5.0)
                (abort
                        "If using AS225, you must be using inet.library 5.0"
                        "or higher to install Envoy."
                )
        )
)

(set serverMode FALSE)
(if 	(= (expandpath (pathonly @icon)) "SoftServe:Public/NewEnvoy")
	(
            (set serverMode TRUE)
            (makeassign "EnvoyInstall" "SoftServe:Public/NewEnvoy")
	)
)

(if 	(= (expandpath (pathonly @icon)) "SoftPublic:NewEnvoy")
	(
            (set serverMode TRUE)
            (makeassign "EnvoyInstall" "SoftPublic:NewEnvoy")
	)
)

;
;  Figure out where to put it if installing to a hard disk.
;
(run "setenv envoydev SYS:" (safe))
(run "envoyinstall:guessenvoydev >env:envoydev" (safe))
(set @default-dest (getenv "envoydev"))

(set envoy_dest
        (tackon
                (askdir
                        (prompt
                                "\nIn which disk (or drawer) should the Envoy "
                                "drawer be installed (or updated)?"
                        )
                        (help @askdir-help)
                        (default @default-dest)
                )
                "Envoy"
        )
)
(set is_update 0)
(if (exists envoy_dest)
        (if (askbool
                (prompt
                        "\nDo you wish to update your current installation? "
                )
                (help
                        "\nThe installer has determined that you may already "
                        "have a copy of Envoy correctly installed on your "
                        "system.  If this is incorrect or if you want to "
                        "update from scratch select 'No' as an answer.  "
                        "Otherwise, select 'Yes.'"
                )
                (choices "Update" "Re-Install")
                (default 1)
            )
        (set is_update 1)       ; if user wants in same place
        )
)

(if (NOT (exists envoy_dest))
        (makedir envoy_dest
                (infos)
        )
)

;
;
;  Now that we know where it goes and whether we are overwriting it:
;
(if (NOT @pretend)
        (set @default-dest envoy_dest)
)
;
;  do the main copying.  Because pattern and all are mutually exclusive,
;  I have to copy each freaking directory separately!!!  Copylib is used
;  for each library individually so that no smaller version is overwritten
;  by a higher version.  Last time I checked though, this feature was broken.
;
(copyfiles (source "EnvoyInstall:libs")
           (dest "libs:")
           (all)
)

(copyfiles (prompt "Copy Envoy Configuration Editors.")
           (source "EnvoyInstall:Configuration")
           (dest (tackon envoy_dest "Configuration"))
           (all)
           (infos)
           (help
                   "\nConfiguration editors for NIPC, Services, EFS"
           )
)

; Now comes the somthing really fun. Depending on which version of the
; OS the user has, we may or may not need to update some of the commands
; in the C: directory.

(copylib (prompt "Updating C: commands.")
         (source "EnvoyInstall:c/mount")
         (dest "C:")
)

(copylib
        (source "EnvoyInstall:C/protect")
        (dest "C:")
)

(copylib
        (source "EnvoyInstall:C/list")
        (dest "C:")
)

(copyfiles
        (source "EnvoyInstall:C/owner")
        (dest "C:")
)

(copyfiles
        (source "EnvoyInstall:C/group")
        (dest "C:")
)

(copylib
        (prompt "Installing the Envoy Network File System.")
        (source "EnvoyInstall:L/EnvoyFileSystem")
        (dest "L:")
)

(if (NOT (exists "env:Envoy"))
        (makedir "env:Envoy")
)

(if (NOT (exists "envarc:Envoy"))
        (makedir "envarc:Envoy")
)

(if (NOT (exists "devs:Networks"))
        (makedir "devs:Networks")
)

(copyfiles
        (prompt "Copy Envoy services.")
        (source "EnvoyInstall:Services")
        (dest (tackon envoy_dest "Services"))
        (all)
        (infos)
        (optional)
)
(copyfiles
        (source "EnvoyInstall:Accounts")
        (dest (tackon envoy_dest "Accounts"))
        (all)
        (infos)
)
(tooltype
        (dest (tackon envoy_dest "Services/Services Manager"))
        (settooltype "DONOTWAIT" "TRUE")
)
(tooltype
        (dest (tackon envoy_dest "Accounts/Accounts Manager"))
        (settooltype "UserDB" (tackon envoy_dest "Accounts/UserData"))
        (settooltype "GroupDB" (tackon envoy_dest "Accounts/GroupData"))
        (settooltype "DONOTWAIT" "TRUE")
)
(set svcs   (askoptions
                    (prompt         "Please Select which Envoy Services that you would like "
                                    "to be activated when Services Manager is run. "
                    )
                    (choices        "Printer"
                                    "File System"
                    )
                    (default        2)
                    (help           "Printer\n"
                                    "\n"
                                    "Leave this option checked if you plan on exporting a printer "
                                    "to other machines on the network.  If not, you may click on "
                                    "this button to disable printer exporting.\n"
                                    "\n"
                                    "File System\n"
                                    "Leave this option checked if you plan on exporting file systems"
                                    "to other machines on the network.  If not, you may click on "
                                    "this button to disable file sharing.\n")
            )
)
(textfile
        (dest   "T:servicesconfig.txt")
        (append (cat	"SERVICE \"Print Spooler\" "
        		(tackon envoy_dest "Services/printspool.service")
        	)
        )
        (if (in svcs 0)
            (append (cat    " Active\n"
                    )
            )
            (append "\n")
        )
        (append (cat	"SERVICE \"Filesystem\" "
        		(tackon envoy_dest "Services/filesystem.service")
        	)
        )
        (if (in svcs 1)
            (append (cat    " Active\n"
                    )
            )
            (append "\n")
        )

)
(run "EnvoyInstall:C/BuildSMPrefs t:servicesconfig.txt ENVARC:Envoy/Services.prefs")
(run "EnvoyInstall:C/BuildSMPrefs t:servicesconfig.txt ENV:Envoy/Services.prefs")
(set UserDBPath (tackon envoy_dest "Accounts/UserData"))
(set GroupDBPath (tackon envoy_dest "Accounts/GroupData"))

(copyfiles
        (source (tackon envoy_dest "Services/Services Manager"))
        (dest   "SYS:WBStartup")
        (infos)
)
(copyfiles
        (source (tackon envoy_dest "Accounts/Accounts Manager"))
        (dest   "SYS:WBStartup")
        (infos)
)
;
;   Need to delete old drivers located in devs: rather than devs:Networks.
;   Need to ask which SANA-II drivers to install
;   Note that this also copies mount examples to devs:DOSDrivers
;

(copylib
        (source "EnvoyInstall:DEVS/envoyprint.device")
        (dest "DEVS:")
)

(copylib
        (source "EnvoyInstall:DEVS/printer.device")
        (dest "DEVS:")
)

(copylib
	(source "EnvoyInstall:L/port-handler")
	(dest "L:")
)

(copylib (source "EnvoyInstall:Prefs/Printer")
	(dest "SYS:Prefs")
	(infos)
)

(if (= (/ (getversion) 65536) 37)
	(startup "NetworkPrinting"
		(prompt "Adding an ASSIGN to your user-startup.")
		(command
			("ASSIGN PRINTERS: DEVS:Printers")
		)
		(help
			"\nIn order for network printing to work on V37 systems,"
			"an assign must be made for PRINTERS:. "
		)
	)
)

(if is_update (exit))

(set NetworkType
        (askchoice
                (prompt "Please Select what type of network configuration you are using." )
                (choices "Simple Network"
                         "Complex"
                )
                (help
                        "\nSimple Network\n"
                        "\n"
                        "Select this option if your Amiga will not be on a network that"
                        "is connected to the Internet.  Please see Chapter Two of the Envoy "
                        "User's Manual for more information regarding this option.\n"
                        "\n"
                        "Complex"
                        "\n"
                        "Select this option if the Amigas that will be using Envoy will be "
                        "on a network that has more than section.  Please see Chapter Seven"
                        "of the Envoy User's Manual for more information regarding this option."
                )
        )
)

;  No Internet:
;
;  Hostname
;  Address (1-254)
;  SANA-II Device  (no need to support multiple devices if not internet)
;               (don't need subnet mask)

(if (= NetworkType 0)
        (
                (set HostNumber "137.239.1.")

		(set OwnerName
			(askstring
				(prompt "Please Enter the name of this Amiga's owner.")
				(default "None")
				(help
					"\nEnvoy allows you to enter the name of the person "
					"that owns the machine.  You may leave this set to "
					"None if you wish."
				)
			)
		)
                (set HostName
                        (askstring
                                (prompt "Please Enter a name for your Amiga.")
                                (default "Amiga")
                                (help
                                        "\nEach machine on a network must have a unique "
                                        "name and number.  Machine names are limited to "
                                        "31 characters or less, and may not contain a ':' "
                                        "character. "
                                )
                        )
                )
                (set HostNumber
                        (cat    HostNumber
                                (asknumber
                                        (prompt "Please enter a network address for your Amiga.")
                                        (default "1")
                                        (help
                                                "\nEach machine on a network must have a unique "
                                                "name and number.  Machine numbers must be in the "
                                                "range of 1 to 254."
                                        )
                                        (range 1 254)
                                )
                        )
                )
                (set DeviceDriver
                        (askchoice
                                (prompt "Please Select what type of Network Interface you will be using")
                                (choices        "Commodore Ethernet Card (A2065)"
                                                "Commodore Arcnet Card   (A2060 or A560)"
                                                "Other"
                                )
                                (default 0)
                                (help
                                        "\nCommodore Ethernet Card (A2065)\n"
                                        "\n"
                                        "Select this option if you will be using the Commodore A2065 "
                                        "Ethernet card with Envoy.  (See *Note* below.)\n"
                                        "\n"
                                        "Commodore Arcnet Card (A2060 or A560)\n"
                                        "\n"
                                        "Select this option if you will be using the Commodore A2060 "
                                        "or the Commodore A560 Arcnet card with Envoy.\n"
                                        "\n"
                                        "Other\n"
                                        "\n"
                                        "Select this option if you will be using a third-party network "
                                        "interface with Envoy.  Please have the documentation supplied "
                                        "with your interface handy for the next couple of questions.\n"
                                        "\n"
                                        "*Note: This option may also be used if you will be using one "
                                        "of the original Ameristar Ethernet cards.\n"

                                )
                        )
                )
                (if (= DeviceDriver 2)
                        (
                                (message
                                       "\nIf you will be using a SANA-II device which has not yet been "
                                       "copied to DEVS:Networks/ yet, copy it there now manually.  Come back to "
                                       "installer when you are done."
                                )
                                (set DeviceName
                                        (askfile
                                                (default "DEVS:Networks")
                                                (prompt "Please select the SANA-II driver for your network interface. ")
                                                (help
                                                        "\nEach network interface must have an associated software driver "
                                                        "to work with Envoy.  This driver should be placed in DEVS:Networks "
                                                        "along with other SANA-II device drivers.  The documentation "
                                                        "supplied with your network interface should supply you with the "
                                                        "name of the device driver for your network interface. "
                                                )
                                        )
                                )
                                (set IPType
                                        (asknumber
                                                (default 2048)
                                                (prompt "Please enter the packet type number for IP packets "
                                                        "for your Network Interface."
                                                )
                                                (help
                                                        "\nPlease enter the decimal number for the IP packet type used "
                                                        "with your network interface.  This value should be supplied with "
                                                        "the documentaion for your network interface if the value isn't "
                                                        "listed in the table below.\n"
                                                        "\n"
                                                        "Ethernet Interfaces:	2048\n"
                                                        "Arcnet Interfaces:	 240\n"

                                                )
                                        )
                                )
                                (set ARPType
                                        (asknumber
                                                (default 2054)
                                                (prompt "Please enter the packet type number for ARP packets "
                                                        "for your Network Interface."
                                                )
                                                (help
                                                        "\nPlease enter the decimal number for the ARP packet type used "
                                                        "with your network interface.  This value should be supplied with "
                                                        "the documentation for your network interface if the value isn't "
                                                        "listed in the table below.\n"
                                                        "\n"
                                                        "Ethernet Interfaces:	2054\n"
                                                        "Arcnet Interfaces:	 241\n"

                                                )
                                        )
                                )
                        )
                )

                (if (= DeviceDriver 0)
                        (
                                (set DeviceName "DEVS:Networks/a2065.device")
                                (copylib
                                        (source "EnvoyInstall:DEVS/Networks/a2065.device")
                                        (dest   "DEVS:Networks")
                                )
                                (set IPType 2048)
                                (set ARPType 2054)
                        )
                        (if (= DeviceDriver 1)
                                (
                                        (set DeviceName "DEVS:Networks/a2060.device")
                                        (copylib
                                                (source "EnvoyInstall:DEVS/Networks/a2060.device")
                                                (dest   "DEVS:Networks")
                                        )
                                        (set IPType 240)
                                        (set ARPType 241)
                                )
                        )
                )
                (set HostInfo
                        (cat    "HOSTINFO "
                                HostName
                                " OWNER \""
                                OwnerName
                                "\"\n"
                        )
                )
                (set DevInfo
                        (cat    "DEVINFO "
                                DeviceName
                                " "
                                HostNumber
                                " "
                                ("%ld " IPType)
                                ("%ld\n" ARPType)

                        )
                )
                (textfile
                        (dest   "T:nipcconfig.txt")
                        (append HostInfo)
                        (append DevInfo)
                )
                (run "EnvoyInstall:C/BuildNIPCPrefs t:nipcconfig.txt ENVARC:Envoy/NIPC.prefs")
                (run "EnvoyInstall:C/BuildNIPCPrefs t:nipcconfig.txt ENV:Envoy/NIPC.prefs")
        )

;  Internet:
;
;  Hostname
;  Address (full IP number)
;  SANA-II Devices (with all the fixin's)
;  Realm name
;  Realm Server
;  Realms
;  Gateway config
;
        (
                (set HostNumber "137.239.1.")
		(set OwnerName
			(askstring
				(prompt "Please Enter the name of this Amiga's owner.")
				(default "None")
				(help
					"\nEnvoy allows you to enter the name of the person "
					"that owns the machine.  You may leave this set to "
					"None if you wish."
                                )
                        )
                )
                (set HostName
                        (askstring
                                (prompt "Please Enter a name for your Amiga.")
                                (default "Amiga")
                                (help
                                        "\nEach machine on a network must have a unique "
                                        "name and number.  Machine names are limited to"
                                        "31 characters or less, and may not contain a ':' "
                                        "character. "
                                )
                        )
                )
                (set HostIP
                        (askstring
                                (prompt "Please enter a network address for your Amiga.")
                                (default "137.239.1.1")
                                (help
                                        "\nEach machine on a network must have a unique "
                                        "network address.  Please consult Chapter Six of the "
                                        "Envoy User's Manual for more information, or contact "
                                        "your Network Administrator."
                                )
                        )
                )
                (if     (askbool
                                (prompt "Is your network using a default subnet mask?")
                                (default 0)
                                (help
                                        "\nSubnet masks are used to break a large network into smaller"
                                        "networks.  If you are unsure of whether or not subnet masks"
                                        "are in use, please contact your Network Administrator, or consult "
                                        "Chapter Six of the Envoy User's Manual."
                                )
                        )
                        (set SubnetMask "none")
                        (set SubnetMask
                                (askstring
                                        (prompt "Please enter the subnet mask being used on your network.")
                                        (default "255.255.255.0")
                                        (help
                                                "\nThe subnet mask determines what portion of a network "
                                                "address is used to specify the network, and which portion "
                                                "is used to specify a host.  For more information, please "
                                                "consult Chapter Six of the Envoy User's Manual, or contact "
                                                "your Network Administrator."

                                        )
                                )
                        )
                )
                (if     (askbool
                                (prompt "Would you like to select the network address for a "
                                        "default router?"
                                )
                                (default 1)
                                (help
                                        "\nRouters are machines that transfer packets from one network "
                                        "to another.  Usually each physical network has one router that "
                                        "is designated as the 'default' router.  If you are not sure "
                                        "if you have a default router, please contact your Network "
                                        "Administrator."
                                )
                        )
                        (set DefaultRouter
                                (askstring
                                        (prompt "Please enter the network address of the default router.")
                                        (help
                                                "\nAs with other hosts on a network, routers must also have "
                                                "a network address, usually one for each network that they "
                                                "are connected to.  For more information, please consult Chapter "
                                                "Six of the Envoy User's Manual, or contact your Network Administrator."
                                        )
                                )
                        )
                        (set DefaultRouter "none")

                )
                (set RealmName
                        (askstring
                                (prompt "Please Enter the name of the Realm that your Amiga is in.")
                                (default "Realm1")
                                (help
                                        "\nAn Envoy realm is a logical grouping of physical networks. "
                                        "Realms are typically used to organize large numbers of "
                                        "machines into reasonable sized groups."
                                )
                        )
                )
                (set RealmIP
                        (askstring
                                (prompt "Please enter the network address of your Realm Server.")
                                (default "137.239.1.254")
                                (help
                                        "\nA Realm Server is a machine that knows about all of the Realms "
                                        "being used on your network.  Please enter the network address of "
                                        "that machine.  For more information, please consult Chapter Six "
                                        "of the Envoy User's Manual, or contact your Network Administrator."
                                )
                        )
                )
                (set DeviceDriver
                        (askchoice
                                (prompt "Please Select what type of Network Interface you will be using")
                                (choices        "Commodore Ethernet Card (A2065)"
                                                "Commodore Arcnet Card   (A2060)"
                                                "Other"
                                )
                                (default 0)
                                (help
                                        "\nCommodore Ethernet Card (A2065)\n"
                                        "\n"
                                        "Select this option if you will be using the Commodore A2065 "
                                        "Ethernet card with Envoy.  (See *Note* below.)\n"
                                        "\n"
                                        "Commodore Arcnet Card (A2060 or A560)\n"
                                        "\n"
                                        "Select this option if you will be using the Commodore A2060 "
                                        "or the Commodore A560 Arcnet card with Envoy.\n"
                                        "\n"
                                        "Other\n"
                                        "\n"
                                        "Select this option if you will be using a third-party network "
                                        "interface with Envoy.  Please have the documentation supplied "
                                        "with your interface handy for the next couple of questions.\n"
                                        "\n"
                                        "*Note: This option may also be used if you will be using one "
                                        "of the original Ameristar Ethernet cards.\n"
                                )
                        )
                )
                (if (= DeviceDriver 2)
                        (
                                (set DeviceName
                                        (askfile
                                                (default "DEVS:Networks")
                                                (prompt "Please select the SANA-II driver for your network interface.")
                                                (help
                                                        "\nEach network interface must have an associated software driver "
                                                        "to work with Envoy.  This driver should be placed in DEVS:Networks "
                                                        "along with other SANA-II device drivers.  The documentation "
                                                        "supplied with your network interface should supply you with the "
                                                        "name of the device driver for your network interface. "
                                                )
                                        )
                                )
                                (set IPType
                                        (asknumber
                                                (default 2048)
                                                (prompt "Please enter the packet type number for IP packets"
                                                        "for your Network Interface."
                                                )
                                                (help
                                                        "\nPlease enter the decimal number for the IP packet type used "
                                                        "with your network interface.  This value should be supplied with "
                                                        "the documentaion for your network interface if the value isn't "
                                                        "listed in the table below.\n"
                                                        "\n"
                                                        "Ethernet Interfaces:	2048\n"
                                                        "Arcnet Interfaces:	 240\n"
                                                )
                                        )
                                )
                                (set ARPType
                                        (asknumber
                                                (default 2054)
                                                (prompt "Please enter the packet type number for ARP packets"
                                                        "for your Network Interface."
                                                )
                                                (help
                                                        "\nPlease enter the decimal number for the ARP packet type used "
                                                        "with your network interface.  This value should be supplied with "
                                                        "the documentation for your network interface if the value isn't "
                                                        "listed in the table below.\n"
                                                        "\n"
                                                        "Ethernet Interfaces:	2054\n"
                                                        "Arcnet Interfaces:	 241\n"
                                                )
                                        )
                                )
                        )
                )
                (if (= DeviceDriver 0)
                        (
                                (set DeviceName "DEVS:Networks/a2065.device")
                                (copylib
                                        (source "EnvoyInstall:DEVS/Networks/a2065.device")
                                        (dest   "DEVS:Networks")
                                )
                                (set IPType 2048)
                                (set ARPType 2054)
                        )
                        (if (= DeviceDriver 1)
                                (
                                        (set DeviceName "DEVS:Networks/a2060.device")
                                        (copylib
                                                (source "EnvoyInstall:DEVS/Networks/a2060.device")
                                                (dest   "DEVS:Networks")
                                        )
                                        (set IPType 240)
                                        (set ARPType 241)
                                )
                        )
                )
                (set HostInfo
                        (cat    "HOSTINFO "
                                HostName
                                " REALMNAME "
                                RealmName
                                " REALMIP "
                                RealmIP
                                " OWNER \""
                                OwnerName
                                "\"\n"
                        )
                )
                (set DevInfo
                        (cat    "DEVINFO "
                                DeviceName
                                " "
                                HostIP
                                " "
                                ("%ld " IPType)
                                ("%ld " ARPType)

                        )
                )
                (if (<> "none" SubnetMask)
                        (set DevInfo
                                (cat    DevInfo
                                        " SUBMASK "
                                        SubnetMask
                                )
                        )
                )
                (set DevInfo
                        (cat    Devinfo
                                "\n"
                        )
                )
                (textfile
                        (dest   "T:nipcconfig.txt")
                        (append HostInfo)
                        (append DevInfo)
                        (if (<> "none" DefaultRouter)
                                (append
                                        (cat    "ROUTEINFO "
                                                "0.0.0.0 "
                                                DefaultRouter
                                                " 1\n"
                                        )
                                )
                        )
                )
                (run "EnvoyInstall:C/BuildNIPCPrefs t:nipcconfig.txt ENVARC:Envoy/NIPC.prefs")
                (run "EnvoyInstall:C/BuildNIPCPrefs t:nipcconfig.txt ENV:Envoy/NIPC.prefs")
        )
)
(
    (run ("\"%s\" %s %s \"%s\" %s \"%s\" " (tackon envoy_dest "Accounts/Accounts Manager")
                              "InitDB"
                              "UserDB"
                              UserDBPath
                              "GroupDB"
                              GroupDBPath))

)
(copyfiles
	(source "EnvoyInstall:C/Reboot")
	(dest	"ram:")

)

(message "\nThe installation of Envoy is now complete.\n\n"
     	 "To enable Envoy, you must reboot your Amiga.\n\n"
         "Remove the \"Envoy Install\" disk from the floppy drive, "
         "and select the \"Proceed\" gadget to reboot your Amiga."
)
(run "ram:Reboot")

.key PATTERN/k,QUIET/s,opt1,opt2,opt3,opt4
.bra {
.ket }
IF NOT {QUIET} ;env:RefreshQuiet
	echo "Refresh overwrites all files in the current directory." NOLINE
	ask  "  Continue (Y/N)?"
	IF NOT WARN
		QUIT
	ENDIF
ENDIF
failat 21
IF EXISTS RCS_link
	copy RCS_link env:RefreshTemp QUIET
ELSE
	echo "No RCS_link!  This better not be source which belongs on HOG!"
	IF EXISTS RCS
		echo >env:RefreshTemp RCS/
	ELSE
		echo "No RCS_link *and* no RCS directory.  I quit."
		quit
	ENDIF
ENDIF
list >t:q{$$} $RefreshTemp{PATTERN$~(#?.info|.#?)} lformat="co {opt1$} {opt2} {opt3} {opt4} %s%s"
IF NOT FAIL
execute t:q{$$}
ELSE
echo "Uh-oh, can't find: '$RefreshTemp{PATTERN$~(#?.info|.#?)}'"
ENDIF
delete env:RefreshTemp QUIET
failat 10
;Refresh all files in current dir from the server.
;use -sStab to get the most recent stable versions.
;if you get a warning about a writeable file, something is wrong.  Use
;"rlog filename" on the file with the error -- you've probably got a
;lock on that file, but you might have set locking to non-strict or done
;something else naughty.
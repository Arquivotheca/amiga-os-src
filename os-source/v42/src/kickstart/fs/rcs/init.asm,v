head     1.29;
branch   ;
access   ;
symbols  ;
locks    ; strict;
comment  @@;


1.29
date     94.03.02.14.46.15;  author jesup;  state Exp;
branches ;
next     1.28;

1.28
date     93.02.03.21.48.48;  author jesup;  state Exp;
branches ;
next     1.27;

1.27
date     92.08.25.00.57.24;  author jesup;  state Exp;
branches ;
next     1.26;

1.26
date     92.08.03.00.50.47;  author jesup;  state Exp;
branches ;
next     1.25;

1.25
date     92.04.09.01.47.45;  author jesup;  state Exp;
branches ;
next     1.24;

1.24
date     91.04.25.06.06.59;  author steveb;  state Exp;
branches ;
next     1.23;

1.23
date     91.04.20.06.11.34;  author steveb;  state Exp;
branches ;
next     1.22;

1.22
date     91.04.18.10.04.25;  author steveb;  state Exp;
branches ;
next     1.21;

1.21
date     91.04.04.06.40.02;  author steveb;  state Exp;
branches ;
next     1.20;

1.20
date     91.02.01.11.20.56;  author steveb;  state Exp;
branches ;
next     1.19;

1.19
date     91.01.17.14.31.46;  author steveb;  state Exp;
branches ;
next     1.18;

1.18
date     90.12.14.09.15.48;  author steveb;  state Exp;
branches ;
next     1.17;

1.17
date     90.11.14.12.30.18;  author steveb;  state Exp;
branches ;
next     1.16;

1.16
date     90.09.20.17.40.40;  author steveb;  state Exp;
branches ;
next     1.15;

1.15
date     90.06.05.13.04.26;  author steveb;  state Exp;
branches ;
next     1.14;

1.14
date     90.04.06.15.19.12;  author steveb;  state Exp;
branches ;
next     1.13;

1.13
date     89.12.08.16.27.09;  author steveb;  state Exp;
branches ;
next     1.12;

1.12
date     89.12.08.01.06.27;  author steveb;  state Exp;
branches ;
next     1.11;

1.11
date     89.12.06.10.55.26;  author steveb;  state Exp;
branches ;
next     1.10;

1.10
date     89.10.05.11.58.01;  author steveb;  state Exp;
branches ;
next     1.9;

1.9
date     89.07.10.17.00.29;  author steveb;  state Exp;
branches ;
next     1.8;

1.8
date     89.07.07.15.44.59;  author steveb;  state Exp;
branches ;
next     1.7;

1.7
date     89.07.07.15.36.39;  author steveb;  state Exp;
branches ;
next     1.6;

1.6
date     89.07.07.10.34.31;  author steveb;  state Exp;
branches ;
next     1.5;

1.5
date     89.07.07.09.48.24;  author steveb;  state Exp;
branches ;
next     1.4;

1.4
date     89.05.08.12.04.06;  author steveb;  state Exp;
branches ;
next     1.3;

1.3
date     89.04.17.09.01.10;  author steveb;  state Exp;
branches ;
next     1.2;

1.2
date     89.03.13.17.00.20;  author steveb;  state Exp;
branches ;
next     1.1;

1.1
date     89.03.13.16.46.46;  author steveb;  state Exp;
branches ;
next     ;


desc
@1.3.1
@


1.29
log
@Use utility lib for divide
also, make bitmapcount and blocksperbm longs
@
text
@		SECTION	filesystem,CODE

		NOLIST
		INCLUDE	"exec/types.i"
		INCLUDE	"exec/io.i"
		INCLUDE	"exec/memory.i"
		INCLUDE	"exec/interrupts.i"
		INCLUDE	"devices/timer.i"
		INCLUDE	"devices/inputevent.i"
		INCLUDE	"libraries/dosextens.i"
		INCLUDE	"moredos.i"
		INCLUDE	"globals.i"
		INCLUDE	"private.i"
		INCLUDE	"printf.mac"
		LIST

;		DEBUGENABLE

SPARESTACK EQU 2048	use a different stack for opening the device

		XDEF	Init,GetBuffers,GeometryInit
		XDEF	myUDivMod32

		XREF	_LVOFreeMem,_LVOAllocMem,_LVOOpenDevice
		XREF	_LVOOpenLibrary,_LVOCloseLibrary
		XREF	_LVOAllocSignal,_LVOFreeSignal,_LVOAddPort
		XREF	GetPubMem,_LVOCopyMem,_LVOCloseDevice
		XREF	InitCo,CreateCo,CallCo,DeleteCo,Restart
		XREF	CompString
		XREF	_LVOUDivMod32

		XREF	Disk,Kill		;co-routines

;==============================================================================
; Success = Init(packet),_AbsExecBase
;   d0		   a0	      a6
;
; Initialises globals, allocates required memory and makes filesystem public
; if nothing failed during the initialisation.  Global memory is assumed to
; start with 0 (FALSE) in all slots.
;==============================================================================
Init		movem.l	a2-a4,-(sp)
		movea.l	dp_Arg2(a0),a2		device specific info
		adda.l	a2,a2			it was a BPTR
		adda.l	a2,a2
		movea.l	fs_Env(a2),a3		environment vector
		adda.l	a3,a3			it was a BPTR too
		adda.l	a3,a3
		move.l	a3,CurrentEnv(a5)	stash for Restart
		movea.l	dp_Arg3(a0),a4		Devlist entry
		adda.l	a4,a4			another goddamn BPTR!!
		adda.l	a4,a4
		move.l	a4,DevEntry(a5)		save devicenode entry

; set up global information about the device we are going to be using.  Watch
; out for the device name, it's a NULL terminated BCPL string, yuck!
		move.l	fs_Unit(a2),UnitNumber(a5)
		move.l	fs_Flags(a2),OpenFlags(a5)
		move.l	fs_DevName(a2),DeviceName(a5)	**** BSTR here ****

		moveq.l	#TRUE,d0		flag timer and disk not open
		move.l	d0,TimerOpened(a5)
		move.l	d0,DiskOpened(a5)

; initialise all of the cache buffer list headers to be empty
		lea.l	ValidBufferQueue(a5),a0
		NEWLIST	a0
		lea.l	PendingQueue(a5),a0
		NEWLIST	a0
		lea.l	WaitingQueue(a5),a0
		NEWLIST	a0
; and the dir semaphore list
		lea.l	DirSemList(a5),a0
		NEWLIST	a0

; get all the memory we are going to need and store pointers in relevant places
		move.l	a4,-(sp)		stash devicelist pointer
		lea.l	MemRequired(pc),a4
10$		moveq.l	#0,d0
		move.w	(a4)+,d0		get size
		beq.s	GotMemOK		last entry in table
		bsr	GetPubMem		get clear public memory
		move.w	(a4)+,d1		get offset to store at
		move.l	d0,0(a5,d1.w)		and store mem pointer
		bne.s	10$			no problems, get next
		addq.l	#4,sp			error, scrap value on stack
		bra	InitFail		and free whatever we did get
GotMemOK	move.l	(sp)+,a4		restore devlist pointer

	printf <'EnvecSize       = %ld\n'>,EnvecSize(a3)
	printf <'SectorSize      = %ld\n'>,SectorSize(a3)
	printf <'SectorOrigin    = %ld\n'>,SectorOrigin(a3)
	printf <'Surfaces        = %ld\n'>,Surfaces(a3)
	printf <'SectorsPerBlock = %ld\n'>,SectorsPerBlock(a3)
	printf <'BlocksPerTrack  = %ld\n'>,BlocksPerTrack(a3)
	printf <'ReservedBlocks  = %ld\n'>,ReservedBlocks(a3)
	printf <'PreAllocated    = %ld\n'>,PreAllocated(a3)
	printf <'Interleave      = %ld\n'>,Interleave(a3)
	printf <'LowerCylinder   = %ld\n'>,LowerCylinder(a3)
	printf <'UpperCylinder   = %ld\n'>,UpperCylinder(a3)
	printf <'Buffers         = %ld\n'>,Buffers(a3)
	printf <'BuffMemType     = %ld\n'>,BuffMemType(a3)
	printf <'MaxTransfer     = %ld\n'>,MaxTransfer(a3)
	printf <'AddressMask     = %lx\n'>,AddressMask(a3)

; fail the initialisation if this isn't a DOSType that the fs supports
		moveq.l	#16,d0			size is # longwords
		cmp.l	EnvecSize(a3),d0	see if there is a DOStype entry
		bgt.s	NoDOSType

		move.l	dostype(a3),d0
		clr.b	d0
		cmpi.l	#'DOS'<<8,d0
		bne	InitFail

NoDOSType:
		; must open utility before calling GeometryInit!
		lea.l	UtilLibName(pc),a1	open utility library
		moveq.l	#0,d0			any version will do
		jsr	_LVOOpenLibrary(a6)
		move.l	d0,UtilityBase(a5)
		beq	InitFail		didn't get it

		bsr	GeometryInit
		tst.l	d0
		beq	InitFail

; this fixes a strange bug.  If both the filesystem and the device are loaded
; from disk, a lot of stack get's eaten up.  Switch to a new stack while we
; open the device we are going to be using.  If we don't get the memory then
; fail this packet, it's probably going to fail soon anyway.
	IFD SPARESTACK
		move.l	#SPARESTACK,d0
		moveq.l	#MEMF_PUBLIC,d1
		jsr	_LVOAllocMem(a6)
		tst.l	d0
		beq	InitFail
		exg.l	d0,sp			switch to new stack
		lea.l	2048(sp),sp		point to the end
		move.l	d0,-(sp)		save old stack
	ENDC

		move.l	DeviceName(a5),d0	open the correct device
		lsl.l	#2,d0			remember its null terminated
		addq.l	#1,d0			but still a BSTR
		movea.l	d0,a0
		movea.l	DiskDev(a5),a1
		move.l	UnitNumber(a5),d0
		move.l	OpenFlags(a5),d1
	printf <'OpenDevice("%s,%ld,%ld) ... '>,a0,d0,d1
		jsr	_LVOOpenDevice(a6)
	printf <'returned %ld\n'>,d0
		move.l	d0,DiskOpened(a5)	save the disk opened flag

	IFD SPARESTACK
;free up the temp stack we used and switch back to the old one (end of bug fix)
		move.l	(sp)+,d0		get old stack pointer
		lea.l	-2048(sp),a1		beginning of temp stack
		move.l	d0,sp			switch back to old stack
		move.l	#SPARESTACK,d0
		jsr	_LVOFreeMem(a6)		free temp stack
		tst.l	DiskOpened(a5)		did everything work ?
	ENDC

		bne	InitFail		nope, clean up and exit

		movea.l	DiskDev(a5),a0		copy the structure
		move.l	ThisDevProc(a5),MN_REPLYPORT(a0)  reply to us
		movea.l	DiskExtraDev(a5),a1
		moveq.l	#IOSTD_SIZE,d0
		jsr	_LVOCopyMem(a6)
		movea.l	DiskExtraDev(a5),a0	replies to a different port
		move.l	DoIOPort(a5),MN_REPLYPORT(a0)

; set up the global flag to determine if we are using trackdisk.device so we
; will know if it's safe to use the TD_GETGEOMETRY command.  Many other device
; drivers crash or give bogus information when they get this command.
		lea.l	TDName(pc),a0
		movea.l	DeviceName(a5),a1
		adda.l	a1,a1
		adda.l	a1,a1
		addq.l	#1,a1			can't do BSTR compare
		moveq.l	#15,d0			comparing 16 bytes
15$		cmp.b	(a0)+,(a1)+
		bne.s	NotTD
		dbra	d0,15$
		moveq.l	#TRUE,d0		TRUE==trackdisk
		move.l	d0,IsTrackDisk(a5)
	printf <'IsTrackDisk = %ld\n'>,d0

NotTD:
; special case test for carddisk.device which also understands TD_GETGEOMETRY

              lea.l   CCName(pc),a0
              movea.l DeviceName(a5),a1
              adda.l  a1,a1
              adda.l  a1,a1
              addq.l  #1,a1
              moveq   #14,d0                  ;arg - need equates
isCCDev:      cmp.b   (a0)+,(a1)+
              bne.s   NotCCD
              dbra.s  d0,isCCDev
              moveq   #TRUE,d0
              move.l  d0,IsTrackDisk(a5)
      printf <'IsCardDisk = %ld\n'>,d0

; need a timer for the disk flushing and record locking code
NotCCD:		lea.l	TimerName(pc),a0	open timer device
		moveq.l	#1,d0			unit 1
		moveq.l	#0,d1			no special flags
		movea.l	TimerDev(a5),a1		this IORequest
		jsr	_LVOOpenDevice(a6)
		move.l	d0,TimerOpened(a5)	did we get the timer ?
		bne	InitFail		nope, error out

		movea.l	TimerDev(a5),a0		copy to the spare TimerReq
		move.l	ThisDevProc(a5),MN_REPLYPORT(a0)
		movea.l	TimerExtraDev(a5),a1
		moveq.l	#IOTV_SIZE,d0
		jsr	_LVOCopyMem(a6)
		movea.l	TimerExtraDev(a5),a0	replies to a different port
		move.l	DoIOPort(a5),MN_REPLYPORT(a0)

		lea.l	InputName(pc),a0	open input device
		moveq.l	#0,d0
		moveq.l	#0,d1
		movea.l	InputDev(a5),a1
		jsr	_LVOOpenDevice(a6)
		move.l	d0,InputOpened(a5)
		bne	InitFail

; Initialise the spare message port we are using for returning DoIO() requests
		moveq.l	#-1,d0
		jsr	_LVOAllocSignal(a6)	get sigbit for spare port
		movea.l	DoIOPort(a5),a0
		move.b	d0,MP_SIGBIT(a0)
		move.b	#NT_MSGPORT,LN_TYPE(a0)
		move.l	ThisTask(a5),MP_SIGTASK(a0)
		lea.l	MP_MSGLIST(a0),a0
		NEWLIST	a0

		move.w	#TRUE,WrProt(a5)	until we know for sure anyway

		lea.l	DosLibName(pc),a1	open dos library
		moveq.l	#0,d0			any version will do
		jsr	_LVOOpenLibrary(a6)
		move.l	d0,DosLib(a5)
		beq.s	InitFail		didn't get it

;initialise root co-routine and startup the disk and kill co-routines
		bsr	InitCo			initialise co-routines
		tst.l	d0
		beq.s	InitFail		didn't get the memory

		lea.l	Disk(pc),a0		create disk co-routine
		bsr	CreateCo
		move.l	d0,DiskCo(a5)
		beq.s	InitFail		sorry, it didn't work

		lea.l	Kill(pc),a0		create kill co-routine
		bsr	CreateCo
		move.l	d0,KillCo(a5)
		beq.s	InitFail		didn't work

20$		movea.l	KillCo(a5),a0		now call the co-routines
		moveq.l	#0,d0			no args to start
		bsr	CallCo			it will WaitCo back to us
		movea.l	DiskCo(a5),a0		start main disk co-routine too
		moveq.l	#0,d0			no args either
		bsr	CallCo			and no return code

		bsr	Restart			initialise the disk

; Init worked all the way through so put our MsgPort address into dl_Task to
; make us a public filehandler that stays resident in memory.  Restart will
; still be running at this point (it may in fact have called the validator
; if the bitmap was bad).  Until MemValid is true the disk cannot be written.
; Apparently, devlist pointer can be null on bootup, added a test for this.
; DiskType is returned in d0 by Restart, use this as returncode for Init too.
		cmpa.l	#0,a4
		beq.s	InitExit
		move.l	ThisDevProc(a5),dl_Task(a4)	make us public
		bra.s	InitExit		return disktype in d0

InitFail	printf <'Init failed\n'>
		bsr.s	CleanUp			free what we allocated
		moveq.l	#0,d0			return false

InitExit	movem.l	(sp)+,a2-a4
		rts

;==============================================================================
; CleanUp()
;
; Cleans up any allocated memory and closes devs if Init failed for any reason.
;==============================================================================
CleanUp		move.l	a2,-(sp)
		tst.l	DiskCo(a5)		did we create disk co-routine
		beq.s	KillDevs		nope, so no kill either
		movea.l	DiskCo(a5),a0
		bsr	DeleteCo
		tst.l	KillCo(a5)		did we create kill co-routine
		beq.s	KillDevs		nope
		movea.l	KillCo(a5),a0
		bsr	DeleteCo

KillDevs	tst.l	DiskOpened(a5)		did we open the disk
		bne.s	KillMem			nope, so no timer either
		movea.l	DiskDev(a5),a1
		jsr	_LVOCloseDevice(a6)
		tst.l	TimerOpened(a5)		did we open the timer ?
		bne.s	KillMem			nope
		movea.l	TimerDev(a5),a1
		jsr	_LVOCloseDevice(a6)
		tst.l	InputOpened(a5)		did we open input
		bne.s	KillMem			nope
		movea.l	InputDev(a5),a1
		jsr	_LVOCloseDevice(a6)

KillMem		lea.l	MemRequired(pc),a2	point to memory table
10$		moveq.l	#0,d0
		move.w	(a2)+,d0		get size to be freed
		beq.s	20$			last entry, we did them all
		move.w	(a2)+,d1		find where pointer is stored
		move.l	0(a5,d1.w),d1		get mem pointer
		beq.s	20$			no pointer, failed here
		movea.l	d1,a1			free this chunk
		jsr	_LVOFreeMem(a6)
		bra.s	10$

20$		movea.l	FreeBufferQueue(a5),a2	free any cache buffers
30$		cmpa.w	#0,a2
		beq.s	40$			all done
		movea.l	a2,a1
		movea.l	(a2),a2			a2 points to next buffer
		moveq.l	#cb_SIZEOF,d0		get buff header size
		add.l	BlockSize(a5),d0	add data area size
		jsr	_LVOFreeMem(a6)		and free it
		bra.s	30$

40$		tst.l	UtilityBase(a5)		did we get utility library
		beq.s	45$
		movea.l	UtilityBase(a5),a1
		jsr	_LVOCloseLibrary(a6)

45$		tst.l	DosLib(a5)		did we get DOS library
		beq.s	50$
		movea.l	DosLib(a5),a1
		jsr	_LVOCloseLibrary(a6)

50$		tst.l	BitmapKeys(a5)		did we get bitmap memory ?
		beq.s	CleanedUp		no
		movea.l	BitmapKeys(a5),a1	yes, so free it
		move.l	BitmapCount(a5),d0	calculate size to free
		lsl.l	#2,d0			4 bytes per bitmap entry
		jsr	_LVOFreeMem(a6)
CleanedUp	move.l	(sp)+,a2
		rts

;==============================================================================
; Success = GetBuffers(Number),_AbsExecBase
;   d0			d0	    a6
;
; Allocates memory for cache buffers and links them into the free buffer queue.
;==============================================================================
GetBuffers	movem.l	d2-d3/a2,-(sp)
		lea.l	FreeBufferQueue(a5),a2	point to first link field
		move.w	d0,d2			save buffer count
		clr.l	d3
		bra.s	15$

10$		move.l	BufferMemType(a5),d1	observe memory type
		ori.l	#MEMF_CLEAR,d1		let's clear em out first time
		moveq.l	#cb_SIZEOF,d0		allow for special info
		add.l	BlockSize(a5),d0	and size of a block
		jsr	_LVOAllocMem(a6)	get the memory
		tst.l	d0			did it work
		beq.s	20$			nope, error out (d0 = 0)
		movea.l	d0,a0
		move.l	(a2),(a0)		singly linked list
		move.l	a0,(a2)			link it in
		addq.l	#1,d3
15$		dbra	d2,10$			and go for another
20$		move.l	d3,d0			return buffer count
		movem.l	(sp)+,d2-d3/a2
		rts

;==============================================================================
; success = GeometryInit(envec)
;   d0		 	  a3
;
; This routine was broken out of the main Init routine so that it can be
; called by Restart if the disk geometry changes on removeable media.  This
; will be called iff LowCyl for the partition is 0.  We assume the partition
; spans the whole surface of the media in this case.
;
; set up global information about the arrangement of this disk. (It would
; probably work out a little better to just clone the environment vector
; but I guess I'd better stick with the old format that the BCPL had).
;==============================================================================
GeometryInit	move.l	d2,-(sp)
		moveq	#0,d2			default sector/blk shift
		move.l	SectorSize(a3),d0
		move.l	SectorsPerBlock(a3),d1	was required to be 1...
		beq.s	2$			avoid stupid people
	printf <'init: sectors/block %ld'>,d1
		mulu.w	d1,d0			d0 and d1 were max <32k...
2$		move.l	d0,d1			save size in longwords
		lsl.l	#2,d0
		move.l	d0,BlockSize(a5)
		subi.l	#56,d1			compute hash table size
		move.l	d1,HTSize(a5)		that's #entries in table
		lsl.l	#2,d1			now calculate maximum byte...
		addi.w	#fhb_HashTable,d1	...offset to end of table
		subq.w	#4,d1			but point at last entry
		move.w	d1,MaxEntry(a5)		not past the end

		moveq.l	#-fdb_Data,d1
		add.l	BlockSize(a5),d1
		move.l	d1,BytesPerData(a5)	OFS format, data bytes per block
		
		moveq.l	#-1,d1			compute block shift
5$		addq.w	#1,d1			shift one more
		lsr.w	#1,d0			wait for the bit to drop out
		bcc.s	5$
		move.w	d1,BlockShift(a5)

		move.l	Surfaces(a3),NSurfaces(a5)
		move.l	BlocksPerTrack(a3),NSecsPerTrack(a5)
		move.l	ReservedBlocks(a3),Reserved(a5)
		move.l	Interleave(a3),Intleave(a5)
		move.l	LowerCylinder(a3),LowerCyl(a5)
		move.l	UpperCylinder(a3),UpperCyl(a5)
		move.l	Buffers(a3),d0
		moveq	#MINBUFFERS,d1		our minimum
		cmp.l	d0,d1
		bls.s	10$			buffers>=MINBUFFERS (unsigned)
		move.l	d1,d0			use MINBUFFERS
10$		move.l	d0,NumBuffers(a5)

; The mount command now ensures that there is a default memory type set up.
		moveq.l	#MEMF_PUBLIC,d0		assume buffmemtype is public
		moveq.l	#12,d1			size is # longwords
		cmp.l	EnvecSize(a3),d1	see if there is a memtype entry
		bgt.s	11$			nope, struct not big enough
		move.l	BuffMemType(a3),d0	yep, get the type
11$		move.l	d0,BufferMemType(a5)

; new things added for various nasties with CSA board and devs that can't
; handle transfers of more than 255 blocks in one go.  THIS IS A KLUGE!!!
		move.l	#$7fffffff,GMaxTransfer(a5)	default values
		move.l	#$ff000001,GAddressMask(a5)	default addr mask
		moveq.l	#14,d1				make sure envec...
		cmp.l	EnvecSize(a3),d1		...is big enough
		bgt.s	12$

		move.l	MaxTransfer(a3),GMaxTransfer(a5)
		move.l	AddressMask(a3),d0
		not.l	d0			going to and with address
		move.l	d0,GAddressMask(a5)

12$		move.l	NSurfaces(a5),d1
		mulu.w	NSecsPerTrack+2(a5),d1	get BlocksPerCyl
		move.l	d1,NSecsPerCyl(a5)	Actually SectorsPerCyl!!!!!

		move.l	UpperCyl(a5),d0		get highest block number
		sub.l	LowerCyl(a5),d0
		addq.l	#1,d0
		mulu.w	d1,d0			(upper-lower+1)*secspercyl

	printf <'init: numsecs %ld'>,d0
		; d0 = number of sectors
		; figure out shift for sectors/block
		move.l	SectorsPerBlock(a3),d1	1-64
		beq.s	30$			in case someone was stupid...
		moveq	#-1,d2			shift
20$		addq.l	#1,d2			ends up 0-6
		lsr.l	#1,d1			is the low bit 1?
		bcc.s	20$			continue until we see a 1 bit
	printf <'init: sector/block shift %ld'>,d2

		; convert number of sectors to number of blocks
		lsr.l	d2,d0			d0 >>= d2 (log2 sectors/block)
30$
		; d0 = number of blocks
	printf <'init: numblocks %ld'>,d0
		subq.l	#1,d0			highest block number

		move.l	d0,HighestBlock(a5)	high blk (partition relative)

		sub.l	Reserved(a5),d0		calculate total blocks on disk
		addq.l	#1,d0
		move.l	d0,NBlocks(a5)

; figure out the maximum practical limit for write requests (size of disk)
		move.w	BlockShift(a5),d1
		lsl.l	d1,d0
		move.l	d0,LargestWrite(a5)
		bpl.s	LargeOK
		move.l	#$7fffffff,LargestWrite(a5)

; calculate how many data blocks can be referenced by a single bitmap block
LargeOK		move.l	BlockSize(a5),d0	calculate BlocksPerBM
		subq.l	#4,d0			don't include checksum
		lsl.l	#3,d0			8 blocks per byte
		move.l	d0,BlocksPerBM(a5)	save this (only need a word)

; Calculate how many bitmap blocks are required for a disk of the given size.
		subq.l	#1,d0			BlocksPerBM-1
		add.l	HighestBlock(a5),d0	plus HighestBlock
		sub.l	Reserved(a5),d0		minus reserved blocks
		move.l	BlocksPerBM(a5),d1	divided by BlocksPerBM
		bsr	myUDivMod32		d0/d1 -> 32 (d0), rem 32 (d1)
		move.l	d0,BitmapCount(a5)	gives number of bitmap blocks
		lsl.l	#2,d0			allocating array of longwords
		bsr	GetPubMem		get array for bitmap
		move.l	d0,BitmapKeys(a5)	store in global area
		beq.s	GInitFail		didn't get memory

; calculate where the root block goes (always in the middle of the partition)
		move.l	Reserved(a5),d0
		add.l	HighestBlock(a5),d0
		lsr.l	#1,d0
		move.l	d0,RootKey(a5)		where root block is
	printf <'init: RootKey = %ld\n'>,d0

		move.l	LowerCyl(a5),d0
		move.l	NSecsPerCyl(a5),d1
		mulu.w	d0,d1			low sector
		move.w	BlockShift(a5),d0	= sectorshift + sector/blk shift
		sub.w	d2,d0			leaves SectorShift in d0!
		lsl.l	d0,d1			turn lowsector into bytes
		move.l	d1,LowerByte(a5)	now its actually LowerByte!
	printf <'init: LowerByte = $%lx'>,d1
		
		move.l	NumBuffers(a5),d0	get the buffers we need
		addq.l	#1,d0			add one for the bitmap
		bsr	GetBuffers		and link into FreeBufferQueue
		tst.l	d0			did it work
		beq.s	GInitFail		nope, clean up and exit

		movea.l	FreeBufferQueue(a5),a0
		move.l	(a0),FreeBufferQueue(a5)
		clr.l	(a0)
		move.l	a0,BMBlock(a5)		spare cache for bitmaps
		moveq.l	#TRUE,d0
GInitFail	move.l	(sp)+,d2
		rts

;==============================================================================
; call UDivMod32
;==============================================================================
myUDivMod32:
	printf <'UDivMod(%ld/%ld):'>,d0,d1
	move.l	a6,-(sp)
	move.l	UtilityBase(a5),a6
	jsr	_LVOUDivMod32(a6)
	move.l	(sp)+,a6
	printf <'	= %ld rem %ld'>,d0,d1
	rts

;==============================================================================
; Table of memory requirements for this handler.  Each table entry contains:-
; Size required,  Offset from a5 for storing mem pointer.
;==============================================================================
MemRequired	DC.W	IOSTD_SIZE,DiskDev
		DC.W	IOSTD_SIZE,DiskExtraDev
		DC.W	IOTV_SIZE,TimerDev
		DC.W	IOTV_SIZE,TimerExtraDev
		DC.W	MP_SIZE,DoIOPort
		DC.W	IOSTD_SIZE,InputDev
		DC.W	dp_Res1,TimerPacket		linked into TimerDev
		DC.W	dp_Res1,InputPacket		linked into InputDev
		DC.W	ie_SIZEOF,InpEvent		propogated disk events
		DC.W	IS_SIZE,ChangeInt		ChangeInt handler
		DC.W	128*4,CacheHash			buffer hash table
		DC.W	128,ReqString			built requester string
		DC.W	64,ButtonString			built retry|cancel string
		DC.W	64,ReqArgs			argument list for requester
		DC.W	0

TimerName	DC.B	'timer.device',0
InputName	DC.B	'input.device',0
DosLibName	DC.B	'dos.library',0
UtilLibName	DC.B	'utility.library',0
TDName		DC.B	'trackdisk.device',0
CCName		DC.B	'carddisk.device',0

		END
@


1.28
log
@changes for SectorsPerBlock
Many changes of variable names to be more correct
@
text
@d22 1
d30 1
d116 9
a124 1
NoDOSType	bsr	GeometryInit
d341 6
a346 1
40$		tst.l	DosLib(a5)		did we get DOS library
d354 1
a354 2
		move.w	BitmapCount(a5),d0	calculate size to free
		ext.l	d0
d506 1
a506 1
		move.w	d0,BlocksPerBM(a5)	save this (only need a word)
d512 3
a514 3
		divu.w	BlocksPerBM(a5),d0	divided by BlocksPerBM
		move.w	d0,BitmapCount(a5)	gives number of bitmap blocks
		ext.l	d0			don't need remainder
d550 11
d585 1
@


1.27
log
@Use #define for minimum number of buffers
@
text
@d92 1
a92 1
	printf <'SectorsPerBlock = %ld\n'>,Surfaces(a3)
d387 8
a394 2
GeometryInit	move.l	SectorSize(a3),d0
		move.l	d0,d1			save size in longwords
d415 1
a415 1
		move.l	BlocksPerTrack(a3),NBlocksPerTrack(a5)
d449 2
a450 2
		mulu.w	NBlocksPerTrack+2(a5),d1	get BlocksPerCyl
		move.l	d1,NBlocksPerCyl(a5)
d455 20
a474 2
		mulu.w	d1,d0			(upper-lower+1)*blockspercyl
		subq.l	#1,d0
d514 7
a520 3
		move.l	NBlocksPerCyl(a5),d1
		mulu.w	d0,d1
		move.l	d1,LowerCyl(a5)		now its actually LowerBlock
d533 2
a534 1
GInitFail	rts
@


1.26
log
@Re-added the carddisk stuff, also saved some space (unneeded CNOPs)
(Darren forgot to add it to V39)
@
text
@d415 4
a418 2
		bgt.s	10$			must be >0
		moveq.l	#1,d0			else make it 1
@


1.25
log
@Init the directory semaphore list
@
text
@d181 16
d198 1
a198 1
NotTD		lea.l	TimerName(pc),a0	open timer device
a526 1
		CNOP	0,2
a527 1
		CNOP	0,2
a528 1
		CNOP	0,2
d530 1
a530 1
		CNOP	0,2
@


1.24
log
@fixed test for size of envec.  Sign of branch was wrong
@
text
@d69 3
@


1.23
log
@fixed off-by-one bug when checking size of environment vector to see if a 
DOSType entry is present.  Previously 16, now 17.  Forgot to include the
size entry in the count
@
text
@d102 1
a102 1
		moveq.l	#17,d0			size is # longwords
d104 1
a104 1
		blt.s	NoDOSType
@


1.22
log
@check that DOSType field is present in envec before rejecting volume if type is wrong
@
text
@d102 2
a103 2
		moveq.l	#16,d0			size is # longwords
		cmp.l	EnvecSize(a3),d0	see if there is a memtype entry
@


1.21
log
@fail initialisation packet if envec has a DOSType we can't handle
@
text
@d102 4
d111 1
a111 1
		bsr	GeometryInit
@


1.20
log
@support for using DOS Fault() command to get requester strings
@
text
@d101 6
@


1.19
log
@stash device node entry to use name in requesters.
setup an IsTrackDisk flag to determine if GETGEOMETRY can be used in restart
@
text
@d159 6
a164 2
		bsr	CompString
		not.l	d0			invert result so TRUE==trackdisk
d169 1
a169 1
		lea.l	TimerName(pc),a0	open timer device
d492 3
d503 3
a505 1
TDName		DC.B	16,'trackdisk.device'
@


1.18
log
@calcualte and store maximum write size for each partition
@
text
@d28 1
d51 1
d127 1
d129 1
d152 13
d242 2
a243 1
InitFail	bsr.s	CleanUp			free what we allocated
d496 1
a496 1

@


1.17
log
@re-organised geometry calcs and cache buffer allocation into a subroutine
so it can be called from Restart() when the disk geometry changes.  
Changed GetBuffers routine to only fail if 0 buffers were allocated,
otherwise it returns the number allocated.
@
text
@d404 7
d412 1
a412 1
		move.l	BlockSize(a5),d0	calculate BlocksPerBM
@


1.16
log
@
@
text
@d21 1
a21 1
		XDEF	Init,GetBuffers
d46 1
d99 3
a101 13
; set up global information about the arrangement of this disk. (It would
; probably work out a little better to just clone the environment vector
; but I guess I'd better stick with the old format that the BCPL had).
		move.l	SectorSize(a3),d0
		move.l	d0,d1			save size in longwords
		lsl.l	#2,d0
		move.l	d0,BlockSize(a5)
		subi.l	#56,d1			compute hash table size
		move.l	d1,HTSize(a5)		that's #entries in table
		lsl.l	#2,d1			now calculate maximum byte...
		addi.w	#fhb_HashTable,d1	...offset to end of table
		subq.w	#4,d1			but point at last entry
		move.w	d1,MaxEntry(a5)		not past the end
a102 98
		moveq.l	#-fdb_Data,d1
		add.l	BlockSize(a5),d1
		move.l	d1,BytesPerData(a5)	OFS format, data bytes per block
		
		moveq.l	#-1,d1			compute block shift
5$		addq.w	#1,d1			shift one more
		lsr.w	#1,d0			wait for the bit to drop out
		bcc.s	5$
		move.w	d1,BlockShift(a5)

		move.l	Surfaces(a3),NSurfaces(a5)
		move.l	BlocksPerTrack(a3),NBlocksPerTrack(a5)
		move.l	ReservedBlocks(a3),Reserved(a5)
		move.l	Interleave(a3),Intleave(a5)
		move.l	LowerCylinder(a3),LowerCyl(a5)
		move.l	UpperCylinder(a3),UpperCyl(a5)
		move.l	Buffers(a3),d0
		bgt.s	10$			must be >0
		moveq.l	#1,d0			else make it 1
10$		move.l	d0,NumBuffers(a5)

; The mount command now ensures that there is a default memory type set up.
		moveq.l	#MEMF_PUBLIC,d0		assume buffmemtype is public
		moveq.l	#12,d1			size is # longwords
		cmp.l	EnvecSize(a3),d1	see if there is a memtype entry
		bgt.s	11$			nope, struct not big enough
		move.l	BuffMemType(a3),d0	yep, get the type
11$		move.l	d0,BufferMemType(a5)

; new things added for various nasties with CSA board and devs that can't
; handle transfers of more than 255 blocks in one go.  THIS IS A KLUGE!!!
		move.l	#$7fffffff,GMaxTransfer(a5)	default values
		move.l	#$ff000001,GAddressMask(a5)	default addr mask
		moveq.l	#14,d1				make sure envec...
		cmp.l	EnvecSize(a3),d1		...is big enough
		bgt.s	12$

		move.l	MaxTransfer(a3),GMaxTransfer(a5)
		move.l	AddressMask(a3),d0
		not.l	d0			going to and with address
		move.l	d0,GAddressMask(a5)

12$		move.l	NSurfaces(a5),d1
		mulu.w	NBlocksPerTrack+2(a5),d1	get BlocksPerCyl
		move.l	d1,NBlocksPerCyl(a5)

		move.l	UpperCyl(a5),d0		get highest block number
		sub.l	LowerCyl(a5),d0
		addq.l	#1,d0
		mulu.w	d1,d0			(upper-lower+1)*blockspercyl
		subq.l	#1,d0
		move.l	d0,HighestBlock(a5)	high blk (partition relative)

		sub.l	Reserved(a5),d0		calculate total blocks on disk
		addq.l	#1,d0
		move.l	d0,NBlocks(a5)

; calculate how many data blocks can be referenced by a single bitmap block
		move.l	BlockSize(a5),d0	calculate BlocksPerBM
		subq.l	#4,d0			don't include checksum
		lsl.l	#3,d0			8 blocks per byte
		move.w	d0,BlocksPerBM(a5)	save this (only need a word)

; Calculate how many bitmap blocks are required for a disk of the given size.
		subq.l	#1,d0			BlocksPerBM-1
		add.l	HighestBlock(a5),d0	plus HighestBlock
		sub.l	Reserved(a5),d0		minus reserved blocks
		divu.w	BlocksPerBM(a5),d0	divided by BlocksPerBM
		move.w	d0,BitmapCount(a5)	gives number of bitmap blocks
		ext.l	d0			don't need remainder
		lsl.l	#2,d0			allocating array of longwords
		bsr	GetPubMem		get array for bitmap
		move.l	d0,BitmapKeys(a5)	store in global area
		beq	InitFail		didn't get memory

; calculate where the root block goes (always in the middle of the partition)
		move.l	Reserved(a5),d0
		add.l	HighestBlock(a5),d0
		lsr.l	#1,d0
		move.l	d0,RootKey(a5)		where root block is
	printf <'init: RootKey = %ld\n'>,d0

		move.l	LowerCyl(a5),d0
		move.l	NBlocksPerCyl(a5),d1
		mulu.w	d0,d1
		move.l	d1,LowerCyl(a5)		now its actually LowerBlock
		
		move.l	NumBuffers(a5),d0	get the buffers we need
		addq.l	#1,d0			add one for the bitmap
		bsr	GetBuffers		and link into FreeBufferQueue
		tst.l	d0			did it work
		beq	InitFail		nope, clean up and exit

		movea.l	FreeBufferQueue(a5),a0
		move.l	(a0),FreeBufferQueue(a5)
		clr.l	(a0)
		move.l	a0,BMBlock(a5)		spare cache for bitmaps

d301 1
a301 1
GetBuffers	movem.l	d2/a2,-(sp)
d304 1
d317 1
d319 2
a320 2
		moveq.l	#TRUE,d0		return true
20$		movem.l	(sp)+,d2/a2
d322 125
@


1.15
log
@
@
text
@d200 1
d204 5
@


1.14
log
@variable block size support.  calculates all required offsets. cleaned up
@
text
@d82 15
a96 15
;	printf <'EnvecSize       = %ld\n'>,EnvecSize(a3)
;	printf <'SectorSize      = %ld\n'>,SectorSize(a3)
;	printf <'SectorOrigin    = %ld\n'>,SectorOrigin(a3)
;	printf <'Surfaces        = %ld\n'>,Surfaces(a3)
;	printf <'SectorsPerBlock = %ld\n'>,Surfaces(a3)
;	printf <'BlocksPerTrack  = %ld\n'>,BlocksPerTrack(a3)
;	printf <'ReservedBlocks  = %ld\n'>,ReservedBlocks(a3)
;	printf <'PreAllocated    = %ld\n'>,PreAllocated(a3)
;	printf <'Interleave      = %ld\n'>,Interleave(a3)
;	printf <'LowerCylinder   = %ld\n'>,LowerCylinder(a3)
;	printf <'UpperCylinder   = %ld\n'>,UpperCylinder(a3)
;	printf <'Buffers         = %ld\n'>,Buffers(a3)
;	printf <'BuffMemType     = %ld\n'>,BuffMemType(a3)
;	printf <'MaxTransfer     = %ld\n'>,MaxTransfer(a3)
;	printf <'AddressMask     = %lx\n'>,AddressMask(a3)
d289 1
a289 1
		beq	InitFail		didn't get it
d294 1
a294 1
		beq	InitFail		didn't get the memory
d299 1
a299 1
		beq	InitFail		sorry, it didn't work
d304 1
a304 1
		beq	InitFail		didn't work
d326 1
a326 1
InitFail	bsr	CleanUp			free what we allocated
@


1.13
log
@fixed BCPL startup bug
@
text
@d56 1
a56 3
	printf <'Device address = 0x%lx\n'>,DeviceName(a5)
	
		moveq.l	#-1,d0			flag timer and disk not open
d60 8
a79 1

a80 3
;		moveq.l	#11,d0			fill in defaults for >11
;		cmp.l	EnvecSize(a3),d0
;		bgt	InitFail
d82 15
a96 15
	printf <'EnvecSize       = %ld\n'>,EnvecSize(a3)
	printf <'SectorSize      = %ld\n'>,SectorSize(a3)
	printf <'SectorOrigin    = %ld\n'>,SectorOrigin(a3)
	printf <'Surfaces        = %ld\n'>,Surfaces(a3)
	printf <'SectorsPerBlock = %ld\n'>,Surfaces(a3)
	printf <'BlocksPerTrack  = %ld\n'>,BlocksPerTrack(a3)
	printf <'ReservedBlocks  = %ld\n'>,ReservedBlocks(a3)
	printf <'PreAllocated    = %ld\n'>,PreAllocated(a3)
	printf <'Interleave      = %ld\n'>,Interleave(a3)
	printf <'LowerCylinder   = %ld\n'>,LowerCylinder(a3)
	printf <'UpperCylinder   = %ld\n'>,UpperCylinder(a3)
	printf <'Buffers         = %ld\n'>,Buffers(a3)
	printf <'BuffMemType     = %ld\n'>,BuffMemType(a3)
	printf <'MaxTransfer     = %ld\n'>,MaxTransfer(a3)
	printf <'AddressMask     = %lx\n'>,AddressMask(a3)
d102 1
d104 18
a121 1
		move.l	d0,SizeBlock(a5)
d124 1
a124 1
		move.l	ReservedBlocks(a3),NResBlocks(a5)
a133 1
;		move.l	BuffMemType(a3),BufferMemType(a5)
d142 1
a142 1
; handle transfers of more than 255 blocks
d154 2
a155 3
12$		move.l	NBlocksPerTrack(a5),d0	get BlocksPerCyl
		move.l	NSurfaces(a5),d1
		mulu.w	d0,d1
a157 15
; SMB 3/4/88:  Added this for adaptec drives that keep info in last block!
; preallocated is now being used to reserve a maximum of seven blocks on the
; last track of the disk.  It is not included in the root block calculation
; because format and disked wouldnt know about it and would therefore screw
; up their idea of where the root block is.

; SMB 10/4/89: took it out again because its incompatible with old FS disks
; that were always mounted with PreAlloc = 2
;		moveq.l	#7,d0
;		cmp.l	PreAllocated(a3),d0
;		ble.s	PreAlloc2Big		limit prealloc to 7
;		move.l	PreAllocated(a3),d0	else use given value
;PreAlloc2Big	move.l	d0,PreAlloc(a5)

		clr.l	PreAlloc(a5)		** compatibility fix **
d163 1
a163 2
		sub.l	PreAlloc(a5),d0		reserve blocks at end of disk
		move.l	d0,Keyupb(a5)		highest block number
d165 3
a167 4
		move.l	NResBlocks(a5),Keylwb(a5)
		sub.l	Keylwb(a5),d0
		addq.l	#1,d0			nblocks = keyupb-keylwb+1
		move.l	d0,NBlocks(a5)		total blocks on disk
d169 5
a173 20
; calculate how many blocks will be required for the bitmap of this disk.
; Allocate memory for the mem copy of the bitmap and the keylist of the bitmap.
; All bitmap handling has been completely changed from the original (SMB)
		moveq.l	#0,d1
		divu.w	#(127*32),d0		bitmap blocks required
		move.w	d0,d1
		swap	d0			any fractional part
		tst.w	d0
		beq.s	1$
		addq.w	#1,d1			yep, allow a whole block for it
1$		move.w	d1,BMBlocks(a5)		save this
		move.w	d1,d0			calculate #extenders required
		subi.w	#25,d0			# bitmap keys in root
		ble.s	3$			no extenders, fits in the root
		addq.w	#1,d1			add 1 to total bitmap blocks
2$		subi.w	#127,d0			127 blocks in each extender
		ble.s	3$
		addq.w	#1,d1
		bra.s	2$
3$		move.w	d1,BMExtend(a5)		blocks including extenders
d175 11
a185 7
		move.w	d1,d0			get memory for BM key list
		ext.l	d0
		lsl.l	#2,d0			4 bytes per entry
		addq.l	#4,d0			for NULL terminator when reading
		bsr	GetPubMem		including extender keys
		move.l	d0,BMList(a5)
		beq	InitFail		didn't get it, fail now
d187 3
a189 13
		move.w	BMBlocks(a5),d0		now get memory for bitmap
		ext.l	d0
		moveq.l	#9,d1			512 bytes per block
		lsl.l	d1,d0
		move.l	BufferMemType(a5),d1	will read direct to here
		jsr	_LVOAllocMem(a6)
		move.l	d0,MemBitMap(a5)
		beq	InitFail		didnt get it

		move.l	Keylwb(a5),d0
		add.l	Keyupb(a5),d0
; fix so format, disked and diskdoctor don't have to know about prealloc
		add.l	PreAlloc(a5),d0		dont include in root block calc
d192 1
a264 1
*****
d313 1
a313 3
		bsr	Restart			set disk type, get bitmap etc.
		tst.l	d0			did it work OK
		beq.s	InitFail		nope, error out now
d316 5
a320 4
; make us a public filehandler that stays resident in memory.  It's possible
; that the validator co-routine is still running from the above call to
; restart.  If this is the case then MemValid will be false.
; APPARENTLY, DEVLIST POINTER CAN BE NULL ON BOOTUP, ADDED A TEST
d377 1
a377 1
		add.l	SizeBlock(a5),d0	add data area size
d386 4
a389 4
50$		tst.l	MemBitMap(a5)		did we get bitmap memory ?
		beq.s	60$			no
		movea.l	MemBitMap(a5),a1	yes, so free it
		move.w	BMBlocks(a5),d0		calculate size to free
d391 1
a391 2
		moveq.l	#9,d1
		lsl.l	d1,d0
a392 10

60$		tst.l	BMList(a5)		did we get bitmap keylist ?
		beq.s	CleanedUp
		movea.l	BMList(a5),a1
		move.w	BMExtend(a5),d0
		ext.l	d0
		lsl.l	#2,d0
		addq.l	#4,d0			yeah!
		jsr	_LVOFreeMem(a6)

a401 1

d410 1
a410 1
		add.l	SizeBlock(a5),d0	and size of a block
d415 1
a415 1
		move.l	(a2),(a0)		not using cb_NextBuff label
d418 1
a418 1
		moveq.l	#-1,d0			return true
@


1.12
log
@notification stuff (took out of here already)
@
text
@d56 2
@


1.11
log
@*** empty log message ***
@
text
@d300 1
a300 2
**** don't need this now
;		jsr	_LVOAddPort(a6)		add into message port list
@


1.10
log
@Removed the FFS interpretation of PreAlloc (not compatible with OFS)
@
text
@@


1.9
log
@fixed defaults for buffmemtype and addressmask, now accepts old envecs
@
text
@a142 5
		moveq.l	#7,d0
		cmp.l	PreAllocated(a3),d0
		ble.s	PreAlloc2Big		limit prealloc to 7
		move.l	PreAllocated(a3),d0	else use given value
PreAlloc2Big	move.l	d0,PreAlloc(a5)
d144 9
@


1.8
log
@*** empty log message ***
@
text
@@


1.7
log
@fixed check of envec size for plugging in AddrMask and MaxTransfer defaults
@
text
@d116 1
a116 1
		ble.s	11$			nope, struct not big enough
@


1.6
log
@*** empty log message ***
@
text
@d126 1
a126 1
		blt.s	12$
@


1.5
log
@Fixed problem with 2090 environment vector being too small.
@
text
@d74 3
a76 3
		moveq.l	#11,d0			fill in defaults for >11
		cmp.l	EnvecSize(a3),d0
		bgt	InitFail
@


1.4
log
@*** empty log message ***
@
text
@d74 1
a74 1
		moveq.l	#14,d0			fail if old mount used
d112 7
a118 7
		move.l	BuffMemType(a3),BufferMemType(a5)
;		moveq.l	#MEMF_PUBLIC,d0		assume buffmemtype is public
;		moveq.l	#12,d1			size is # longwords
;		cmp.l	EnvecSize(a3),d1	see if there is a memtype entry
;		ble.s	10$			nope, struct not big enough
;		move.l	BuffMemType(a3),d0	yep, get the type
;10$		move.l	d0,BufferMemType(a5)
d122 6
d133 1
a133 1
		move.l	NBlocksPerTrack(a5),d0	get BlocksPerCyl
@


1.3
log
@made inputevent iorequest IOSTD_SIZE instead of IO_SIZE
fixed NULL a4 (for devicelist entry) on bootup
@
text
@@


1.2
log
@*** empty log message ***
@
text
@d330 3
d453 1
a453 1
		DC.W	IO_SIZE,InputDev
@


1.1
log
@Initial revision
@
text
@d7 1
d9 1
d272 9
d284 8
a291 6
		movea.l	DoIOPort(a5),a1
		move.b	d0,MP_SIGBIT(a1)
		move.b	#NT_MSGPORT,LN_TYPE(a1)
		move.l	ThisTask(a5),MP_SIGTASK(a1)
		jsr	_LVOAddPort(a6)		add into message port list

d362 4
d450 1
a450 1
;		DC.W	IO_SIZE,InputDev
d452 3
a454 2
;		DC.W	dp_Res1,InputPacket		linked into InputDev
;		DC.W	ie_SIZEOF,InputEvent		propogated disk events
d460 2
a461 2
;InputName	DC.B	'input.device',0
;		CNOP	0,2
@

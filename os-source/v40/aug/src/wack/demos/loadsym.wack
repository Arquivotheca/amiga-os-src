/* LoadSym.wack
 *
 * (c) Copyright 1992-1993 Commodore-Amiga, Inc.  All rights reserved.
 *
 * This software is provided as-is and is subject to change; no warranties
 * are made.  All use is at your own risk.  No liability or responsibility
 * is assumed.
 *
 * Uses loadsym to load symbols for the specified Kickstart.
 * If no Kickstart is specified, it will figure out what
 * Kickstart the target machine is running, and load the symbols
 * for that.
 *
 * bind with 'bindrx loadsym loadsym'
 */

options results

if ( arg() = 0 ) then
DO
	/* Figure out what kind of machine we're running on, and
	 * automatically load the correct Kickstart.  This involves
	 * figuring out where the ROM is located, what the version
	 * and revision is, and scanning for a bonus module to
	 * be able to recognize A4000s and A3000s.
	 */
	/* Figure out where the ROM is.  To do this, we look at the
	 * high word of the ExecBase->lib_Node.ln_Name pointer.
	 */
	readlong 4	/* ExecBase */
	execbase = x2d(result)
	readlong d2x(execbase+10)	/* ExecBase->lib_Node.ln_Name */
	rombase = d2x(x2d(result)%x2d(10000))

	/* Now get the ROM version and revision.  The version is
	 * just ExecBase's library version, and the revision is
	 * the ExecBase->SoftVer value.
	 */
	readword d2x(execbase+20) decimal	/* ExecBase->lib_Version */
	version = result

	readword d2x(execbase+34) decimal	/* ExecBase->SoftVer */
	revision = result

	/* Now try to tell if it's an A3000 or A4000.  To do this,	
	 * we look down the resident modules list, looking for
	 * a module whose rt_Name is "A1000 Bonus", "A4000 Bonus",
	 * or "A3000 Bonus".
	 */
	resmodules = execbase+300
	readlong d2x(resmodules)
	modarray = result

	done = 0
	found = 0
	DO while ( done = 0 )
		readlong modarray
		mod = result
		if ( x2d(mod) = 0 ) then
		DO
			done = 1
		END
		else
		DO
			if ( x2d(mod) < 0 ) then
			DO
				modarray = d2x( x2d(mod) - x2d(80000000) )
			END
			else
			DO
				readlong d2x(x2d(mod)+14)	/* RomTag->rt_Name */
				id = result
				readstring id
				modname = result
				if ( upper(right(modname,5)) = "BONUS" ) then
				DO
				    found = 1
				    done = 1
				END
				modarray = d2x(x2d(modarray)+4)
			END
		END
	END
	if ( found ) then
	DO
		machine = 3
		if ( ( upper(left(modname,5)) = "A1000" ) | ( upper(left(modname,5)) = "A4000" ) ) then
		DO
			machine = 4
		END
	END
	else
	DO
		machine = 2
		if ( rombase = '20' ) then
		DO
			machine = "Z"
		END
		else if ( rombase == 'F0' ) then
		DO
			machine = "F"
		END
	END
	kickstart = machine":"version"."revision
END
else
DO
	kickstart = arg(1)
END

message = LoadSym.rexx(kickstart)
if ( left(message,1) = '*' ) then
DO
	print 'LoadSym Error: 'substr(message,2)
	exit
END
else
DO
	print message
END

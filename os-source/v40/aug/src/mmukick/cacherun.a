	XDEF	BootOpt
	XDEF	CacheRun

	XREF	RSTLoop

	section	PutMeInFast,code

	CNOP	0,4

;===========================================================================
;
;	The following code is very tricky. Because upon reset all fast
;	memory gets blown away and the ROMs are remapped at address 0,
;	chip ram cannot be seen any more. Therefore the following code
;	gets loaded into the instruction cache, and the second time that
;	it runs, it runs out of the cache only, without any memory accesses.
;
;	Because the GVP board inhibits the cache for all chip ram accesses,
;	including instruction fetches, the following code has to be loaded
;	in fast ram, so that it will indeed be loaded into the cache.
;
;	DO NOT USE BRANCHES TO EXTERNS IN THIS CODE!
;
;	Because Blink translates the branch  "beq  RSTLoop"  into
;
;		beq	here
;		...
;	here:
;		jmp	RSTLoop
;
;	the instruction  "jmp  RSTLoop"  will not get loaded into the cache,
;	and since it is in fast ram, it gets blown away after a reset. You
;	have been warned.
;
;===========================================================================

BootOpt:
	move.l	#$00100000,d3
	reset				; Clobber the system

CacheRun:
	move.w	$dff010,d1		; Magic trick
	subq.l	#1,d3
	bne	CacheRun
	move.b	#3,$bfe201		; Turn off Amiga ROM Overlay
	move.b	#2,$bfe001

	subq.b	#1,d0			; Where do we go?
	btst	#0,d0			;
;	beq	RSTLoop			; Don't do this!
	beq	0$			; Branch if this is cache loading

	move.l	#3,d1			; Freeze the cache
	movec	d1,cacr
0$
	jmp	RSTLoop

	nop
	nop
	nop
	nop
	nop
	nop


	END

head     37.4;
branch   ;
access   ;
symbols  ;
locks    ; strict;
comment  @* @;


37.4
date     91.02.08.22.17.34;  author jesup;  state Exp;
branches ;
next     37.3;

37.3
date     91.02.08.21.34.44;  author jesup;  state Exp;
branches ;
next     37.2;

37.2
date     91.02.08.18.54.02;  author jesup;  state Exp;
branches ;
next     37.1;

37.1
date     91.02.08.18.49.20;  author jesup;  state Exp;
branches ;
next     ;


desc
@COnverted it to ASM because it needs to reference a private entry
@


37.4
log
@Fixed a stack offset problem
@
text
@	include "exec/types.i"
	include "exec/memory.i"
	include "dos/dos.i"
	include "dos/dosextens.i"
	include "dos/dosasl.i"

	XREF	_DOSBase

*LONG
*NameFromAnchor (anchor, buffer, buflen)
*	struct AnchorPath *anchor;
*	char *buffer;
*	LONG buflen;
*{
*	struct AChain *chain;
*
*	/* override if bit is set */
*	if (DOSBase->dl_Root->rn_Flags & RNF_PRIVATE1)
*		return DosNameFromAnchor(anchor,buffer,buflen);
*
*	if (buflen == 0)
*	{
*		SetIoErr(ERROR_LINE_TOO_LONG);
*		return FALSE;
*	}
*
*	*buffer = '\0';
*
*	for (chain = anchor->ap_Base;
*	     chain != NULL;
*	     chain = chain->an_Child)
*	{
*		/* Must check if the node's a pattern node! */
*		if (!AddPart(buffer,
*			     chain->an_Flags & DDF_PatternBit ?
*					&(chain->an_Info.fib_FileName[0]) :
*					&chain->an_String[0]),
*			     buflen)
*		{
*			return FALSE;	/* IoErr already set */
*		}
*	}
*
*	return DOSTRUE;
*}

* The LVO is private!!  Comes after MatchPattern in the library

	XREF	_LVOMatchPattern
	XREF	_LVOSetIoErr
	XREF	_LVOAddPart

_LVODosNameFromAnchor EQU _LVOMatchPattern-6

	XDEF	_NameFromAnchor

_NameFromAnchor:
	link	a5,#0
	movem.l	d2-d3/a2-a4/a6,-(a7)
	movem.l	8(a5),D1/D2/D3		; get args - 4 for rtn addr, 4 for lnk

	move.l	_DOSBase,a6
	move.l	dl_Root(a6),a0
	btst.b	#RNB_PRIVATE1,rn_Flags+3(a0)
	 beq.s	not_overridden

	;-- overridden by dos lib routine
	jsr	_LVODosNameFromAnchor(a6)
	bra.s	exit

not_overridden:
	tst.l	d3
	bne.s	bufflen_not_0

	;-- bufflen == 0
	moveq	#ERROR_LINE_TOO_LONG,d1
	jsr	_LVOSetIoErr(a6)
error:
	moveq	#0,d0
	bra.s	exit

bufflen_not_0:
	move.l	d2,a3		; buffer
	clr.b	(a3)
	move.l	d1,a2		; anchor

	move.l	ap_Base(a2),a4	; chain

for_loop:
	move.l	a4,d0
	beq.s	for_done

	move.l	a3,d1				; buffer
	btst.b	#DDB_PatternBit,an_Flags(a4)
	beq.s	1$
	lea	an_Info+fib_FileName(a4),a0	;chain->an_Info.fib_FileName
	bra.s	2$
1$	lea	an_String(a4),a0		;chain->an_String
2$	move.l	a0,d2
	;-- d3 already buflen

	jsr	_LVOAddPart(a6)
	tst.l	d0
	beq.s	error

	;-- loop
	move.l	an_Child(a4),a4
	bra.s	for_loop

	;-- success
for_done:
	moveq	#DOSTRUE,d0
exit:
	movem.l	(a7)+,d2-d3/a2-a4/a6
	unlk	a5
	rts
	

	END
@


37.3
log
@Added XDEF
@
text
@d60 1
a60 1
	movem.l	4(a5),D1/D2/D3			; get args
d73 1
a73 1
	bne.s	buff_not_0
d82 1
a82 1
buff_not_0:
d93 1
a93 1
	move.l	a3,d1
d96 1
a96 1
	lea	an_Info+fib_FileName(a4),a0
d98 1
a98 1
1$	lea	an_String(a4),a0
@


37.2
log
@Fixed some minor typos
@
text
@d55 1
@


37.1
log
@initial checkin
@
text
@a52 2
DOSTRUE	EQU	-1

d57 1
a57 1
	link	#0,a5
d77 1
a77 1
erro:
@

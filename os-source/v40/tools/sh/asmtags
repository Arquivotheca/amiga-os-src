#! /bin/csh -f
#
#	asmtags [<.asm>]*
#
#	build a ctags-like "tags" file from assembly sources to stdout
#
if( $#argv == 0 ) exit 0

foreach file ( $argv )
    set xdef=()
    sed -n -e "s/^[ 	]*XDEF[ 	]*\([^ 	]*\).*/\1/p" \
	    -e "s/^[ 	]*xdef[ 	]*\([^ 	]*\).*/\1/p" $file \
	    | sed "s/,/ /g" >xdefs.$$
    if ((-e xdefs.$$) && (! -z xdefs.$$)) then
	foreach xdef ( `cat xdefs.$$` )
	    echo "$xdef	$file	/^$xdef/" >> tags.$$
	end
    endif
end

if ((-e tags.$$) && (! -z tags.$$)) then
    sort +0 -1 -u tags.$$
endif

/bin/rm -f tags.$$ xdefs.$$

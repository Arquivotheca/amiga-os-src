head     1.1;
access   ;
symbols  ;
locks    kodiak:1.1; strict;
comment  @@;


1.1
date     86.07.11.12.13.48;  author tim;  state Exp;
branches ;
next     ;


desc
@BCPL Compiler: Process command tail
@



1.1
log
@Initial revision
@
text
@
// BCPL-ARGS
// (C) Copyright 1979 Tripos Research Group
//     University of Cambridge
//     Computer Laboratory
// (C) Copyright 1985 All Rights Reserved METACOMCO plc
//     26 Portland Square
//     Bristol

SECTION "ARGS"

GET "LIBHDR"
GET "BCPHDR"


LET call.args( nc ) BE
$(
    LET argv   = VEC 100
    LET errarg = "Bad args*N"
    LET errstr = "Run out of store*N"
    LET errfil = "Can't open %S*N"
    LET initcond = ""            // not "$TRIPOS" as before
    LET oldoutput = output()
    LET ctll = 1
    LET version = 1
    LET release = 9
    LET objbin = "bin"
    LET objo   = "o"
    LET objobj = "obj"
    LET argsres = ?
    LET optobj = ?

$<MCC
$<TOS
$<OVLAID'
    LET args    = "FROM,TO,OCODE/K,VER/K,OPT/K,LIB/K,OBJ/K,CEM/S"
$>OVLAID'
$<OVLAID
    LET args    = "FROM,TO,OCODE/K,VER/K,OPT/K,LIB/K,OBJ/K,CEM/S,OVL/K"
$>OVLAID
$>TOS
$<TOS'
    LET args    = "FROM,TO,OCODE/K,VER/K,OPT/K,LIB/K,OBJ/K"
$>TOS'
$>MCC
$<AMIGA
    LET args    = "FROM,TO,OCODE/K,VER/K,OPT/K,LIB/K"
$>AMIGA
$<ATARI
$<OVLAID'
    LET args    = "FROM,TO,OCODE/K,VER/K,OPT/K,OBJ/K,CEM/S"
$>OVLAID'
$<OVLAID
    LET args    = "FROM,TO,OCODE/K,VER/K,OPT/K,OBJ/K,CEM/S,OVL/K"
$>OVLAID
$>ATARI
$<RELEASE
    LET args    = "FROM,TO,OCODE/K,VER/K,OPT/K"
$>RELEASE
$<IMPERIAL
    LET args    = "FROM,TO,OCODE/K,VER/K,OPT/K"
$>IMPERIAL

$<TOS
    LET ovl     = ""
    LET prompt1 = "Enter command line or <RETURN> to exit"
    LET prompt2 = "Hit <RETURN> to exit"
$>TOS

    rc := 0
    fpsimglob := -1
    transchars := FALSE
    sourcestream, ocodestream, codestream := 0, 0, 0
    verstream := oldoutput

$<TOS'
    argsres := rdargs(args, argv, 100)
    IF argsres = 0 THEN
    $( writes(errarg)
       GOTO fail
    $)
$>TOS'
$<TOS
    $( argsres := rdargs(args,argv,100)
       IF argsres=0 DO
       $( writes(errarg)
          IF userexit(prompt1) THEN goto fail
       $)
    $) REPEATWHILE argsres = 0

$<MCC
    cont.errs := argv!7
$>MCC
$<ATARI
    cont.errs := argv!6
$>ATARI
$>TOS

    UNLESS argv!3=0 DO
    $( verstream := findoutput(argv!3)
       IF verstream=0 DO
       $( writef(errfil,argv!3)
          GOTO fail
       $)
       selectoutput(verstream)
    $)

    UNLESS argv!0=0 DO
    $( LET from = argv!0
       LET len  = from%0
       sourcestream := findinput(from)
       IF sourcestream=0 DO
       $( LET oresult2 = result2
          LET suffix = "b"
          LET l1,l2 = from%0,suffix%0
          LET ofrom = from

          FOR i=1 TO l1 DO
            argsres%i := from%i
$<TRIPOS
          argsres%(l1+1) := '-'
$>TRIPOS
$<TRIPOS'
          argsres%(l1+1) := '.'
$>TRIPOS'
          FOR i=1 TO l2 DO
            argsres%(l1+1+i) := suffix%i
          argsres%0 := l1+l2+1
          from := argsres
          len  := from%0
          argsres := argsres+(l1+l2+1)/bytesperword + 1

          sourcestream := findinput(from)
          IF sourcestream = 0 THEN
          $( result2 := oresult2
             writef(errfil,ofrom)
             GOTO fail
          $)
       $)
       fromfile := getvec(len/bytesperword)
       IF fromfile=0 DO
       $( writes(errstr)
          GOTO fail
       $)
       FOR i = 0 TO len DO fromfile%i := from%i
    $)

    ocode := argv!2\=0
    TEST ocode THEN
    $(
        LET ocodename = argv!2
        LET ocodelen = ocodename%0
        ocodefile := getvec(ocodelen/bytesperword)
        IF ocodefile=0 DO
        $(
           writes(errstr)
           GOTO fail
        $)
        FOR i = 0 TO ocodelen DO ocodefile%i := ocodename%i

        UNLESS sourcestream = 0 DO
        $(
           ocodestream := findoutput(ocodefile)
           IF ocodestream=0 DO
           $( writef(errfil,ocodefile)
              GOTO fail
           $)
        $)
    $)
    ELSE
      IF sourcestream = 0 THEN
      $( writef("No source file specified*N")
         GOTO fail
      $)

    // Set up directory where we should look
    // for any overlays
$<OVLAID
$<OVLOAD'
$<MCC
    IF argv!7 NE 0 THEN
      ovl := argv!8
$>MCC
$<ATARI
    IF argv!6 NE 0 THEN
      ovl := argv!7
$>ATARI
    FOR i = 0 TO ovl%0 DO
      ovhome%i := ovl%i
$>OVLOAD'
$>OVLAID

    // set up defaults

    savespacesize := bytesperword=4 -> 3,2 // Works for most 16 & 32 bit BCPLs!

    // compiler options
    numocode := TRUE
    workspacesize := 20000
    naming := FALSE          // variable names are not put in ocode
    trnoptimise := TRUE      // optimise unless told otherwise
    pptrace := FALSE
    ptree := FALSE
    prsource := FALSE
    forceuppercase := FALSE
    quiet := FALSE
    maxglobals := 50
    lispopt := FALSE
$<MACRO
    macmax := 25
$>MACRO

// cg options
    cglisting := FALSE
    cgnaming := TRUE        // but function names are put in compiled code
    callcounting := FALSE
    profcounting := FALSE
$<TRIPOS
    stkchking := TRUE
$>TRIPOS
$<MTRIPOS
    stkchking := TRUE
$>MTRIPOS
$<AMIDOS
    stkchking := FALSE
$>AMIDOS
$<TOS
    stkchking := TRUE
$>TOS
$<UNIX
    stkchking := FALSE
$>UNIX
    restricted := FALSE
    jumpopt := FALSE
    altobj := FALSE
    cg.a := -1  // god knows why I have to do this !!
    cg.b := 0
    inlinemult  := FALSE
    globexternals := FALSE
    extlowbound := 9999
    dumpsymbols := FALSE

// May need to do floating point for non-tripos
// 68010 and so destination chip must be an option

$<MCC
$<TRIPOS
    chip := c.m68000
$>TRIPOS
$<MTRIPOS
    chip := c.m68000
$>MTRIPOS
$<UNIX4
    chip := c.m68010
$>UNIX4
$>MCC
$<AMIGA
    chip := c.m68000
$>AMIGA
$<IMPERIAL
    chip := c.m68000
$>IMPERIAL
$<RELEASE
    chip := c.m68000
$>RELEASE
$<ATARI
    chip := c.m68000
$>ATARI

$<TRIPOS
    IF datstring(datvec)=0 DO
       FOR i = 0 TO 9 DO datvec%i := "         "%i
$>TRIPOS
$<MTRIPOS
    IF datstring(datvec)=0 DO
       FOR i = 0 TO 9 DO datvec%i := "         "%i
$>MTRIPOS
$<MTRIPOS'
$<TRIPOS'
    FOR i = 0 TO 9 DO datvec%i := "         "%i
$>TRIPOS'
$>MTRIPOS'

// Set up default object module type

$<MCC
$<TRIPOS
    objectmodule := o.tripos
    optobj := objobj
$>TRIPOS
$<MTRIPOS
    objectmodule := o.mtripos
    optobj := objobj
$>MTRIPOS
$<UNIX4
    objectmodule := o.unix4.2
    optobj := objo
$>UNIX4
$<UNIX5
    objectmodule := o.unix5.0
    optobj := objo
$>UNIX5
$>MCC
$<RELEASE
    objectmodule := o.amigados
    optobj := objobj
$>RELEASE
$<IMPERIAL
    objectmodule := o.unix5.0
    optobj := objo
$>IMPERIAL
$<AMIGA
    objectmodule := o.amigados
    optobj := objobj
$>AMIGA
$<ATARI
    objectmodule := o.gst
    optobj := objbin
$>ATARI

$<MCC
    UNLESS argv!6 = 0 DO
    $( LET ch = (argv!6)%1
       SWITCHON capitalch(ch) INTO
       $(
          DEFAULT : writef("Unknown object module option *'%C*'*N", ch)
                    ENDCASE
          CASE 'A': objectmodule := o.amigados; optobj := objobj
                    ENDCASE
          CASE 'G': objectmodule := o.gst; optobj := objbin
                    ENDCASE
          CASE 'J': objectmodule := o.tos; optobj := objo
                    ENDCASE
          CASE 'T': objectmodule := o.tripos; optobj := objobj
                    ENDCASE
          CASE 'M': objectmodule := o.mtripos; optobj := objobj
                    ENDCASE
          CASE 'U': objectmodule := o.unix4.2; optobj := objo
                    ENDCASE
          CASE 'V': objectmodule := o.unix5.0; optobj := objo
                    ENDCASE
       $)
    $)
$>MCC
$<ATARI
    UNLESS argv!5 = 0 DO
    $( LET ch = (argv!5)%1
       SWITCHON capitalch(ch) INTO
       $(
          DEFAULT : writef("Unknown object module option *'%C*'*N", ch)
                    ENDCASE
          CASE 'G': objectmodule := o.gst; optobj := objbin
                    ENDCASE
          CASE 'J': objectmodule := o.tos; optobj := objo
                    ENDCASE
       $)
    $)
$>ATARI

$<ATARI'
$<RELEASE'
$<IMPERIAL'
    TEST argv!5 THEN
    $( LET s = argv!5
       FOR i=0 TO s%0 DO libraryname%i := s%i
    $)
    ELSE
       libraryname := 0
$>IMPERIAL'
$>RELEASE'
$>ATARI'

    ctll := initcond%0 + 1
    FOR i = 0 TO ctll-1 DO condtaglist%i := initcond%i

    // Set up default name for object module
    IF optobj & (argv!1 = 0) THEN
    $( LET safe = argsres
       UNLESS argsres = 0 DO
         argsres := defaultname(fromfile,"b",argsres,optobj,argv+100-argsres)
       IF argsres THEN argv!1 := safe
    $)

    UNLESS argv!1 = 0 DO  // Output file specified
    $( LET name = argv!1
       FOR i=0 TO name%0 DO
         nc%i := name%i
       codestream := findoutput(name)
       IF codestream=0 DO
       $( writef(errfil,name)
          GOTO fail
       $)
       name.codestream := nc    // Indicates we've opened a code file
    $)

    UNLESS argv!4=0 DO
    $( LET opts = argv!4
       LET i = 1
       LET switch = TRUE

       LET rdn(opts,lvi) = VALOF
        $( LET n = 0
           LET i = !lvi
           LET ch = opts%i
           WHILE i<=opts%0 & '0'<=ch<='9' DO
           $( n := n*10+ch-'0'
              i := i+1
              ch := opts%i
           $)
           !lvi := i
           RESULTIS n
        $)

       WHILE i<=opts%0 DO
       $( LET optch = capitalch(opts%i)
          i := i + 1
          SWITCHON optch INTO
          $(
             DEFAULT:  writef("Unknown option %C ignored*N",optch)

             CASE '$':
               condtaglist % ctll := ','
               ctll := ctll + 1
               $(
                  condtaglist % ctll := optch
                  ctll := ctll + 1
                  optch := capitalch(opts%i)
                  i := i + 1
                  IF tagchartable%optch = 0 ENDCASE
               $) REPEAT

             CASE ',': CASE ';':                      ENDCASE

             CASE '-': switch := FALSE;               LOOP

             CASE '**': altobj := switch;             ENDCASE
             CASE '#': dumpsymbols := switch;         ENDCASE
$<MCC
             CASE '%': chip := (chip = c.m68000) ->
                       c.m68010, c.m68000;            ENDCASE
$>MCC
             CASE 'A': cg.a := rdn(opts,@@i);          ENDCASE
             CASE 'B': cg.b := rdn(opts,@@i);          ENDCASE
             CASE 'C': stkchking := switch;           ENDCASE
             CASE 'D': numocode := switch;            ENDCASE
             CASE 'E': errormax := rdn(opts,@@i);      ENDCASE
             CASE 'F': pptrace := switch;             ENDCASE
             CASE 'G': maxglobals := rdn(opts,@@i);    ENDCASE
             CASE 'H': inlinemult := switch;          ENDCASE
             CASE 'I': informmax := rdn(opts,@@i);     ENDCASE
             CASE 'J': jumpopt := switch;             ENDCASE
             CASE 'K': callcounting := switch;        ENDCASE
             CASE 'L': prsource := switch;            ENDCASE
$<MACRO
             CASE 'M': macmax := rdn(opts,@@i);        ENDCASE
$>MACRO
             CASE 'N': naming := switch;              ENDCASE
             CASE 'O': trnoptimise := switch;         ENDCASE
             CASE 'P': profcounting := switch;        ENDCASE
             CASE 'Q': quiet := switch;               ENDCASE
             CASE 'R': restricted := switch;          ENDCASE
             CASE 'S': savespacesize := rdn(opts,@@i); ENDCASE
             CASE 'T': ptree := switch;               ENDCASE
             CASE 'U': forceuppercase := switch;      ENDCASE
             CASE 'V': cgnaming := switch;            ENDCASE
             CASE 'W': workspacesize := rdn(opts,@@i); ENDCASE
$<MCC
             CASE 'X': extlowbound := rdn(opts,@@i)
                       TEST (objectmodule = o.amigados) |
                            (objectmodule = o.gst)      |
                            (objectmodule = o.tos) THEN
                          globexternals := TRUE
                       ELSE
                          writes("Externals only valid with Amiga/Atari modules*N")
                       ENDCASE
             CASE 'Y': fpsimglob := rdn(opts,@@i)
                       ENDCASE
$>MCC
$<ATARI
             CASE 'X': extlowbound := rdn(opts,@@i)
                       globexternals := TRUE
                       ENDCASE
$>ATARI
             CASE 'Z': lispopt := switch;             ENDCASE
          $)

          switch := TRUE
       $)

    $)

    // If the global number for fpsim hasn't been
    // specially assigned using the 'y' option, do so according
    // to the object module being generated

    IF fpsimglob = -1 THEN
       fpsimglob := VALOF
          SWITCHON objectmodule INTO
          $( DEFAULT:
             CASE o.tripos   :
             CASE o.mtripos  : RESULTIS -1
             CASE o.amigados : RESULTIS amigafpsim
             CASE o.gst      :
             CASE o.tos      : RESULTIS atarifpsim
             CASE o.unix4.2  :
             CASE o.unix5.0  : RESULTIS unixfpsim
          $)

    triposfp := (fpsimglob = -1)
   
    condtaglist%ctll := ':'
    condtaglist%0 := ctll

    UNLESS sourcestream=0 DO
    $(
       writef("BCPL V%N.%N", version, release )
$<MCC
       writef( " (%S)", VALOF
          SWITCHON objectmodule INTO
          $( DEFAULT:
             CASE o.tripos   : RESULTIS "TRIPOS"
             CASE o.mtripos  : RESULTIS "MTRIPOS"
             CASE o.unix4.2  : RESULTIS "UNIX4.2"
             CASE o.unix5.0  : RESULTIS "UNIX5.0"
             CASE o.amigados : RESULTIS "AMIGADOS"
             CASE o.gst      : RESULTIS "BIN"
             CASE o.tos      : RESULTIS "TOS"
          $)  )
$>MCC
$<ATARI
       writef( " (%S)", VALOF
          SWITCHON objectmodule INTO
          $( DEFAULT:
             CASE o.gst      : RESULTIS "BIN"
             CASE o.tos      : RESULTIS "TOS"
          $)  )
$>ATARI
       unless argv!4=0 do writef(" Options %S",argv!4)
       newline()
       selectinput(sourcestream)
       linecount := 1
    $)

    RETURN

fail:
    rc := 20
$<TOS
    userexit(prompt2)
$>TOS

 $)

$<MTRIPOS
AND datstring(v) BE
$(
   LET datv = VEC 2
   datstamp(datv)
   dat.to.strings(datv,v)
$)

AND dat.to.strings(datv, v) = VALOF
    $(
    // Returns v containing 3 strings representing the
    // time and date given in datv, where
    // datv!0 = days, datv!1 = mins, datv!2 = ticks.
    // On return, v contains a the date in the form
    // DD-MMM-YY, v+5 contains the time in the format
    // HH:MM:SS, and V+10 contains the day of the week.
    // Vector v should have an upperbound of 14
    // If the date is unset (days = 0) then the strings
    // are all set to "<unset>"

    LET days,  mins,  ticks = datv!0, datv!1, datv!2
    LET datestr, timestr, dowstr = v, v+5, v+10
    LET dayofweek = days REM 7
    LET unset.string = "<unset>"
    LET invalid.string = "<invalid>"
    LET dowtemp = ?
    LET year = 78 // Base year
    LET month = 1
    LET hours, secs = ?, ?
    LET monthtab = TABLE 0,31,59,90,120,151,181,
                         212,243,273,304,334,365
    LET leapmonthtab = TABLE 0,31,60,91,121,152,182,
                             213,244,274,305,335,362
    LET mchars = "JanFebMarAprMayJunJulAugSepOctNovDec"
    LET mcharbase = ?
    LET mtable = ?

    // Deal with case of unset or invalid date

    UNLESS (days > 0) & (0 <= mins < 1440) & (0 <= ticks < tickspersecond*60)
    THEN
      $(
      LET err.string = days=0 -> unset.string, invalid.string

      FOR z=0 TO err.string%0
      DO $(
         LET c = err.string%z
         datestr%z := c
         timestr%z := c
         dowstr%z  := c
         $)
      RESULTIS v
      $)



    days := days + 1
    FOR j=0 TO 9 DO datestr%j := "DD-MMM-YY"%j
    FOR j=0 TO 8 DO timestr%j := "HH:MM:SS"%j

    // Construct date

    $( // Loop to get year
    LET yearlen = (year REM 4)=0 -> 366, 365
    IF (days > 0) & (days <= yearlen) THEN BREAK
    days, year := days - yearlen, year + 1
    $) REPEAT

    IF year > 99 THEN year := year - 100
    datestr%8 := year/10 + '0'
    datestr%9 := year REM 10 + '0'

    // Find month
    mtable := (year REM 4)=0 -> leapmonthtab, monthtab

    $(
    IF days <= mtable ! month THEN BREAK
    month := month + 1
    $) REPEAT

    mcharbase := month*3 - 2
    FOR j=0 TO 2
    DO datestr%(4+j) := mchars % (mcharbase + j)
    days := days - mtable ! (month - 1)
    datestr%1 := days/10 + '0'
    datestr%2 := days REM 10 + '0'

    // Construct time

    hours := mins/60
    mins := mins REM 60
    secs := ticks / tickspersecond

    timestr%1 := hours/10 + '0'
    timestr%2 := hours REM 10 + '0'
    timestr%4 := mins/10 + '0'
    timestr%5 := mins REM 10 + '0'
    timestr%7 := secs/10 + '0'
    timestr%8 := secs REM 10 + '0'

    // Get day of week

    dowtemp := VALOF
    SWITCHON dayofweek
    INTO
        $(
        CASE 0: RESULTIS "Sunday"
        CASE 1: RESULTIS "Monday"
        CASE 2: RESULTIS "Tuesday"
        CASE 3: RESULTIS "Wednesday"
        CASE 4: RESULTIS "Thursday"
        CASE 5: RESULTIS "Friday"
        CASE 6: RESULTIS "Saturday"
        $)

    FOR j=0 TO dowtemp%0 DO dowstr%j := dowtemp%j

    RESULTIS v
    $)
$>MTRIPOS

AND defaultname(root,rootsuf,newname,newsuf,length) = VALOF
$( LET len   = root%0
   LET rsufl = rootsuf%0
   LET nsufl = newsuf%0
   LET matched = FALSE
   LET lwords = ?
   LET p = 0

$<TRIPOS
   LET sepchar = '-'
$>TRIPOS
$<TRIPOS'
   LET sepchar = '.'
$>TRIPOS'

   // First find out whether the name we've been given
   // has the default suffix on it.
   IF len > rsufl THEN
   $( matched := TRUE
      FOR i=1 TO rsufl DO
        IF compch(root%(len-rsufl+i),rootsuf%i) NE 0 THEN
        $( matched := FALSE
           BREAK
        $)
   $)

   // If it contains the suffix, 'strip' it
   IF matched THEN
      len := len - rsufl - 1

   // Now copy the root name into the space provided
   // And append the new suffix to it.
   // First check to see that we have enough room.

   lwords := (len + nsufl + 1)/bytesperword + 1

   IF lwords > length THEN RESULTIS 0

   FOR i=1 TO len DO
   $( p := p+1
      newname%p := root%i
   $)
   p := p+1
   newname%p := sepchar
   FOR i=1 TO nsufl DO
   $( p := p+1
      newname%p := newsuf%i
   $)
   newname%0 := p

   RESULTIS newname+lwords
$)

@

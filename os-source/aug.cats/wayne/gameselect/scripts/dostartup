.key demodrawer/a
.bra [
.ket ]

   failat 21

; NOTE - assign was made resident by our startup-sequence

   CD0:c/assign c: CD0:c
   assign OLDSYS: CD0:

   cd CD0:

   assign >NIL: APP: [demodrawer]

   assign >NIL: PFCD: CD0:PFCD

   echo  >ram:startup-prefix ".key dummy"
   echo >>ram:startup-prefix ".bra {"
   echo >>ram:startup-prefix ".ket }"
   c:join ram:startup-prefix APP:s/startup-sequence as ram:cd-startup-sequence

   assign t: ram:

   assign OLDSYS:  SYS:
   assign SYS:	APP:

   if exists APP:LIBS
      assign LIBS:	APP:LIBS
   endif
   if exists APP:DEVS
      assign DEVS:	APP:DEVS
   endif
   if exists APP:fonts
      assign FONTS:	APP:fonts
   endif
   if exists APP:L
      assign L:	APP:L
   endif
   if exists APP:s
      assign S:	APP:S
   endif
   if exists APP:Locale
      assign Locale: APP:Locale
   endif
   if exists APP:C
      assign C:	APP:C
   endif

   cd APP:

   OLDSYS:c/execute ram:cd-startup-sequence

   assign sys:    OLDSYS:
   assign c:      SYS:c
   assign devs:   SYS:devs
   assign libs:   SYS:libs
   assign l:      SYS:l
   assign fonts:  SYS:fonts
   assign s:      SYS:s
   if exists SYS:Locale
      assign Locale: SYS:Locale
   endif
   cd SYS:

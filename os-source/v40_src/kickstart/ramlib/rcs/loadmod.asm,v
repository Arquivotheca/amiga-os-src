head     36.6;
branch   ;
access   ;
symbols  V40_2:36.6 V40_1:36.6 V39_5:36.6 V39_4:36.6 V39_3:36.6 V39_2:36.6 V37_13:36.6 V37_11:36.5 V37_10:36.5 V37_2:36.5 V36_133:36.5 V36_131:36.5 V36_129:36.5 V36_121:36.4 V36_119:36.3 V36_118:36.3 V36_116:36.2 V36_115:36.1 V36_114:36.0 V36_111:36.0;
locks    ; strict;
comment  @*   @;


36.6
date     91.03.14.17.12.30;  author darren;  state Exp;
branches ;
next     36.5;

36.5
date     90.06.14.16.34.01;  author kodiak;  state Exp;
branches ;
next     36.4;

36.4
date     90.05.14.11.56.28;  author kodiak;  state Exp;
branches ;
next     36.3;

36.3
date     90.05.02.16.51.33;  author kodiak;  state Exp;
branches ;
next     36.2;

36.2
date     90.04.13.11.36.28;  author kodiak;  state Exp;
branches ;
next     36.1;

36.1
date     90.04.02.11.11.39;  author kodiak;  state Exp;
branches ;
next     36.0;

36.0
date     90.03.07.13.46.36;  author kodiak;  state Exp;
branches ;
next     ;


desc
@code to initialize devices & libraries from rom or disk
@


36.6
log
@Don't try to load module of currentdir ptr is NULL.
@
text
@**
**	$Id: loadmod.asm,v 36.5 90/06/14 16:34:01 kodiak Exp Locker: darren $
**
**      ramlib module loader
**
**      (C) Copyright 1985,1989 Commodore-Amiga, Inc.
**          All Rights Reserved
**

	SECTION	ramlib,code

**	Includes

	INCLUDE	"ramlib.i"


**	Exports

	XDEF	EndModule
	XDEF	FindOnList
	XDEF	LoadModule


**	Imports

	XLVO	FindName			; Exec
	XLVO	FindResident			;
	XLVO	InitResident			;

	XLVO	LoadSeg				; Dos
	XLVO	UnLoadSeg			;

	TASK_ABLES


**	Assumptions

	IFNE	rl_DosBase
	FAIL	"rl_DosBase not zero: recode references below"
	ENDC


**********************************************************************
*
*   LoadModule will perform the following:
*
*   5.	Search in the rom resident tags for the module.  If module is
*	not found, skip to 6.  Otherwise, InitResident() the module and
*	return.
*
*   6.	Create the file name for the module -- if no ':' exists in the
*	name already, prepend DEVS: or LIBS: to the name.  LoadSeg the
*	module.  Look for a resident tag in the loaded image -- if not
*	found, initialize with a jump to the first executable
*	instruction and return.
*
*   7.	Check the version and the library name in the resident tag and
*	if sufficient and a name match, InitResident the image and
*	return.  UnLoadSeg a useless image and return.
*

;------ LoadModule ---------------------------------------------------
;
;   a4	OpenParms
;   a5	RamLibPrivate
;   a6	SysBase
;
LoadModule:
		movem.l	a2/a6,-(a7)

		;-- scan the exec list one more time for this module
		move.l	op_ExecList(a4),a0 
		bsr	FindOnList 
		bne	lmDone

		clr.l	op_Segment(a4)

		;-- see if we can find a resident tag
		move.l	op_OpenName(a4),a1
		CALLLVO	FindResident
		move.l	d0,a1		; set up for InitResident
		tst.l	d0
		bne.s	lmInitResident


		;-- try and load module in from disk relative to DEVS:/LIBS:
		move.l	op_LoadName(a4),d1
		move.l	a6,-(a7)
		move.l	(a5),a6		; rl_DosBase(a5)
		CALLLVO	LoadSeg

		move.l	d0,op_Segment(a4)
		bne.s	lmLoadedModule

		;-- try and load module in from disk w/ original name
		move.l	op_ReqName(a4),d1
		cmp.l	op_LoadName(a4),d1
		beq	lmPopDone

		move.l	op_CurrentDir(a4),d0
		beq.s	nocurrentdir	; dont bother if current dir is
					; NULL.

		CALLLVO	LoadSeg
nocurrentdir:
		move.l	d0,op_Segment(a4)
		beq.s	lmPopDone

lmLoadedModule:
		move.l	(a7)+,a6

		;-- well we loaded in something.  try and find a romtag
lmNextSegment:
		lsl.l	#2,d0		; convert from BPTR to APTR
		beq.s	lmNoRamTag	; no more segments to check
		move.l	d0,a0
		move.l	-4(a0),d1	; length of segment (in bytes)
		move.l	(a0)+,d0	; cache next segment BPTR
		sub.l	#6+RT_SIZE,d1	; subtract segment header
					; + sizeof resident tag
		bls.s	lmNextSegment

lmRamTagSearch:
		subq.l	#2,d1
		bls.s	lmNextSegment

		cmp.w	#RTC_MATCHWORD,(a0)+
		bne.s	lmRamTagSearch

		lea	-RT_MATCHTAG(a0),a1
		cmp.l	(a0),a1
		bne.s	lmRamTagSearch

		;-- we found a ram tag

lmInitResident:
		;-- is this OpenLibrary or OpenDevice
		tst.b	op_OpenLibFlag(a4)
		beq.s	lmDevice

		;-- check library version
		move.l	OpenParms_SIZEOF+or_D0(a4),d0
		cmp.b	RT_VERSION(a1),d0
		bgt.s	lmFailUnload

		;-- check dev/lib name
lmDevice:
		move.l	op_OpenName(a4),a0

		move.l	RT_NAME(a1),a2
lmNameCheck:
		move.b	(a0)+,d0
		beq.s	lmNameNull
		cmp.b	(a2)+,d0
		bne.s	lmNameEnd
		bra.s	lmNameCheck
lmNameNull:
		tst.b	(a2)
lmNameEnd:
		bne.s	lmFailUnload

		;-- initialize resident tag at a1
		move.l	op_Segment(a4),d1
		CALLLVO	InitResident

lmCheckLoad:
		move.l	op_ExecList(a4),a0
		bsr.s	FindOnList
		beq.s	lmFailUnload


lmDone:
		movem.l	(a7)+,a2/a6
		rts


		;-- module image inappropriate
lmFailUnload:
		move.l	op_Segment(a4),d1
		beq.s	lmDone
		move.l	a6,-(a7)
		move.l	(a5),a6			; rl_DosBase
		CALLLVO	UnLoadSeg
lmPopDone:
		move.l	(a7)+,a6
		bra.s	lmDone


		;-- no ram tag found
lmNoRamTag:
		move.l	op_Segment(a4),a0
		move.l	a0,d0
		lsl.l	#2,d0
		move.l	d0,a1
		jsr	4(a1)			; w/ segment in a0
		bra.s	lmCheckLoad


;------	FindOnList ---------------------------------------------------
;  a0	list
;  a4	OpenParms
;  a6	SysBase
FindOnList:
		move.l	op_OpenName(a4),a1
		CALLLVO	FindName

		;-- get the condition codes set right
		tst.l	d0
		rts

EndModule:

	END
@


36.5
log
@split stack usage so OpenDevice/OpenLibrary of in-core modules
uses 1.3-compatible amount.
@
text
@d2 1
a2 1
**	$Id: loadmod.asm,v 36.4 90/05/14 11:56:28 kodiak Exp Locker: kodiak $
d98 1
a98 1
		beq.s	lmPopDone
d100 4
d105 1
a105 1

@


36.4
log
@o   attempts a second LoadSeg of the module without the LIBS: or DEVS:
    prepended if the first LoadSeg failed and the original request did not
    specify a volume.
@
text
@d2 1
a2 1
**	$Id: loadmod.asm,v 36.3 90/05/02 16:51:33 kodiak Exp Locker: kodiak $
a37 3
	IFNE	op_ParmRegs
	FAIL	"op_ParmRegs not zero: recode references below"
	ENDC
d138 1
a138 1
		move.l	(a4),d0		; op_ParmRegs(a4)
@


36.3
log
@ensure modules were not initialized by prior opens queued ahead of this one
@
text
@d2 1
a2 1
**	$Id: loadmod.asm,v 36.2 90/04/13 11:36:28 kodiak Exp Locker: kodiak $
d89 1
a89 1
		;-- try and load the library in from disk
a93 1
		move.l	(a7)+,a6
d96 1
a96 1
		beq.s	lmDone
d98 4
d103 8
d183 1
@


36.2
log
@use Id instead of Header for 4.x rcs
@
text
@d2 1
a2 1
**	$Id$
d73 5
@


36.1
log
@for rcs 4.x header change
@
text
@d2 1
a2 1
**	$Header: /usr2/kodiak/src/KS/ramlib/RCS/loadmod.asm,v 36.0 90/03/07 13:46:36 kodiak Exp Locker: kodiak $
@


36.0
log
@initial release
@
text
@d2 1
a2 1
**	$Header$
@

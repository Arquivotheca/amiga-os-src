head     1.19;
branch   ;
access   ;
symbols  ;
locks    jerryh:1.19; strict;
comment  @* @;


1.19
date     94.02.14.09.50.49;  author jerryh;  state Exp;
branches ;
next     1.18;

1.18
date     93.05.18.11.49.29;  author jerryh;  state Exp;
branches ;
next     1.17;

1.17
date     93.03.29.13.34.50;  author jerryh;  state Exp;
branches ;
next     1.16;

1.16
date     93.03.06.12.34.20;  author jerryh;  state Exp;
branches ;
next     1.15;

1.15
date     93.02.15.10.17.45;  author jerryh;  state Exp;
branches ;
next     1.14;

1.14
date     93.02.09.15.52.17;  author jerryh;  state Exp;
branches ;
next     1.13;

1.13
date     93.02.08.16.39.16;  author jerryh;  state Exp;
branches ;
next     1.12;

1.12
date     93.02.03.12.09.57;  author jerryh;  state Exp;
branches ;
next     1.11;

1.11
date     93.02.03.12.02.43;  author jerryh;  state Exp;
branches ;
next     1.10;

1.10
date     93.02.02.17.08.19;  author jerryh;  state Exp;
branches ;
next     1.9;

1.9
date     93.01.15.15.41.26;  author jerryh;  state Exp;
branches ;
next     1.8;

1.8
date     93.01.07.09.39.32;  author jerryh;  state Exp;
branches ;
next     1.7;

1.7
date     92.12.12.17.45.48;  author jerryh;  state Exp;
branches ;
next     1.6;

1.6
date     92.12.11.13.35.59;  author jerryh;  state Exp;
branches ;
next     1.5;

1.5
date     92.12.09.15.21.58;  author jerryh;  state Exp;
branches ;
next     1.4;

1.4
date     92.12.04.10.19.49;  author jerryh;  state Exp;
branches ;
next     1.3;

1.3
date     92.11.23.09.23.10;  author jerryh;  state Exp;
branches ;
next     1.2;

1.2
date     92.11.02.13.33.13;  author jerryh;  state Exp;
branches ;
next     1.1;

1.1
date     92.10.21.15.03.49;  author jerryh;  state Exp;
branches ;
next     ;


desc
@miscellaneous device driver functions.
@


1.19
log
@Added A1200 versions of mute and drive light functions.
@
text
@
        INCLUDE "exec/types.i"
        INCLUDE "exec/nodes.i"
        INCLUDE "exec/lists.i"
        INCLUDE "exec/ports.i"
        INCLUDE "exec/libraries.i"
        INCLUDE "exec/tasks.i"
        INCLUDE "exec/devices.i"
        INCLUDE "exec/memory.i"
        INCLUDE "exec/interrupts.i"
        INCLUDE "exec/resident.i"
        INCLUDE "exec/io.i"
        INCLUDE "exec/errors.i"
        INCLUDE "exec/execbase.i"
        INCLUDE "exec/ables.i"
        INCLUDE "hardware/intbits.i"
        INCLUDE "devices/trackdisk.i"

        INCLUDE "defs.i"
        INCLUDE "cd.i"
        INCLUDE "cdprivate.i"
        INCLUDE "cdgs_hw.i"

        OPT     p=68020

************************************************************************
***
***  External References
***
************************************************************************

        INT_ABLES

        XREF    DoCmd
        XREF    DevName
        XREF    SendPacket
        XREF    DoPacket

        XREF    PutHex
        XREF    PutChar

*
************************************************************************
***
*** LSNtoMSFPOS
***
*** in:         D0 = (long)LSN
***
*** out:        D0 = (long)0x00MMSSFF
***
************************************************************************

 FUNCTION LSNtoMSFPOS

        save    d1

        move.l  d0,d1                                                   ; Add on start of first track
        move.l  db_TOC+TOCEntry_SIZE+TOCE_Position(db),d0
        jsr     MSFtoLSN
        add.l   d0,d1

        clr.l   d0                                                      ; Calculate Frame (Block)
        divu    #75,d1
        swap    d1
        move.b  d1,d0

        clr     d1                                                      ; Calculate Minute and Second
        swap    d1
        divu    #60,d1
        swap    d1
        lsl     #8,d1
        or.l    d1,d0

        restore d1
        rts


************************************************************************
***
*** MSFtoLSNPOS
***
*** in:         D0 = (long)0x00MMSSFF
***
*** out:        D0 = (long)LSN
***
************************************************************************

 FUNCTION MSFtoLSNPOS

        save    d1/d2

        swap    d0                                                      ; Calculate sector from minute
        move.w  d0,d1
        mulu    #60*75,d1

        swap    d0                                                      ; Add in sector from frame
        clr.l   d2
        move.b  d0,d2
        add.l   d2,d1

        lsr.w   #8,d0                                                   ; Add in sector from second
        mulu    #75,d0
        add.l   d1,d0

        move.l  d0,d1                                                   ; Subtract off start of first track
        move.l  db_TOC+TOCEntry_SIZE+TOCE_Position(db),d0
        jsr     MSFtoLSN
        sub.l   d0,d1
        move.l  d1,d0

        restore d1/d2
        rts


*
************************************************************************
***
*** LSNtoMSF
***
*** in:         D0 = (long)LSN
***
*** out:        D0 = (long)0x00MMSSFF
***
************************************************************************

 FUNCTION LSNtoMSF

        save    d1

        move.l  d0,d1                                                   ; Calculate Frame (Block)
        clr.l   d0
        divu    #75,d1
        swap    d1
        move.b  d1,d0

        clr.w   d1                                                      ; Calculate Minute and Second
        swap    d1
        divu    #60,d1
        swap    d1
        lsl     #8,d1
        or.l    d1,d0

        restore d1
        rts


************************************************************************
***
*** MSFtoLSN
***
*** in:         D0 = (long)0x00MMSSFF
***
*** out:        D0 = (long)LSN
***
************************************************************************

 FUNCTION MSFtoLSN

        save    d1/d2

        swap    d0                                                      ; Calculate sector from minute
        move.w  d0,d1
        mulu    #60*75,d1

        swap    d0                                                      ; Add in sector from frame
        clr.l   d2
        move.b  d0,d2
        add.l   d2,d1

        lsr.w   #8,d0                                                   ; Add in sector from second
        mulu    #75,d0
        add.l   d1,d0

        restore d1/d2
        rts


*
************************************************************************
***
*** BCDtoBIN
***
*** in:
***
***     D0 = (byte)BCD
***
*** out:
***
***     D0 = (byte)BIN
***
***
************************************************************************

 FUNCTION BCDtoBIN

        save    d1

        move.w  d0,d1                                                   ; Isolate one's digit
        and.w   #$000f,d1

        lsr.w   #4,d0                                                   ; Isolate ten's digit
        and.w   #$000f,d0

        mulu    #10,d0                                                  ; Multiply ten's digit

        add.w   d1,d0                                                   ; Add one's digit

        restore d1
        rts


************************************************************************
***
*** BINtoBCD
***
*** in:
***
***     D0 = (byte)BIN
***
*** out:
***
***     D0 = (byte)BCD
***
***
************************************************************************

 FUNCTION BINtoBCD

        save    a0                                                      ; Save all effected registers

        and.w   #$00ff,d0                                               ; Convert to BCD
        lea     BCDTable(pc),a0
        move.b  0(a0,d0.w),d0

        restore a0                                                      ; Restore registers
        rts


BCDTable:
        dc.b    $00,$01,$02,$03,$04,$05,$06,$07,$08,$09
        dc.b    $10,$11,$12,$13,$14,$15,$16,$17,$18,$19
        dc.b    $20,$21,$22,$23,$24,$25,$26,$27,$28,$29
        dc.b    $30,$31,$32,$33,$34,$35,$36,$37,$38,$39
        dc.b    $40,$41,$42,$43,$44,$45,$46,$47,$48,$49
        dc.b    $50,$51,$52,$53,$54,$55,$56,$57,$58,$59
        dc.b    $60,$61,$62,$63,$64,$65,$66,$67,$68,$69
        dc.b    $70,$71,$72,$73,$74,$75,$76,$77,$78,$79
        dc.b    $80,$81,$82,$83,$84,$85,$86,$87,$88,$89
        dc.b    $90,$91,$92,$93,$94,$95,$96,$97,$98,$99


************************************************************************
***
*** MSFBINtoBCD
***
*** in:         D0 = (long)0x00MMSSFF (bin)
***
*** out:        D0 = (long)0x00MMSSFF (bcd)
***
************************************************************************

 FUNCTION MSFBINtoBCD

                save    d1/d2
                move.l  d0,d1
                swap    d0
                jsr     BINtoBCD
                move.w  d0,d2
                swap    d2
                move.w  d1,d0
                jsr     BINtoBCD
                move.w  d0,d2
                lsr.l   #8,d1
                move.b  d1,d0
                jsr     BINtoBCD
                lsl.w   #8,d0
                or.w    d0,d2
                move.l  d2,d0
                restore d1/d2
                rts




************************************************************************
***
*** MSFBCDtoBIN
***
*** in:         D0 = (long)0x00MMSSFF (bcd)
***
*** out:        D0 = (long)0x00MMSSFF (bin)
***
************************************************************************

 FUNCTION MSFBCDtoBIN

                save    d1/d2
                move.l  d0,d1
                swap    d0
                jsr     BCDtoBIN
                move.w  d0,d2
                swap    d2
                move.w  d1,d0
                jsr     BCDtoBIN
                move.w  d0,d2
                lsr.l   #8,d1
                move.b  d1,d0
                jsr     BCDtoBIN
                lsl.w   #8,d0
                or.w    d0,d2
                move.l  d2,d0
                restore d1/d2
                rts




************************************************************************
***
*** CheckSeekRange
***
*** in:         D1.l = position
***
*** out:        D0.w = error
***
************************************************************************

 FUNCTION CheckSeekRange
                move.l  db_TOC+TOCS_LeadOut(db),d0                      ; Compare seek position with lead-out track of TOC
                jsr     MSFtoLSNPOS
                cmp.l   d0,d1
                bhs     1$

                clr.w   d0                                              ; Seek within range
                rts
1$
                move.w  #1,d0                                           ; Seek out of range
                rts




************************************************************************
***
*** CheckPlayRange
***
*** in:         D1.l = start position
***             D2.l = stop position
***
*** out:        D0.w = error
***
************************************************************************

 FUNCTION CheckPlayRange

                jsr     CheckSeekRange                                  ; If seek range is bad, play range is bad
                bne     2$

                save    d1                                              ; Check end position of play
                move.l  d2,d1
                beq     1$
                sub.l   #1,d1
1$              jsr     CheckSeekRange
                restore d1
                tst.w   d0
2$
                rts





************************************************************************
***
***  InitIntr
***
***     in:     D0 = Interrupt bit number
***             D1 = Priority
***             DB = Data
***             A0 = Code
***             A1 = Node
***
************************************************************************

 FUNCTION InitIntr

                move.b  #NT_INTERRUPT,LN_TYPE(a1)                       ; Set up interrupt node
                move.b  d1,LN_PRI(a1)
                move.l  db,IS_DATA(a1)
                move.l  a0,IS_CODE(a1)  
                lea     DevName(pc),a0
                move.l  a0,LN_NAME(a1)

                exec    AddIntServer                                    ; Add interrupt to server chain
                rts


************************************************************************
***
***  InitPort
***
***     in:     A0 = Message port
***
************************************************************************

 FUNCTION InitPort

                clr.b   MP_FLAGS(a0)                                    ; Initialize message port
                move.b  d0,MP_SIGBIT(a0)
                lea     db_Task(db),a1
                move.l  a1,MP_SIGTASK(a0)
                lea     MP_MSGLIST(a0),a1
                NEWLIST a1
                rts


************************************************************************
*                                                                      *
*   AllocAligned                                                       *
*                                                                      *
*       in:     D0 = Size of block                                     *
*               D1 = Memory type                                       *
*                                                                      *
*      out:     Memory block that is D0 Aligned                        *
*                                                                      *
************************************************************************

 FUNCTION AllocAligned

                save    d2                                              ; Make copy of size
                move.l  d0,d2

                save    d0,d1                                           ; Don't allow task switching
                exec    Forbid
                restore d0,d1

                lsl.l   #1,d0                                           ; Allocate twice as much memory as we need
                exec    AllocMem
                tst.l   d0
                beq     1$

                save    d0                                              ; Free the memory (don't worry, noone can grab it)
                move.l  d0,a1
                move.l  d2,d0
                lsl.l   #1,d0
                exec    FreeMem
                restore d0

                move.l  d0,d1                                           ; Allocate memory at aligned address
                move.l  d2,d0
                add.l   d2,d1
                sub.l   #1,d2
                not.l   d2
                and.l   d2,d1
                move.l  d1,a1
                exec    AllocAbs
1$
                save    d0                                              ; Permit task switching again
                exec    Permit
                restore d0

                restore d2                                              ; Return the result
                tst.l   d0
                rts


*
************************************************************************
***     
***  Attenuate(d0)
***
***     Set the DAC attenuator.
***
***             D0 == new attenuation value (0-7FFF)
***
************************************************************************

 FUNCTION Attenuate

                jsr     Disable

                cmp.w   #$0800,db_Attenuation(db)                       ; Mute or UnMute?
                bhs     1$
                jsr     MuteCD
                bra     99$
1$              jsr     UnMuteCD
99$
                jsr     Enable                                          ; All done
                rts


    IFD CDGS

************************************************************************
*                                                                      *
*   MuteCD - Mute CD audio                                             *
*                                                                      *
************************************************************************

 FUNCTION MuteCD

                bclr.b  #MUTEB,CIAAPRA                                  ; Mute the audio
                rts



************************************************************************
*                                                                      *
*   UnMuteCD - Mute CD audio                                           *
*                                                                      *
************************************************************************

 FUNCTION UnMuteCD

                bset.b  #MUTEB,CIAAPRA                                  ; UnMute the audio
                rts


    ELSE

************************************************************************
*                                                                      *
*   MuteCD - Mute CD audio                                             *
*                                                                      *
************************************************************************

 FUNCTION MuteCD

                clr.w   d0                                              ; Mute the audio
                and.w   #$FF7D,db_DriveBits(db)
                move.w  db_DriveBits(db),db_Packet(db)
                jsr     SendPacket
                rts


************************************************************************
*                                                                      *
*   UnMuteCD - Mute CD audio                                           *
*                                                                      *
************************************************************************

 FUNCTION UnMuteCD

                clr.w   d0                                              ; UnMute the audio
                and.w   #$FF7F,db_DriveBits(db)
                or.w    #$0002,db_DriveBits(db)
                move.w  db_DriveBits(db),db_Packet(db)
                jsr     SendPacket
                rts
    ENDC

************************************************************************
*                                                                      *
*   LightOn - Turn on drive light                                      *
*                                                                      *
************************************************************************

 FUNCTION LightOn

                or.w    #$0081,db_DriveBits(db)
                move.w  db_DriveBits(db),db_Packet(db)                  ; Turn on drive light
                jsr     DoPacket
                rts


************************************************************************
*                                                                      *
*   LightOff - Turn off drive light                                    *
*                                                                      *
************************************************************************

 FUNCTION LightOff

                clr.w   d0                                              ; Turn off drive light
                and.w   #$FF7E,db_DriveBits(db)
                move.w  db_DriveBits(db),db_Packet(db)
                jsr     SendPacket
                rts


*
************************************************************************
***
***  CauseInterruptList - Cause all interrupts contained in list
***
***     in:     A0 = Interrupt List
***
************************************************************************

 FUNCTION CauseInterruptList

                save    a2                                              ; Traverse interrupt list
                move.l  a0,a2
1$              move.l  (a2),a2
                tst.l   (a2)
                beq.s   2$
                move.l  IO_DATA(a2),a1                                  ; Call interrupt if one is defined
                exec    Cause
                bra.s   1$
2$
                restore a2                                              ; All done
                rts



************************************************************************
***
***  SetSubcodeInterrupt
***
***     Turn subcode interrupt on or off depending on flags.
***     This function will only succeed if playing audio.
***
************************************************************************

 FUNCTION SetSubcodeInterrupt

                move.w  db_Info+CDINFO_Status(db),d0                    ; If we are playing, set subcode interrupt
                btst    #CDSTSB_PLAYING,d0
                beq     99$

                move.b  db_AutoQ(db),d1                                 ; If we don't need subcode interrupts, turn them off

                btst    #CDSTSB_PAUSED,d0                               ; Don't fade or generate frame interrupts if paused
                bne     1$
                or.b    db_AutoFrame(db),d1
                or.b    db_AutoFade(db),d1
1$
                tst.b   d1                                              ; Generate subcode interrupts?
                bne     2$
                and.l   #-1-INTF_SUBCODE,CDINT2ENABLE(hb)
                and.l   #-1-CF_SUBCODE,CDCONFIG(hb)
                rts
2$
                or.l    #CF_SUBCODE,CDCONFIG(hb)                        ; ...otherwise, turn them on
                or.l    #INTF_SUBCODE,CDINT2ENABLE(hb)
99$             rts
                


************************************************************************
***
***  ClearSubcodeInterrupt
***
***     Turn all subcode interrupt off
***
************************************************************************

 FUNCTION ClearSubcodeInterrupt

                and.l   #-1-INTF_SUBCODE,CDINT2ENABLE(hb)
                and.l   #-1-CF_SUBCODE,CDCONFIG(hb)
                rts
                


************************************************************************
***
***  Disable
***
************************************************************************

 FUNCTION Disable

                save    a0                                              ; Disable interrupts
                DISABLE a0
                restore a0
                rts

************************************************************************
***
***  Enable
***
************************************************************************

 FUNCTION Enable

                save    a0                                              ; Enable interrupts
                ENABLE  a0
                restore a0
                rts


************************************************************************
***
***  DisableRX
***
************************************************************************

 FUNCTION DisableRX

                and.l   #-1-INTF_RXDMADONE,CDINT2ENABLE(hb)             ; Disable command receive interrupts
                move.b  #1,db_Disabled(db)
                rts

************************************************************************
***
***  EnableRX
***
************************************************************************

 FUNCTION EnableRX

                clr.b   db_Disabled(db)
                or.l    #INTF_RXDMADONE,CDINT2ENABLE(hb)                ; Enable command receive interrupts
                rts



************************************************************************
*                                                                      *
*   FlickerLight - Invert the state of the drive light                 *
*                                                                      *
************************************************************************

 FUNCTION FlickerLight

                move.w  #$0501,d0                                       ; Invert state of drive light
                tst.b   db_FlickerLight(db)
                beq     1$
                move.w  #$0500,d0
1$              not.b   db_FlickerLight(db)
                move.w  d0,db_Packet(db)
                clr.w   d0
                jmp     SendPacket


**********************************************************************************************************************
@


1.18
log
@Fixed bounds check that prevented getting to the last sector of the disk.
@
text
@d37 1
d492 1
d518 61
@


1.17
log
@no effective change.
@
text
@d356 1
a356 1
                bne     1$
d360 3
a362 1
                jsr     CheckSeekRange
d365 1
a365 1
1$
@


1.16
log
@no effective change.
@
text
@d20 1
a21 1
        INCLUDE "cd.i"
@


1.15
log
@no effective change.
@
text
@a498 1
;                bset.b  #MUTEB,CIAAPRA
a511 1
;                bclr.b  #MUTEB,CIAAPRA
@


1.14
log
@Frame interrupts are not genereted when cd us paused.
@
text
@a327 1

@


1.13
log
@Fixed fading and stuff.
@
text
@d559 3
a561 3
                move.b  db_AutoQ(db),d0                                 ; If we don't need subcode interrupts, turn them off
                or.b    db_AutoFrame(db),d0
                or.b    db_AutoFade(db),d0
d563 5
d571 1
a571 1
1$
@


1.12
log
@internal.i is now cdprivate.i
@
text
@d481 1
a481 1
                bhi     1$
a545 34
***  BumpFade
***
***     Adjust attenuator up or down to create the correct fade
***     effect.
***
************************************************************************

 FUNCTION BumpFade

                move.w  db_Attenuation(db),d0                           ; Load up registers
                move.w  db_TargetAttenuation(db),d1

                cmp.w   d0,d1                                           ; Fade up or down?
                bhi     2$

                sub.w   db_FadeStepSize(db),d0                          ; Fade down until we reach target
                cmp.w   d1,d0
                bhi     1$
                move.w  d1,d0
1$              move.w  d0,db_Attenuation(db)
                bra     4$
2$
                add.w   db_FadeStepSize(db),d0                          ; Fade up until we reach target
                cmp.w   d1,d0
                blo     3$
                move.w  d1,d0
3$              move.w  d0,db_Attenuation(db)
4$
                bsr     Attenuate                                       ; Set the attenuator
                rts


************************************************************************
***
d548 2
a549 2
***     Turn subcode interrupt on or off depending on state of relying
***     functions
d555 4
d569 16
@


1.11
log
@Added drive light to TOC.
@
text
@d20 1
a20 1
        INCLUDE "internal.i"
@


1.10
log
@Fixed problem with converting MSFtoLSN (or visa versa).
@
text
@d667 1
a667 1
                move.w  #$0501,d0
d669 1
@


1.9
log
@mute flip/flop.
@
text
@d36 1
d129 2
a130 1
        clr.l   d0                                                      ; Calculate Frame (Block)
d135 1
a135 1
        clr     d1                                                      ; Calculate Minute and Second
a577 1

d653 18
@


1.8
log
@Attenuate command should work (MUTE only).
@
text
@d498 1
d512 1
@


1.7
log
@Modified SetSubcodeInt and CheckSeekRange.
@
text
@d478 29
a506 1
                move.w  db_Attenuation(db),d0
d508 1
a508 1
                ; store this somewhere
d510 1
a510 1
                jsr     Enable                                          ; All done
d514 1
d528 1
a528 1
1$:             move.l  (a2),a2
d534 2
a535 2

2$:             restore a2                                              ; All done
@


1.6
log
@touched up functions.
@
text
@a326 1
                save    d2
d329 1
a329 5
                move.l  d0,d2
                move.l  db_TOC+TOCSummary_SIZE+TOCE_Position(db),d0
                jsr     MSFtoLSNPOS
                sub.l   d0,d2
                cmp.l   d2,d1
d332 1
a332 2
                restore d2                                              ; Seek within range
                clr.w   d0
d335 1
a335 2
                restore d2                                              ; Seek out of range
                move.w  #1,d0
a556 1
                DISABLE
d560 3
a562 3
                bne     IntOn
                and.l   #$FFFFFFFF-INTF_SUBCODE,CDINT2ENABLE(hb)
                ENABLE
d564 3
a566 3
IntOn
                or.l    #INTF_SUBCODE,CDINT2ENABLE(hb)                  ; ...otherwise, turn them on
                ENABLE
@


1.5
log
@Added some interrupt enable/disable functions.
@
text
@d24 2
d37 3
d432 1
d440 1
a440 1
                add.l   d0,d0                                           ; Allocate twice as much memory as we need
d448 1
a448 1
                add.l   d0,d0
a588 1
                move.b  #1,db_Disabled(db)
a600 1
                clr.b   db_Disabled(db)
d615 1
d626 1
@


1.4
log
@who knows.
@
text
@a415 27
***
***  Disable
***
************************************************************************

 FUNCTION Disable
                save    a6                                              ; Disable interrupts
                DISABLE a6
                restore a6
                rts


************************************************************************
***
***  Enable
***
************************************************************************

 FUNCTION Enable
                save    a6                                              ; Enable interrupts
                ENABLE  a6
                restore a6
                rts



************************************************************************
d571 52
@


1.3
log
@Includes now indirect, not absolute.
@
text
@d38 73
d123 1
a123 4
        add.l   #150,d0                                                 ; Add on 00:02:00

        move.l  d0,d1                                                   ; Calculate Frame (Block)
        clr.l   d0
a165 2
        sub.l   #150,d0                                                 ; Subtract off 00:02:00

d322 1
d324 6
a329 2
                jsr     MSFtoLSN
                cmp.l   d0,d1
d332 2
a333 1
                clr.w   d0                                              ; Seek within range
d336 2
a337 1
                move.w  #1,d0                                           ; Seek out of range
d347 2
a348 2
*** in:         D1.l = position
***             D2.l = length
d360 1
a360 1
                add.l   d2,d1
@


1.2
log
@all kinds of changes.
@
text
@d2 16
a17 16
        INCLUDE "include:exec/types.i"
        INCLUDE "include:exec/nodes.i"
        INCLUDE "include:exec/lists.i"
        INCLUDE "include:exec/ports.i"
        INCLUDE "include:exec/libraries.i"
        INCLUDE "include:exec/tasks.i"
        INCLUDE "include:exec/devices.i"
        INCLUDE "include:exec/memory.i"
        INCLUDE "include:exec/interrupts.i"
        INCLUDE "include:exec/resident.i"
        INCLUDE "include:exec/io.i"
        INCLUDE "include:exec/errors.i"
        INCLUDE "include:exec/execbase.i"
        INCLUDE "include:exec/ables.i"
        INCLUDE "include:hardware/intbits.i"
        INCLUDE "include:devices/trackdisk.i"
@


1.1
log
@Initial revision
@
text
@a37 124
***  MakeXL
***
***     Read requests are converted into transfer list requests.
***
************************************************************************

 FUNCTION MakeXL

                lea     db_XferNodes(db),a1                             ; Create initial skip entry (can be null)
                move.l  db_SkipBuf(db),CDXL_Buffer(a1)
                move.l  IO_OFFSET(ior),d0
                and.l   #BLOCK_SIZE-1,d0
                move.l  d0,CDXL_Length(a1)
                clr.l   CDXL_DoneFunc(a1)

                move.l  MLN_SUCC(a1),a1                                 ; Create transfer entry
                move.l  IO_DATA(ior),CDXL_Buffer(a1)
                move.l  IO_LENGTH(ior),CDXL_Length(a1)
                clr.l   CDXL_DoneFunc(a1)

                move.l  MLN_SUCC(a1),a1                                 ; Create end skip entry (can be null)
                move.l  db_SkipBuf(db),CDXL_Buffer(a1)
                move.l  IO_OFFSET(ior),d1
                add.l   IO_LENGTH(ior),d1
                and.l   #BLOCK_SIZE-1,d1
                move.l  #BLOCK_SIZE,d0
                sub.l   d1,d0
                cmp.l   #BLOCK_SIZE,d0
                bne.s   1$
                clear   d0
1$:             move.l  d0,CDXL_Length(a1)
                clr.l   CDXL_DoneFunc(a1)

                lea     db_XferNodes(db),a1                             ; Return pointer to first XL node
                rts

*
************************************************************************
***
***  StartXL
***
***     Fire up CD XL by beginning DMA for first segment.
***
***             A1 -> transfer list
***
************************************************************************

 FUNCTION StartXL

                save    a2                                              ; Save registers
1$:
                cmp.l   #0,a1                                           ; Are we done with list?
                beq     9$
                tst.l   MLN_SUCC(a1)
                beq     9$

                move.l  a1,db_XferEntry(db)                             ; Save entry pointer
                clr.l   CDXL_Actual(a1)

                move    #1,d1                                           ; Start transfer of an entry
                move.l  a1,a2
                bsr     StartTransfer
                bne     9$

                move.l  (a1),a1                                         ; If this entry's transfer is already complete, try the next entry
                tst.l   (a1)
                bne     1$

9$:             restore a2                                              ; Restore registers
                rts

*
************************************************************************
***     
***     StartTransfer - Start transfer of xfer list entry
***
***             D1 : 0 = (start transfer), 1 = (resume transfer)
***             a2 -> transfer list entry
***             A6 -> device node
***
***             Return D0 != 0 when xfer in progress
***             else, D0 == 0 when transfer list entry done.
***
***             Start to transfer the first or next part of
***             a transfer list entry.  Will transfer upto
***             maximum size and update entry accordingly.
***
************************************************************************

 FUNCTION StartTransfer

                move.l  CDXL_Length(a2),d0                              ; Determine number of bytes to transfer
                beq     7$
                move.l  d0,CDXL_Actual(a2)

                save    d1                                              ; Save transfer flag

                move.l  CDXL_Buffer(a2),d1

                move.l  d1,db_ARG3(db)                                  ; Store new address and transfer count
                move.l  d0,db_ARG4(db)

                cmp.b   #CDERR_ABORTED,db_ClassAErr(db)                 ; Has read command been aborted?
                beq     8$

                restore d1

                ; Start or resume DMA

                moveq.l #1,d0                                           ; Starting new transfer
                rts
7$
                cmp.l   #0,d0                                           ; Transfer complete
                rts
8$
                restore d1
                moveq.l #1,d0
                rts



*
************************************************************************
***
d138 160
d300 2
a301 1
***     in:     D1 = Priority
d439 1
a440 2
***     
***  GetQCodePacket
d442 1
a442 1
***     Get a valid time-encoded packet.
d444 1
a444 1
***     out: D0 == error
d447 74
a520 5

 FUNCTION GetQCodePacket

                move.w  #CDCMD_QCODE,db_CMD(db)                         ; Retrieve a raw Q-Code packet
                jsr     DoCmd
d522 1
a522 33




*
************************************************************************
***
***  GetClassAError
***
***     Get error status information. Return error code in D0.
***
************************************************************************

 FUNCTION GetClassAError
                move.b  db_DriveError(db),d0                            ; Don't destroy an existing error
                bne     1$
                move.b  d0,db_ClassAErr(db)
1$              rts


************************************************************************
***
***  GetClassDError
***
***     Get error status information. Return error code in D0.
***
************************************************************************

 FUNCTION GetClassDError
                move.b  db_DrivePlayError(db),d0                        ; Don't destroy an existing error
                bne     1$
                move.b  d0,db_ClassDErr(db)
1$              rts
@

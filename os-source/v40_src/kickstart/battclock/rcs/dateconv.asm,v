head     36.2;
branch   ;
access   ;
symbols  V39_3:36.2 V39_1:36.2 V36_7:36.1 V36_6:36.1 V36_5:36.1 V36_4:36.1 V36_3:35.1 V36_1:35.1;
locks    ; strict;
comment  @* @;


36.2
date     90.03.28.16.28.20;  author rsbx;  state Exp;
branches ;
next     36.1;

36.1
date     89.10.06.13.57.21;  author rsbx;  state Exp;
branches ;
next     35.1;

35.1
date     89.10.06.11.54.15;  author rsbx;  state Exp;
branches ;
next     ;


desc
@Date conversion routines. Will eventually become part of utility.library.
@


36.2
log
@RCS version change.
@
text
@	SECTION	BattClock

	XREF	UDivMod32
	XREF	UMult32

*$*	XDEF	CalcDate
	XDEF	Amiga2Date
	XDEF	Date2Amiga

;	#include <exec/types.h>

	INCLUDE	"exec/types.i"
	INCLUDE	"date.i"

;
;	struct ClockData
;		{
;		UWORD	sec;
;		UWORD	min;
;		UWORD	hour;
;		UWORD	mday;
;		UWORD	month;
;		UWORD	year;
;		UWORD	wday;
;		};
;
;
;	/*
;	 * an interesting and useful macro I designed (the 'function' form
;	 * is much simpler, but for constants everthing is resolved at compile
;	 * time).
;	 */
;	#define CALC_DATE(YEAR,MONTH,DAY) \
;	  ( \
;	  ((YEAR)-1+((MONTH)+9)/12)*365L \
;	  +((YEAR)-1+((MONTH)+9)/12)/4 \
;	  -((YEAR)-1+((MONTH)+9)/12)/100 \
;	  +((YEAR)-1+((MONTH)+9)/12)/400 \
;	  +((((MONTH)+9)%12)*306+5)/10 \
;	  +(DAY) \
;	  -1 \
;	 )
;
;
;	ULONG __asm CalcDate(register __d0 ULONG year, register __d1 ULONG month, register __d2 ULONG day)
;		{
CalcDate:
	MOVE.L    D2,-(A7)

;		month += 9;
;		year = year-1 + (month/12);
;		month %= 12;
;		month = month*306 +5;
;
;		return (year*365) + (year/4) - (year/100) + (year/400)
;			+ (month/10) + day -1;

	MOVE.L    D0,D2		; d2=year
	MOVEQ     #09,D0
	ADD.L     D1,D0		; d0=month+9
	MOVEQ     #12,D1
	JSR       UDivMod32	; d1=(month+9)%12
	ADD.L     D0,D2		; d2=year+(month+9)/12
	MOVE.L    #306,D0
	JSR       UMult32		; d0=((month+9)%12)*306
	ADDQ.L    #5,D0		; d0=((month+9)%12)*306+5
	MOVEQ     #10,D1
	JSR       UDivMod32	; d0=(((month+9)%12)*306+5)/10
	MOVE.L    D0,-(SP)
	SUBQ.L    #1,D2		; d2=year+(month+9)/12 -1
	MOVE.L    D2,D0		; d0=year+(month+9)/12 -1
	MOVEQ     #100,D1
	LSL.L     #2,D1
	JSR       UDivMod32	; d0=(year+(month+9)/12-1)/400
	MOVE.L    D0,-(SP)
	MOVE.L    D2,D0		; d0=year+(month+9)/12 -1
	MOVEQ     #100,D1
	JSR       UDivMod32	; d0=(year+(month+9)/12-1)/100
	MOVE.L    D0,-(SP)
	MOVE.L    D2,D1		; d1=year+(month+9)/12-1
	LSR.L     #2,D2		; d2=(year+(month+9)/12-1)/4
	MOVE.L    #365,D0
	JSR       UMult32		; d0=(year+(month+9)/12-1)*365

	ADD.L     D2,D0		; d0=d0 + (year+(month+9)/12-1)/4
	SUB.L     (SP)+,D0	; d0=d0 - (year+(month+9)/12-1)/100
	ADD.L     (SP)+,D0	; d0=d0 + (year+(month+9)/12-1)/400
	ADD.L     (SP)+,D0	; d0=d0 + (((month+9)%12)*306+5)/10
	MOVE.L    (A7)+,D2
	ADD.L     D2,D0		; d0=d0 + day
	SUBQ.L    #1,D0		; d0=d0 -1

;		}

	RTS

;
;
;	void __asm Amiga2Date(register __d0 ULONG amiga, register __a0 struct ClockData *cd)
;	  {

Amiga2Date:
	MOVEM.L   D2-D3,-(A7)

;	   LONG year;
;	   LONG month;
;	   LONG day;
;	   LONG hour;
;	   LONG minute;
;	   LONG second;
;	   LONG absday;
;	   LONG work;
;
;
;	   second  = amiga % 60;
;	   minute  = amiga / 60;
;	   hour    = minute / 60;
;	   minute %= 60;
;	   hour   %= 24;
;
;	   absday  = amiga / (60 * 60 * 24);
;	   absday += CALC_DATE(1978,1,1);      /* add the Amiga date offset */
;
;	   cd->wday  = (absday+3)%7
;
;	   /*
;	    * the following date logic took a great deal of time to develop and prove
;	    * so please give credit if you use it. That also includes the macro
;	    * in AmigArc.h.
;	    * NOTE: the following were split into separate lines for aestetic(sp?)
;	    * reasons as well as to prevent the (sometimes very stupid!) 'C'
;	    * optimizer from changing the math (implicit floor function is very
;	    * important!).
;	    */
;
;	   /* find year */
;	   work = absday;
;	   work -= (absday + 1) / 146097;   /* subtract quadcentury leap days */
;	   work += work / 36524;            /* add century leap days */
;	   work -= (work + 1) / 1461;       /* subtract all leap days */
;	   year = work / 365;
;
;	   /* find day of year */
;	   work = absday;
;	   work -= year * 365;
;	   work -= year / 4;
;	   work += year / 100;
;	   work -= year / 400;
;
;	   /* find month */
;	   month = work / 153;
;	   month *= 5;
;	   month += 10 * (work % 153) / 305;
;
;	   /* find day */
;	   day = 1 + work;
;	   day -= (long)((month * 306 + 5) / 10);
;
;	   /* adjust to Gregorian calendar */
;	   month += 2;
;	   year += month / 12;
;	   month %= 12;
;	   month++;
;
;
;
;	   cd->sec   = second;

	MOVEQ     #60,D1
	JSR       UDivMod32
	MOVE.W    D1,sec(A0)	; d1=amiga%60
	MOVEQ     #60,D1	; d0=amiga/60
	JSR       UDivMod32

;	   cd->min   = minute;

	MOVE.W    D1,min(A0)	; d1=(amiga/60)%60
	MOVEQ     #24,D1	; d0=amiga/3600
	JSR       UDivMod32

;	   cd->hour  = hour;

	MOVE.W    D1,hour(A0)	; d1=(amiga/3600)%24
	ADDI.L    #722390,D0	; d0=absday
	MOVE.L    D0,-(SP)
	MOVE.L    D0,D2
	ADDQ.L    #3,D0		; offset so sunday = 0
	MOVEQ.L   #7,d1
	JSR       UDivMod32	; d1=dayofweek
	MOVE.W    d1,wday(A0)
	MOVE.L    D2,D0
	ADDQ.L    #1,D0
	MOVE.L    #146097,D1
	JSR       UDivMod32	; d0=(absday+1)/146097
	SUB.L     D0,D2		; work=absday - (absday+1)/146097
	MOVE.L    D2,D0
	MOVE.L    #36524,D1
	JSR       UDivMod32
	ADD.L     D0,D2		; work += work/36524
	MOVE.L    D2,D0
	ADDQ.L    #1,D0
	MOVE.L    #1461,D1
	JSR       UDivMod32
	SUB.L     D0,D2		; work -= (work+1)/1461
	MOVE.L    D2,D0
	MOVE.L    #365,D1
	JSR       UDivMod32

	MOVE.L    (SP)+,D2	; work = absday
	MOVE.L    D0,-(SP)	; save year
	MOVE.L    D0,D3
	MOVEQ     #100,D1
	LSL.L     #2,D1
	JSR       UDivMod32	; d0=year/400
	SUB.L     D0,D2		; work-=year/400
	MOVE.L    D3,D0
	MOVEQ     #100,D1
	JSR       UDivMod32	; d0=year/100
	ADD.L     D0,D2		; work+=year/100
	MOVE.L    D3,D0
	LSR.L     #2,D0
	SUB.L     D0,D2		; work-=year/4
	MOVE.L    D3,D0
	MOVE.L    #365,D1
	JSR       UMult32		; d0=year*365
	SUB.L     D0,D2		; d2=work-=year*365

	MOVE.L    D2,D0
	MOVEQ     #102,D1
	NOT.B     D1
	JSR       UDivMod32	; d1=work%153
	MOVEQ     #10,D0
	JSR       UMult32		; d0=10*(work%153)
	MOVE.L    #305,D1
	JSR       UDivMod32	; d0=10*(work%153)/305
	MOVE.L    D0,D3
	MOVE.L    D2,D0
	MOVEQ     #102,D1
	NOT.B     D1
	JSR       UDivMod32	; d0=work/153
	MOVE.L    D0,D1
	LSL.L     #2,D1
	ADD.L     D0,D1		; d1=5*(work/153)
	ADD.L     D1,D3		; d2==work; d3==month

	MOVE.L    #306,D0
	MOVE.L    D3,D1
	JSR       UMult32		; d0=month*306
	ADDQ.L    #5,D0		; d0=month*306+5
	MOVEQ     #10,D1
	JSR       UDivMod32	; d0=(month*306+5)/10
	ADDQ.L    #1,D2
	SUB.L     D0,D2		; d2=day=1+work-(month*306+5)/10

;	   cd->day   = day;

	MOVE.W    D2,mday(A0)
	MOVE.L    D3,D0
	ADDQ.L    #2,D0
	MOVEQ     #12,D1
	JSR       UDivMod32	; d0=(month+2)/12
	ADDQ.L    #1,D1		; d1=(month+2)%12 +1

;	   cd->month = month;

	MOVE.W    D1,month(A0)
	ADD.L     (SP)+,D0	; year+=(month+2)/12

;	   cd->year  = year;

	MOVE.W    D0,year(A0)

;	  }

	MOVEM.L   (A7)+,D2-D3
	RTS

;
;
;	ULONG __asm Date2Amiga(register __a0 struct ClockData *cd)
;	  {

Date2Amiga:
	MOVEM.L   D2-D3,-(A7)

;	   return (((CalcDate(cd->year,cd->month,cd->day) - CALC_DATE(1978,1,1))

	MOVEQ     #00,D0
	MOVE.W    year(A0),D0
	MOVEQ     #00,D1
	MOVE.W    month(A0),D1
	MOVEQ     #00,D2
	MOVE.W    mday(A0),D2
	BSR       CalcDate

;	         * 24 + cd->hour) * 60 + cd->min) * 60 + cd->sec;

	MOVEQ     #00,D1
	MOVE.W    sec(A0),D1
	MOVEQ     #00,D2
	MOVE.W    min(A0),D2
	MOVEQ     #00,D3
	MOVE.W    hour(A0),D3
	SUBI.L    #722390,D0
	MOVE.L    D1,-(A7)
	MOVEQ     #24,D1
	JSR       UMult32
	ADD.L     D3,D0
	MOVEQ     #60,D1
	JSR       UMult32
	ADD.L     D2,D0
	MOVEQ     #60,D1
	JSR       UMult32
	MOVE.L    (A7)+,D1
	ADD.L     D1,D0

;	  }

	MOVEM.L   (A7)+,D2-D3
	RTS


	END
@


36.1
log
@Fixed revision number.
@
text
@@


35.1
log
@Initial checkin.
@
text
@@

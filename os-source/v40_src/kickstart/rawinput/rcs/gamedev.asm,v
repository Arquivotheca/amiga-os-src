head     36.8;
branch   ;
access   ;
symbols  V36_14:36.2 V36_13:36.2 V36_11:36.2 L36_5:35.1;
locks    ; strict;
comment  @*   @;


36.8
date     90.05.04.10.30.27;  author kodiak;  state Exp;
branches ;
next     36.7;

36.7
date     90.04.13.12.43.29;  author kodiak;  state Exp;
branches ;
next     36.6;

36.6
date     90.04.02.16.28.25;  author kodiak;  state Exp;
branches ;
next     36.5;

36.5
date     90.04.02.12.56.44;  author kodiak;  state Exp;
branches ;
next     36.4;

36.4
date     90.01.04.13.26.24;  author kodiak;  state Exp;
branches ;
next     36.3;

36.3
date     89.11.10.13.39.16;  author kodiak;  state Exp;
branches ;
next     36.2;

36.2
date     88.11.03.12.35.17;  author kodiak;  state Exp;
branches ;
next     35.1;

35.1
date     88.08.02.12.26.50;  author kodiak;  state Exp;
branches ;
next     35.0;

35.0
date     87.10.26.11.30.50;  author kodiak;  state Exp;
branches ;
next     ;


desc
@@


36.8
log
@remove gameport unit exclusion, but keep initialization upon first open
@
text
@	TTL	'$Id: gamedev.asm,v 36.7 90/04/13 12:43:29 kodiak Exp Locker: kodiak $'
**********************************************************************
*
*			---------------
*   gameport.asm	GAMEPORT DEVICE		device functions
*			---------------
*
*   Copyright 1985, 1987 Commodore-Amiga Inc.
*
*   Source Control	$Locker: kodiak $
*
*   $Log:	gamedev.asm,v $
*   Revision 36.7  90/04/13  12:43:29  kodiak
*   use Id instead of Header for 4.x rcs
*   
*   Revision 36.6  90/04/02  16:28:25  kodiak
*   back to using dd_ExecBase, not SYSBASE (4)
*   
*   Revision 36.5  90/04/02  12:56:44  kodiak
*   for rcs 4.x header change
*   
*   Revision 36.4  90/01/04  13:26:24  kodiak
*   keep seperate unit open count for exclusive access and fix exclusive access
*   
*   Revision 36.3  89/11/10  13:39:16  kodiak
*   make gameport exclusive access
*   
*   Revision 36.2  88/11/03  12:35:17  kodiak
*   use AUTOINIT in ResidentTag
*   
*   Revision 35.1  88/08/02  12:26:50  kodiak
*   use SYSBASE (4) vs. dd_ExecBase
*   (a poor decision, in hindsight)
*   
*   Revision 35.0  87/10/26  11:30:50  kodiak
*   initial from V34, but w/ stripped log
*
**********************************************************************

	SECTION		rawinput

*------ Included Files -----------------------------------------------

	INCLUDE		"exec/types.i"
	INCLUDE		"exec/nodes.i"
	INCLUDE		"exec/lists.i"
	INCLUDE		"exec/memory.i"
	INCLUDE		"exec/ports.i"
	INCLUDE		"exec/libraries.i"
	INCLUDE		"exec/devices.i"
	INCLUDE		"exec/tasks.i"
	INCLUDE		"exec/io.i"
	INCLUDE		"exec/strings.i"
	INCLUDE		"exec/interrupts.i"
	INCLUDE		"exec/errors.i"
	INCLUDE		"exec/initializers.i"
	INCLUDE		"exec/execbase.i"

	INCLUDE		"hardware/cia.i"
	INCLUDE		"hardware/intbits.i"

	INCLUDE		"resources/potgo.i"

	INCLUDE		"macros.i"
	INCLUDE		"gpdata.i"


*------ Imported Globals ---------------------------------------------

	XREF		_ciaa

	XREF		GDName
	XREF		VERSION
	XREF		REVISION


*------ Imported Functions -------------------------------------------

	XREF_EXE	AddIntServer
	XREF_EXE	OpenResource
	XREF_EXE	RemIntServer

	XREF		BeginIO
	XREF		AbortIO
	XREF		SuccessfulExpunge
	XREF		DeferredExpunge
	XREF		ExtFunc
	XREF		Close

	XREF		Invalid
	XREF		ResetCmd
	XREF		Read
	XREF		Write
	XREF		Update
	XREF		GDClear
	XREF		StopCmd
	XREF		Start
	XREF		Flush

	XREF		GDReadEvent
	XREF		GDAskCType
	XREF		GDSetCType
	XREF		GDAskTrigger
	XREF		GDSetTrigger

	XREF		CBInvalid
	XREF		CBResetCmd
	XREF		CBRead
	XREF		CBWrite
	XREF		CBUpdate
	XREF		GDCBClear
	XREF		CBStopCmd
	XREF		CBStart
	XREF		CBFlush

	XREF		GDCBReadEvent
	XREF		GDCBAskCType
	XREF		GDCBSetCType
	XREF		GDCBAskTrigger
	XREF		GDCBSetTrigger

	XREF		GDInterrupt


*------ Exported Globals ---------------------------------------------

	XDEF		GDFuncInit
	XDEF		GDStructInit
	XDEF		GDInit


**********************************************************************
GDInit:
		movem.l a6,-(sp)
*	    ;------ This is called from within MakeLibrary, after
*	    ;------ all the memory has been allocated

		exg	d0,a6			; save device library pointer
		move.l	d0,dd_ExecBase(a6)
		move.l	a0,dd_Segment(a6)

*	    ;------ get the potgo resource
		lea	PRName(pc),a1
		LINKEXE	OpenResource
		move.l	d0,gd_PotgoResource(a6)

*	    ;------ initialize the unit command queues
		lea	gd_Unit0+MP_MSGLIST(a6),a0
		NEWLIST	a0
		lea	gd_Unit1+MP_MSGLIST(a6),a0
		NEWLIST	a0

*	;-- set up the vertical blank interrupt routine for gameport
*	    ;-- constant initialization already done
		move.l	a6,gd_IS+IS_DATA(a6)
		moveq	#INTB_VERTB,d0
		lea	gd_IS(a6),a1
		LINKEXE AddIntServer

*       ;-- set 6522 GamePort button 0s to input
		move.l	#_ciaa,a0
CIAFS_GAMEPORT	EQU	CIAF_GAMEPORT0!CIAF_GAMEPORT1
	    ;------- port A button pins are input
		andi.b	#(~CIAFS_GAMEPORT)&$0FF,ciaddra(a0)
		or.b	#CIAFS_GAMEPORT,ciapra(a0)	; pulled high

		move.l	a6,d0		    ;return library pointer
		movem.l (sp)+,a6
		rts

PRName		POTGONAME


*----------------------------------------------------------------------
GDStructInit:
*	;------ initialize the device library structure
		INITBYTE	LN_TYPE,NT_DEVICE
		INITLONG	LN_NAME,GDName
		INITBYTE	LIB_FLAGS,LIBF_SUMUSED!LIBF_CHANGED
		INITWORD	LIB_VERSION,VERSION
		INITWORD	LIB_REVISION,REVISION

		INITLONG	dd_CmdVectors,gdCmdTable
		INITLONG	dd_CmdBytes,gdCmdBytes
		INITWORD	dd_NumCommands,GD_NUMCOMMANDS

*	;------ initialize kb_IS
		INITBYTE	gd_IS+LN_TYPE,NT_INTERRUPT
		INITBYTE	gd_IS+LN_PRI,GPIS_PRIORITY
		INITLONG	gd_IS+LN_NAME,GDName
		INITLONG	gd_IS+IS_CODE,GDInterrupt

*	;------ initialize the unit command queues
		INITBYTE	gd_Unit0+LN_TYPE,NT_MSGPORT
		INITLONG	gd_Unit0+LN_NAME,GDName
		INITBYTE	gd_Unit1+LN_TYPE,NT_MSGPORT
		INITLONG	gd_Unit1+LN_NAME,GDName

*       ;------ Initialize the units
		INITBYTE	gd_Unit1+gu_Flags,UF_RIGHTPORT

		dc.w	0


GDFuncInit:
		dc.w	-1

		dc.w	GDOpen-GDFuncInit
		dc.w	GDClose-GDFuncInit
		dc.w	GDExpunge-GDFuncInit
		dc.w	ExtFunc+(*-GDFuncInit)

		dc.w	BeginIO+(*-GDFuncInit)
		dc.w	AbortIO+(*-GDFuncInit)

		dc.w	-1


gdCmdTable:
		dc.l	Invalid
		dc.l	ResetCmd
		dc.l	Read
		dc.l	Write
		dc.l	Update
		dc.l	GDClear
		dc.l	StopCmd
		dc.l	Start
		dc.l	Flush

		dc.l	GDReadEvent
		dc.l	GDAskCType
		dc.l	GDSetCType
		dc.l	GDAskTrigger
		dc.l	GDSetTrigger

gdCmdBytes:
		dc.b	CBInvalid
		dc.b	CBResetCmd
		dc.b	CBRead
		dc.b	CBWrite
		dc.b	CBUpdate
		dc.b	GDCBClear
		dc.b	CBStopCmd
		dc.b	CBStart
		dc.b	CBFlush

		dc.b	GDCBReadEvent
		dc.b	GDCBAskCType
		dc.b	GDCBSetCType
		dc.b	GDCBAskTrigger
		dc.b	GDCBSetTrigger

GD_NUMCOMMANDS	EQU	(*-gdCmdBytes)

		ds.w	0


*------ gameport.device/Open *****************************************
*
*   NAME
*	Open - a request to open the GamePort device
*
*   SYNOPSIS
*	OpenDevice("gameport.device", unit, iORequest, 0)
*
*   FUNCTION
*	The open routine grants access to a device.  There are two
*	fields in the iORequest block that will be filled in: the
*	IO_DEVICE field and the IO_UNIT field.
*
*	The device open count will be incremented.  The device cannot
*	be expunged unless this open is matched by a Close device.
*
*   INPUTS
*	unit  -
*		0   unit associated with left game port controller
*		1   unit associated with right game port controller
*
*   RESULTS
*	If the open was unsuccessful, IO_ERROR will be set, IO_UNIT
*	and IO_DEVICE will not be valid.
*
**********************************************************************
GDOpen:
		move.l	a2,-(a7)
		bclr	#LIBB_DELEXP,LIB_FLAGS(a6)

		;-- this device has two units, determine which one
		tst.l	d0
		bne.s	oTry1
		lea	gd_Unit0(a6),a2
		bra.s	oGotUnit
oTry1:
		subq.l	#1,d0
		bne.s	oOpenFail
		lea	gd_Unit1(a6),a2

oGotUnit:
		tst.w	gu_OpenCnt(a2)
		bne.s	oNoteOpen
	
		;-- clear controller variables for first open
		lea	gu_AccumTimeout(a2),a0
		moveq	#gt_SIZEOF/2-1,d0
oInitTLoop:
		clr.w	(a0)+
		dbf	d0,oInitTLoop
		clr.b	gu_Type(a2)	; show no controller allocated

oNoteOpen:
		addq.w	#1,LIB_OPENCNT(a6)
		addq.w	#1,gu_OpenCnt(a2)
		move.l	a2,IO_UNIT(a1)
		moveq	#0,d0

oRts:
		move.b	d0,IO_ERROR(a1)
		move.l	(a7)+,a2
		rts

oOpenFail:
		moveq   #IOERR_OPENFAIL,d0
		bra.s	oRts

*------ Expunge ------------------------------------------------------
*
*   NAME
*	Expunge - indicate a desire to remove this device
*
*   SYNOPSIS
*	segment = Expunge(), gameportDev
*	d0		     a6
*
*   FUNCTION
*	The Expunge routine is called when a user issues a RemDevice
*	call.  The existance of any other users of the device, as
*	determined by the library open count being non-zero, will
*	cause the Expunge to be deferred.  When the device is not
*	in use, or no longer in use, the Expunge is actually
*	performed, and the device removed from the system list.
*
*---------------------------------------------------------------------
GDExpunge:
	    ;-- see if any one is using the device
		tst.w	LIB_OPENCNT(a6)
		bne	DeferredExpunge

*	    ;-- this is really it.  Free up all the resources

*		;-- remove the interrupt service routine
		lea	gd_IS(a6),a1
		moveq	#INTB_VERTB,d0
		LINKEXE	RemIntServer
		bra	SuccessfulExpunge


*------ Close --------------------------------------------------------
GDClose:
		move.l	IO_UNIT(a1),a0
		subq.w	#1,gu_OpenCnt(a0)
		bra	Close


	END
@


36.7
log
@use Id instead of Header for 4.x rcs
@
text
@d1 1
a1 1
	TTL	'$Id$
d13 3
d300 2
a301 9
		beq.s	oFirstOpen

		;-- inhibit non-exclusive access
		move.l	dd_ExecBase(a6),a0
		move.l	ThisTask(a0),d1
		cmp.l	gu_OwnerTask(a2),d1
		beq.s	oAllowOpen
		bra.s	oOpenFail

a302 1
oFirstOpen:
a308 1
		move.l	dd_ExecBase(a6),a0
d310 1
a310 1
oAllowOpen:
a312 1
		move.l	ThisTask(a0),gu_OwnerTask(a2)
@


36.6
log
@back to using dd_ExecBase, not SYSBASE (4)
@
text
@d1 1
a1 1
	TTL	'$Header: /usr2/kodiak/src/KS/rawinput/RCS/gamedev.asm,v 36.5 90/04/02 12:56:44 kodiak Exp Locker: kodiak $'
d13 3
@


36.5
log
@for rcs 4.x header change
@
text
@d1 1
a1 1
	TTL	'$Header: /usr2/kodiak/src/KS/rawinput/RCS/gamedev.asm,v 36.4 90/01/04 13:26:24 kodiak Exp Locker: kodiak $'
d13 3
d132 2
a133 1
		move.l	d0,a6			; save device library pointer
d297 1
a297 1
		move.l	SYSBASE,a0
d311 1
a311 1
		move.l	SYSBASE,a0
@


36.4
log
@keep seperate unit open count for exclusive access and fix exclusive access
@
text
@d1 1
a1 1
	TTL	'$Header: gamedev.asm,v 36.3 89/11/10 13:39:16 kodiak Exp $'
d13 3
@


36.3
log
@make gameport exclusive access
@
text
@d1 1
a1 1
	TTL	'$Header: gamedev.asm,v 36.2 88/11/03 12:35:17 kodiak Exp $'
d13 3
d196 1
a196 1
		dc.w	Close+(*-GDFuncInit)
a244 32
*------ Expunge ------------------------------------------------------
*
*   NAME
*	Expunge - indicate a desire to remove this device
*
*   SYNOPSIS
*	segment = Expunge(), gameportDev
*	d0		     a6
*
*   FUNCTION
*	The Expunge routine is called when a user issues a RemDevice
*	call.  The existance of any other users of the device, as
*	determined by the library open count being non-zero, will
*	cause the Expunge to be deferred.  When the device is not
*	in use, or no longer in use, the Expunge is actually
*	performed, and the device removed from the system list.
*
*---------------------------------------------------------------------
GDExpunge:
	    ;-- see if any one is using the device
		tst.w	LIB_OPENCNT(a6)
		bne	DeferredExpunge

*	    ;-- this is really it.  Free up all the resources

*		;-- remove the interrupt service routine
		lea	gd_IS(a6),a1
		moveq	#INTB_VERTB,d0
		LINKEXE	RemIntServer
		bra	SuccessfulExpunge


d286 1
a286 1
		tst.w	LIB_OPENCNT(a6)
d308 1
d321 39
@


36.2
log
@use AUTOINIT in ResidentTag
@
text
@d1 1
a1 1
	TTL	'$Header: gamedev.asm,v 35.1 88/08/02 12:26:50 kodiak Exp $'
d13 3
d42 1
d119 1
a119 1
		MOVEM.L A6,-(SP)
d123 1
a123 1
		MOVE.L	D0,A6			; save device library pointer
d127 1
a127 1
		LEA	PRName(PC),A1
d129 1
a129 1
		MOVE.L	D0,gd_PotgoResource(A6)
d132 4
a135 4
		LEA	gd_Unit0+MP_MSGLIST(A6),A0
		NEWLIST	A0
		LEA	gd_Unit1+MP_MSGLIST(A6),A0
		NEWLIST	A0
d139 3
a141 3
		MOVE.L	A6,gd_IS+IS_DATA(A6)
		MOVEQ	#INTB_VERTB,D0
		LEA	gd_IS(A6),A1
d145 1
a145 1
		MOVE.L	#_ciaa,A0
d148 2
a149 2
		ANDI.B	#(~CIAFS_GAMEPORT)&$0FF,ciaddra(A0)
		OR.B	#CIAFS_GAMEPORT,ciapra(A0)	; pulled high
d151 3
a153 3
		MOVE.L	A6,D0		    ;return library pointer
		MOVEM.L (SP)+,A6
		RTS
d186 1
a186 1
		DC.W	0
d190 1
a190 1
		DC.W	-1
d192 4
a195 4
		DC.W	GDOpen-GDFuncInit
		DC.W	Close+(*-GDFuncInit)
		DC.W	GDExpunge-GDFuncInit
		DC.W	ExtFunc+(*-GDFuncInit)
d197 2
a198 2
		DC.W	BeginIO+(*-GDFuncInit)
		DC.W	AbortIO+(*-GDFuncInit)
d200 1
a200 1
		DC.W	-1
d204 9
a212 9
		DC.L	Invalid
		DC.L	ResetCmd
		DC.L	Read
		DC.L	Write
		DC.L	Update
		DC.L	GDClear
		DC.L	StopCmd
		DC.L	Start
		DC.L	Flush
d214 5
a218 5
		DC.L	GDReadEvent
		DC.L	GDAskCType
		DC.L	GDSetCType
		DC.L	GDAskTrigger
		DC.L	GDSetTrigger
d221 9
a229 9
		DC.B	CBInvalid
		DC.B	CBResetCmd
		DC.B	CBRead
		DC.B	CBWrite
		DC.B	CBUpdate
		DC.B	GDCBClear
		DC.B	CBStopCmd
		DC.B	CBStart
		DC.B	CBFlush
d231 5
a235 5
		DC.B	GDCBReadEvent
		DC.B	GDCBAskCType
		DC.B	GDCBSetCType
		DC.B	GDCBAskTrigger
		DC.B	GDCBSetTrigger
d239 1
a239 1
		DS.W	0
d249 1
a249 1
*	D0		     A6
d262 2
a263 2
		TST.W	LIB_OPENCNT(A6)
		BNE	DeferredExpunge
d268 2
a269 2
		LEA	gd_IS(A6),A1
		MOVEQ	#INTB_VERTB,D0
d271 1
a271 1
		BRA	SuccessfulExpunge
d301 2
a302 11
*	    ;-- this device has two units
		TST.L	D0
		BNE.S	tu1
		LEA	gd_Unit0(A6),A0
		BRA.S	ou
tu1:
		SUBQ	#1,D0
		BNE.S	openFail
		LEA	gd_Unit1(A6),A0
ou:
		MOVE.L	A0,IO_UNIT(A1)
d304 9
a312 2
		BCLR	#LIBB_DELEXP,LIB_FLAGS(A6)
		ADDQ.W	#1,LIB_OPENCNT(A6)
d314 27
d342 3
a344 1
		RTS
d346 3
a348 4
openFail:
		MOVEQ   #IOERR_OPENFAIL,D0
		MOVE.B	D0,IO_ERROR(A1)
		BRA.S	oRts
d350 1
a350 1
		END
@


35.1
log
@use SYSBASE (4) vs. dd_ExecBase
(a poor decision, in hindsight)
@
text
@d1 1
a1 1
	TTL	'$Header: gameport.asm,v 35.0 87/10/26 11:32:27 kodiak Exp $'
d10 1
a10 1
*   Source Control	$Locker:  $
d13 4
a63 1
	XREF		Init
d108 2
a114 8
		MOVEM.L	A0/A2,-(A7)
		LEA	gdFuncInit(PC),A0
		LEA	gdStructInit(PC),A1
		LEA	gdInit(PC),A2
		MOVE.L	#gd_SIZEOF,D0
		BRA	Init

gdInit:
d120 1
d155 1
a155 1
gdStructInit:
d185 1
a185 1
gdFuncInit:
d188 4
a191 4
		DC.W	GDOpen-gdFuncInit
		DC.W	Close+(*-gdFuncInit)
		DC.W	GDExpunge-gdFuncInit
		DC.W	ExtFunc+(*-gdFuncInit)
d193 2
a194 2
		DC.W	BeginIO+(*-gdFuncInit)
		DC.W	AbortIO+(*-gdFuncInit)
@


35.0
log
@initial from V34, but w/ stripped log
@
text
@d1 1
a1 1
	TTL    '$Header: gamedev.asm,v 33.1 86/06/18 12:26:21 kodiak Exp $'
a2 11
*								     *
*   Copyright 1985, Commodore-Amiga Inc.   All rights reserved.	     *
*   No part of this program may be reproduced, transmitted,	     *
*   transcribed, stored in retrieval system, or translated into	     *
*   any language or computer language, in any form or by any	     *
*   means, electronic, mechanical, magnetic, optical, chemical,	     *
*   manual or otherwise, without the prior written permission of     *
*   Commodore-Amiga Incorporated, 983 University Ave. Building #D,   *
*   Los Gatos, California, 95030				     *
*								     *
**********************************************************************
d4 3
a6 1
*	gameport device functions
d8 1
a8 3
*   Source Control
*   --------------
*   $Header: gamedev.asm,v 33.1 86/06/18 12:26:21 kodiak Exp $
d10 1
a10 1
*   $Locker:  $
d13 2
d122 1
a122 3
		MOVE.L	D0,A0
		MOVE.L	A6,dd_ExecBase(A0)
		MOVE.L	D0,A6			; library pointer
@

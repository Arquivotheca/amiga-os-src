head     36.10;
branch   ;
access   ;
symbols  V36_14:36.2 V36_13:36.2 V36_11:36.2 L36_5:35.0;
locks    ; strict;
comment  @*   @;


36.10
date     91.02.12.10.28.01;  author darren;  state Exp;
branches ;
next     36.9;

36.9
date     90.04.13.12.43.32;  author kodiak;  state Exp;
branches ;
next     36.8;

36.8
date     90.04.09.09.43.51;  author kodiak;  state Exp;
branches ;
next     36.7;

36.7
date     90.04.02.12.56.29;  author kodiak;  state Exp;
branches ;
next     36.6;

36.6
date     90.01.29.09.29.17;  author kodiak;  state Exp;
branches ;
next     36.5;

36.5
date     90.01.04.13.27.22;  author kodiak;  state Exp;
branches ;
next     36.4;

36.4
date     89.11.10.13.39.03;  author kodiak;  state Exp;
branches ;
next     36.3;

36.3
date     89.07.12.13.27.42;  author kodiak;  state Exp;
branches ;
next     36.2;

36.2
date     88.11.03.12.35.13;  author kodiak;  state Exp;
branches ;
next     35.0;

35.0
date     87.10.26.11.30.30;  author kodiak;  state Exp;
branches ;
next     ;


desc
@@


36.10
log
@Fix spelling error - AUTODOCS
@
text
@	TTL	'$Id: gamecmds.asm,v 36.9 90/04/13 12:43:32 kodiak Exp Locker: darren $
**********************************************************************
*								     *
*   Copyright 1985, Commodore-Amiga Inc.   All rights reserved.	     *
*   No part of this program may be reproduced, transmitted,	     *
*   transcribed, stored in retrieval system, or translated into	     *
*   any language or computer language, in any form or by any	     *
*   means, electronic, mechanical, magnetic, optical, chemical,	     *
*   manual or otherwise, without the prior written permission of     *
*   Commodore-Amiga Incorporated, 983 University Ave. Building #D,   *
*   Los Gatos, California, 95030				     *
*								     *
**********************************************************************
*
*	gameport device commands
*
*   Source Control
*   --------------
*   $Id: gamecmds.asm,v 36.9 90/04/13 12:43:32 kodiak Exp Locker: darren $
*
*   $Locker: darren $
*
*   $Log:	gamecmds.asm,v $
*   Revision 36.9  90/04/13  12:43:32  kodiak
*   use Id instead of Header for 4.x rcs
*   
*   Revision 36.8  90/04/09  09:43:51  kodiak
*   avoid implicit $4 reference by converting DISABLE/ENABLE to NOFETCH form
*   
*   Revision 36.7  90/04/02  12:56:29  kodiak
*   for rcs 4.x header change
*   
*   Revision 36.6  90/01/29  09:29:17  kodiak
*   rename [Gg]ameportTrigger to [Gg]amePortTrigger in autodocs
*   
*   Revision 36.5  90/01/04  13:27:22  kodiak
*   polish autodoc headers
*   
*   Revision 36.4  89/11/10  13:39:03  kodiak
*   make gameport exclusive access
*   
*   Revision 36.3  89/07/12  13:27:42  kodiak
*   have input task ReadEvent multiple events from gameport
*   (and have it work)
*   
*   Revision 36.2  88/11/03  12:35:13  kodiak
*   use AUTOINIT in ResidentTag
*   
*   Revision 35.0  87/10/26  11:30:30  kodiak
*   initial from V34, but w/ stripped log
*   
*   
**********************************************************************

	SECTION		rawinput

*------ Included Files -----------------------------------------------

	INCLUDE		"exec/types.i"
	INCLUDE		"exec/nodes.i"
	INCLUDE		"exec/lists.i"
	INCLUDE		"exec/memory.i"
	INCLUDE		"exec/ports.i"
	INCLUDE		"exec/libraries.i"
	INCLUDE		"exec/devices.i"
	INCLUDE		"exec/tasks.i"
	INCLUDE		"exec/io.i"
	INCLUDE		"exec/strings.i"
	INCLUDE		"exec/interrupts.i"
	INCLUDE		"exec/ables.i"
	INCLUDE		"exec/errors.i"

	INCLUDE		"hardware/custom.i"

	INCLUDE		"devices/timer.i"
	INCLUDE		"devices/inputevent.i"

	INCLUDE		"gameport.i"

	INCLUDE		"macros.i"
	INCLUDE		"gpdata.i"


*------ Imported Names -----------------------------------------------

	XREF		_custom
	XREF		_intena


*------ Imported Functions -------------------------------------------

	XREF_PGR	AllocPotBits
	XREF_PGR	FreePotBits

	XREF		EndCommand


*------ Exported Names -----------------------------------------------

*------ Functions ----------------------------------------------------

	XDEF		GDClear
	XDEF		GDReadEvent
	XDEF		GDAskCType
	XDEF		GDSetCType
	XDEF		GDAskTrigger
	XDEF		GDSetTrigger

	XDEF		GDCBClear
	XDEF		GDCBReadEvent
	XDEF		GDCBAskCType
	XDEF		GDCBSetCType
	XDEF		GDCBAskTrigger
	XDEF		GDCBSetTrigger

*****i* gameport.device/CMD_CLEAR ************************************
*
*   NAME
*	Clear - clear gameport input buffer
*
*   FUNCTION
*	Remove from the input buffer any gameport reports waiting to
*	satisfy read requests.
*
*   IO REQUEST
*	io_Message	mn_ReplyPort set if quick I/O is not possible
*	io_Device	preset by the call to OpenDevice
*	io_Unit		preset by the call to OpenDevice
*	io_Command	CMD_CLEAR
*	io_Flags	IOB_QUICK set if quick I/O is possible
*
**********************************************************************
GDCBClear	EQU	-1

GDClear:
		move.l	IO_UNIT(a1),a0
		move.l	dd_ExecBase(a6),a1
		DISABLE	a1,NOFETCH
		clr.w	gu_BufHead(a0)
		clr.w	gu_BufTail(a0)
		ENABLE	a1,NOFETCH
		move.l	a3,a1		; restore IORequest
		bra	EndCommand


******* gameport.device/GPD_READEVENT ********************************
*
*   NAME
*	GPD_READEVENT -- Return the next game port event.
*
*   FUNCTION
*	Read game port events from the game port and put them in the
*	data area of the iORequest.  If there are no pending game port
*	events, this command will not be satisfied, but if there are
*	some events, but not as many as can fill IO_LENGTH, the
*	request will be satisfied with those currently available.
*
*   IO REQUEST
*	io_Message	mn_ReplyPort set if quick I/O is not possible
*	io_Device	preset by the call to OpenDevice
*	io_Unit		preset by the call to OpenDevice
*	io_Command	GPD_READEVENT
*	io_Flags	IOB_QUICK set if quick I/O is possible
*	io_Length	the size of the io_Data area in bytes: there
*			are sizeof(inputEvent) bytes per input event.
*	io_Data		a buffer area to fill with input events.  The
*			fields of the input event are:
*	    ie_NextEvent
*			links the events returned
*	    ie_Class
*			is IECLASS_RAWMOUSE
*	    ie_SubClass
*			is 0 for the left, 1 for the right game port
*	    ie_Code 
*			contains any gameport button reports.  No
*			report is indicated by the value 0xff.
*	    ie_Qualifier 
*			only the relative and button bits are set
*	    ie_X, ie_Y
*			the x and y values for this report, in either
*			relative or absolute device dependent units.
*	    ie_TimeStamp 
*			the delta time since the last report, given
*			not as a standard timestamp, but as the frame
*			count in the TV_SECS field.
*
*   RESULTS
*	This function sets the error field in the iORequest, and fills
*	the iORequest with the next game port events (but not partial
*	events).
*
*   SEE ALSO
*	gameport.device/SetCType, gameport.device/SetTrigger
*
**********************************************************************
GDCBReadEvent	EQU	0

GDReadEvent:
		movem.l d2-d4/a2-a4,-(a7)
		move.l	a1,a3
		move.l	IO_DATA(a3),a2
		move.l	IO_UNIT(a3),a4

		move.l	dd_ExecBase(a6),a1
		DISABLE	a1,NOFETCH

		;------ check if ReadEvent already running here
		bset	#IOB_SERVICING,IO_FLAGS(a3)
		bne	readAlreadyRunning

		;------ check for room in ReadEvent
		moveq	#ie_SIZEOF,d1
		moveq	#0,d2
		move.l	IO_LENGTH(a3),d3
		sub.l	d1,d3
		blt	readTooSmall

		;------ check for data for ReadEvent
		move.w	gu_BufHead(a4),d0
		cmp.w	gu_BufTail(a4),d0
		beq	readPending

readLoop:

		;------	grab critical data from buffer while disabled
		move.b	gu_BufQueue(a4,d0.w),ie_Code+1(a2)
		move.b	gu_BufQueue+1(a4,d0.w),ie_Qualifier(a2)
		move.l  gu_BufQueue+2(a4,d0.w),ie_X(a2)	; and ie_Y
		move.w  gu_BufQueue+6(a4,d0.w),ie_TimeStamp+2(a2)
		addq.w	#8,d0

		;------ increment buffer head
		andi.w	#(GPBUFSIZE-1),d0
		move.w	d0,gu_BufHead(a4)

		move.l	dd_ExecBase(a6),a1
		ENABLE	a1,NOFETCH

		;------ finish filling in input event
		clr.b	ie_Code(a2)		; clear high byte
		clr.b	ie_Qualifier+1(a2)	; clear low byte
		clr.w	ie_TimeStamp(a2)	; clear high word
		clr.l	ie_TimeStamp+4(a2)	; clear low long
		move.b	#IECLASS_RAWMOUSE,ie_Class(a2)
		btst    #UB_RIGHTPORT,gu_Flags(a4)
		beq.s   leftPort
		move.b  #1,ie_SubClass(a2)
		bra.s   bumpEvent
leftPort:
		clr.b   ie_SubClass(a2)

bumpEvent:
		add.l	d1,a2
		move.l	a2,ie_NextEvent-ie_SIZEOF(a2)
		add.l	d1,d2			;increment actual

		;------ check for more space in I/O request data area
		sub.l	d1,d3
		blt.s	readDone

		move.l	dd_ExecBase(a6),a1
		DISABLE	a1,NOFETCH

		;------ get next data from buffer
		move.w	gu_BufHead(a4),d0
		cmp.w	gu_BufTail(a4),d0
		beq.s	readEnableDone

		bra	readLoop


readPending:
		andi.b	#~(IOF_SERVICING!IOF_QUICK),IO_FLAGS(a3)

readAlreadyRunning:
		move.l	dd_ExecBase(a6),a1
		ENABLE	a1,NOFETCH

		movem.l (a7)+,d2-d4/a2-a4
		rts

readTooSmall:
		move.l	dd_ExecBase(a6),a1
		ENABLE	a1,NOFETCH
		moveq	#IOERR_BADLENGTH,d0
		bra.s	readEndCommand

readEnableDone:
		move.l	dd_ExecBase(a6),a1
		ENABLE	a1,NOFETCH

readDone:
*	    ;------ the destination for the read is full or read exhausted
		clr.l	ie_NextEvent-ie_SIZEOF(a2)
		move.l	d2,IO_ACTUAL(a3)
		moveq	#0,d0

readEndCommand:
		move.l	a3,a1
		bsr	EndCommand

readNextCommand:
		move.l	MP_MSGLIST(a4),a1

		movem.l (a7)+,d2-d4/a2-a4

		tst.l	(a1)
		bne	GDReadEvent

		rts


******* gameport.device/GPD_ASKCTYPE *********************************
*
*   NAME
*	GPD_ASKCTYPE -- Acquire the current game port controller type
*
*   FUNCTION
*	This command identifies the type of controller at the game
*	port, so that the signals at the port may be properly
*	interpreted.  The controller type has been set by a previous
*	SetCType.
*
*	This command always executes immediately.
*
*   IO REQUEST
*	io_Message	mn_ReplyPort set if quick I/O is not possible
*	io_Device	preset by the call to OpenDevice
*	io_Unit		preset by the call to OpenDevice
*	io_Command	GPD_ASKCTYPE
*	io_Flags	IOB_QUICK set if quick I/O is possible
*	io_Length	at least 1
*	io_Data		the address of the byte variable for the
*			result
*
**********************************************************************
*
*   IMPLEMENTATION NOTES
*	This quite simply puts the contents of the correct game port
*	variable into IO_DATA.
*
*---------------------------------------------------------------------
GDCBAskCType	EQU	-1

GDAskCType:
		move.l	IO_LENGTH(a1),d0
		subq.w	#1,d0
		bcs	lenErr

		move.l	IO_UNIT(a1),a0
		move.b	gu_Type(a0),d1
		move.l	IO_DATA(a1),a0
		move.b	d1,(a0)+
		moveq	#1,d0
		move.l	d0,IO_ACTUAL(a1)
		moveq	#0,d0
		bra	EndCommand


******* gameport.device/GPD_SETCTYPE *********************************
*
*   NAME
*	GPD_SETCTYPE -- Set the current game port controller type
*
*   FUNCTION
*	This command sets the type of device at the game port, so that
*	the signals at the port may be properly interpreted.  The port
*	can also be turned off, so that no reports are generated.
*
*	This command always executes immediately.
*
*   IO REQUEST
*	io_Message	mn_ReplyPort set if quick I/O is not possible
*	io_Device	preset by the call to OpenDevice
*	io_Unit		preset by the call to OpenDevice
*	io_Command	GPD_SETCTYPE
*	io_Flags	IOB_QUICK set if quick I/O is possible
*	io_Length	1
*	io_Data		the address of the byte variable describing
*			the controller type, as per the equates in
*			the gameport include file
*
**********************************************************************
*
*   IMPLEMENTATION NOTES
*	This stores the IO_DATA into the correct game port variable,
*	and initializes the port.
*
*---------------------------------------------------------------------
GDCBSetCType	EQU	-1

GDSetCType:
		move.l	IO_LENGTH(a3),d0
		subq.w	#1,d0
		bcs	lenErr

*	    ;------ get the controller type
		movem.l d2/a2,-(sp)
		move.l	IO_UNIT(a3),a2

		move.l	dd_ExecBase(a6),a1
		DISABLE	a1,NOFETCH		;protect from VERTB interrupt
						; and other potgo accessors

		move.l	IO_DATA(a3),a0
		move.b	(a0),d0
		ext.w	d0
		cmpi.w	#GPCT_ALLOCATED,d0
		blt	setFail
		cmpi.w	#MAXCONTROLLER,d0
		bgt	setFail

		move.b	d0,gu_Type(a2)

	    ;------ zero the trigger conditions
		lea	gu_AccumTimeout(a2),a0
		moveq	#gt_SIZEOF/2-1,d1
initTLoop:
		clr.w	(a0)+
		dbf	d1,initTLoop


*	    ;------ initialize for the new controller type
		clr.w	gu_LastSave(a2)
		clr.w	gu_CurrSave(a2)
		move.b	gu_Flags(a2),d1
		and.w	#4,d1		    ;get 0 for left, 4 for right
		or.b	#UF_INITIALIZE!UF_RELATIVE,gu_Flags(a2)

*	    ;-- do port-type specific initialization code
		lsl.w	#2,d0
		move.l	cInitTable(d0.w),a0
		jmp	(a0)

		dc.l	potFree
cInitTable:
		dc.l	potFree
		dc.l	potReadPulseInit
		dc.l	potReadRelInit
		dc.l	potReadAbsInit

potFree:
		bsr.s	potClear
		bsr.s	potReallocBits
		bra.s	setCEnd

potReallocBits:
		move.w	d0,gd_PotAlloc(a6)
		btst	#4,d0
		beq.s	allocBits
		bset	#0,d0
allocBits:
		LINKPGR	AllocPotBits
		move.w	d0,gd_PotMask(a6)
		rts

potClear:
		move.w	gd_PotMask(a6),d0
		move.w	d1,d2
		LINKPGR	FreePotBits
		move.w	d2,d1
		move.w	#$0F01,d0
		lsl.w	d1,d0
		not.w	d0
		and.w	d0,gd_PotGo(a6)
		and.w	gd_PotAlloc(a6),d0
		rts

potReadPulseInit:
		move.l	#_custom,a0
		lsr.w	#1,d1
		move.w	joy0dat(a0,d1.W),gu_CurrSave(a2)
		add.w	d1,d1

potReadRelInit:
		bsr.s	potClear
		move.w	#$0F00,d2
		lsl.w	d1,d2
		or.w	d2,d0
		or.w	d2,gd_PotGo(a6)
		bsr.s	potReallocBits
		bra.s	potChkAlloc

potReadAbsInit:
		bclr	#UB_RELATIVE,gu_Flags(a2)
		bra.s	potReadRelInit

potChkAlloc:
		and.w	d2,d0
		cmp.w	d2,d0
		bne.s	setFail
		bra.s	setCEnd

setFail:
		move.b	#GPDERR_SETCTYPE,IO_ERROR(a3)

setCEnd:
		move.l	dd_ExecBase(a6),a1
		ENABLE	a1,NOFETCH

		moveq	#1,d0
		move.l	d0,IO_ACTUAL(a3)
		movem.l (sp)+,d2/a2
		moveq	#0,d0
		move.l	a3,a1
		bra	EndCommand


******* gameport.device/GPD_ASKTRIGGER *******************************
*
*   NAME
*	GPD_ASKTRIGGER -- Inquire the conditions for a game port report
*
*   FUNCTION
*	This command inquires what conditions must be met by a game
*	port unit before a pending Read request will be satisfied.
*	These conditions, called triggers, are independent -- that
*	any one occurs is sufficient to queue a game port report to
*	the Read queue.	 These conditions are set by SetTrigger.
*
*	This command always executes immediately.
*
*   IO REQUEST
*	io_Message	mn_ReplyPort set if quick I/O is not possible
*	io_Device	preset by the call to OpenDevice
*	io_Unit		preset by the call to OpenDevice
*	io_Command	GPD_ASKTRIGGER
*	io_Flags	IOB_QUICK set if quick I/O is possible
*	io_Length	sizeof(gamePortTrigger)
*	io_Data		a structure of type GamePortTrigger, which
*			has the following elements
*	    gpt_Keys -
*		    GPTB_DOWNKEYS set if button down transitions
*		    trigger a report, and GPTB_UPKEYS set if button up
*		    transitions trigger a report
*	    gpt_Timeout	-
*		    a time which, if exceeded, triggers a report;
*		    measured in vertical blank units (60/sec)
*	    gpt_XDelta	-
*		    a distance in x which, if exceeded, triggers a
*		    report
*	    gpt_YDelta	-
*		    a distance in x which, if exceeded, triggers a
*		    report
*
**********************************************************************
*
*   IMPLEMENTATION NOTES
*	This quite simply stores the correct game port trigger info
*	into the buffer pointed to by IO_DATA.
*
*---------------------------------------------------------------------
GDCBAskTrigger	EQU	-1

GDAskTrigger:
		move.l	IO_LENGTH(a1),d0
		subq.w	#gt_SIZEOF,d0
		bcs.s	lenErr

		movem.l a2,-(sp)
		move.l	IO_UNIT(a1),a2
		lea	gu_Timeout(a2),a2
		move.l	IO_DATA(a1),a0
		moveq	#gt_SIZEOF/2-1,d0
askTLoop:
		move.w	(a2)+,(a0)+
		dbf	d0,askTLoop

		movem.l (sp)+,a2
		moveq	#gt_SIZEOF,d0
		move.l	d0,IO_ACTUAL(a1)
		moveq	#0,d0
		bra	EndCommand


******* gameport.device/GPD_SETTRIGGER *******************************
*
*   NAME
*	GPD_SETTRIGGER -- Set the conditions for a game port report
*
*   FUNCTION
*	This command sets what conditions must be met by a game
*	port unit before a pending Read request will be satisfied.
*	These conditions, called triggers, are independent -- that
*	any one occurs is sufficient to queue a game port report to
*	the Read queue.	 These conditions are inquired with
*	AskTrigger.
*
*	This command always executes immediately.
*
*   IO REQUEST
*	io_Message	mn_ReplyPort set if quick I/O is not possible
*	io_Device	preset by the call to OpenDevice
*	io_Unit		preset by the call to OpenDevice
*	io_Command	GPD_SETTRIGGER
*	io_Flags	IOB_QUICK set if quick I/O is possible
*	io_Length	sizeof(gamePortTrigger)
*	io_Data		a structure of type GamePortTrigger, which
*			has the following elements
*	    gpt_Keys -
*		    GPTB_DOWNKEYS set if button down transitions
*		    trigger a report, and GPTB_UPKEYS set if button up
*		    transitions trigger a report
*	    gpt_Timeout	-
*		    a time which, if exceeded, triggers a report;
*		    measured in vertical blank units (60/sec)
*	    gpt_XDelta	-
*		    a distance in x which, if exceeded, triggers a
*		    report
*	    gpt_YDelta	-
*		    a distance in x which, if exceeded, triggers a
*		    report
*
**********************************************************************
*
*   IMPLEMENTATION NOTES
*	This quite simply stores the correct game port trigger info
*	into the buffer pointed to by IO_DATA.
*
*---------------------------------------------------------------------
GDCBSetTrigger	EQU	-1

GDSetTrigger:
		move.l	IO_LENGTH(a1),d0
		subq.w	#gt_SIZEOF,d0
		bcs.s	lenErr

		movem.l a2,-(sp)
		move.l	IO_UNIT(a1),a2
		lea	gu_Timeout(a2),a2
		move.l	IO_DATA(a1),a0
		moveq	#gt_SIZEOF/2-1,d0
*	    ;------ no need to disable, trigger can be mixed
setTLoop:
		move.w	(a0)+,(a2)+
		dbf	d0,setTLoop

		movem.l (sp)+,a2
		moveq	#gt_SIZEOF,d0
		move.l	d0,IO_ACTUAL(a1)
		moveq	#0,d0
		bra	EndCommand

lenErr:
		moveq	#IOERR_BADLENGTH,d0
		bra	EndCommand

		END
@


36.9
log
@use Id instead of Header for 4.x rcs
@
text
@d1 1
a1 1
	TTL	'$Id$
d19 1
a19 1
*   $Id$
d21 1
a21 1
*   $Locker: kodiak $
d24 3
d316 1
a316 1
*	GPD_ASKCTYPE -- Anquire the current game port controller type
@


36.8
log
@avoid implicit $4 reference by converting DISABLE/ENABLE to NOFETCH form
@
text
@d1 1
a1 1
	TTL	'$Header: /usr2/kodiak/src/KS/rawinput/RCS/gamecmds.asm,v 36.7 90/04/02 12:56:29 kodiak Exp Locker: kodiak $'	
d19 1
a19 1
*   $Header: /usr2/kodiak/src/KS/rawinput/RCS/gamecmds.asm,v 36.7 90/04/02 12:56:29 kodiak Exp Locker: kodiak $
d24 3
@


36.7
log
@for rcs 4.x header change
@
text
@d1 1
a1 1
	TTL	'$Header: /usr2/kodiak/src/KS/rawinput/RCS/gamecmds.asm,v 36.6 90/01/29 09:29:17 kodiak Exp Locker: kodiak $'	
d19 1
a19 1
*   $Header: /usr2/kodiak/src/KS/rawinput/RCS/gamecmds.asm,v 36.6 90/01/29 09:29:17 kodiak Exp Locker: kodiak $
d24 3
d131 2
a132 1
		DISABLE	a1
d135 1
a135 1
		ENABLE	a1
d198 2
a199 1
		DISABLE	a1
d230 2
a231 1
		ENABLE	a1
d255 2
a256 1
		DISABLE	a1
d270 2
a271 1
		ENABLE	a1
d277 2
a278 1
		ENABLE	a1
d283 2
a284 1
		ENABLE	a1
d395 2
a396 1
		DISABLE	a1			;protect from VERTB interrupt
d492 2
a493 1
		ENABLE	a1
@


36.6
log
@rename [Gg]ameportTrigger to [Gg]amePortTrigger in autodocs
@
text
@d1 1
a1 1
	TTL	'$Header: gamecmds.asm,v 36.5 90/01/04 13:27:22 kodiak Exp $'	
d19 1
a19 1
*   $Header: gamecmds.asm,v 36.5 90/01/04 13:27:22 kodiak Exp $
d24 3
@


36.5
log
@polish autodoc headers
@
text
@d1 1
a1 1
	TTL	'$Header: gamecmds.asm,v 36.4 89/11/10 13:39:03 kodiak Exp $'	
d19 1
a19 1
*   $Header: gamecmds.asm,v 36.4 89/11/10 13:39:03 kodiak Exp $
d24 3
d508 2
a509 2
*	io_Length	sizeof(gameportTrigger)
*	io_Data		a structure of type GameportTrigger, which
d576 2
a577 2
*	io_Length	sizeof(gameportTrigger)
*	io_Data		a structure of type GameportTrigger, which
@


36.4
log
@make gameport exclusive access
@
text
@d1 1
a1 1
	TTL	'$Header: gamecmds.asm,v 36.3 89/07/12 13:27:42 kodiak Exp $'	
d19 1
a19 1
*   $Header: gamecmds.asm,v 36.3 89/07/12 13:27:42 kodiak Exp $
d24 3
d101 1
a101 1
******* gameport.device/command/Clear ********************************
d130 1
a130 1
******* gameport.device/command/ReadEvent ****************************
d133 1
a133 1
*	ReadEvent - return the next game port event.
d291 1
a291 1
******* gameport.device/command/AskCType *****************************
d294 1
a294 1
*	AskCType - inquire the current game port controller type
d338 1
a338 1
******* gameport.device/command/SetCType *****************************
d341 1
a341 1
*	SetCType - set the current game port controller type
d485 1
a485 1
******* gameport.device/command/AskTrigger ***************************
d488 1
a488 1
*	AskTrigger - inquire the conditions for a game port report
d552 1
a552 1
******* gameport.device/command/SetTrigger ***************************
d555 1
a555 1
*	SetTrigger - set the conditions for a game port report
@


36.3
log
@have input task ReadEvent multiple events from gameport
(and have it work)
@
text
@d1 1
a1 1
	TTL	'$Header: gamecmds.asm,v 36.2 88/11/03 12:35:13 kodiak Exp $'	
d19 1
a19 1
*   $Header: gamecmds.asm,v 36.2 88/11/03 12:35:13 kodiak Exp $
d24 4
a378 7
*	    ;------ zero the trigger conditions
		lea	gu_AccumTimeout(a2),a0
		moveq	#gt_SIZEOF/2-1,d0
initTLoop:
		clr.w	(a0)+
		dbf	d0,initTLoop

d382 5
d389 8
d465 1
a465 1
		bne.s	potAllocFail
d468 1
a468 1
potAllocFail:
@


36.2
log
@use AUTOINIT in ResidentTag
@
text
@d1 1
a1 1
	TTL	'$Header: gamecmds.asm,v 35.0 87/10/26 11:30:30 kodiak Exp $'	
d19 1
a19 1
*   $Header: gamecmds.asm,v 35.0 87/10/26 11:30:30 kodiak Exp $
d24 3
d48 1
d65 1
a69 3
	XREF_EXE	Disable
	XREF_EXE	Enable

d114 7
a120 6
		MOVE.L	IO_UNIT(A1),A0
		LINKEXE Disable
		CLR.W	gu_BufHead(A0)
		CLR.W	gu_BufTail(A0)
		LINKEXE Enable
		RTS
d176 4
a179 3
		MOVEM.L D2-D4/A2-A4,-(A7)
		MOVE.L	A1,A3
		MOVE.L	IO_UNIT(A3),A4
d181 1
a181 1
		LINKEXE Disable
d184 2
a185 2
		BSET	#IOB_SERVICING,IO_FLAGS(A3)
		BNE	readEnableRts
d188 5
a192 5
		MOVEQ	#ie_SIZEOF,D1
		MOVEQ	#0,D2
		MOVE.L	IO_LENGTH(A3),D3
		SUB.L	D1,D3
		BLT	readTooSmall
d195 3
a197 3
		MOVE.W	gu_BufHead(A4),D0
		CMP.W	gu_BufTail(A4),D0
		BEQ	readPending
d199 1
a199 1
		LINKEXE Enable
d201 6
a206 2
		;------ initialize transfer variables
		MOVE.L	IO_DATA(A3),A2
d208 3
a210 2
readLoop:
		LINKEXE	Disable
d212 1
a212 4
		;------ get data from buffer
		MOVE.W	gu_BufHead(A4),D0
		CMP.W	gu_BufTail(A4),D0
		BEQ	readDone
d214 10
a223 5
		MOVE.B	#IECLASS_RAWMOUSE,ie_Class(A2)
		BTST    #UB_RIGHTPORT,gu_Flags(A4)
		BEQ.S   leftPort
		MOVE.B  #1,ie_SubClass(A2)
		BRA.S   codeData
d225 1
a225 13
		CLR.B   ie_SubClass(A2)
codeData:
		MOVEQ	#0,D4
		MOVE.B	gu_BufQueue(A4,D0.W),D4
		MOVE.W	D4,ie_Code(A2)
		MOVE.B	gu_BufQueue+1(A4,D0.W),D4
		LSL	#8,D4
		MOVE.W	D4,ie_Qualifier(A2)
		MOVE.L  gu_BufQueue+2(A4,D0.W),ie_X(A2)	; and ie_Y
		MOVE.L  gu_BufQueue+6(A4,D0.W),ie_TimeStamp(A2)
		ADDQ.W	#8,D0
		ADD.L	D1,A2
		MOVE.L	A2,ie_NextEvent-ie_SIZEOF(A2)
d227 4
a230 2
		ANDI.W	#(GPBUFSIZE-1),D0
		MOVE.W	D0,gu_BufHead(A4)
d232 3
a234 1
		ADD.L	D1,D2			;increment actual
d236 1
a236 1
		LINKEXE	Enable
d238 4
a241 3
		;------ check for more space in I/O request data area
		SUB.L	D1,D3
		BLT.S	readDone
d243 1
a243 1
		BRA.S	readLoop
d247 1
a247 1
		ANDI.B	#~(IOF_SERVICING!IOF_QUICK),IO_FLAGS(A3)
d249 2
a250 2
readEnableRts:
		LINKEXE	Enable
d252 2
a253 3
readRts:
		MOVEM.L (A7)+,D2-D4/A2-A4
		RTS
d256 3
a258 5
		LINKEXE	Enable
		MOVEQ	#IOERR_BADLENGTH,D0
		MOVE.L	A3,A1
		BSR	EndCommand
		BRA.S	readNextCommand
d260 3
d264 4
a267 6
*	    ;------ the destination for the read is full
		CLR.L	ie_NextEvent-ie_SIZEOF(A2)
		MOVE.L	D2,IO_ACTUAL(A3)
		MOVE.L	A3,A1
		MOVEQ	#0,D0
		BSR	EndCommand
d269 4
d274 1
a274 3
		MOVE.L	MP_MSGLIST(A4),A1
		TST.L	(A1)
		BEQ.S	readRts
d276 1
a276 2
		BSR	GDReadEvent
		BRA.S	readRts
d278 2
d281 3
d317 3
a319 3
		MOVE.L	IO_LENGTH(A1),D0
		SUBQ.W	#1,D0
		BCS	lenErr
d321 8
a328 8
		MOVE.L	IO_UNIT(A1),A0
		MOVE.B	gu_Type(A0),D1
		MOVE.L	IO_DATA(A1),A0
		MOVE.B	D1,(A0)+
		MOVEQ	#1,D0
		MOVE.L	D0,IO_ACTUAL(A1)
		MOVEQ	#0,D0
		BRA	EndCommand
d364 3
a366 3
		MOVE.L	IO_LENGTH(A1),D0
		SUBQ.W	#1,D0
		BCS	lenErr
d369 2
a370 2
		MOVEM.L D2/A2,-(SP)
		MOVE.L	IO_UNIT(A1),A2
d372 1
a372 1
		LINKEXE	Disable			;protect from VERTB interrupt
d376 2
a377 2
		LEA	gu_AccumTimeout(A2),A0
		MOVEQ	#gt_SIZEOF/2-1,D0
d379 2
a380 2
		CLR.W	(A0)+
		DBF	D0,initTLoop
d382 4
a385 4
		MOVE.L	IO_DATA(A1),A0
		MOVE.B	(A0),D0
		EXT.W	D0
		MOVE.B	D0,gu_Type(A2)
d388 5
a392 5
		CLR.W	gu_LastSave(A2)
		CLR.W	gu_CurrSave(A2)
		MOVE.B	gu_Flags(A2),D1
		AND.W	#4,D1		    ;get 0 for left, 4 for right
		OR.B	#UF_INITIALIZE!UF_RELATIVE,gu_Flags(A2)
d395 3
a397 3
		LSL.W	#2,D0
		MOVE.L	cInitTable(D0.W),A0
		JMP	(A0)
d399 1
a399 1
		DC.L	potFree
d401 4
a404 4
		DC.L	potFree
		DC.L	potReadPulseInit
		DC.L	potReadRelInit
		DC.L	potReadAbsInit
d407 3
a409 3
		BSR.S	potClear
		BSR.S	potReallocBits
		BRA.S	setCEnd
d412 4
a415 4
		MOVE.W	D0,gd_PotAlloc(A6)
		BTST	#4,D0
		BEQ.S	allocBits
		BSET	#0,D0
d418 2
a419 2
		MOVE.W	D0,gd_PotMask(A6)
		RTS
d422 2
a423 2
		MOVE.W	gd_PotMask(A6),D0
		MOVE.W	D1,D2
d425 7
a431 7
		MOVE.W	D2,D1
		MOVE.W	#$0F01,D0
		LSL.W	D1,D0
		NOT.W	D0
		AND.W	D0,gd_PotGo(A6)
		AND.W	gd_PotAlloc(A6),D0
		RTS
d434 4
a437 4
		MOVE.L	#_custom,A0
		LSR.W	#1,D1
		MOVE.W	joy0dat(A0,D1.W),gu_CurrSave(A2)
		ADD.W	D1,D1
d440 7
a446 7
		BSR.S	potClear
		MOVE.W	#$0F00,D2
		LSL.W	D1,D2
		OR.W	D2,D0
		OR.W	D2,gd_PotGo(A6)
		BSR.S	potReallocBits
		BRA.S	potChkAlloc
d449 2
a450 2
		BCLR	#UB_RELATIVE,gu_Flags(A2)
		BRA.S	potReadRelInit
d453 4
a456 4
		AND.W	D2,D0
		CMP.W	D2,D0
		BNE.S	potAllocFail
		BRA.S	setCEnd
d459 1
a459 1
		MOVE.B	#GPDERR_SETCTYPE,IO_ERROR(A1)
d462 1
a462 1
		LINKEXE	Enable
d464 6
a469 5
		MOVEQ	#1,D0
		MOVE.L	D0,IO_ACTUAL(A1)
		MOVEM.L (SP)+,D2/A2
		MOVEQ	#0,D0
		BRA	EndCommand
d519 3
a521 3
		MOVE.L	IO_LENGTH(A1),D0
		SUBQ.W	#gt_SIZEOF,D0
		BCS.S	lenErr
d523 5
a527 5
		MOVEM.L A2,-(SP)
		MOVE.L	IO_UNIT(A1),A2
		LEA	gu_Timeout(A2),A2
		MOVE.L	IO_DATA(A1),A0
		MOVEQ	#gt_SIZEOF/2-1,D0
d529 2
a530 2
		MOVE.W	(A2)+,(A0)+
		DBF	D0,askTLoop
d532 5
a536 5
		MOVEM.L (SP)+,A2
		MOVEQ	#gt_SIZEOF,D0
		MOVE.L	D0,IO_ACTUAL(A1)
		MOVEQ	#0,D0
		BRA	EndCommand
d587 3
a589 3
		MOVE.L	IO_LENGTH(A1),D0
		SUBQ.W	#gt_SIZEOF,D0
		BCS.S	lenErr
d591 5
a595 5
		MOVEM.L A2,-(SP)
		MOVE.L	IO_UNIT(A1),A2
		LEA	gu_Timeout(A2),A2
		MOVE.L	IO_DATA(A1),A0
		MOVEQ	#gt_SIZEOF/2-1,D0
d598 2
a599 2
		MOVE.W	(A0)+,(A2)+
		DBF	D0,setTLoop
d601 5
a605 5
		MOVEM.L (SP)+,A2
		MOVEQ	#gt_SIZEOF,D0
		MOVE.L	D0,IO_ACTUAL(A1)
		MOVEQ	#0,D0
		BRA	EndCommand
d608 2
a609 2
		MOVEQ	#IOERR_BADLENGTH,D0
		BRA	EndCommand
@


35.0
log
@initial from V34, but w/ stripped log
@
text
@d1 1
a1 1
	TTL	'$Header: gamecmds.asm,v 33.1 86/06/18 12:18:05 kodiak Exp $'	
d19 1
a19 1
*   $Header: gamecmds.asm,v 33.1 86/06/18 12:18:05 kodiak Exp $
d21 1
a21 1
*   $Locker:  $
d24 2
d27 1
d228 1
a228 1
		ANDI.W	#(BUFSIZE-1),D0
@

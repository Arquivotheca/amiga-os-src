head     40.1;
branch   ;
access   ;
symbols  V36_14:36.2 V36_13:36.2 V36_11:36.2 L36_5:35.1;
locks    ; strict;
comment  @*   @;


40.1
date     93.03.12.16.03.07;  author darren;  state Exp;
branches ;
next     40.0;

40.0
date     93.03.08.10.57.45;  author darren;  state Exp;
branches ;
next     36.18;

36.18
date     91.11.04.13.19.15;  author darren;  state Exp;
branches ;
next     36.17;

36.17
date     91.01.21.18.46.13;  author darren;  state Exp;
branches ;
next     36.16;

36.16
date     91.01.16.12.43.36;  author darren;  state Exp;
branches ;
next     36.15;

36.15
date     90.05.30.13.04.37;  author kodiak;  state Exp;
branches ;
next     36.14;

36.14
date     90.05.30.12.40.09;  author kodiak;  state Exp;
branches ;
next     36.13;

36.13
date     90.05.10.18.48.18;  author kodiak;  state Exp;
branches ;
next     36.12;

36.12
date     90.04.13.12.44.30;  author kodiak;  state Exp;
branches ;
next     36.11;

36.11
date     90.04.02.16.30.10;  author kodiak;  state Exp;
branches ;
next     36.10;

36.10
date     90.04.02.12.58.13;  author kodiak;  state Exp;
branches ;
next     36.9;

36.9
date     90.01.19.18.06.32;  author kodiak;  state Exp;
branches ;
next     36.8;

36.8
date     89.12.19.14.17.12;  author kodiak;  state Exp;
branches ;
next     36.7;

36.7
date     89.09.21.11.13.44;  author kodiak;  state Exp;
branches ;
next     36.6;

36.6
date     89.09.20.20.50.36;  author kodiak;  state Exp;
branches ;
next     36.5;

36.5
date     89.08.31.17.26.08;  author kodiak;  state Exp;
branches ;
next     36.4;

36.4
date     89.08.08.09.54.38;  author kodiak;  state Exp;
branches ;
next     36.3;

36.3
date     89.06.29.15.39.13;  author kodiak;  state Exp;
branches ;
next     36.2;

36.2
date     88.11.03.12.35.25;  author kodiak;  state Exp;
branches ;
next     35.1;

35.1
date     88.08.02.12.27.24;  author kodiak;  state Exp;
branches ;
next     35.0;

35.0
date     87.10.26.11.31.28;  author kodiak;  state Exp;
branches ;
next     ;


desc
@@


40.1
log
@Use XREF'ed device name - save ROM space
@
text
@**
**	$Id: inputdev.asm,v 40.0 93/03/08 10:57:45 darren Exp Locker: darren $
**
**	input device init and functions
**
**	(C) Copyright 1985,1986,1987,1988 Commodore-Amiga, Inc.
**	    All Rights Reserved
**
	SECTION		rawinput

**	Includes

	INCLUDE		"exec/types.i"
	INCLUDE		"exec/nodes.i"
	INCLUDE		"exec/lists.i"
	INCLUDE		"exec/memory.i"
	INCLUDE		"exec/ports.i"
	INCLUDE		"exec/libraries.i"
	INCLUDE		"exec/devices.i"
	INCLUDE		"exec/tasks.i"
	INCLUDE		"exec/io.i"
	INCLUDE		"exec/strings.i"
	INCLUDE		"exec/interrupts.i"
	INCLUDE		"exec/errors.i"
	INCLUDE		"exec/initializers.i"
	INCLUDE		"exec/execbase.i"

	INCLUDE		"devices/timer.i"

	INCLUDE		"keyboard.i"
	INCLUDE		"gameport.i"
	INCLUDE		"inputevent.i"

	INCLUDE		"macros.i"
	INCLUDE		"stddevice.i"
	INCLUDE		"iddata.i"


**	Exports

	XDEF		IDFuncInit
	XDEF		IDStructInit
	XDEF		IDInit


**	Imports
	XREF		KDName
	XREF		IDName
	XREF		VERSION
	XREF		REVISION


	XREF_EXE	AddTask
	XREF_EXE	CloseDevice
	XREF_EXE	FreeMem
	XREF_EXE	OpenDevice
	XREF_EXE	Wait

	XREF		BeginIO
	XREF		AbortIO
	XREF		SuccessfulExpunge
	XREF		DeferredExpunge
	XREF		ExtFunc
	XREF		Close

	XREF		Invalid
	XREF		Read
	XREF		Write
	XREF		Update
	XREF		IDClear
	XREF		StopCmd
	XREF		IDStart
	XREF		Flush

	XREF		IDSetThresh
	XREF		IDSetPeriod
	XREF		IDSetMPort
	XREF		IDSetMType
	XREF		IDSetMTrig

	XREF		CBInvalid
	XREF		CBRead
	XREF		CBWrite
	XREF		CBUpdate
	XREF		IDCBClear
	XREF		CBStopCmd
	XREF		IDCBStart
	XREF		CBFlush

	XREF		IDCBSetThresh
	XREF		IDCBSetPeriod
	XREF		IDCBSetMPort
	XREF		IDCBSetMType
	XREF		IDCBSetMTrig

	XREF		IDTaskStart



**********************************************************************
IDInit:
		movem.l a2-a3/a6,-(sp)
*	    ;------ This is called from within MakeLibrary, after
*	    ;------ all the memory has been allocated

		exg	d0,a6			; save device library pointer
		move.l	d0,dd_ExecBase(a6)
		move.l	a0,dd_Segment(a6)

		lea	id_Unit+MP_MSGLIST(a6),a0
		NEWLIST	a0

	;------ open the Timer device
		suba.l	a0,a0			;Tagged way for V40 (save ROM)

	;	lea	TDName(pc),a0
		moveq	#UNIT_VBLANK,d0		; VBLANK TIMER
		moveq	#0,d1
		lea	id_RIOR(a6),a1
		LINKEXE OpenDevice
		tst.l	d0
		bne	initTDErr

		move.l	id_RIOR+IO_DEVICE(a6),id_TIOR+IO_DEVICE(a6)
		move.l	id_RIOR+IO_UNIT(a6),id_TIOR+IO_UNIT(a6)

	;------ open the Keyboard device
		lea	KDName(pc),a0
		moveq	#0,d0
		moveq	#0,d1
		lea	id_K1IOR(a6),a1
		LINKEXE OpenDevice
		tst.l	d0
		bne	initKDErr

		move.l	id_K1IOR+IO_DEVICE(a6),id_K2IOR+IO_DEVICE(a6)
		move.l	id_K1IOR+IO_UNIT(a6),id_K2IOR+IO_UNIT(a6)

	;------ initialize the input handler list
		lea	id_HandlerList(a6),a0
		NEWLIST	a0

	;------ initialize input device task
		lea	id_TC(a6),a1
		lea	id_Stk(a6),a0
		move.l	a0,TC_SPLOWER(a1)
		move.w	#(ID_STKSIZE/2)-1,d0
		move.w	#$0A6A6,d1
pattern0Loop:
		move.w	d1,(a0)+
		dbf	d0,pattern0Loop
	;---	LEA	id_Stk+ID_STKSIZE(a6),a0
		move.l	a0,TC_SPUPPER(a1)
		move.l	a6,-(a0)		; argument (input data)
		move.l	a0,TC_SPREG(a1)

		move.l	dd_ExecBase(a6),a0
		move.l	ThisTask(a0),id_Stk(a6) ; cache this task

		lea	IDTaskStart(pc),a2
		suba.l	a3,a3
		LINKEXE AddTask

		moveq	#SIGF_SINGLE,d0
		LINKEXE	Wait

		move.l	a6,d0

itInitRts:
		movem.l (sp)+,a2-a3/a6
		rts

initKDErr:
		lea	id_TIOR(a6),a1
		CALLEXE CloseDevice

initTDErr:
		move.l	a6,a1
		move.w	LIB_NEGSIZE(a6),d0
		suba.w	d0,a1
		add.w	LIB_POSSIZE(a6),d0
		ext.l	d0
		LINKEXE FreeMem

initMemErr:
		moveq	#0,d0
		bra	itInitRts



*----------------------------------------------------------------------
*
* Definitions for Initialization
*
*----------------------------------------------------------------------
IDStructInit:
*	;------ initialize the device library structure
		INITBYTE	LN_TYPE,NT_DEVICE
		INITLONG	LN_NAME,IDName
		INITBYTE	LIB_FLAGS,LIBF_SUMUSED!LIBF_CHANGED
		INITWORD	LIB_VERSION,VERSION
		INITWORD	LIB_REVISION,REVISION

		INITLONG	dd_CmdVectors,idCmdTable
		INITLONG	dd_CmdBytes,idCmdBytes
		INITWORD	dd_NumCommands,ID_NUMCOMMANDS

*	;------ initialize the unit command queue
		INITBYTE	id_Unit+LN_TYPE,NT_MSGPORT
		INITLONG	id_Unit+LN_NAME,IDName

*	;------ Initialize the tasks
		INITBYTE	id_TC+LN_TYPE,NT_TASK
		INITBYTE	id_TC+LN_PRI,ID_PRIORITY
		INITLONG	id_TC+LN_NAME,IDName

*	;------ Initialize the repeat rates
		INITLONG	id_Thresh+TV_SECS,ID_THRESHSECS
		INITLONG	id_Thresh+TV_MICRO,ID_THRESHMICRO
		INITLONG	id_Period+TV_SECS,ID_PERIODSECS
		INITLONG	id_Period+TV_MICRO,ID_PERIODMICRO

		INITWORD	id_RepeatCode,-1

	;------ Initialize the mouse controller
		INITBYTE	id_MType,GPCT_MOUSE
		INITWORD	id_MTrig+gpt_Keys,GPTF_DOWNKEYS!GPTF_UPKEYS
		INITLONG	id_MTrig+gpt_XDelta,$00010001

		dc.w	0


IDFuncInit:
		dc.w	-1

		dc.w	IDOpen-IDFuncInit
		dc.w	Close+(*-IDFuncInit)
		dc.w	IDExpunge-IDFuncInit
		dc.w	ExtFunc+(*-IDFuncInit)

		dc.w	BeginIO+(*-IDFuncInit)
		dc.w	AbortIO+(*-IDFuncInit)

		dc.w	PeekQualifier-IDFuncInit

		dc.w	-1


idCmdTable:
		dc.l	Invalid
		dc.l	IDReset
		dc.l	Read
		dc.l	Write
		dc.l	Update
		dc.l	IDClear
		dc.l	StopCmd
		dc.l	IDStart
		dc.l	Flush

		dc.l	IDTaskCommand	; IDAddHandler
		dc.l	IDTaskCommand	; IDRemHandler
		dc.l	IDTaskCommand	; IDWriteEvent
		dc.l	IDSetThresh
		dc.l	IDSetPeriod
		dc.l	IDTaskCommand	; IDSetMPort
		dc.l	IDTaskCommand	; IDSetMType
		dc.l	IDTaskCommand	; IDSetMTrig

idCmdBytes:
		dc.b	CBInvalid
		dc.b	IDCBReset
		dc.b	CBRead
		dc.b	CBWrite
		dc.b	CBUpdate
		dc.b	IDCBClear
		dc.b	CBStopCmd
		dc.b	IDCBStart
		dc.b	CBFlush

		dc.b	IDCBTaskCommand	; IDCBAddHandler
		dc.b	IDCBTaskCommand	; IDCBRemHandler
		dc.b	IDCBTaskCommand	; IDCBWriteEvent
		dc.b	IDCBSetThresh
		dc.b	IDCBSetPeriod
		dc.b	IDCBTaskCommand	; IDCBSetMPort
		dc.b	IDCBTaskCommand	; IDCBSetMType
		dc.b	IDCBTaskCommand	; IDCBSetMTrig

ID_NUMCOMMANDS	EQU	(*-idCmdBytes)

		ds.w	0

*****i* input.device/command/Reset ***********************************
*
*   NAME
*	Reset - reset the input device
*
*   FUNCTION
*	Reset resets the input device without destroying handles
*	to the open device.
*
*   IO REQUEST
*	io_Message	mn_ReplyPort set if quick I/O is not possible
*	io_Device	preset by the call to OpenDevice
*	io_Unit		preset by the call to OpenDevice
*	io_Command	CMD_RESET
*	io_Flags	IOB_QUICK set if quick I/O is possible
*
**********************************************************************
IDCBReset	EQU	-1

IDReset:
		bsr	StopCmd
		bsr	Flush
		bsr	IDStart
		rts


*------ TaskCommand --------------------------------------------------
*
*   FUNCTION
*	Keep the command on the message queue till task time.
*
*---------------------------------------------------------------------
IDCBTaskCommand	EQU	0

IDTaskCommand:
		bclr    #IOB_QUICK,IO_FLAGS(a1)	; jimm: 8/1/86
		rts


*------ Expunge ------------------------------------------------------
*
*   NAME
*	Expunge - indicate a desire to remove this device
*
*   SYNOPSIS
*	segment = Expunge(), inputDev
*	d0		     a6
*
*   FUNCTION
*	The Expunge routine is called when a user issues a RemDevice
*	call.  The existance of any other users of the device, as
*	determined by the library open count being non-zero, will
*	cause the Expunge to be deferred.  When the device is not
*	in use, or no longer in use, the Expunge is actually
*	performed, and the device removed from the system list.
*
*---------------------------------------------------------------------
IDExpunge:
	    ;-- see if any one is using the device
		tst.w	LIB_OPENCNT(a6)
		bne	DeferredExpunge

*	    ;-- this is really it.  Free up all the resources

;!!!		BRA	SuccessfulExpunge
		bra	DeferredExpunge


*------ input.device/Open *****************************************
*
*   NAME
*	Open - a request to open the input device
*
*   SYNOPSIS
*	OpenDevice("input.device", 0, iORequest, 0)
*
*   FUNCTION
*	The open routine grants access to a device.  There are two
*	fields in the iORequest block that will be filled in: the
*	IO_DEVICE field and the IO_UNIT field.
*
*	The device open count will be incremented.  The device cannot
*	be expunged unless this open is matched by a Close device.
*
*   RESULTS
*	If the open was unsuccessful, IO_ERROR will be set, IO_UNIT
*	and IO_DEVICE will not be valid.
*
**********************************************************************
IDOpen:
	    ;-- this device has only one unit
		lea	id_Unit(a6),a0
		move.l	a0,IO_UNIT(a1)

		bclr	#LIBB_DELEXP,LIB_FLAGS(a6)
		addq.w	#1,LIB_OPENCNT(a6)
		rts

******* input.device/PeekQualifier ***********************************
*
*   NAME
*	PeekQualifier -- get the input device's current qualifiers (V36)
*
*   SYNOPSIS
*	qualifier = PeekQualifier()
*	d0
*
*	UWORD PeekQualifier( VOID );
*
*   FUNCTION
*	This function takes a snapshot of what the input device thinks
*	the current qualifiers are.
*
*   RESULTS
*	qualifier - a word with the following bits set according to
*	    what the input device knows their state to be:
*		IEQUALIFIER_LSHIFT, IEQUALIFIER_RSHIFT,
*		IEQUALIFIER_CAPSLOCK, IEQUALIFIER_CONTROL, 
*		IEQUALIFIER_LALT, IEQUALIFIER_RALT,
*		IEQUALIFIER_LCOMMAND, IEQUALIFIER_RCOMMAND,
*		IEQUALIFIER_LEFTBUTTON, IEQUALIFIER_RBUTTON,
*		IEQUALIFIER_MIDBUTTON
*
*   NOTE
*	This function is new for V36.
*
*   SEE ALSO
*	devices/inputevent.h
*
**********************************************************************
PeekQualifier:
		move.w	id_Qualifier(a6),d0
		and.l	#$70ff,d0
		rts

	END
@


40.0
log
@use tagged open device to open timer.device - saves ROM
@
text
@d2 1
a2 1
**	$Id: inputdev.asm,v 36.18 91/11/04 13:19:15 darren Exp Locker: darren $
d47 1
a47 1

a188 5

KDName		DC.B    'keyboard.device'
		dc.b	0

		ds.w 0
@


36.18
log
@Autodoc change - PeekQualifier() takes VOID as argument.
@
text
@d2 1
a2 1
**	$Id: inputdev.asm,v 36.18 91/11/04 11:48:29 darren Exp $
a44 1
	XDEF		TDName
a45 1

d114 3
a116 1
		lea	TDName(pc),a0
a189 2
TDName		DC.B    'timer.device'
		dc.b	0
@


36.17
log
@Remove tab in PeekQualifier AutoDocs.
@
text
@d2 1
a2 1
**	$Id: inputdev.asm,v 36.16 91/01/16 12:43:36 darren Exp Locker: darren $
d407 1
a407 1
*	UWORD PeekQualifier();
@


36.16
log
@*** empty log message ***
@
text
@d2 1
a2 1
**	$Id: inputdev.asm,v 36.15 90/05/30 13:04:37 kodiak Exp Locker: darren $
d398 1
a398 1
*******	input.device/PeekQualifier ***********************************
@


36.15
log
@rename PeekQualifiers to PeekQualifier
@
text
@d2 1
a2 1
**	$Id: inputdev.asm,v 36.14 90/05/30 12:40:09 kodiak Exp Locker: kodiak $
@


36.14
log
@added PeekQualifiers function
@
text
@d2 1
a2 1
**	$Id: input.i,v 36.0 90/05/01 11:16:04 kodiak Exp $
d251 1
a251 1
		dc.w	PeekQualifiers-IDFuncInit
d398 1
a398 1
*******	input.device/PeekQualifiers **********************************
d401 1
a401 1
*	PeekQualifiers -- get the input device's current qualifiers (V36)
d404 1
a404 1
*	qualifiers = PeekQualifiers()
d407 1
a407 1
*	UWORD PeelQualifiers();
d414 1
a414 1
*	qualifiers - a word with the following bits set according to
d430 1
a430 1
PeekQualifiers:
@


36.13
log
@use GetSysTime function instead of GETSYSTIME command
@
text
@d1 8
a8 56
	TTL	'$Id: inputdev.asm,v 36.12 90/04/13 12:44:30 kodiak Exp Locker: kodiak $
**********************************************************************
*
*			------------
*   inputdev.asm	INPUT DEVICE		device functions
*			------------
*
*   Copyright 1985, 1987 Commodore-Amiga Inc.
*
*   Source Control	$Locker: kodiak $
*
*   $Log:	inputdev.asm,v $
*   Revision 36.12  90/04/13  12:44:30  kodiak
*   use Id instead of Header for 4.x rcs
*   
*   Revision 36.11  90/04/02  16:30:10  kodiak
*   back to using dd_ExecBase, not SYSBASE (4)
*   
*   Revision 36.10  90/04/02  12:58:13  kodiak
*   for rcs 4.x header change
*   
*   Revision 36.9  90/01/19  18:06:32  kodiak
*   make SetMXxxx commands synchronous
*   
*   Revision 36.8  89/12/19  14:17:12  kodiak
*   autodoc update
*   
*   Revision 36.7  89/09/21  11:13:44  kodiak
*   correctly return lib base in d0 from input init
*   
*   Revision 36.6  89/09/20  20:50:36  kodiak
*   first cut at fixing initialization race condition (message port
*   initialization of command queue in task)
*   
*   Revision 36.5  89/08/31  17:26:08  kodiak
*   export TDName for keyboard use
*   
*   Revision 36.4  89/08/08  09:54:38  kodiak
*   fix repeat strategy to allow corruption of repeat input event on food chain
*   
*   Revision 36.3  89/06/29  15:39:13  kodiak
*   double buffer keyboard requests
*   
*   Revision 36.2  88/11/03  12:35:25  kodiak
*   use AUTOINIT in ResidentTag
*   
*   Revision 35.1  88/08/02  12:27:24  kodiak
*   use SYSBASE (4) vs. dd_ExecBase
*   (a poor decision, in hindsight)
*   
*   Revision 35.0  87/10/26  11:31:28  kodiak
*   initial from V34, but w/ stripped log
*   
*   
**********************************************************************

d11 1
a11 1
*------ Included Files -----------------------------------------------
d39 1
a39 1
*------ Imported Globals ---------------------------------------------
d41 9
a54 2
*------ Imported Functions -------------------------------------------

a100 1
*------ Exported Globals ---------------------------------------------
a101 7
	XDEF		IDFuncInit
	XDEF		IDStructInit
	XDEF		IDInit

	XDEF		TDName


d104 1
a104 1
		MOVEM.L A2-A3/A6,-(SP)
d112 2
a113 2
		LEA	id_Unit+MP_MSGLIST(A6),A0
		NEWLIST	A0
d116 4
a119 4
		LEA	TDName(PC),A0
		MOVEQ	#UNIT_VBLANK,D0		; VBLANK TIMER
		MOVEQ	#0,D1
		LEA	id_RIOR(A6),A1
d121 2
a122 2
		TST.L	D0
		BNE	initTDErr
d124 2
a125 2
		MOVE.L	id_RIOR+IO_DEVICE(A6),id_TIOR+IO_DEVICE(A6)
		MOVE.L	id_RIOR+IO_UNIT(A6),id_TIOR+IO_UNIT(A6)
d128 4
a131 4
		LEA	KDName(PC),A0
		MOVEQ	#0,D0
		MOVEQ	#0,D1
		LEA	id_K1IOR(A6),A1
d133 2
a134 2
		TST.L	D0
		BNE	initKDErr
d140 2
a141 2
		LEA	id_HandlerList(A6),A0
		NEWLIST	A0
d144 5
a148 5
		LEA	id_TC(A6),A1
		LEA	id_Stk(A6),A0
		MOVE.L	A0,TC_SPLOWER(A1)
		MOVE.W	#(ID_STKSIZE/2)-1,D0
		MOVE.W	#$0A6A6,D1
d150 6
a155 6
		MOVE.W	D1,(A0)+
		DBF	D0,pattern0Loop
	;---	LEA	id_Stk+ID_STKSIZE(A6),A0
		MOVE.L	A0,TC_SPUPPER(A1)
		MOVE.L	A6,-(A0)		; argument (input data)
		MOVE.L	A0,TC_SPREG(A1)
d160 1
a160 1
		LEA	IDTaskStart(PC),A2
d167 1
a167 1
		MOVE.L	A6,D0
d170 2
a171 2
		MOVEM.L (SP)+,A2-A3/A6
		RTS
d174 1
a174 1
		LEA	id_TIOR(A6),A1
d178 5
a182 5
		MOVE.L	A6,A1
		MOVE.W	LIB_NEGSIZE(A6),D0
		SUBA.W	D0,A1
		ADD.W	LIB_POSSIZE(A6),D0
		EXT.L	D0
d186 2
a187 2
		MOVEQ	#0,D0
		BRA	itInitRts
d191 1
a191 1
		DC.B	0
d193 1
a193 1
		DC.B	0
d195 1
a195 1
		DS.W 0
d237 1
a237 1
		DC.W	0
d241 1
a241 1
		DC.W	-1
d243 4
a246 4
		DC.W	IDOpen-IDFuncInit
		DC.W	Close+(*-IDFuncInit)
		DC.W	IDExpunge-IDFuncInit
		DC.W	ExtFunc+(*-IDFuncInit)
d248 2
a249 2
		DC.W	BeginIO+(*-IDFuncInit)
		DC.W	AbortIO+(*-IDFuncInit)
d251 1
a251 1
		DC.W	-1
d253 1
d255 1
d257 9
a265 9
		DC.L	Invalid
		DC.L	IDReset
		DC.L	Read
		DC.L	Write
		DC.L	Update
		DC.L	IDClear
		DC.L	StopCmd
		DC.L	IDStart
		DC.L	Flush
d267 8
a274 8
		DC.L	IDTaskCommand	; IDAddHandler
		DC.L	IDTaskCommand	; IDRemHandler
		DC.L	IDTaskCommand	; IDWriteEvent
		DC.L	IDSetThresh
		DC.L	IDSetPeriod
		DC.L	IDTaskCommand	; IDSetMPort
		DC.L	IDTaskCommand	; IDSetMType
		DC.L	IDTaskCommand	; IDSetMTrig
d277 9
a285 9
		DC.B	CBInvalid
		DC.B	IDCBReset
		DC.B	CBRead
		DC.B	CBWrite
		DC.B	CBUpdate
		DC.B	IDCBClear
		DC.B	CBStopCmd
		DC.B	IDCBStart
		DC.B	CBFlush
d287 8
a294 8
		DC.B	IDCBTaskCommand	; IDCBAddHandler
		DC.B	IDCBTaskCommand	; IDCBRemHandler
		DC.B	IDCBTaskCommand	; IDCBWriteEvent
		DC.B	IDCBSetThresh
		DC.B	IDCBSetPeriod
		DC.B	IDCBTaskCommand	; IDCBSetMPort
		DC.B	IDCBTaskCommand	; IDCBSetMType
		DC.B	IDCBTaskCommand	; IDCBSetMTrig
d298 1
a298 1
		DS.W	0
d320 4
a323 4
		BSR	StopCmd
		BSR	Flush
		BSR	IDStart
		RTS
d335 2
a336 2
		BCLR    #IOB_QUICK,IO_FLAGS(A1)	; jimm: 8/1/86
		RTS
d346 1
a346 1
*	D0		     A6
d359 2
a360 2
		TST.W	LIB_OPENCNT(A6)
		BNE	DeferredExpunge
d365 1
a365 1
		BRA	DeferredExpunge
d391 2
a392 2
		LEA	id_Unit(A6),A0
		MOVE.L	A0,IO_UNIT(A1)
d394 3
a396 3
		BCLR	#LIBB_DELEXP,LIB_FLAGS(A6)
		ADDQ.W	#1,LIB_OPENCNT(A6)
		RTS
d398 38
a435 1
		END
@


36.12
log
@use Id instead of Header for 4.x rcs
@
text
@d1 1
a1 1
	TTL	'$Id$
d13 3
a172 2
		MOVE.L	id_RIOR+IO_DEVICE(A6),id_SIOR+IO_DEVICE(A6)
		MOVE.L	id_RIOR+IO_UNIT(A6),id_SIOR+IO_UNIT(A6)
@


36.11
log
@back to using dd_ExecBase, not SYSBASE (4)
@
text
@d1 1
a1 1
	TTL	'$Header: /usr2/kodiak/src/KS/rawinput/RCS/inputdev.asm,v 36.10 90/04/02 12:58:13 kodiak Exp Locker: kodiak $'
d13 3
@


36.10
log
@for rcs 4.x header change
@
text
@d1 1
a1 1
	TTL	'$Header: /usr2/kodiak/src/KS/rawinput/RCS/inputdev.asm,v 36.9 90/01/19 18:06:32 kodiak Exp Locker: kodiak $'
d13 3
d151 2
a152 1
		MOVE.L	D0,A6			; device library pointer
d202 1
a202 1
		move.l	SYSBASE,a0
@


36.9
log
@make SetMXxxx commands synchronous
@
text
@d1 1
a1 1
	TTL	'$Header: inputdev.asm,v 36.8 89/12/19 14:17:12 kodiak Exp $'
d10 1
a10 1
*   Source Control	$Locker:  $
d13 3
@


36.8
log
@autodoc update
@
text
@d1 1
a1 1
	TTL	'$Header: inputdev.asm,v 36.7 89/09/21 11:13:44 kodiak Exp $'
d10 1
a10 1
*   Source Control	$Locker: kodiak $
d13 3
a274 2
		INITBYTE	id_Unit+du_Flags,DUF_MOUSECHG

d308 3
a310 3
		DC.L	IDSetMPort
		DC.L	IDSetMType
		DC.L	IDSetMTrig
d328 3
a330 3
		DC.B	IDCBSetMPort
		DC.B	IDCBSetMType
		DC.B	IDCBSetMTrig
@


36.7
log
@correctly return lib base in d0 from input init
@
text
@d1 1
a1 1
	TTL	'$Header: inputdev.asm,v 36.6 89/09/20 20:50:36 kodiak Exp $'
d10 1
a10 1
*   Source Control	$Locker:  $
d13 3
d335 1
a335 1
******* input.device/command/Reset ***********************************
@


36.6
log
@first cut at fixing initialization race condition (message port
initialization of command queue in task)
@
text
@d1 1
a1 1
	TTL	'$Header: inputdev.asm,v 36.5 89/08/31 17:26:08 kodiak Exp $'
d10 1
a10 1
*   Source Control	$Locker: kodiak $
d13 4
a194 1
		MOVE.L	A6,D0
d198 2
@


36.5
log
@export TDName for keyboard use
@
text
@d1 1
a1 1
	TTL	'$Header: inputdev.asm,v 36.4 89/08/08 09:54:38 kodiak Exp $'
d13 3
d52 1
d78 1
d182 1
a182 1
		MOVE.L	A2,-(A0)		; argument (input data)
d185 3
d189 1
a189 2
		MOVEQ	#0,D0
		MOVE.L	D0,A3
d192 3
@


36.4
log
@fix repeat strategy to allow corruption of repeat input event on food chain
@
text
@d1 1
a1 1
	TTL	'$Header: inputdev.asm,v 36.3 89/06/29 15:39:13 kodiak Exp $'
d13 3
d120 2
@


36.3
log
@double buffer keyboard requests
@
text
@d1 1
a1 1
	TTL	'$Header: inputdev.asm,v 36.2 88/11/03 12:35:25 kodiak Exp $'
d13 3
d242 1
a242 1
		INITWORD	id_RepeatKey+ie_Code,-1
@


36.2
log
@use AUTOINIT in ResidentTag
@
text
@d1 1
a1 1
	TTL	'$Header: inputdev.asm,v 35.1 88/08/02 12:27:24 kodiak Exp $'
d13 3
d146 1
a146 1
		LEA	id_KIOR(A6),A1
d150 3
@


35.1
log
@use SYSBASE (4) vs. dd_ExecBase
(a poor decision, in hindsight)
@
text
@d1 1
a1 1
	TTL	'$Header: inputdev.asm,v 35.0 87/10/26 11:32:27 kodiak Exp $'
d10 1
a10 1
*   Source Control	$Locker:  $
d13 4
a65 1
	XREF		Init
d108 2
a114 9
		MOVEM.L	A0/A2,-(A7)
		LEA	idFuncInit(PC),A0
		LEA	idStructInit(PC),A1
		LEA	idInit(PC),A2
		MOVE.L	#id_SIZEOF,D0
		BRA	Init


idInit:
d120 1
d206 1
a206 1
idStructInit:
d245 1
a245 1
idFuncInit:
d248 4
a251 4
		DC.W	IDOpen-idFuncInit
		DC.W	Close+(*-idFuncInit)
		DC.W	IDExpunge-idFuncInit
		DC.W	ExtFunc+(*-idFuncInit)
d253 2
a254 2
		DC.W	BeginIO+(*-idFuncInit)
		DC.W	AbortIO+(*-idFuncInit)
@


35.0
log
@initial from V34, but w/ stripped log
@
text
@d1 1
a1 1
	TTL    '$Header: inputdev.asm,v 33.1 86/06/18 12:26:40 kodiak Exp $'
a2 11
*								     *
*   Copyright 1985, Commodore-Amiga Inc.   All rights reserved.	     *
*   No part of this program may be reproduced, transmitted,	     *
*   transcribed, stored in retrieval system, or translated into	     *
*   any language or computer language, in any form or by any	     *
*   means, electronic, mechanical, magnetic, optical, chemical,	     *
*   manual or otherwise, without the prior written permission of     *
*   Commodore-Amiga Incorporated, 983 University Ave. Building #D,   *
*   Los Gatos, California, 95030				     *
*								     *
**********************************************************************
d4 3
a6 1
*	input device functions
d8 1
a8 3
*   Source Control
*   --------------
*   $Header: inputdev.asm,v 33.1 86/06/18 12:26:40 kodiak Exp $
d10 1
a10 1
*   $Locker:  $
d13 2
d16 1
d123 1
a123 3
		MOVE.L	D0,A0
		MOVE.L	A6,dd_ExecBase(A0)
		MOVE.L	D0,A6			; library pointer
d166 1
a166 3
		MOVE.L	dd_ExecBase(A6),A2
		MOVE.L	A2,-(A0)		; arguments (input data
		MOVE.L	A6,-(A0)		;   & ExecBase)
@

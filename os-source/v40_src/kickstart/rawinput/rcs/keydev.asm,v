head     40.0;
branch   ;
access   ;
symbols  V36_14:36.4 V36_13:36.3 V36_11:36.2 L36_5:36.1;
locks    ; strict;
comment  @*   @;


40.0
date     93.03.08.10.57.21;  author darren;  state Exp;
branches ;
next     36.10;

36.10
date     90.04.13.12.44.00;  author kodiak;  state Exp;
branches ;
next     36.9;

36.9
date     90.04.02.16.30.19;  author kodiak;  state Exp;
branches ;
next     36.8;

36.8
date     90.04.02.13.00.29;  author kodiak;  state Exp;
branches ;
next     36.7;

36.7
date     89.08.31.17.27.24;  author kodiak;  state Exp;
branches ;
next     36.6;

36.6
date     89.07.31.17.14.06;  author kodiak;  state Exp;
branches ;
next     36.5;

36.5
date     89.06.09.12.12.35;  author kodiak;  state Exp;
branches ;
next     36.4;

36.4
date     89.03.25.14.09.36;  author kodiak;  state Exp;
branches ;
next     36.3;

36.3
date     89.02.20.13.02.36;  author kodiak;  state Exp;
branches ;
next     36.2;

36.2
date     88.11.03.12.35.30;  author kodiak;  state Exp;
branches ;
next     36.1;

36.1
date     88.08.02.12.22.01;  author kodiak;  state Exp;
branches ;
next     36.0;

36.0
date     88.04.05.12.38.36;  author kodiak;  state Exp;
branches ;
next     35.0;

35.0
date     87.10.26.11.32.07;  author kodiak;  state Exp;
branches ;
next     ;


desc
@@


40.0
log
@use tagged open device to open timer.device - saves ROM
@
text
@	TTL	'$Id: keydev.asm,v 36.10 90/04/13 12:44:00 kodiak Exp Locker: darren $
**********************************************************************
*
*			---------------
*   keydev.asm		KEYBOARD DEVICE		device functions
*			---------------
*
*   Copyright 1985, 1987 Commodore-Amiga Inc.
*
*   Source Control	$Locker: darren $
*
*   $Log:	keydev.asm,v $
*   Revision 36.10  90/04/13  12:44:00  kodiak
*   use Id instead of Header for 4.x rcs
*   
*   Revision 36.9  90/04/02  16:30:19  kodiak
*   back to using dd_ExecBase, not SYSBASE (4)
*   
*   Revision 36.8  90/04/02  13:00:29  kodiak
*   for rcs 4.x header change
*   
*   Revision 36.7  89/08/31  17:27:24  kodiak
*   perform key handshake timeout w/ timer ReadEClock function
*   
*   Revision 36.6  89/07/31  17:14:06  kodiak
*   remove timer cia-a-a use in preparation for timer device use of it
*   
*   Revision 36.5  89/06/09  12:12:35  kodiak
*   autodoc changes
*   
*   Revision 36.4  89/03/25  14:09:36  kodiak
*   don't destroy TOD when initting keyboard
*   allocate timer A
*   use keyint's handshake routines ensuring 75us pulse width
*   
*   Revision 36.3  89/02/20  13:02:36  kodiak
*   make names reflect usage: CIA-A, not B
*   
*   Revision 36.2  88/11/03  12:35:30  kodiak
*   use AUTOINIT in ResidentTag
*   
*   Revision 36.1  88/08/02  12:22:01  kodiak
*   first cut at timer-based handshake
*   
*   Revision 36.0  88/04/05  12:38:36  kodiak
*   remove dd_ExecBase references
*   
*   Revision 35.0  87/10/26  11:32:07  kodiak
*   initial from V34, but w/ stripped log
*   
**********************************************************************

	SECTION		rawinput

*------ Included Files -----------------------------------------------

	INCLUDE		"exec/types.i"
	INCLUDE		"exec/nodes.i"
	INCLUDE		"exec/lists.i"
	INCLUDE		"exec/memory.i"
	INCLUDE		"exec/ports.i"
	INCLUDE		"exec/libraries.i"
	INCLUDE		"exec/devices.i"
	INCLUDE		"exec/tasks.i"
	INCLUDE		"exec/io.i"
	INCLUDE		"exec/strings.i"
	INCLUDE		"exec/interrupts.i"
	INCLUDE		"exec/execbase.i"
	INCLUDE		"exec/errors.i"
	INCLUDE		"exec/initializers.i"

	INCLUDE		"hardware/cia.i"

	INCLUDE		"macros.i"
	INCLUDE		"kbdata.i"


*------ Imported Globals ---------------------------------------------

	XREF		KDName
	XREF		VERSION
	XREF		REVISION


*------ Imported Functions -------------------------------------------

	XREF_EXE	CloseDevice
	XREF_EXE	FindName
	XREF_EXE	OpenDevice
	XREF_EXE	OpenResource

	XREF_CIA	AbleICR
	XREF_CIA	AddICRVector
	XREF_CIA	RemICRVector

	XREF		BeginIO
	XREF		AbortIO
	XREF		SuccessfulExpunge
	XREF		DeferredExpunge
	XREF		ExtFunc
	XREF		Close

	XREF		Invalid
	XREF		Read
	XREF		Write
	XREF		Update
	XREF		KDClear
	XREF		StopCmd
	XREF		Start
	XREF		Flush

	XREF		KDReadEvent
	XREF		KDReadMatrix
	XREF		KDAddResetHandler
	XREF		KDRemResetHandler
	XREF		KDResetHandlerDone

	XREF		CBInvalid
	XREF		CBRead
	XREF		CBWrite
	XREF		CBUpdate
	XREF		KDCBClear
	XREF		CBStopCmd
	XREF		CBStart
	XREF		CBFlush

	XREF		KDCBReadEvent
	XREF		KDCBReadMatrix
	XREF		KDCBAddResetHandler
	XREF		KDCBRemResetHandler
	XREF		KDCBResetHandlerDone

	XREF		KDInterrupt
	XREF		KeyHandshakeBegin
	XREF		KeyHandshakeEnd


*------ Exported Globals ---------------------------------------------

	XDEF		KDFuncInit
	XDEF		KDStructInit
	XDEF		KDInit



**********************************************************************
KDInit:
		movem.l a6,-(sp)
*	    ;------ This is called from within MakeLibrary, after
*	    ;------ all the memory has been allocated

		exg	d0,a6			; save device library pointer
		move.l	d0,dd_ExecBase(a6)
		move.l	a0,dd_Segment(a6)

*	    ;------ initialize the unit command queue
		lea	kd_Unit+MP_MSGLIST(a6),a0
		NEWLIST	a0

	    ;------ initialize the reset handler list
		lea	kd_HandlerList(a6),a0
		NEWLIST	a0

*	    ;-- open timer.device for ReadEClock
		suba.l	a0,a0			;ODTAG_TIMER (save some ROM)
	;;	lea	TDName(pc),a0
		moveq	#UNIT_ECLOCK,d0
		lea	kd_Tick(a6),a1
		moveq	#0,d1
		LINKEXE	OpenDevice
		tst.l	d0
		bne.s	initErr			; (not very robust)

*	    ;------ open the CIA-A for the keyboard
		lea	CIAAName(pc),a1
		moveq	#0,d0
		LINKEXE OpenResource
		move.l	d0,kd_CIAAResource(a6)
		beq.s	initErr			; (not very robust)

		bsr	KeyHandshakeBegin

*	;-- set up the CIA interrupt routine for keyboard shift-in
*	    ;-- constant initialization already done
		move.l	a6,kd_IS+IS_DATA(a6)
		moveq	#CIAICRB_SP,d0
		lea	kd_IS(a6),a1
		LINKCIA AddICRVector

		bsr	KeyHandshakeEnd

initErr:
		move.l	a6,d0			; return library pointer
		movem.l (sp)+,a6
		rts

CIAAName:
		STRING	<'ciaa.resource'>


*----------------------------------------------------------------------
KDStructInit:
*	;------ initialize the device library structure
		INITBYTE	LN_TYPE,NT_DEVICE
		INITLONG	LN_NAME,KDName
		INITBYTE	LIB_FLAGS,LIBF_SUMUSED!LIBF_CHANGED
		INITWORD	LIB_VERSION,VERSION
		INITWORD	LIB_REVISION,REVISION

		INITLONG	dd_CmdVectors,kdCmdTable
		INITLONG	dd_CmdBytes,kdCmdBytes
		INITWORD	dd_NumCommands,KD_NUMCOMMANDS

*	;------ initialize kb_IS
		INITBYTE	kd_IS+LN_TYPE,NT_INTERRUPT
		INITLONG	kd_IS+LN_NAME,KDName
		INITLONG	kd_IS+IS_CODE,KDInterrupt

*	;------ initialize the unit command queue
		INITBYTE	kd_Unit+LN_TYPE,NT_MSGPORT
		INITLONG	kd_Unit+LN_NAME,KDName

		dc.w	0


KDFuncInit:
		dc.w	-1

		dc.w	KDOpen-KDFuncInit
		dc.w	Close+(*-KDFuncInit)
		dc.w	KDExpunge-KDFuncInit
		dc.w	ExtFunc+(*-KDFuncInit)

		dc.w	BeginIO+(*-KDFuncInit)
		dc.w	AbortIO+(*-KDFuncInit)

		dc.w	-1


kdCmdTable:
		dc.l	Invalid
		dc.l	KDReset
		dc.l	Read
		dc.l	Write
		dc.l	Update
		dc.l	KDClear
		dc.l	StopCmd
		dc.l	Start
		dc.l	Flush

		dc.l	KDReadEvent
		dc.l	KDReadMatrix
		dc.l	KDAddResetHandler
		dc.l	KDRemResetHandler
		dc.l	KDResetHandlerDone

kdCmdBytes:
		dc.b	CBInvalid
		dc.b	KDCBReset
		dc.b	CBRead
		dc.b	CBWrite
		dc.b	CBUpdate
		dc.b	KDCBClear
		dc.b	CBStopCmd
		dc.b	CBStart
		dc.b	CBFlush

		dc.b	KDCBReadEvent
		dc.b	KDCBReadMatrix
		dc.b	KDCBAddResetHandler
		dc.b	KDCBRemResetHandler
		dc.b	KDCBResetHandlerDone

KD_NUMCOMMANDS	EQU	(*-kdCmdBytes)

		ds.w	0

*****i* keyboard.device/CMD_RESET ************************************
*
*   NAME
*	CMD_RESET -- Reset the keyboard.
*
*   FUNCTION
*	CMD_RESET resets the keyboard device without destroying handles
*	to the open device.
*
*   IO REQUEST
*	io_Message	mn_ReplyPort set if quick I/O is not possible
*	io_Device	preset by the call to OpenDevice
*	io_Command	CMD_RESET
*	io_Flags	IOB_QUICK set if quick I/O is possible
*
**********************************************************************
KDCBReset	EQU	-1

KDReset:
		bsr	StopCmd
		bsr	KeyHandshakeBegin
		bsr	Flush
		bsr	KeyHandshakeEnd
		bsr	Start
		rts


*------ keyboard.device/Expunge --------------------------------------
*
*   NAME
*	Expunge - indicate a desire to remove the Keyboard device
*
*   SYNOPSIS
*	segment = Expunge(), keyboardDev
*	d0		     a6
*
*   FUNCTION
*	The Expunge routine is called when a user issues a RemDevice
*	call.  The existance of any other users of the device, as
*	determined by the library open count being non-zero, will
*	cause the Expunge to be deferred.  When the device is not
*	in use, or no longer in use, the Expunge is actually
*	performed, and the device removed from the system list.
*
*---------------------------------------------------------------------
KDExpunge:
	    ;-- see if any one is using the device
		tst.w	LIB_OPENCNT(a6)
		bne	DeferredExpunge

*	    ;-- this is really it.  Free up all the resources

		;-- free timer.device
		move.l	kd_Tick(a6),d0
		beq.s	exCIA

		move.l	d0,a1
		LINKEXE	CloseDevice

*		;-- disable keyboard interrupts
exCIA:
		moveq	#CIAICRF_SP,d0
		LINKCIA AbleICR		    ; disable shift
*		;-- remove the interrupt service routines
		moveq	#CIAICRB_SP,d0
		lea	kd_IS(a6),a1
		LINKCIA	RemICRVector
		bra	SuccessfulExpunge


*------ keyboard.device/Open -----------------------------------------
*
*   NAME
*	Open - a request to open the Keyboard device
*
*   SYNOPSIS
*	Open(iORequest), keyboardDev
*	     a1		 a6
*
*   FUNCTION
*	The open routine grants access to a device.  There are two
*	fields in the iORequest block that will be filled in: the
*	IO_DEVICE field has already been initialized by OpenDevice;
*	the Open routine will to initialize the IO_UNIT field as
*	appropriate.
*
*	The device open count will be incremented.  The device cannot
*	be expunged unless this open is matched by a Close device.
*
*   RESULTS
*	If the open was unsuccessful, IO_ERROR will be set, IO_UNIT
*	and IO_DEVICE will not be valid.
*
*---------------------------------------------------------------------
KDOpen:
	    ;-- this device has only one unit
		lea	kd_Unit(a6),a0
		move.l	a0,IO_UNIT(a1)

		bclr	#LIBB_DELEXP,LIB_FLAGS(a6)
		addq.w	#1,LIB_OPENCNT(a6)
		rts

		END
@


36.10
log
@use Id instead of Header for 4.x rcs
@
text
@d1 1
a1 1
	TTL	'$Id$
d10 1
a10 1
*   Source Control	$Locker: kodiak $
d13 3
a80 1
	XREF		TDName
d165 2
a166 1
		lea	TDName(pc),a0
@


36.9
log
@back to using dd_ExecBase, not SYSBASE (4)
@
text
@d1 1
a1 1
	TTL	'$Header: /usr2/kodiak/src/KS/rawinput/RCS/keydev.asm,v 36.8 90/04/02 13:00:29 kodiak Exp Locker: kodiak $'
d13 3
@


36.8
log
@for rcs 4.x header change
@
text
@d1 1
a1 1
	TTL	'$Header: /usr2/kodiak/src/KS/rawinput/RCS/keydev.asm,v 36.7 89/08/31 17:27:24 kodiak Exp Locker: kodiak $'
d13 3
d147 2
a148 1
		move.l	d0,a6			; device library pointer
@


36.7
log
@perform key handshake timeout w/ timer ReadEClock function
@
text
@d1 1
a1 1
	TTL	'$Header: keydev.asm,v 36.6 89/07/31 17:14:06 kodiak Exp $'
d13 3
@


36.6
log
@remove timer cia-a-a use in preparation for timer device use of it
@
text
@d1 1
a1 1
	TTL	'$Header: keydev.asm,v 36.5 89/06/09 12:12:35 kodiak Exp $'
d13 3
d69 1
d76 1
d78 1
d152 9
d317 7
d325 1
@


36.5
log
@autodoc changes
@
text
@d1 1
a1 1
	TTL	'$Header: keydev.asm,v 36.4 89/03/25 14:09:36 kodiak Exp $'
d13 3
d53 1
a64 2
	XREF		_ciaacra

d72 1
d131 1
a131 1
		MOVEM.L A6,-(SP)
d135 1
a135 1
		MOVE.L	D0,A6			; device library pointer
d139 2
a140 2
		LEA	kd_Unit+MP_MSGLIST(A6),A0
		NEWLIST	A0
d143 2
a144 2
		LEA	kd_HandlerList(A6),A0
		NEWLIST	A0
d147 2
a148 2
		LEA	CIAAName(PC),A1
		MOVEQ	#0,D0
d150 2
a151 2
		MOVE.L	D0,kd_CIAAResource(A6)
		BEQ	initCIAAErr
d153 1
a153 6
		;-- allocate the keyboard timer
		moveq	#CIAICRB_TA,d0
		lea	kd_IS(A6),A1
		LINKCIA AddICRVector
		tst.l	d0
		bne.s	initCIAAErr
a154 9
		;-- disable keyboard timer interrupts
		moveq	#CIAICRF_TA,d0
		LINKCIA	AbleICR

		MOVEQ	#$ffffff80,D0		; really just $80 used
		and.b	D0,_ciaacra		; ensure the clock is stopped
		and.b	D0,_ciaacra		;   and the shift bit is clear
		BSR	KeyHandshakeBegin

d157 3
a159 3
		MOVE.L	A6,kd_IS+IS_DATA(A6)
		MOVEQ	#CIAICRB_SP,D0
		LEA	kd_IS(A6),A1
d162 1
a162 1
		BSR	KeyHandshakeEnd
d164 4
a167 4
initCIAAErr:
		MOVE.L	A6,D0		    ;return library pointer
		MOVEM.L (SP)+,A6
		RTS
d195 1
a195 1
		DC.W	0
d199 1
a199 1
		DC.W	-1
d201 4
a204 4
		DC.W	KDOpen-KDFuncInit
		DC.W	Close+(*-KDFuncInit)
		DC.W	KDExpunge-KDFuncInit
		DC.W	ExtFunc+(*-KDFuncInit)
d206 2
a207 2
		DC.W	BeginIO+(*-KDFuncInit)
		DC.W	AbortIO+(*-KDFuncInit)
d209 1
a209 1
		DC.W	-1
d213 9
a221 9
		DC.L	Invalid
		DC.L	KDReset
		DC.L	Read
		DC.L	Write
		DC.L	Update
		DC.L	KDClear
		DC.L	StopCmd
		DC.L	Start
		DC.L	Flush
d223 5
a227 5
		DC.L	KDReadEvent
		DC.L	KDReadMatrix
		DC.L	KDAddResetHandler
		DC.L	KDRemResetHandler
		DC.L	KDResetHandlerDone
d230 9
a238 9
		DC.B	CBInvalid
		DC.B	KDCBReset
		DC.B	CBRead
		DC.B	CBWrite
		DC.B	CBUpdate
		DC.B	KDCBClear
		DC.B	CBStopCmd
		DC.B	CBStart
		DC.B	CBFlush
d240 5
a244 5
		DC.B	KDCBReadEvent
		DC.B	KDCBReadMatrix
		DC.B	KDCBAddResetHandler
		DC.B	KDCBRemResetHandler
		DC.B	KDCBResetHandlerDone
d248 1
a248 1
		DS.W	0
d269 6
a274 6
		BSR	StopCmd
		BSR	KeyHandshakeBegin
		BSR	Flush
		BSR	KeyHandshakeEnd
		BSR	Start
		RTS
d284 1
a284 1
*	D0		     A6
d297 2
a298 2
		TST.W	LIB_OPENCNT(A6)
		BNE	DeferredExpunge
d303 1
a303 1
		MOVEQ	#CIAICRF_SP,D0
d306 2
a307 2
		MOVEQ	#CIAICRB_SP,D0
		LEA	kd_IS(A6),A1
d309 1
a309 4
		MOVEQ	#CIAICRB_TA,D0
		LEA	kd_IS(A6),A1
		LINKCIA	RemICRVector
		BRA	SuccessfulExpunge
d319 1
a319 1
*	     A1		 A6
d338 2
a339 2
		LEA	kd_Unit(A6),A0
		MOVE.L	A0,IO_UNIT(A1)
d341 3
a343 3
		BCLR	#LIBB_DELEXP,LIB_FLAGS(A6)
		ADDQ.W	#1,LIB_OPENCNT(A6)
		RTS
@


36.4
log
@don't destroy TOD when initting keyboard
allocate timer A
use keyint's handshake routines ensuring 75us pulse width
@
text
@d1 1
a1 1
	TTL	'$Header: keydev.asm,v 36.3 89/02/20 13:02:36 kodiak Exp $'
d13 5
d261 1
a261 1
******* keyboard.device/command/Reset ********************************
d264 1
a264 1
*	Reset - reset the keyboard
d267 1
a267 1
*	Reset resets the keyboard device without destroying handles
@


36.3
log
@make names reflect usage: CIA-A, not B
@
text
@d1 1
a1 1
	TTL	'$Header: keydev.asm,v 36.2 88/11/03 12:35:30 kodiak Exp $'
d13 3
a55 1
	XREF		_ciaa
d109 2
d120 1
d156 4
a159 1
		BSR.S	handshakeStart
d168 1
a168 1
		BSR.S	handshakeComplete
a178 17
handshakeStart:
	;-- start the keyboard handshake
		MOVE.L	#_ciaa,A0
		MOVEQ	#0,D0
					    ; TOD 60 Hz set here!
		MOVE.B	D0,ciacra(A0)	    ; ensure the clock is stopped
		MOVE.B	D0,ciacra(A0)	    ;	and the shift bit is clear
		MOVE.B	#$040,ciacra(A0)    ; drive data line low
		RTS

handshakeComplete:
	    ;-- handshake to keyboard
					    ; TOD 60 Hz set here !
		MOVE.B	#$000,_ciaacra	    ; set up to shift in
		RTS


d276 1
a276 1
		BSR	handshakeStart
d278 1
a278 1
		BSR	handshakeComplete
@


36.2
log
@use AUTOINIT in ResidentTag
@
text
@d1 1
a1 1
	TTL	'$Header: keydev.asm,v 36.1 88/08/02 12:22:01 kodiak Exp $'
d13 3
d133 2
a134 2
*	    ;------ open the CIA-B for the keyboard
		LEA	CIABName(PC),A1
d137 2
a138 2
		MOVE.L	D0,kd_CIABResource(A6)
		BEQ	initCIABErr
d145 1
a145 1
		bne.s	initCIABErr
d162 1
a162 1
initCIABErr:
d167 1
a167 1
CIABName:
@


36.1
log
@first cut at timer-based handshake
@
text
@d1 1
a1 1
	TTL	'$Header: keydev.asm,v 36.0 88/04/05 12:38:36 kodiak Exp $'
d13 3
a65 1
	XREF		Init
d108 2
a114 9
		MOVEM.L	A0/A2,-(A7)
		LEA	kdFuncInit(PC),A0
		LEA	kdStructInit(PC),A1
		LEA	kdInit(PC),A2
		MOVE.L	#kd_SIZEOF,D0
		BRA	Init


kdInit:
d120 1
d186 1
a186 1
kdStructInit:
d210 1
a210 1
kdFuncInit:
d213 4
a216 4
		DC.W	KDOpen-kdFuncInit
		DC.W	Close+(*-kdFuncInit)
		DC.W	KDExpunge-kdFuncInit
		DC.W	ExtFunc+(*-kdFuncInit)
d218 2
a219 2
		DC.W	BeginIO+(*-kdFuncInit)
		DC.W	AbortIO+(*-kdFuncInit)
@


36.0
log
@remove dd_ExecBase references
@
text
@d1 1
a1 1
	TTL	'$Header: keydev.asm,v 35.0 87/10/26 11:32:27 kodiak Exp $'
d10 1
a10 1
*   Source Control	$Locker:  $
d13 3
d141 11
d321 5
a325 1
*		;-- remove the interrupt service routine
@


35.0
log
@initial from V34, but w/ stripped log
@
text
@d1 1
a1 1
	TTL    '$Header: keydev.asm,v 33.1 86/06/18 12:26:57 kodiak Exp $'
a2 11
*								     *
*   Copyright 1985, Commodore-Amiga Inc.   All rights reserved.	     *
*   No part of this program may be reproduced, transmitted,	     *
*   transcribed, stored in retrieval system, or translated into	     *
*   any language or computer language, in any form or by any	     *
*   means, electronic, mechanical, magnetic, optical, chemical,	     *
*   manual or otherwise, without the prior written permission of     *
*   Commodore-Amiga Incorporated, 983 University Ave. Building #D,   *
*   Los Gatos, California, 95030				     *
*								     *
**********************************************************************
d4 3
a6 1
*	keyboard device functions
d8 1
a8 3
*   Source Control
*   --------------
*   $Header: keydev.asm,v 33.1 86/06/18 12:26:57 kodiak Exp $
d10 1
a10 1
*   $Locker:  $
d13 2
d121 1
a121 3
		MOVE.L	D0,A0
		MOVE.L	A6,dd_ExecBase(A0)
		MOVE.L	D0,A6			; library pointer
@

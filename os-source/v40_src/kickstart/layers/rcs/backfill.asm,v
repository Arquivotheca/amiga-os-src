head     38.15;
branch   ;
access   ;
symbols  V40_1:38.15 V39_62:38.15 V39_61:38.14 V39_60:38.12 V39_58:38.12 V39_56:38.12 V39_55:38.12 V39_54:38.11 V39_53:38.10 V39_52:38.10 V39_51:38.10 V39_50:38.8 V39_49:38.6 V39_48:38.5 V39_46:38.5 V39_45:38.4 V39_44:38.3 V39_42:38.3 V39_41:38.3 V39_40:38.3 V39_39:38.3 V39_38:38.3 V39_37:38.3 V39_36:38.3 V39_35:38.4 V39_34:38.3 V39_33:38.3 V39_32:38.3 V39_30:38.3 V39_28:38.2 V39_27:38.2 V39_26:38.2 V39_25:38.2 V39_24:38.2 V39_23:38.2 V39_22:38.2 V39_21:38.2 V39_20:38.2 V39_18:38.2 V39_16:38.2 V39_15:38.2 V39_14:38.2 V39_12:38.2 V39_10:38.2 V38_7:38.2 V38_6:38.2 First_Native_Build:38.2;
locks    ; strict;
comment  @@;


38.15
date     92.10.15.17.15.55;  author mks;  state Exp;
branches ;
next     38.14;

38.14
date     92.09.18.14.49.32;  author mks;  state Exp;
branches ;
next     38.13;

38.13
date     92.06.16.18.21.49;  author mks;  state Exp;
branches ;
next     38.12;

38.12
date     92.04.13.17.07.07;  author mks;  state Exp;
branches ;
next     38.11;

38.11
date     92.04.08.09.42.55;  author mks;  state Exp;
branches ;
next     38.10;

38.10
date     92.03.26.18.20.30;  author mks;  state Exp;
branches ;
next     38.9;

38.9
date     92.03.26.18.10.28;  author mks;  state Exp;
branches ;
next     38.8;

38.8
date     92.03.26.10.12.46;  author mks;  state Exp;
branches ;
next     38.7;

38.7
date     92.03.26.08.44.30;  author mks;  state Exp;
branches ;
next     38.6;

38.6
date     92.03.23.12.19.17;  author mks;  state Exp;
branches ;
next     38.5;

38.5
date     92.03.05.08.53.38;  author mks;  state Exp;
branches ;
next     38.4;

38.4
date     92.03.03.18.01.15;  author mks;  state Exp;
branches ;
next     38.3;

38.3
date     91.11.18.13.26.17;  author mks;  state Exp;
branches ;
next     38.2;

38.2
date     91.08.02.10.17.23;  author mks;  state Exp;
branches ;
next     38.1;

38.1
date     91.07.25.09.54.28;  author bart;  state Exp;
branches ;
next     ;


desc
@@


38.15
log
@No longer clears the rp_Layer pointer in order to keep
the RastPort locking working.
@
text
@*******************************************************************************
*
*	$Id: backfill.asm,v 38.14 92/09/18 14:49:32 mks Exp Locker: mks $
*
*******************************************************************************
*
	include 'exec/types.i'
	include 'exec/nodes.i'
	include 'exec/lists.i'
	include	'exec/memory.i'
	include	'graphics/gfx.i'
	include 'graphics/clip.i'
	include	'graphics/layers.i'
	include	'graphics/rastport.i'
	include 'utility/hooks.i'
	include	'layersbase.i'
*
	xref	_intersect
	xref	LockLayer
	xref	UnlockLayer
*
*******************************************************************************
*
* FullBackFill(layer)
*
* This routine calls the backfill hook on all cliprects in the layer...
*
*	a0=layer
*
_FullBackFill:	xdef	_FullBackFill
		move.l	lr_rp(a0),a1		; Get RastPort...
		move.l	lr_BackFill(a0),a0	; Get hook...
		move.l	a2,-(sp)		; Save a2
		sub.l	a2,a2			; Clear a2 (no rectangle)
		CALLSYS	DoHookClipRects		; Do the hook (via LVO)
		move.l	(sp)+,a2		; Restore
		rts
*
*******************************************************************************
*
* Ok, so we needed a DoHookRect function that will call the given hook
* for each rectangle (or do the default hook which is colour 0 erase)
* This will then replace the graphics.library rastrect.c code (and others)
*
******* layers.library/DoHookClipRects ****************************************
*
*    NAME
*	DoHookClipRects - Do the given hook for each of the ClipRects    (V39)
*
*    SYNOPSIS
*	DoHookClipRects(hook,rport,rect)
*	                a0   a1    a2
*
*	void DoHookClipRects(struct Hook *,struct RastPort *,struct Rectangle *);
*
*    FUNCTION
*	This function will call the given hook for each cliprect in the
*	layer that can be rendered into.  This is how the backfill hook
*	in Layers is implemented.  This means that hidden simple-refresh
*	cliprects will be ignored.  It will call the SuperBitMap cliprects,
*	smart refresh off-screen cliprects, and all on screen cliprects.
*	If the rect parameter is not NULL, the cliprects are bounded to
*	the rectangle given.
*
*    INPUTS
*	hook - pointer to layer callback Hook which will be called
*	       with object == (struct RastPort *) result->RastPort
*	       and message == [ (Layer *) layer, (struct Rectangle) bounds,
*	                        (LONG) offsetx, (LONG) offsety ]
*
*	       This hook should fill the Rectangle in the RastPort
*	       with the BackFill pattern appropriate for offset x/y.
*
*	       If hook is LAYERS_BACKFILL, the default backfill is
*	       used for the layer.
*
*	       If hook is LAYERS_NOBACKFILL, the layer will not be
*	       backfilled (NO-OP).
*
*	rport- A pointer to the RastPort that is to be operated on.
*	       This function will lock the layer if the RastPort is
*	       layered...
*	       If the rport is non-layered your hook will be called with
*	       the rectangle as passed, the RastPort, and a NULL layer...
*
*	rect - The bounding rectangle that should be used on the layer.
*	       This rectangle "clips" the cliprects to the bound given.
*	       If this is NULL, no bounding will take place.
*	       *MUST* not be NULL if the RastPort is non-layered!
*
*    NOTES
*	The RastPort you are passed back is the same one passed to the
*	function.  You should *not* use "layered" rendering functions
*	on this RastPort.  Generally, you will wish to do BitMap operations
*	such as BltBitMap().  The callback is a raw, low-level rendering
*	call-back.  If you need to call a rendering operation with a
*	RastPort, make sure you use a copy of the RastPort and NULL the
*	Layer pointer.
*
*    SEE ALSO
*	graphics/clip.h utility/hooks.h
*
*******************************************************************************
*
* Registers:
*		a2 - The Layer
*		a3 - Temp Rectangle pointer
*		a4 - Temp Rectangle pointer
*		a5 - The current cliprect...
*
*		d2 - layer->bounds.MinX - layer->Scroll_X
*		d3 - layer->bounds.MinY - layer->Scroll_Y
*		d4 - The layer (storage)
*		d5 - Temp RASTPORT
*		d6 - The original rectangle bounds
*		d7 - The hook
*
DoHookClipRects:	xdef	DoHookClipRects
		move.l	a0,d0			; Get hook...
		subq.l	#1,d0			; Check for noop...
		bne.s	dhr_RealHook		; If not noop, we do real hook
		rts				; Exit (noop hook)
dhr_RealHook:	movem.l	a2-a5/d2-d7,-(sp)	; Save these...
		move.l	a2,d6			; Get the original rectangle
		move.l	rp_Layer(a1),a2		; Get the original layer
		move.l	a0,d7			; Get the hook...
		move.l	a1,d5			; Save the RastPort...
		move.l	rp_BitMap(a1),-(sp)	; Save bitmap...
		subq.l	#8,sp			; Make space for a rectangle...
*
* Now check for a layer and do the "right thing"
*
		move.l	a2,d4			; Store layer...
		bne.s	DoHookLayer		; A layer?
*
* We have no layer, just call the hook once for this rectangle...
*
		move.l	d7,a0			; Get the hook...
		move.l	d4,a1			; Get layer...
		move.l	d5,a2			; Get RastPort...
		move.l	d6,a3			; Get bounds...
		move.l	d6,a4			; Other bounds...
		moveq.l	#0,d2			; Clear offsets...
		moveq.l	#0,d3			; Clear offsets...
		pea	dhr_CleanUp(pc)		; Put the cleanup on stack...
		bra	DoBackFill		; Do backfill and exit...
*
* We have a layer so lock it first...
*
DoHookLayer:	move.l	a2,a1			; Get layer...
		bsr	LockLayer		; Lock the layer (layers call)
*
* Precalculate the offset difference...
*
		move.w	lr_MinX(a2),d2		; Get MinX
		sub.w	lr_Scroll_X(a2),d2	; - layer->Scroll_X
		move.w	lr_MinY(a2),d3		; Get MinY
		sub.w	lr_Scroll_Y(a2),d3	; - layer->Scroll_Y
*
* Now, calculate a new rectangle for the layer from the original rectangle...
*
		move.l	sp,a1			; Address of new rectangle...
		move.l	d6,a0			; Get the rectangle
		move.l	d6,-(sp)		; Save old rectangle
		beq.s	dhr_NoRect1		; If none...
		move.l	a1,d6			; Store new rectangle...
		move.w	(a0)+,d0		; Get MinX
		add.w	d2,d0			; Add in delta
		move.w	d0,(a1)+		; Save it...
		move.w	(a0)+,d0		; Get MinY
		add.w	d3,d0			; Add in delta
		move.w	d0,(a1)+		; Save it...
		move.w	(a0)+,d0		; Get MaxX
		add.w	d2,d0			; Add in delta
		move.w	d0,(a1)+		; Save it...
		move.w	(a0)+,d0		; Get MaxY
		add.w	d3,d0			; Add in delta
		move.w	d0,(a1)+		; Save it...
*
* Ok, so we now wish to walk the cliprects in the layer and do as needed...
*
dhr_NoRect1:	lea	lr_ClipRect(a2),a5	; Get cliprect list...
		subq.l	#8,sp			; Make space for one rectangle
dhr_Loop:	move.l	(a5),d0			; Get next cliprect
		beq.s	dhr_EndLoop		; If NULL, end of cliprect list
		move.l	d0,a5			; put into a5
		lea	cr_MinX(a5),a3		; Get rectangle (cliprect)
		tst.l	d6			; Check for NULL limits rect...
		beq.s	dhr_NoRect2		; If NULL, skip limits work...
		move.l	a3,a0			; Get limits set up...
		move.l	d6,a1			; Get active rectangle
		move.l	sp,a2			; Get new rectangle storage
		bsr	_intersect		; Do the intersect
		move.l	(a2)+,d0		; Get MinX/MinY
		move.l	(a2)+,d1		; Get MaxX/MaxY
		cmp.w	d0,d1			; Max - Min  (y)
		blt.s	dhr_Loop		; Less than 0?
		cmp.l	d0,d1			; Max - Min  (x/y)
		blt.s	dhr_Loop		; Less than 0?
		move.l	sp,a3			; a3 now points at this
dhr_NoRect2:	move.l	a3,a4			; Assume on screen first...
*
* Set up for call...
*
		move.l	d4,a1			; Put layer into real reg...
		move.l	d7,a0			; Get hook...
		move.l	d5,a2			; Get the rastport...
		tst.l	cr_lobs(a5)		; Are we obscured?
		bne.s	dhr_Hidden		; If so, we are hidden...
		bsr	DoBackFill		; Do the backfill...
		bra.s	dhr_Loop		; and loop back for next...
*
* Now, for hidden fills...
*
dhr_Hidden:	tst.l	cr_BitMap(a5)		; Do we have backing store?
		beq.s	dhr_Loop		; If not, we loop back...
*
* Build bounds within the backing store bitmap...
*
		moveq.l	#$F,d1			; Set up mask
		and.w	cr_MinX(a5),d1		; Mask for d1...
		sub.w	cr_MinX(a5),d1		; Subtract CR offset...
		move.w	ra_MaxX(a4),d0		; Get MaxX
		add.w	d1,d0			; addjust it for everything
		swap	d0			; Put MaxX in high word...
		move.w	ra_MaxY(a4),d0		; So adjust the Y...
		sub.w	cr_MinY(a5),d0		; to match...
		move.l	d0,-(sp)		; Put MaxX/Y on stack
*
		add.w	ra_MinX(a4),d1		; Make MinX...
		swap	d1			; Put MinX in upper word
		move.w	ra_MinY(a4),d1		; Get MinY
		sub.w	cr_MinY(a5),d1		; Make real MinY
		move.l	d1,-(sp)		; Put MinX/Y on stack
		move.l	sp,a3			; Get pointer to bounds
*
* Now change the bitmap of the rastport...
*
		move.l	rp_BitMap(a2),-(sp)	; Save bitmap...
		move.l	cr_BitMap(a5),rp_BitMap(a2)	; Get new bitmap
		bsr.s	DoBackFill		; do the backfill...
		move.l	(sp)+,rp_BitMap(a2)	; Restore bitmap
*
		addq.l	#8,sp			; Clean off the bounds
		bra.s	dhr_Loop		; Go back for more...
*
* End of the main loop.  Now all we have to check for is the SuperBitMap
* But first, lets get back the original rectangle...
*
dhr_EndLoop:	addq.l	#8,sp			; Get stack back...
		move.l	(sp)+,d6		; Get original back
*
* Ok, now set up for doing (maybe?) the super bitmap part...
*
		move.l	d4,a2			; Get layer back...
		move.l	lr_SuperBitMap(a2),d0	; Get SuperBitMap
		beq.s	dhr_CleanUp		; If no bitmap, exit...
*
* Ok, now set up as needed...
*
		move.l	d5,a0			; Get RastPort
		move.l	d0,rp_BitMap(a0)	; Set bitmap...
		moveq.l	#0,d2			; Clear the deltas...
		moveq.l	#0,d3			; (not used in superbitmap)
		lea	lr_SuperClipRect(a2),a5	; Point at cliprect list
dhr_SLoop:	move.l	(a5),d0			; Get next cliprect...
		beq.s	dhr_CleanUp		; If no more, exit...
		move.l	d0,a5			; put into a5
		lea	cr_MinX(a5),a3		; Get rectangle
		tst.l	d6			; Check for NULL limits rect...
		beq.s	dhr_NoRect3		; If NULL, skip limits work...
		move.l	a3,a0			; Get limits set up...
		move.l	d6,a1			; Get active rectangle
		move.l	sp,a2			; Get new rectangle storage
		bsr	_intersect		; Do the intersect
		move.l	(a2)+,d0		; Get MinX/MinY
		move.l	(a2)+,d1		; Get MaxX/MaxY
		cmp.w	d0,d1			; Max - Min
		blt.s	dhr_SLoop		; Less than 0?
		swap	d0			; Get other coordinate
		swap	d1			; ... into d0/d1
		cmp.w	d0,d1			; Max - Min
		blt.s	dhr_SLoop		; Less than 0?
		move.l	sp,a3			; a3 now points at this
dhr_NoRect3:	move.l	a3,a4			; Offset is the same...
*
* Set up for call
*
		move.l	d4,a1			; Put layer into real reg...
		move.l	d7,a0			; Get hook...
		move.l	d5,a2			; Get the rastport...
		bsr.s	DoBackFill		; Do the backfill...
		bra.s	dhr_SLoop		; Go back for more...
*
* Now, clean up the stack
*
dhr_CleanUp:	addq.l	#8,sp			; Adjust stack...
		move.l	d5,a0			; Get RastPort back...
		move.l	(sp)+,rp_BitMap(a0)	; Get bitmap back...
		tst.l	d4			; Check for layer pointer...
		beq.s	dhr_NoWork		; If no layer, exit...
		move.l	d4,a0			; Get layer...
		bsr	UnlockLayer		; Unlock the layer...
dhr_NoWork:	movem.l	(sp)+,a2-a5/d2-d7	; Restore...
		rts				; and exit...
*
*******************************************************************************
*
* This routine is the new, faster, smaller backfill hook routine.
* You call it with:
*
*	a0=hook
*	a1=layer
*	a2=RastPort
*	a3=Bounds (for bitmap)
*	a4=Bounds (for offset)
*	d2=delta X offset
*	d3=delta Y offset
*
* Special for graphics use *ONLY* and *ONLY* from DoHookClipRects...
*	a5=ClipRect (only for Graphics use of DoHookClipRects...)
*
* It will automatically handle HOOK==0 and HOOK==1 cases...
*
DoBackFill:	move.l	a0,d0	; Get the hook
		beq.s	Default	; If we have a NULL, it is default
		subq.l	#1,d0	; Drop by 1
		bne.s	DoFill	; If not 1, then we do something
		rts		; It was 1, so just return...
*
* Ok, we have a normal hook, so do the magic...
* (We need to place some stuff on the stack)
*
DoFill:		move.w	ra_MinY(a4),d0		; Get Y offset
		sub.w	d3,d0			; Subtract delta
		ext.l	d0			; Make a long...
		move.l	d0,-(sp)		; ...and place on stack
*
		move.w	ra_MinX(a4),d0		; Get X offset
		sub.w	d2,d0			; Subtract delta
		ext.l	d0			; Make a long...
		move.l	d0,-(sp)		; ...and place on stack
*
		move.l	ra_MaxX(a3),-(sp)	; Now do the whole bounds
		move.l	ra_MinX(a3),-(sp)	; on the stack...
		move.l	a1,-(sp)		; And place the layer
*
		move.l	sp,a1			; A1 now points at packet
		move.l	a6,-(sp)		; Save a6
		move.l	h_Entry(a0),a6		; Get hook address
		jsr	(a6)			; Call the hook...
*
		move.l	(sp)+,a6		; Get a6
		lea	5*4(sp),sp		; Clean up the 5 longwords
		rts				; And we are done...
*
*******************************************************************************
*
* This routine is the new, faster, smaller backfill hook routine.
* You call it with:
*
*	a0=Cliprect
*	a1=layer
*
* It will automatically handle HOOK==0 and HOOK==1 cases...
* (Note:  this is placed here to make short branch work...)
* (Note:  This is only for calling on-screen cliprects...)
*
_CallBackFillHook:	xdef	_CallBackFillHook
		movem.l	d2/d3/a2/a3/a4,-(sp)	; Save these
*
* Make copy of RastPort
*
		moveq.l	#rp_SIZEOF,d0		; Get size of RP
		sub.l	d0,sp			; Make space on stack
		moveq.l	#((rp_SIZEOF)/4)-1,d0	; Get number of long words...
		move.l	sp,a4			; Get new RP
		move.l	lr_rp(a1),a2		; Get RP
copyRP_Loop:	move.l	(a2)+,(a4)+		; Copy it...
		dbra.s	d0,copyRP_Loop		; Do the whole thing...
		move.l	sp,a2			; Get new RP
		clr.l	rp_Layer(a2)		; Clear layer pointer...
*
		lea	cr_MinX(a0),a4		; Get rectangle (bounds)
		move.l	a4,a3			; into both a3 and a4
*
		move.w	lr_MinX(a1),d2		; Get MinX
		sub.w	lr_Scroll_X(a1),d2	; - layer->Scroll_X
		move.w	lr_MinY(a1),d3		; Get MinY
		sub.w	lr_Scroll_Y(a1),d3	; - layer->Scroll_Y
*
		move.l	lr_BackFill(a1),a0	; Get the hook...
		bsr.s	DoBackFill		; Do the backfill
		moveq.l	#rp_SIZEOF,d0		; Get RP size
		add.l	d0,sp			; Release RP from stack...
		movem.l	(sp)+,d2/d3/a2/a3/a4	; Restore
		rts
*
*******************************************************************************
*
* This is the default backfill...
*
Default:	movem.l	d2-d7/a6,-(sp)		; Save these
*
* Set up for the empty blit...
*
		move.l	rp_BitMap(a2),a1	; Get bitmap
		move.l	a1,a0			; (both are the same)
*
		moveq.l	#0,d2			; Clear d2
		move.w	ra_MinX(a3),d2		; Get X pos
*
		moveq.l	#0,d3
		move.w	ra_MinY(a3),d3		; Get Y pos
*
		moveq.l	#0,d4
		move.w	ra_MaxX(a3),d4
		sub.l	d2,d4
		addq.l	#1,d4			; Get X size
*
		moveq.l	#0,d5
		move.w	ra_MaxY(a3),d5
		sub.l	d3,d5
		addq.l	#1,d5			; Get Y size
*
		move.l	d2,d0			; X Source
		move.l	d3,d1			; Y Source
		moveq.l	#0,d6			; NULL minterm
		moveq.l	#-1,d7			; FF mask
*
		move.l	lb_GfxBase(a6),a6	; Get GfxBase
		CALLSYS	BltBitMap		; Blit it
*
		movem.l	(sp)+,d2-d7/a6		; Restore
		rts
*
*******************************************************************************
*
		end
@


38.14
log
@Updated the autodoc
@
text
@d3 1
a3 1
*	$Id: backfill.asm,v 38.13 92/06/16 18:21:49 mks Exp Locker: mks $
a127 1
		clr.l	rp_Layer(a1)		; Clear the layer...
d300 1
a300 1
		move.l	d4,rp_Layer(a0)		; Get layer back...
@


38.13
log
@Removed special case code for SetRast and now uses the DoHookClipRects
directly with the default backfill hook.
@
text
@d3 1
a3 1
*	$Id: backfill.asm,v 38.12 92/04/13 17:07:07 mks Exp Locker: mks $
d90 9
@


38.12
log
@Cleaned up stack usage (no longer copies RastPort)
@
text
@d3 1
a3 1
*	$Id: backfill.asm,v 38.11 92/04/08 09:42:55 mks Exp Locker: mks $
a32 4
*
		move.l	a0,d0			; Check for NULL hook?
		beq.s	fbf_Default		; If NULL, we do default case
*
d35 1
a35 1
		bsr.s	DoHookClipRects		; Do the hook routines
a36 10
fbf_Exit:	rts
*
* Since SetRast() is much faster than BlitBitMap() for clearing a layer,
* we will use it to clear the layer...
*
fbf_Default:	; D0 is 0 when we get here...
		move.l	a6,-(sp)		; Save a6
		move.l	lb_GfxBase(a6),a6	; Get GfxBase
		CALLSYS	SetRast			; And call SetRast(a1,d0)
		move.l	(sp)+,a6		; Get a6
@


38.11
log
@Fixed non-layered case of DoHookCliprects
@
text
@d3 1
a3 1
*	$Id: backfill.asm,v 38.10 92/03/26 18:20:30 mks Exp Locker: mks $
d132 3
a134 11
*
* Now, make a copy of the rastport...
*
		moveq.l	#rp_SIZEOF,d0		; Get size of rastport
		sub.l	d0,sp			; Get space on the stack...
		move.l	sp,d5			; Save the pointer...
		moveq.l	#((rp_SIZEOF)/4)-1,d0	; Get number of long words...
		move.l	d5,a0			; Get new rastport
dhr_CopyRP:	move.l	(a1)+,(a0)+		; Copy the layer's RP to
		dbra.s	d0,dhr_CopyRP		; ...the temp RP on the stack
		clr.l	rp_Layer(sp)		; Clear the layer pointer...
d243 1
a243 1
* Now, clear the layer and change the bitmap of the temp rastport...
d301 1
a301 1
* Now, clean up the stack (remove the rastport from the stack, etc)
d303 6
a308 5
dhr_CleanUp:	moveq.l	#rp_SIZEOF+8,d0		; size of rastport + rect
		add.l	d0,sp			; Get back the space...
		move.l	d4,a0			; Get layer back...
		tst.l	d4			; Do we have one?
		beq.s	dhr_NoWork		; If not, exit...
@


38.10
log
@Autodoc change
@
text
@d3 1
a3 1
*	$Id: backfill.asm,v 38.9 92/03/26 18:10:28 mks Exp Locker: mks $
d152 1
a152 1
		move.l	d7,d0			; Get the hook...
@


38.9
log
@New DoHookClipRects...  Changed to use RastPorts rather the Layers...
@
text
@d3 1
a3 1
*	$Id: backfill.asm,v 38.8 92/03/26 10:12:46 mks Exp Locker: mks $
d103 1
@


38.8
log
@Fixed a tough problem with off-screen cliprects with some really
sick optimized code!  Sick, sick, sick!
@
text
@d3 1
a3 1
*	$Id: backfill.asm,v 38.7 92/03/26 08:44:30 mks Exp Locker: mks $
a17 2
	xref	_LVOBltBitMap
	xref	_LVOSetRast
d19 2
d31 4
a34 1
		move.l	lr_BackFill(a0),d0	; Get hook...
d36 1
a36 5
		subq.l	#1,d0			; Check for NO-OP hook
		beq.s	fbf_Exit		; Exit if it is NO-OP
		addq.l	#1,d0			; Add it back in...
		move.l	a0,a1			; Put registers right...
		move.l	d0,a0			; Put hook into a0
d46 1
a46 1
fbf_Default:
a47 2
		move.l	lr_rp(a0),a1		; Get RastPort
		moveq.l	#0,d0			; Pen-0
d49 1
a49 1
		jsr	_LVOSetRast(a6)		; And call SetRast(a1,d0)
d65 1
a65 1
*	DoHookClipRects(hook,layer,rect)
d68 1
a68 1
*	void DoHookClipRects(struct Hook *,struct Layer *,struct Rectangle *);
d94 5
a98 2
*	layer- A pointer to the layer that is to be operated on.  You
*	       must have this layer locked in order to make this call.
d123 5
a127 1
		movem.l	a2-a5/d2-d7,-(sp)	; Save these...
d129 1
a129 2
		move.l	a1,a2			; Get the original layer
		move.l	a2,d4			; Store layer...
a130 10
		move.l	d7,d0			; Now check the hook
		subq.l	#1,d0			; Subtract 1
		beq	dhr_NoWork		; Nothing to do...
*
* Precalculate the offset difference...
*
		move.w	lr_MinX(a2),d2		; Get MinX
		sub.w	lr_Scroll_X(a2),d2	; - layer->Scroll_X
		move.w	lr_MinY(a2),d3		; Get MinY
		sub.w	lr_Scroll_Y(a2),d3	; - layer->Scroll_Y
d138 2
a139 3
		move.l	d5,a1			; Get new rastport
		move.l	lr_RastPort(a2),a0	; Get old one...
dhr_CopyRP:	move.l	(a0)+,(a1)+		; Copy the layer's RP to
d142 30
d175 1
a176 2
		subq.l	#8,sp			; Make space for another one...
		move.l	sp,a1			; New rectangle...
d312 4
d444 1
a444 1
		jsr	_LVOBltBitMap(a6)	; Blit it
@


38.7
log
@Cleaned up the backfill stuff...
@
text
@d3 1
a3 1
*	$Id: backfill.asm,v 38.6 92/03/23 12:19:17 mks Exp Locker: mks $
d188 1
a188 1
		cmp.w	d0,d1			; Max - Min
d190 1
a190 3
		swap	d0			; Get other coordinate
		swap	d1			; ... into d0/d1
		cmp.w	d0,d1			; Max - Min
a211 3
		move.w	ra_MaxY(a4),d0		; So adjust the Y...
		sub.w	ra_MinY(a4),d0		; to match...
		move.w	d0,-(sp)		; Place Y Max on stack
d213 2
a214 1
		and.w	ra_MinX(a4),d1		; Mask for d1...
d216 11
a226 5
		sub.w	ra_MinX(a4),d0		; and adjust it
		add.w	d1,d0			; for everything
		move.w	d0,-(sp)		; Put MaxX on stack
		swap	d1			; Put the 0 in lower word
		move.l	d1,-(sp)		; Put MinX and (MinY==0)
d306 3
@


38.6
log
@Oops, bug in DoHookClipRects() that trashed the first longword of
the copied rastport...
@
text
@d3 1
a3 1
*	$Id: backfill.asm,v 38.5 92/03/05 08:53:38 mks Exp Locker: mks $
d149 1
a228 2
		moveq.l	#0,d0			; Get a NULL...
		move.l	d0,rp_Layer(a2)		; Clear layer pointer
a232 1
		move.l	d4,rp_Layer(a2)		; Get layer back...
a254 1
		move.l	d2,rp_Layer(a0)		; Clear layer...
d298 1
a299 1
*	a1=layer
d344 1
a344 2
*	a0=hook
*	a2=RastPort
a345 1
*	a3=Cliprect
d352 15
a366 2
		movem.l	d2/d3/a3/a4,-(sp)	; Save these
		lea	cr_MinX(a3),a4		; Get rectangle (bounds)
d374 1
d376 3
a378 1
		movem.l	(sp)+,d2/d3/a3/a4	; Restore a3/a4
@


38.5
log
@Fixed bug in _CallBackFillHook in that I was using a2 where a1 was required
@
text
@d3 1
a3 1
*	$Id: backfill.asm,v 38.4 92/03/03 18:01:15 mks Exp Locker: mks $
d163 1
a163 1
		move.l	d0,(a1)+		; Save it...
d169 1
a169 1
		move.l	d0,(a1)+		; Save it...
@


38.4
log
@Completely new code for doing the backfill hooks and a new LVO
This code should make life a bit more interesting for those
who make use of it (and make graphics smaller)

Also fixed a bug with the OffsetX and OffsetY values...
(I hate bugs like that...)
@
text
@d3 1
a3 1
*	$Id: backfill.asm,v 38.3 91/11/18 13:26:17 mks Exp Locker: mks $
d362 1
a362 1
		sub.w	lr_Scroll_X(a2),d2	; - layer->Scroll_X
d364 1
a364 1
		sub.w	lr_Scroll_Y(a2),d3	; - layer->Scroll_Y
@


38.3
log
@All of the fancy hook calling and default hook handling is here
@
text
@d3 1
a3 1
*	$Id$
d20 1
d31 12
a42 2
		move.l	lr_BackFill(a0),d0	; Get backfill hook
		bne.s	NotDefault		; If not 0, it is not default
d44 2
a45 1
* Now for the simple SetRast case
d47 1
d54 52
a105 1
FullRTS:	rts
d107 90
a196 2
NotDefault:	subq.l	#1,d0			; Check for NOP hook
		beq.s	FullRTS
d198 3
a200 13
		movem.l	d2/d3/a2-a5,-(sp)	; Save
		lea	lr_ClipRect(a0),a5	; Point at first cliprect...
		move.l	a0,d2			; Save layer for later
		move.l	lr_rp(a0),a2		; Get RastPort
		move.l	rp_BitMap(a2),d3	; Save bitmap...
*
FillLoop:	move.l	(a5),d0			; Get next cliprect
		beq.s	FillDone		; If none, we are done
		move.l	d0,a5			; Store it again
		lea	cr_MinX(a5),a3		; Get bounds into a3
		move.l	a3,a4			; ...and into a4
		move.l	d2,a1			; Get layer...
		move.l	lr_BackFill(a1),a0	; Get hook
d202 10
a211 3
		bne.s	HiddenFill		; If so, do hidden fill
		bsr.s	DoBackFill		; Do the backfill
		bra.s	FillLoop		; ...and check for the next
a212 2
HiddenFill:	tst.l	cr_BitMap(a5)		; Do we have a bitmap?
		beq.s	FillLoop		; Nope?  So don't fill
d226 1
a226 26
* Now temp clear the RP and layer
*
		move.l	cr_BitMap(a5),rp_BitMap(a2)	; Set the bitmap
		clr.l	rp_Layer(a2)		; Clear layer
		bsr.s	DoBackFill		; Do the backfill
		move.l	d2,rp_Layer(a2)		; Get layer back
		move.l	d3,rp_BitMap(a2)	; Get bitmap back
		addq.l	#8,sp			; Release the rectangle
		bra.s	FillLoop		; Go back and do next
*
* Now, clean up this mess
*
FillDone:	movem.l	(sp)+,d2/d3/a2-a5	; Restore
		rts
*
*******************************************************************************
*
* This routine is the new, faster, smaller backfill hook routine.
* You call it with:
*
*	a0=hook
*	a2=RastPort
*	a1=layer
*	a3=Cliprect
*
* It will automatically handle HOOK==0 and HOOK==1 cases...
d228 66
a293 7
_CallBackFillHook:	xdef	_CallBackFillHook
		movem.l	a3/a4,-(sp)	; Save these
		lea	cr_MinX(a3),a4	; Get rectangle (bounds)
		move.l	a4,a3		; into both a3 and a4
		bsr.s	DoBackFill	; Do the backfill
		movem.l	(sp)+,a3/a4	; Restore a3/a4
		rts
d305 2
d320 1
d325 1
d339 1
a339 1
		lea	4*5(sp),sp		; Clean up the 5 longwords
d341 28
@


38.2
log
@Native build version checkin...
@
text
@d1 197
a197 34
	INCLUDE "exec/types.i"
	INCLUDE "utility/hooks.i"

	xdef	_hookEntry

_hookEntry:
 	move.l	a1,-(sp)		; push message packet pointer
 	move.l	a2,-(sp)		; push object pointer
 	move.l	a0,-(sp)		; push hook pointer
	move.l	h_SubEntry(a0),a0	; fetch C entry point ...
	jsr	(a0)			; ... and call it
	lea	12(sp),sp		; fix stack
	rts

	xref	_refreshclean
	xdef	_HookFill

_HookFill:
	move.l	4(sp),a0		; fetch hook
	move.l	a6,4(sp)		; save current global context
	move.l	h_Data(a0),a6		; fetch local context from hook
	move.l	8(sp),a0		; object (rastport) pointer
	move.l  12(sp),a1		; message packet pointer
	move.l	16(a1),-(sp)		; offsety
	move.l	12(a1),-(sp)		; offsetx
	move.l	8(a1),-(sp)		; struct Rectangle (maxx/y)
	move.l	4(a1),-(sp)		; struct Rectangle (minx/y)
	move.l	a0,-(sp)		; object (rastport) pointer
	jsr	_refreshclean
	lea	20(sp),sp
	move.l	4(sp),a6		; restore current global context
	rts

	end
@


38.1
log
@begin_v38_development
@
text
@a3 6
	xdef	_FetchLayersBase

_FetchLayersBase:
	move.l	a6,d0
	rts

@

head     36.3;
branch   ;
access   ;
symbols  V40_4:36.3 V37_2:36.3 V36_177:36.3 V36_176:36.3 V36_175:36.3 V36_174:36.2 V36_172:36.1 V36_171:36.1 V36_167:36.1 V36_166:36.1 V36_161:36.1 V36_160:36.0;
locks    ; strict;
comment  @*   @;


36.3
date     90.04.13.11.27.56;  author kodiak;  state Exp;
branches ;
next     36.2;

36.2
date     90.04.02.11.06.16;  author kodiak;  state Exp;
branches ;
next     36.1;

36.1
date     89.06.30.12.14.46;  author kodiak;  state Exp;
branches ;
next     36.0;

36.0
date     89.06.05.18.20.00;  author kodiak;  state Exp;
branches ;
next     ;


desc
@@


36.3
log
@use Id instead of Header for 4.x rcs
@
text
@**
**	$Id$
**
**      init keymap library and resource
**
**      (C) Copyright 1985,1989 Commodore-Amiga, Inc.
**          All Rights Reserved
**
	SECTION	keymap

**	Included Files

	INCLUDE	"kldata.i"

	INCLUDE	"exec/initializers.i"

**	Exported Names

	XDEF	KLInitTable


**	Imported Names

	XREF	KLName
	XREF	KLID
	XREF	KRName
	XREF	REVISION
	XREF	VERSION

	XREF	SetKeyMapDefault
	XREF	AskKeyMapDefault
	XREF	MapRawKey
	XREF	MapANSI

	XREF	USAName
	XREF	USA1Name
	XREF	USALowMapType
	XREF	USALowMap
	XREF	USALCapsable
	XREF	USALRepeatable
	XREF	USAHighMapType
	XREF	USAHighMap
	XREF	USAHCapsable
	XREF	USAHRepeatable


	XLVO	AddResource


klFuncInit:
		dc.w	-1
		dc.w	open-klFuncInit
		dc.w	close-klFuncInit
		dc.w	expunge-klFuncInit
		dc.w	extFunc-klFuncInit
		dc.w	SetKeyMapDefault+(*-klFuncInit)
		dc.w	AskKeyMapDefault+(*-klFuncInit)
		dc.w	MapRawKey+(*-klFuncInit)
		dc.w	MapANSI+(*-klFuncInit)
		dc.w	-1

klStructInit:
	    INITBYTE	LN_TYPE,NT_LIBRARY
	    INITLONG	LN_NAME,KLName
	    INITBYTE	LIB_FLAGS,LIBF_SUMUSED!LIBF_CHANGED
	    INITWORD	LIB_REVISION,REVISION
	    INITWORD	LIB_VERSION,VERSION
	    INITWORD	LIB_OPENCNT,1	; never closes
	    INITBYTE	kl_R+LN_TYPE,NT_RESOURCE
	    INITLONG	kl_R+LN_NAME,KRName
	    INITSTRUCT	0,kl_USA+LN_NAME,,9-1
		dc.l	USAName
		dc.l	USALowMapType
		dc.l	USALowMap
		dc.l	USALCapsable
		dc.l	USALRepeatable
		dc.l	USAHighMapType
		dc.l	USAHighMap
		dc.l	USAHCapsable
		dc.l	USAHRepeatable
	    INITSTRUCT	0,kl_USA1+LN_NAME,,9-1
		dc.l	USA1Name
		dc.l	USALowMapType
		dc.l	USALowMap
		dc.l	USALCapsable
		dc.l	USALRepeatable
		dc.l	USAHighMapType
		dc.l	USAHighMap
		dc.l	USAHCapsable
		dc.l	USAHRepeatable

		dc.w	0

KLInitTable:
		dc.l	kl_SIZEOF
		dc.l	klFuncInit
		dc.l	klStructInit
		dc.l	init

;------	init
;
;	d0	library base
;
init:
		move.l	a5,-(a7)
		move.l	d0,a5

		lea	kl_USA1+kn_KeyMap(a5),a0
		move.l	a0,kl_KMDefault(a5)

		;-- kr_List -> kl_USA -> kl_USA1 -> kr_List
		lea	kl_R+kr_List(a5),a0
		move.l	a0,kl_USA+LN_PRED(a5)
		addq	#4,a0
		move.l	a0,kl_USA1+LN_SUCC(a5)
		lea	kl_USA(a5),a0
		move.l	a0,kl_R+kr_List+LH_HEAD(a5)
		move.l	a0,kl_USA1+LN_PRED(a5)
		lea	kl_USA1(a5),a0
		move.l	a0,kl_USA+LN_SUCC(a5)
		move.l	a0,kl_R+kr_List+LH_TAILPRED(a5)

		lea	kl_R(a5),a1
		CALLLVO	AddResource

		move.l	a5,d0
		move.l	(a7)+,a5
		rts


;------	open
;
;	a6	library base
;
open:
		move.l	a6,d0
		rts


;------	close
;------	expunge
;------	extFunc
close:
expunge:
extFunc:
		moveq	#0,d0
		rts

	END
@


36.2
log
@for rcs 4.x header change
@
text
@d2 1
a2 1
**	$Header: /usr2/kodiak/src/KS/keymap/RCS/library.asm,v 36.1 89/06/30 12:14:46 kodiak Exp Locker: kodiak $
@


36.1
log
@fix initstruct count
@
text
@d2 1
a2 1
**	$Header: library.asm,v 36.0 89/06/05 18:20:00 kodiak Exp $
@


36.0
log
@assembler version
@
text
@d2 1
a2 1
**	$Header: ramlib.asm,v 36.4 89/06/05 14:20:29 kodiak Exp $
d71 1
a71 1
	    INITSTRUCT	0,kl_USA+LN_NAME,,9
d81 1
a81 1
	    INITSTRUCT	0,kl_USA1+LN_NAME,,9
@

head     39.1;
branch   ;
access   ;
symbols  rel39_24:39.1 rel39_18:39.1 rel39_97:39.1 rel39_90:39.1 rel39_89:39.1 rel39_82:39.1 rel39_76:39.1 rel39_71:39.1 rel39_65:39.1 rel39_64:39.1 rel39_61:39.1 rel39_60:39.1 rel39_55:39.1 rel39_52:39.0 rel39_47:39.0 rel39_39:39.0 rel39_38:39.0 rel39_37:39.0 rel39_35:39.0 rel39_34:39.0 V37_30:37.1 V37_29:37.1 V37_28:37.1 V37_27:37.1 V37_25:37.1 V37_24:37.1 V37_23:37.1 V37_22:37.1 V37_21:37.1 V37_20:37.1 V37_19:37.1 V37_18:37.1 V37_17:37.1 V37_15:37.1 V37_14:37.1 V37_13:37.0 V37_12:37.0 V37_11:37.0 V37_10:37.0 V37_9:37.0 V37_8:37.0 V37_7:37.0 V37_6:37.0 V37_5:37.0 V37_4:37.0 V37_3:37.0 V37_2:37.0 V37_1:37.0 V36_209:1.11 V36_208:1.11 V36_207:1.11 V36_205:1.11 V36_203:1.11 V36_202:1.11 V36_201:1.11 V36_200:1.11 V36_199:1.11 V36_198:1.11 V36_196:1.11 V36_195:1.11 V36_194:1.11 V36_193:1.11 V36_192:1.10 V36_191:1.10 V36_190:1.10 V36_189:1.10 V36_188:1.10 V36_187:1.10 V36_186:1.10 V36_185:1.10 V36_184:1.10 V36_183:1.9 V36_182:1.8 V36_181:1.8 V36_180:1.8;
locks    ; strict;
comment  @@;


39.1
date     92.04.27.14.30.10;  author chrisg;  state Exp;
branches ;
next     39.0;

39.0
date     91.08.21.17.24.50;  author chrisg;  state Exp;
branches ;
next     37.2;

37.2
date     91.05.01.14.04.22;  author chrisg;  state Exp;
branches ;
next     37.1;

37.1
date     91.02.12.13.09.04;  author chrisg;  state Exp;
branches ;
next     37.0;

37.0
date     91.01.07.15.24.28;  author spence;  state Exp;
branches ;
next     1.11;

1.11
date     90.10.18.15.27.27;  author bart;  state Exp;
branches ;
next     1.10;

1.10
date     90.09.20.11.03.51;  author bart;  state Exp;
branches ;
next     1.9;

1.9
date     90.09.13.12.26.47;  author bart;  state Exp;
branches ;
next     1.8;

1.8
date     90.07.27.16.37.52;  author bart;  state Exp;
branches ;
next     1.7;

1.7
date     90.03.27.16.19.41;  author bart;  state Exp;
branches ;
next     1.6;

1.6
date     90.03.17.10.17.04;  author bart;  state Exp;
branches ;
next     1.5;

1.5
date     90.03.13.12.53.03;  author bart;  state Exp;
branches ;
next     1.4;

1.4
date     88.08.25.10.26.34;  author bart;  state Exp;
branches ;
next     1.3;

1.3
date     88.06.17.21.37.06;  author dale;  state Exp;
branches ;
next     1.2;

1.2
date     88.06.17.16.50.07;  author dale;  state Exp;
branches ;
next     1.1;

1.1
date     88.05.01.15.36.14;  author dale;  state Exp;
branches ;
next     ;


desc
@@


39.1
log
@ killed stub
@
text
@*******************************************************************************
*
*	$Id: draw.asm,v 39.0 91/08/21 17:24:50 chrisg Exp Locker: chrisg $
*
*******************************************************************************

    include 'exec/types.i'
    include 'graphics/gfxbase.i'
    include 'graphics/gfx.i'
    include 'exec/nodes.i'
    include 'exec/lists.i'
    include 'exec/ports.i'
    include 'exec/interrupts.i'
    include 'exec/libraries.i'

    include 'graphics/clip.i'
    include 'graphics/rastport.i'
    include 'hardware/blit.i'
    include 'hardware/custom.i'
    include 'submacs.i'
    include '/sane_names.i'

    section	graphics
    xdef    _Draw
    xref    cdraw
    xref    waitblitdone

    xref    _custom

******* graphics.library/Draw *************************************************
*
*   NAME
*       Draw -- Draw a line between the current pen position
*                       and the new x,y position.
*
*   SYNOPSIS
*       Draw( rp,   x,     y)
*             a1  d0:16  d1:16
*
*	void Draw( struct RastPort *, SHORT, SHORT);
*
*   FUNCTION
*       Draw a line from the current pen position to (x,y).
*
*   INPUTS
*
*	rp - pointer to the destination RastPort
*	x,y - coordinates of where in the RastPort to end the line.
*
*   BUGS
*
*   SEE ALSO
*	Move() graphics/rastport.h
*
******************************************************************************


doflat:
*   do fast horizontal lines
*   this goes left to right
    btst    #ONE_DOTn,rp_Flags+1(a1)
    bne     nodoflat
*   begin reinstalling fast horizontal lines
*   bra     nodoflat

    move.w  d0,d1	* scratch
    move.w  d2,d5	* scratch
    add.w   d5,d1       * ending x position
    asr.w   #3,d5       * get x byte address in a row
    ext.l   d5
    asr.w   #1,d5
    lsl.w   #4,d5
    sub.w   d5,d1
    asr.w   #4,d1
    addq.w  #1,d1
    or.w    #$40,d1     * 1 row plus this many words d0= blitsize
    cmp.w   #$40,d1
    ble.s   continue_flat
    clr.w   d1		* restore
    bra	    nodoflat	* too big for now -- resort to alternate method
continue_flat:

    move.w  rp_LinePtrn(a1),d7  * get drawing pattern
    move.b  rp_linpatcnt(a1),d1
    sub.b   d0,rp_linpatcnt(a1)
    moveq   #15,d5
    sub.w   d5,d1
    add.w   d2,d1
    and.w   d5,d1          * this line may not be needed?
    ror.w   d1,d7

    move.w  d7,d6
    moveq   #-1,d7
    lsr.w   d4,d7
    lea     _custom,a0
    OWNBLITTER
    WAITBLITDONE a0
*    WAITBLITDONE a0
    move.w  d7,fwmask(a0)
    moveq   #-1,d7
    add.w   d2,d0           * ending x position
    move.w  d5,d1
    sub.w   d0,d1
    and.w   d5,d1          * this line may be needed?
    asl.w   d1,d7
    move.w  d7,lwmask(a0)
    asr.w   #3,d2           * get x byte address in a row
    ext.l   d2
    add.l   d2,d3           * address offset into raster planes
    asr.w   #1,d2
    lsl.w   #4,d2
    sub.w   d2,d0
    asr.w   #4,d0
    addq.w  #1,d0
    or.w    #$40,d0     * 1 row plus this many words d0= blitsize
    move.w  d4,d2   * save for compit
    move.w  #DEST+SRCC,d4
    move.w  #$FFFF,adata(a0)
    clr.l   d5
    bra     flatback

_Draw:
*	rp_layer must be 0 offset of layer
	ifne	rp_Layer
		fail
	endc
	tst.l	(a1)
	bne		cdraw

; void __asm baredraw(register __a1 struct RastPort *rp, register __d0 LONG x, register __d1 LONG y);
_baredraw::
BareDraw::
    movem.l d2-d7/a2/a3,-(sp)    * save registers on stack
    movem.w rp_cp_x(a1),d2/d3   * get cp_x and cp_y
    movem.w d0/d1,rp_cp_x(a1)   * new cp_x and cp_y

    move.l  rp_BitMap(a1),a2

    moveq   #15,d4
    and.w   d2,d4       * bit position b

    sub.w   d3,d1       * dy = y - cpy
    ifne	bm_BytesPerRow
	    fail
    endc
    mulu    (a2),d3   * bperrow time number of rows
    sub.w   d2,d0       * dx = x - cpx
    if >=
		if d1>=#0.w
	    	beq         doflat
nodoflat:
	    	if d0>d1.w
				moveq  #LINEMODE+OCTANT1,d7
	    	else
				moveq  #LINEMODE+OCTANT2,d7
				exg d0,d1
	    	endif
		else
	    	neg.w   d1  * dy = -dy
	    	if d0>d1.w
				moveq  #LINEMODE+OCTANT8,d7
	    	else
				moveq  #LINEMODE+OCTANT7,d7
				exg d0,d1
	    	endif
		endif
    else
		neg.w   d0      * dx = -dx
		if d1>=#0.w
	    	if d0>d1.w
				moveq  #LINEMODE+OCTANT4,d7
	    	else
				moveq  #LINEMODE+OCTANT3,d7
				exg d0,d1
	    	endif
		else
	    	neg.w   d1  * dy = -dy
	    	if d0>d1.w
				moveq  #LINEMODE+OCTANT5,d7
	    	else
				moveq  #LINEMODE+OCTANT6,d7
				exg d0,d1
	    	endif
		endif
    endif
    add.w   d1,d1       * use dy as 'i' in bresblit

*   now using d1 as 'i' and d0 as dx or count
    asr.w   #3,d2           * get x byte address in a row
    ext.l   d2
    add.l   d2,d3           * address offset into raster planes
* calculate initial error term
    move.w  d1,d2
    sub.w   d0,d2           * d2 = initial error term
    if <
		or.w    #SIGNFLAG,d7
    endif
    btst    #ONE_DOTn,rp_Flags+1(a1)
    if <>
		or.w    #ONEDOT,d7
    endif
*   load up ptr to registers in a0
    lea _custom,a0

    move.w  d2,a3               * bltpta reloaded each pass
    move.w  d1,d6 		* save for later use
    sub.w   d0,d2
    move.w  d2,d1
    swap    d2			* stash in upper word 

    OWNBLITTER
    WAITBLITDONE a0

    move.w  d6,bltmdb(a0)
    swap    d6			* stash in upper word 
    move.w  rp_LinePtrn(a1),d6  * reloaded each pass
    move.w  d1,bltmda(a0)

    moveq.l #-1,d1
    move.l  d1,fwmask(a0)   * and ar_lwmask
    move.w  #$8000,adata(a0)
    move.w  (a2),bltmdc(a0)

    move.b  rp_linpatcnt(a1),d1 * reusing d1

	swap    d1
    clr.w   d1
    asr.l   #4,d1
    or.w    d1,d7               * put preshift for pattern in con1
    sub.b   d0,rp_linpatcnt(a1) * update to next shift value
    move.w  d7,d5

    move.w  d4,d2       * save here for compit
    swap    d4
    asr.l   #4,d4       * a real 12 bit left shift
    or.w    #DEST+SRCA+SRCC,d4

*   setup bltsize
    addq.w  #1,d0
    ext.l	d0
    asl.l   #HSIZEBITS,d0
    addq.w  #2,d0
    if #$10002<d0.l
	swap    d4
	move.w  #15,d4
	sub.w   d2,d4	* save bit offset for big_pain
	swap    d4
	bra	big_line
    endif

flatback:
    clr.w   d1                  * i

*   next statement true only for FRSTDOTn<8
    bclr    #FRST_DOTn,rp_Flags+1(a1)
    if =	* first dot?
		cmp.b	#2,rp_DrawMode(a1)
		beq.s	compit	* must recomplement first dot
    endif

    move.b  bm_Depth(a2),d2         * count to
    lea     bm_Planes(a2),a2    * get ptr to raster table
    repeat
		move.l  (a2)+,d7    * get a ptr to a raster
		btst    d1,rp_Mask(a1)
		if <>
	    	swap    d5
	    	move.w  d4,d5
	    	move.b  rp_minterms(a1,d1.w),d5
	    	swap    d5
	    	add.l   d3,d7   * d7 is destination ptr
	    	WAITBLITDONE a0
*    WAITBLITDONE a0
	    	move.l  d5,bltcon0(a0)  * and bltcon1
	    	move.w  a3,bltpta+2(a0)
	    	move.l  d7,bltptc(a0)
	    	move.l  d7,bltptd(a0)
	    	move.w  d6,bdata(a0)    * and draw thru bdata shifter
	    	move.w  d0,bltsize(a0)  * start blit
		endif
		addq.b  #1,d1
    until d1=d2.b
compit_return:
    DISOWNBLITTER
    movem.l (sp)+,d2-d7/a2/a3    * restore parameters from stack
    rts

compit:
*           have to reblast this dot, but what about the current pattern?
* this code uses d2 differently
    moveq   #15,d1
    sub.w   d2,d1
    move.w  d1,d2

    clr.w   d1                  * i
	move.l	a4,-(sp)
	move.l	a2,a4
    lea     bm_Planes(a4),a2    * get ptr to raster table
    repeat
		move.l  (a2)+,d7    * get a ptr to a raster
		btst    d1,rp_Mask(a1)
		if <>
	    	swap    d5
	    	move.w  d4,d5
	    	move.b  rp_minterms(a1,d1.w),d5
	    	swap    d5
	    	add.l   d3,d7   * d7 is destination ptr
	    	move.l  a3,-(sp)
	    	move.l  d7,a3
	    	bchg    d2,(a3)
	    	move.l  (sp)+,a3
	    	WAITBLITDONE a0
*    WAITBLITDONE a0
	    	move.w  a3,bltpta+2(a0)
	    	move.l  d7,bltptc(a0)
	    	move.l  d7,bltptd(a0)
	    	move.l  d5,bltcon0(a0)  * and bltcon1
	    	move.w  d6,bdata(a0)    * and draw thru bdata shifter
	    	move.w  d0,bltsize(a0)  * start blit
		endif
		addq.b  #1,d1
    until bm_Depth(a4).b=d1
	move.l	(sp)+,a4
    bra compit_return

big_line:
    clr.w   d1                  * i

*   next statement true only for FRSTDOTn<8
    bclr    #FRST_DOTn,rp_Flags+1(a1)
    if =	* first dot?
		cmp.b	#2,rp_DrawMode(a1)
		beq	bigcompit	* must recomplement first dot
    endif

    move.b  bm_Depth(a2),d2         * count to
    lea     bm_Planes(a2),a2    * get ptr to raster table
    repeat
		move.l  (a2)+,d7    * get a ptr to a raster
		btst    d1,rp_Mask(a1)
		if <>	.extend
    			swap	d5
			exg	a3,d4
			move.w	d4,d5
    			swap	d4
			move.w	d5,d4
    			swap	d4
			exg	a3,d4
		    	move.w  d4,d5
		    	move.b  rp_minterms(a1,d1.w),d5
		    	swap    d5
		    	add.l   d3,d7   * d7 is destination ptr
		    	WAITBLITDONE a0
*    WAITBLITDONE a0
		    	move.l  d5,bltcon0(a0)  * and bltcon1
		    	move.w  a3,bltpta+2(a0)
		    	move.l  d7,bltptc(a0)
		    	move.l  d7,bltptd(a0)
		    	move.w  d6,bdata(a0)    * and draw thru bdata shifter
			move.l	d0,-(sp) 	* save total bltsize
			repeat
			move.w	#2,bltsize(a0)
			btst    #ONE_DOTn,rp_Flags+1(a1) * one_dot ?
			beq.s   bigline_cnt
			btst	#4,d5		* horizontal ?
			beq.s   bigline_cnt
			jsr	big_pain	* better be careful
bigline_cnt:		sub.l	#$10000,d0
			WAITBLITDONE a0
			until	#$10002>d0.l
			cmp.l	#$42,d0
			blt.s	bigline_exact
		    	move.w  d0,bltsize(a0)  * start blit
bigline_exact:		move.l (sp)+,d0		* restore total bltsize
		endif
		addq.b  #1,d1
    until d1=d2.b
bigcompit_return:
    DISOWNBLITTER
    movem.l (sp)+,d2-d7/a2/a3    * restore parameters from stack
    rts

bigcompit:
*           have to reblast this dot, but what about the current pattern?
* this code uses d2 differently
    moveq   #15,d1
    sub.w   d2,d1
    move.w  d1,d2

    clr.w   d1                  * i
	move.l	a4,-(sp)
	move.l	a2,a4
    lea     bm_Planes(a4),a2    * get ptr to raster table
    repeat
		move.l  (a2)+,d7    * get a ptr to a raster
		btst    d1,rp_Mask(a1)
		if <>	.extend
		    	swap    d5
			exg	a3,d4
			move.w	d4,d5
			swap	d4
			move.w	d5,d4
    			swap	d4
			exg	a3,d4
		    	move.w  d4,d5
		    	move.b  rp_minterms(a1,d1.w),d5
		    	swap    d5
		    	add.l   d3,d7   * d7 is destination ptr
		    	move.l  a3,-(sp)
		    	move.l  d7,a3
		    	bchg    d2,(a3)
		    	move.l  (sp)+,a3
		    	WAITBLITDONE a0
*    WAITBLITDONE a0
		    	move.w  a3,bltpta+2(a0)
		    	move.l  d7,bltptc(a0)
		    	move.l  d7,bltptd(a0)
		    	move.l  d5,bltcon0(a0)  * and bltcon1
		    	move.w  d6,bdata(a0)    * and draw thru bdata shifter
			move.l	d0,-(sp) 	*s ave total bltsize
			repeat
			move.w	#2,bltsize(a0)
			btst    #ONE_DOTn,rp_Flags+1(a1) * one_dot ?
			beq.s   bigcomp_cnt
			btst	#4,d5		* horizontal ?
			beq.s   bigcomp_cnt
			jsr	big_pain	* better be careful
bigcomp_cnt:    	sub.l	#$10000,d0
			WAITBLITDONE a0
			until	#$10002>d0.l
			cmp.l	#$42,d0
			blt.s	bigcomp_exact
			move.w  d0,bltsize(a0)  * start blit
bigcomp_exact:		move.l (sp)+,d0		* restore total bltsize
		endif
		addq.b  #1,d1
    until bm_Depth(a4).b=d1
	move.l	(sp)+,a4
    bra bigcompit_return


*	ok, the hardware may be in ONEDOT mode. restarting this line will
*	render an extraneous dot unless the bresenham step has just occurred.
*	so, we may have to modify the initial pixel for each succesive segment
*	unless we are just about to render the ONEDOT. yuck! bart

*	a cusp condition (B10406) can occur when final error term is
*	exactly 4y -2x, upon blitter restart an extraneous bit is blitted
*	to fix this we must complement the extraneous bit as well. bart

big_pain:	
	movem.l d0-d2/a0,-(sp)		* preserve context
	moveq	#0,d0
	swap	d4
	move.w	d4,d2			* recover offset into byte
	swap	d4
	exg	a3,d4
	swap	d2			* retrieve 2y-2x from upper word
	swap	d4			* retrieve error from upper word
	swap	d6			* retrieve 2y    from upper word
	move.w	#1024,d1
	bset	#31,d0 
	tst.w	d4
	bra.s	doit
loop    bgt.s	down
	bset	#31,d0
	bne.s	next
	add.w	#1,d0
next	add.w   d6,d4
	bra.s	doit
down	bclr	#31,d0
	add.w   d2,d4
doit	dbra    d1,loop
	bgt.s	just			* if test greater than more
	bchg	#31,d0			* if just negative will set bit
	bne.s	just
	add.w	d6,d2			* calculate 4y - 2x
	cmp.w	d4,d2			* if error = 4y - 2x then
	bne.s	fixx			* blitter cusp condition exists
	addq.w	#1,d0			* so guaranteed positive, fall thru
just	bmi.s	fixx			* if test negative we are all done
	bclr	#31,d0			* if just greater or equal clear bit
fixx	swap	d2
	swap	d4					
	swap	d6
	exg	a3,d4
	btst	#31,d0
	bne.s	brts
	move.l  rp_BitMap(a1),a0
	mulu	(a0),d0			* bytesperrow * rows
	btst	#3,d5
	beq.s	ypos
	neg.l	d0
ypos    moveq	#0,d1
	move.w	d2,d1
	btst	#2,d5			* octant xdirection?
	beq.s	xpos
xneg	sub.l	#128,d0
	bra.s	padd
xpos	add.l	#128,d0
padd    add.l   d0,d7
	move.l  d7,a0			* point to the pix
	bchg    d1,(a0)			* complement pixel
brts	movem.l (sp)+,d0-d2/a0 		* restore context
	rts

	end
@


39.0
log
@Bumped
@
text
@d3 1
a3 1
*	$Id: Draw.asm,v 37.2 91/05/01 14:04:22 chrisg Exp Locker: chrisg $
a55 7
* temporary C interface
    xdef    _baredraw
	xdef	BareDraw
_baredraw:
    move.l 4(sp),a1
    movem.l 8(sp),d0/d1
    bra     BareDraw
d130 3
a132 1
BareDraw:
@


37.2
log
@killed ".." for cape
@
text
@d3 1
a3 1
*	$Id: Draw.asm,v 37.1 91/02/12 13:09:04 chrisg Exp Locker: chrisg $
@


37.1
log
@edited autodoc.
@
text
@d3 1
a3 1
*	$Id: Draw.asm,v 37.0 91/01/07 15:24:28 spence Exp Locker: chrisg $
d21 1
a21 1
    include '../sane_names.i'
@


37.0
log
@initial switchover from V36
@
text
@d3 1
a3 1
*	$Id: Draw.asm,v 1.11 90/10/18 15:27:27 bart Exp Locker: spence $
d40 1
a40 2
*	struct RastPort *rp;
*	SHORT x,y;
d47 2
a48 2
*	rp - pointer to a RastPort
*	x,y - point in the RastPort to end the line.
d53 1
a53 1
*	Move graphics/rastport.h
@


1.11
log
@fix bug B10406 - blitter extraneous bit under cusp condition
@
text
@d3 1
a3 1
*	$Id: Draw.asm,v 1.10 90/09/20 11:03:51 bart Exp $
@


1.10
log
@fix length one lines
@
text
@d3 1
a3 1
*	$Id: Draw.asm,v 1.9 90/09/13 12:26:47 bart Exp Locker: bart $
d250 4
d453 4
d458 1
a458 1
	movem.l d0-d1/a0,-(sp)		* preserve context
d460 3
d482 5
d500 3
a502 1
ypos    btst	#2,d5			* octant xdirection?
d509 2
a510 2
	bchg    d2,(a0)			* complement pixel
brts	movem.l (sp)+,d0-d1/a0 		* restore context
@


1.9
log
@fix big lines that are exact multiples of 1024 (2048, 3072 etc)
@
text
@d3 1
a3 1
*	$Id: Draw.asm,v 1.8 90/07/27 16:37:52 bart Exp Locker: bart $
d362 1
a362 1
				move.l	d0,-(sp) 		* save total bltsize
d373 1
a373 1
			cmp.l	#$82,d0
d433 1
a433 1
			cmp.l	#$82,d0
@


1.8
log
@id
@
text
@d3 1
a3 1
*	$Id: $
d373 2
d376 1
a376 1
			move.l (sp)+,d0		* restore total bltsize
d433 2
d436 1
a436 1
			move.l (sp)+,d0		* restore total bltsize
@


1.7
log
@*** empty log message ***
@
text
@d3 1
a3 1
*	$Header: Draw.asm,v 1.6 90/03/17 10:17:04 bart Exp $
@


1.6
log
@fix bug #B6329 areafill with polygon segments >1024
@
text
@d3 1
a3 1
*	$Header: Draw.asm,v 1.5 90/03/13 12:53:03 bart Exp $
@


1.5
log
@save/restore total bltsize for big blits
@
text
@d3 1
a3 1
*	$Header: Draw.asm,v 1.4 88/08/25 10:26:34 bart Exp $
d8 1
d15 1
a15 1
    include 'graphics/gfxbase.i'
d23 1
a23 1
	section	graphics
d149 3
a151 3
	ifne	bm_BytesPerRow
		fail
	endc
d193 1
a207 1

d211 5
a215 2
    move.w  d2,a3       * bltpta reloaded each pass
    move.w  rp_LinePtrn(a1),d6  * reloaded each pass
d220 3
a222 3
    move.w  d1,bltmdb(a0)
    move.w  d2,d1
    sub.w   d0,d1
d224 1
d228 1
a228 1
	move.w  (a2),bltmdc(a0)
d232 1
a232 1
    swap    d1
d306 1
a306 1
	    	move    d4,d5
d343 1
a343 1
		if <>
d345 6
d364 8
a371 3
				move.w	#2,bltsize(a0)
				sub.l	#$10000,d0
				WAITBLITDONE a0
d374 1
a374 1
				move.l (sp)+,d0			* restore total bltsize
d397 1
a397 1
		if <>
d399 7
a405 1
		    	move    d4,d5
d420 1
a420 1
				move.l	d0,-(sp) 		* save total bltsize
d422 8
a429 3
				move.w	#2,bltsize(a0)
				sub.l	#$10000,d0
				WAITBLITDONE a0
d431 2
a432 2
		    	move.w  d0,bltsize(a0)  * start blit
				move.l (sp)+,d0			* restore total bltsize
d438 53
@


1.4
log
@big horizontal lines -- check for greater than 64 words horizontally
resort to alternate (slow) method at the moment
@
text
@d3 1
a3 1
*	$Header: Draw.asm,v 1.3 88/06/17 21:37:06 dale Exp $
d351 1
d358 1
d398 1
d405 1
@


1.3
log
@Removed a debug tst.w $444444
@
text
@d3 1
a3 1
*	$Header: Draw.asm,v 1.2 88/06/17 16:50:07 dale Exp $
d72 18
@


1.2
log
@Now supports drawing lines >1024 bits (Does not use new registers)
@
text
@d3 1
a3 1
*	$Header: Draw.asm,v 1.1 88/05/01 15:36:14 dale Exp $
a306 1
	tst.w	$444444
@


1.1
log
@Initial revision
@
text
@d3 1
a3 1
*	$Header: Draw.asm,v 33.0 86/05/17 15:05:39 bart Exp $
d215 1
a215 4
    addq.w  #1,d0
    asl.w   #HSIZEBITS,d0
    addq.w  #2,d0
******  d0 is constant and equal to bltsize
d221 9
d305 85
@

head     39.1;
branch   ;
access   ;
symbols  rel39_24:39.1 rel39_18:39.1 rel39_97:39.1 rel39_90:39.1 rel39_89:39.1 rel39_82:39.1 rel39_76:39.1 rel39_71:39.1 rel39_65:39.0 rel39_64:39.0 rel39_61:39.0 rel39_60:39.0 rel39_55:39.0 rel39_52:39.0 rel39_47:39.0 rel39_39:39.0 rel39_38:39.0 rel39_37:39.0 rel39_35:39.0 rel39_34:39.0 V37_30:37.0 V37_29:37.0 V37_28:37.0 V37_27:37.0 V37_25:37.0 V37_24:37.0 V37_23:37.0 V37_22:37.0 V37_21:37.0 V37_20:37.0 V37_19:37.0 V37_18:37.0 V37_17:37.0 V37_15:37.0 V37_14:37.0 V37_13:37.0 V37_12:37.0 V37_11:37.0 V37_10:37.0 V37_9:37.0 V37_8:37.0 V37_7:37.0 V37_6:37.0 V37_5:37.0 V37_4:37.0 V37_3:37.0 V37_2:37.0 V37_1:37.0 V36_209:1.4 V36_208:1.4 V36_207:1.4 V36_205:1.4 V36_203:1.4 V36_202:1.4 V36_201:1.3 V36_200:1.4 V36_199:1.3 V36_198:1.3 V36_196:1.3 V36_195:1.3 V36_194:1.3 V36_193:1.3 V36_192:1.3 V36_191:1.3 V36_190:1.3 V36_189:1.3 V36_188:1.3 V36_187:1.3 V36_186:1.3 V36_185:1.3 V36_184:1.3 V36_183:1.3 V36_182:1.3 V36_181:1.3 V36_180:1.3;
locks    ; strict;
comment  @@;


39.1
date     92.06.16.12.13.08;  author chrisg;  state Exp;
branches ;
next     39.0;

39.0
date     91.08.21.17.28.40;  author chrisg;  state Exp;
branches ;
next     37.1;

37.1
date     91.05.01.14.01.56;  author chrisg;  state Exp;
branches ;
next     37.0;

37.0
date     91.01.07.15.26.53;  author spence;  state Exp;
branches ;
next     1.4;

1.4
date     90.12.07.15.38.42;  author spence;  state Exp;
branches ;
next     1.3;

1.3
date     90.08.24.11.29.13;  author bart;  state Exp;
branches ;
next     1.2;

1.2
date     90.03.27.17.25.23;  author bart;  state Exp;
branches ;
next     1.1;

1.1
date     88.05.01.15.38.12;  author dale;  state Exp;
branches ;
next     ;


desc
@@


39.1
log
@short branches
@
text
@*******************************************************************************
*
*	$Id: cdraw.asm,v 37.1 91/05/01 14:01:56 chrisg Exp $
*
*******************************************************************************

    include 'exec/types.i'
	include 'graphics/rastport.i'
	include 'graphics/clip.i'
	include 'hardware/blit.i'
	include '/c/cdrawinfo.i'
	include 'submacs.i'

	section	graphics
    xdef    cdraw
	xref	_cdraw

cdraw:
	movem.l	d2/d3/d4/d5/d6/d7/a2/a3/a5,-(sp)
	lea		-cd_sizeof(sp),sp
	move.l	(a1),a5		* get layer pointer
	LOCKLAYER
	movem.w	rp_cp_x(a1),d2/d3	* get old cp_x,cp_y

*	bart - 06.30.86 - don't update cp_x, cp_y yet !!!
*	movem.w	d0/d1,rp_cp_x(a1)	* new cp_x,cp_y

*	this code assumes top of sp has cdrawinfo structure
	move.w	d0,d4				* use d4/d5 for scratch
	move.w	d1,d5
	sub.w	d2,d4				* generate dx
	sub.w	d3,d5				* genereate dy
	move.w	d4,cd_dx(sp)
	if <						* get abs(dx)
		neg.w	d4
	endif
	move.w	d5,cd_dy(sp)
	if <						* get abs(dy)
		neg.w	d5
	endif
	if d5>d4.w					* octant 2/3/6/7
		clr.b	cd_xmajor(sp)
		move.w	d4,cd_absdy(sp)	* swap them
		move.w	d5,cd_absdx(sp)
		if cd_dy(sp)<#0			* octant 6/7
			if cd_dx(sp)<#0		* octant 6
				moveq	#OCTANT6,d4
			else				* octant 7
				moveq	#OCTANT7,d4
			endif
		else					* octant 2/3
			if cd_dx(sp)<#0		* octant 3
				moveq	#OCTANT3,d4
			else				* octant 2
				moveq	#OCTANT2,d4
			endif
		endif
	else						* octant 1/4/5/8
		move.b	#1,cd_xmajor(sp)
		movem.w	d4/d5,cd_absdx(sp)	* set up standard absdx/dy
		if cd_dy(sp)<#0			* octant 5/8
			if cd_dx(sp)<#0		* octant 5
				moveq	#OCTANT5,d4
			else				* octant 8
				moveq	#OCTANT8,d4
			endif
		else					* octant 1/4
			if cd_dx(sp)<#0		* octant 4
				moveq	#OCTANT4,d4
			else				* octant 1
				moveq	#OCTANT1,d4
			endif
		endif
	endif
	move.w	d4,cd_con1(sp)

	movem.w	lr_MinX(a5),d4/d5	* get bounds.MinX/MinY
	sub.w	lr_Scroll_X(a5),d4	* real delta's
	sub.w	lr_Scroll_Y(a5),d5	* "
	move.l	d0,d6				* copies of to_x,to_y
	move.l	d1,d7
	add.w	d4,d0				* real tx,ty
	add.w	d5,d1
	add.w	d2,d4				* oldx,oldy
	add.w	d3,d5
*	pack in a single long
	swap	d4
	move.w	d5,d4				* d4.l = oldx.oldy
	move.l	d0,d5
	swap	d5
	move.w	d1,d5				* d5.l = tx.ty
*	d0/d1 unused at this time

	move.l	a1,a3				* rastport *
	move.l	lr_ClipRect(a5),d0
	while <>
		move.l d0,a2
		move.l	sp,a0		* save pointer to cdi
*		move.l	d5,-(sp)	* t
*		move.l	d4,-(sp)	* old
*		move.l	a0,-(sp)	* cdi
*		move.l	a2,-(sp)	* cr
*		move.l	a3,-(sp)	* rastport
		bsr.s		cr_cdraw
*		lea		5*4(sp),sp
		tst.l	d0
		bne.s	my_exit
		move.l (a2),d0		* get next ClipRect
	whend
*   only need to do this if superbitmap
	if	lr_SuperBitMap(a5).l
		move.l	sp,-(sp)		* pointer to cdrawinfo
		move.w	d7,-(sp)		* new x,y
		move.w	d6,-(sp)
		move.w	d3,-(sp)		* old cp_x,cp_y
		move.w	d2,-(sp)
		move.l	a3,-(sp)
		jsr		_cdraw
		lea		16(sp),sp
	endif
my_exit:
	UNLOCKLAYER

*	bart - 06.30.86 - OK to update cp_x, cp_y now !!!
	movem.w	d6/d7,rp_cp_x(a3)

	move.w	cd_absdx(sp),d0
	sub.b	d0,rp_linpatcnt(a3)
	lea		cd_sizeof(sp),sp
	movem.l	(sp)+,d2/d3/d4/d5/d6/d7/a2/a3/a5
	rts

*new_draw_vect:
*	movem.l	d2-d7/a2-a3,-(sp)
*	move.l	(8*4)+4(sp),a1	* get rastport
*	move.l	rp_BitMap(a1),a2
*	move.l	(8*4)+8(sp),a0	* get cdi
*	movem.l	(8*4)+12(sp),d2/d3	* get (x0,y0)
*	muls	(a2),d3
*	move.l	d2,d4
*	asr.l	#3,d2
*	add.l	d2,d3				* d3 = offset into bitmap
*	and.w	#15,d4				* bit number to start
*	move.w	cd_absdx(a0),d1
*	add.w	d1,d1				* 2*abs(dy)
*	move.l	(8*4)+24(sp),d2		* initial error term
*	move.l	(8*4)+20(sp),

	xdef	_cr_cdraw
_cr_cdraw:
*	take stuff off stack
	movem.l	d4/d5/a2/a3,-(sp)
		move.l	16+4(sp),a3	* rastport
		move.l	16+8(sp),a2	* cr
		move.l	16+12(sp),a0	* cdi
		move.l	16+16(sp),d4	* old
		move.l	16+20(sp),d5	* t
		bsr.s		cr_cdraw
	movem.l	(sp)+,d4/d5/a2/a3
	rts

	xref	_c_cr_cdraw
	xref	getcode
	xref	BareDraw
	xref	_clipdraw
cr_cdraw:
*   cdi->code1 = getcode(cr,old.x,old.y);
*   cdi->code2 = getcode(cr,t.x,t.y);
	move.w	d4,d1			* transfer y old
	move.l	d4,d0
	swap d0
	bsr	getcode
	move.b	d0,cd_code1(a0)
	move.w	d5,d1			* transfer y  't'
	move.l	d5,d0
	swap d0
	bsr	getcode
	move.b	d0,cd_code2(a0)
	or.b	cd_code1(a0),d0
	if =
*		trivial accept
		if cr_lobs(a2)=#0.l
*			on screen
			move.l	d4,rp_cp_x(a3)
			move.w	d5,d1
			move.l	d5,d0
			swap	d0
			move.l	a3,a1		* rastport
			move.w	rp_linpatcnt(a3),-(sp)
			bsr	BareDraw		* BareDraw also updates linpatcnt - spence Dec  7 1990
			move.w	(sp)+,rp_linpatcnt(a3)
		else
*			obscured
			move.l	cr_BitMap(a2),d0
			if <>
*				is there a bitmap to put bits in?
				move.l	rp_BitMap(a3),-(sp)	* save old BitMap on stack
				move.l	d0,rp_BitMap(a3)	* use new BitMap
				moveq	#~15,d1
				and.w	cr_MinX(a2),d1
				move.l	d4,d0		* compute offset x
				swap	d0
				sub.w	d1,d0
				move.w	d4,d1		* compute offset y
				sub.w	cr_MinY(a2),d1
				movem.w	d0/d1,rp_cp_x(a3)
				moveq	#~15,d1
				and.w	cr_MinX(a2),d1
				move.l	d5,d0		* compute offset x
				swap	d0
				sub.w	d1,d0
				move.w	d5,d1		* compute offset y
				sub.w	cr_MinY(a2),d1
				move.w	rp_linpatcnt(a3),-(sp)
				bsr	BareDraw		* BareDraw also updates linpatcnt - spence Dec  7 1990
				move.w	(sp)+,rp_linpatcnt(a3)
				move.l	(sp)+,rp_BitMap(a3)
			endif
		endif
		moveq	#1,d0
	else
		move.b	cd_code1(a0),d0
		and.b	cd_code2(a0),d0
		if =		* cross over somewhere
			if cr_BitMap(a2).l
*				call clipdraw routine d5=t d4=old
or_entry:
*				move.l	d5,-(sp)		* to
				move.w	d5,d0
				ext.l	d0
				move.l	d0,-(sp)
				move.l	d5,d0
				swap	d0
				ext.l	d0
				move.l	d0,-(sp)

*				move.l	d4,-(sp)		* from
				move.w	d4,d0
				ext.l	d0
				move.l	d0,-(sp)
				move.l	d4,d0
				swap	d0
				ext.l	d0
				move.l	d0,-(sp)

				move.l	a0,-(sp)		* cdi
				move.l	a2,-(sp)		* cr
				move.l	a3,-(sp)		* rp
				jsr	_clipdraw
				lea	7*4(sp),sp
			else
				tst.l	cr_lobs(a2)
				beq.s	or_entry
			endif
		endif
		moveq	#0,d0
	endif
	rts

	end
@


39.0
log
@Bumped
@
text
@d3 1
a3 1
*	$Id: cdraw.asm,v 37.1 91/05/01 14:01:56 chrisg Exp Locker: chrisg $
d104 1
a104 1
		bsr		cr_cdraw
d158 1
a158 1
		bsr		cr_cdraw
@


37.1
log
@killed ".." for cape
@
text
@d3 1
a3 1
*	$Id: cdraw.asm,v 37.0 91/01/07 15:26:53 spence Exp Locker: chrisg $
@


37.0
log
@initial switchover from V36
@
text
@d3 1
a3 1
*	$Id: cdraw.asm,v 1.4 90/12/07 15:38:42 spence Exp Locker: spence $
d11 1
a11 1
	include '../c/cdrawinfo.i'
@


1.4
log
@fixed rp->linpatcnt problem (being updated twice if the line went through
BareDraw() ).
@
text
@d3 1
a3 1
*	$Id: cdraw.asm,v 1.3 90/08/24 11:29:13 bart Exp Locker: spence $
@


1.3
log
@id
@
text
@d3 1
a3 1
*	$Id: $
d189 3
a191 1
			bsr	BareDraw
d214 3
a216 1
				bsr	BareDraw
@


1.2
log
@*** empty log message ***
@
text
@d3 1
a3 1
*	$Header: cdraw.asm,v 1.1 88/05/01 15:38:12 dale Exp $
@


1.1
log
@Initial revision
@
text
@d3 1
a3 1
*	$Header: cdraw.asm,v 33.1 86/07/01 11:26:07 bart Exp $
@

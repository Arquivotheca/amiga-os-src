head     39.2;
branch   ;
access   ;
symbols  rel39_24:39.2 rel39_18:39.2 rel39_97:39.2 rel39_90:39.2 rel39_89:39.2 rel39_82:39.2 rel39_76:39.2 rel39_71:39.2 rel39_65:39.2 rel39_64:39.2 rel39_61:39.2 rel39_60:39.2 rel39_55:39.2 rel39_52:39.2 rel39_47:39.2 rel39_39:39.1 rel39_38:39.1 rel39_37:39.1 rel39_35:39.1 rel39_34:39.1;
locks    ; strict;
comment  @* @;


39.2
date     92.03.24.10.33.46;  author spence;  state Exp;
branches ;
next     39.1;

39.1
date     92.01.29.16.31.56;  author spence;  state Exp;
branches ;
next     39.0;

39.0
date     91.10.30.18.39.31;  author spence;  state Exp;
branches ;
next     ;


desc
@GetVecTable() function.
@


39.2
log
@Now gets correct default VecTable if DualPfd
@
text
@*******************************************************************************
*
* $Id: getvec.asm,v 39.1 92/01/29 16:31:56 spence Exp Locker: spence $
*
*******************************************************************************

	section graphics

	include "/gfxbase.i"
	include "/view.i"
	include "/displayinfo.i"

	xdef	_GetVecTable
	xref	_VecLists

***i*** graphics.library/GetVecTable *******************************************
*
*   NAME
*	GetVecTable -- Find the VecTable for a given ViewPort (V39)
*
*   SYNOPSIS
*	VecTable = GetVecTable(vp, vinfo);
*	d0                     a0  a1
*
*	struct VecTable *GetVecTable(struct VecTable *, struct VecInfo *);
*
*   FUNCTION
*	To get the best VecTable for a given ViewPort.
*	The VecTable comes from the vp->ColorMap->ViewPortExtra->Vec.
*	If any pointer in the chain is NULL, then return the
*	vinfo->Vec.
*	If this is NULL, return a default depending on the GB->ChipRevBits0
*
*   INPUTS
*	vp    = viewport. If NULL, use vinfo.
*       vinfo = VecInfo. Can be NULL.
*
*   RESULT
*	VecTable = VecTable to use.
*
*   BUGS
*
*   SEE ALSO
*	<graphics/displayinfo.h> "vp_internal.h"
*
********************************************************************************

; struct VecTable * __asm GetVecTable(register __a0 struct ViewPort *vp, register __a1 struct VecInfo *vinfo);

; try for the ViewPort->ColorMap->ViewPortExtra->VecTable.
; If that is NULL, try for the VecInfo->Vec.
; If that is NULL, use the default.

_GetVecTable:
	moveq	#0,d1
	move.l	a0,d0			; check ViewPort
	beq.s	chk_vinfo
	move.w	vp_Modes(a0),d0
	and.w	#V_DUALPF,d0
	bne.s	is_dpf			; if dualpf, don't rely on vpe.
	move.l	vp_ColorMap(a0),d0	; check vp->Colormap
	beq.s	use_vl_dflt
	move.l	d0,a0
	tst.b	cm_Type(a0)		; check cm->Type
	beq.s	use_vl_dflt
	move.l	cm_vpe(a0),d0		; check cm->vpe
	beq.s	use_vl_dflt
	move.l	d0,a0
	move.l	vpe_VecTable(a0),d0	; check vpe->VecTable
	bne.s	gotvectable		; aha! got it.
chk_vinfo:
	move.l	a1,d0			; damn. Try through the VecInfo
	beq.s	use_vl_dflt
	move.l	vec_Vec(a1),d0		; check vec->Vec
	bne.s	gotvectable		; got it.
use_vl_dflt:
	lea	_VecLists,a0		; get the default list
	lea	0(a0,d1),a0		; use default AA VecList
	move.l	(a0),d0
	move.b	gb_ChipRevBits0(a6),d1
	btst.b	#GFXB_AA_LISA,d1
	bne.s	gotvectable		; is in fact AA
	move.l	8(a0),d0		; use ECS
	btst.b	#GFXB_HR_DENISE,d1
	bne.s	gotvectable		; is in fact ECS
	move.l	16(a0),d0		; use A
gotvectable:
	rts

is_dpf:
	moveq	#4,d1			; offset in VecLists for DualPlayfield
	bra.s	chk_vinfo

	end
@


39.1
log
@USe A VectorTable in A systems
@
text
@d3 1
a3 1
* $Id: getvec.asm,v 39.0 91/10/30 18:39:31 spence Exp Locker: spence $
d55 1
d60 1
a60 1
	bne.s	chk_vinfo		; if dualpf, don't rely on vpe.
d78 2
a79 1
	move.l	(a0),d0			; use default AA VecList
d89 6
@


39.0
log
@Initial log in
@
text
@d3 1
a3 1
* $Id:  Exp  $
d78 2
a79 1
	btst.b	#GFXB_AA_LISA,gb_ChipRevBits0(a6)
d82 3
@

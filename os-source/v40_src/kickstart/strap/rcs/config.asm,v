head     36.12;
branch   ;
access   ;
symbols  V37_23:36.10 V37_21:36.10 V37_19:36.10 V37_17:36.10 V37_7:36.10 V37_3:36.10 V36_118:36.10 V36_117:36.9 V36_109:36.9 V36_108:36.9 V36_107:36.9 V36_106:36.8 V36_105:36.8 V36_104:36.7 V36_102:36.6 V36_98:36.5 V36_96:36.5 V36_90:36.4 V36_86:36.3 V36_85:36.3 V36_83:36.3 V36_82:36.3 V36_80:36.3 V36_75:36.3 V36_73:36.3 V36_56:36.3 V36_55:36.3 V36_54:36.3 V36_51:36.3 V36_50:36.3 V36_49:36.3 V36_33:36.1 V36_26:36.1 V36_22:36.1 L36_19:36.1;
locks    ; strict;
comment  @*   @;


36.12
date     92.05.19.16.47.51;  author darren;  state Exp;
branches ;
next     36.11;

36.11
date     92.04.06.14.26.37;  author darren;  state Exp;
branches ;
next     36.10;

36.10
date     90.12.03.06.02.43;  author bryce;  state Exp;
branches ;
next     36.9;

36.9
date     90.04.13.11.37.15;  author kodiak;  state Exp;
branches ;
next     36.8;

36.8
date     90.04.06.10.31.06;  author kodiak;  state Exp;
branches ;
next     36.7;

36.7
date     90.04.02.11.13.46;  author kodiak;  state Exp;
branches ;
next     36.6;

36.6
date     90.03.26.17.52.42;  author kodiak;  state Exp;
branches ;
next     36.5;

36.5
date     90.01.09.14.03.06;  author kodiak;  state Exp;
branches ;
next     36.4;

36.4
date     89.12.30.22.22.58;  author kodiak;  state Exp;
branches ;
next     36.3;

36.3
date     89.04.07.12.55.02;  author kodiak;  state Exp;
branches ;
next     36.2;

36.2
date     89.04.04.14.42.43;  author kodiak;  state Exp;
branches ;
next     36.1;

36.1
date     89.01.06.17.27.50;  author kodiak;  state Exp;
branches ;
next     36.0;

36.0
date     88.12.20.14.27.19;  author kodiak;  state Exp;
branches ;
next     ;


desc
@eb_MountList & floppy boot
@


36.12
log
@Use of TaggedOpenLibrary
@
text
@**
**	$Id: config.asm,v 36.11 92/04/06 14:26:37 darren Exp Locker: darren $
**
**	configure boards asking for CONFIG w/ a valid diag area.
**
**	(C) Copyright 1987,1988 Commodore-Amiga, Inc.
**	    All Rights Reserved
**
	SECTION	strap

**	Includes

	INCLUDE	"strap.i"

	INCLUDE	"exec/nodes.i"
	INCLUDE	"exec/lists.i"
	INCLUDE	"exec/ports.i"
	INCLUDE	"exec/tasks.i"
	INCLUDE	"exec/libraries.i"
	INCLUDE	"exec/memory.i"
	INCLUDE	"exec/execbase.i"
	INCLUDE	"exec/resident.i"
	INCLUDE	"exec/alerts.i"

	INCLUDE	"libraries/expansionbase.i"

	IFND	eb_BoardList
eb_BoardList	EQU	eb_Private05
	ENDC

	INCLUDE	"libraries/configvars.i"

	INCLUDE	"devices/trackdisk.i"
	INCLUDE	"resources/disk.i"
	INCLUDE "internal/librarytags.i"


**	Exports

	XDEF	RMInit


**	Imports

	XLVO	AddHead			; Exec
	XLVO	AllocMem		;
	XLVO	CloseLibrary		;
	XLVO	Enqueue			;
	XLVO	InitResident		;
	XLVO	OpenLibrary		;
	XLVO	OpenResource		;
	XLVO	TaggedOpenLibrary	;

	XLVO	ObtainConfigBinding	; Expansion
	XLVO	ReleaseConfigBinding	;
	XLVO	SetCurrentBinding	;

	XREF	SMAlert


**	Locals

 STRUCTURE	StrapFloppyNode,0
	STRUCT	sfn_DevNode,DeviceNode_SIZEOF
	STRUCT	sfn_FSSM,FileSysStartupMsg_SIZEOF
	STRUCT	sfn_Envec,(DE_BOOTBLOCKS+1)*4
	STRUCT	sfn_DriveName,5		; 3,'DF0',0
	ALIGNLONG
	STRUCT	sfn_DevName,18		; 17,'trackdisk.device',0
	ALIGNWORD
	STRUCT	sfn_BootNode,BootNode_SIZEOF
	LABEL	StrapFloppyNode_SIZEOF


**	Assumptions

	IFNE	CurrentBinding_SIZEOF-16
	FAIL	"consider using newer CurrentBinding structure"
	ENDC
	IFNE	DAC_CONFIGTIME-$10
	FAIL	"DAC_CONFIGTIME not bit 4, recode"
	ENDC
	IFNE	da_Config
	FAIL	"da_Config not zero, recode"
	ENDC
	IFNE	cb_ConfigDev
	FAIL	"cb_ConfigDev not zero, recode"
	ENDC
	IFNE	DRT_AMIGA
	FAIL	"DRT_AMIGA not zero, recode"
	ENDC
	IFNE	sfn_DevNode
	FAIL	"sfn_DevNode not zero, recode"
	ENDC


**********************************************************************
RMInit:
		movem.l	d2/a2-a6,-(a7)

		;-- initialize CurrentBinding local variable invariants
		moveq	#0,d0
		move.l	d0,-(a7)	; cb_ToolTypes
		move.l	d0,-(a7)	; cb_ProductString
		move.l	d0,-(a7)	; cb_FileName
		subq.l	#4,a7		; cb_ConfigDev

		;-- get expansion library

		moveq	#OLTAG_EXPANSION,d0
		CALLLVO	TaggedOpenLibrary
		tst.l	d0
		bne.s	rExpLibOK
		move.l	#AN_BootStrap!AG_OpenLib!AO_ExpansionLib,d0
		bsr	SMAlert
		bra	rError1

rExpLibOK:
		move.l	d0,a5		; save ExpansionBase
		exg	a5,a6		; use ExpansionBase, save SysBase

		;-- obtain expansion config binding semaphore
		CALLLVO	ObtainConfigBinding

		;-- scan expansion board list for boards to config
		move.l	eb_BoardList(a6),a2

rScanEBList:
		move.l	(a2),d2		; check for end of board list
		beq.s	rScanEBDone

		;--	check if this board is to be configured
		btst.b	#CDB_CONFIGME,cd_Flags(a2)
		beq.s	rScanEBNext

		btst.b	#ERTB_DIAGVALID,cd_Rom+er_Type(a2)
		beq.s	rScanEBNext

		move.l	cd_Rom+er_Reserved0c(a2),d0
		beq.s	rScanEBNext

		move.l	d0,a3
		btst.b	#4,(a3)		; DAC_CONFIGTIME,da_Config(a3)
		beq.s	rScanEBNext

		;--	find the resident tag in the diagnostic area
		move.w	da_Size(a3),d0	; words to search
		lsr.w	#1,d0
		subq.w	#1,d0
rResTagScan:
		cmp.w	#RTC_MATCHWORD,(a3)+
rResTagScanDBF:
		dbeq	d0,rResTagScan
		bne.s	rScanEBNext	; no resident tag

		lea	-2(a3),a0
		cmp.l	(a3),a0
		bne.s	rResTagScanDBF

		;--	set the cb_ConfigDev field in the config binding
		move.l	a7,a0

		move.l	a2,(a0)		; move.l a2,cb_ConfigDev(a0)

		moveq	#CurrentBinding_SIZEOF,d0
		CALLLVO	SetCurrentBinding

		;--	configure the board
		moveq	#0,d1		; NULL seglist
		lea	-2(a3),a1
		exg	a5,a6		; use SysBase, save ExpansionBase
		CALLLVO	InitResident
		exg	a5,a6		; use ExpansionBase, save SysBase

rScanEBNext:
		move.l	d2,a2
		bra.s	rScanEBList

rScanEBDone:
		;-- release expansion config binding semaphore
		CALLLVO	ReleaseConfigBinding


		;-- add nodes for floppies
		exg	a5,a6		; use SysBase, save ExpansionBase

		;-- get disk resource
		lea	DRName(pc),a1
		CALLLVO	OpenResource
		tst.l	d0
		bne.s	rDiskResOK
		move.l	#AN_BootStrap!AG_OpenRes!AO_DiskRsrc,d0
		bsr	SMAlert
		bra	rError2

rDiskResOK:
		;-- cycle thru all floppies
		moveq	#NUMUNITS,d2
		move.l	a6,a4		; save SysBase
		move.l	d0,a6		; use DiskBase
		bra	rGetUnitIDDBF
rGetUnitIDLoop:
		;--	check if unit is 3.5" drive
		move.l	d2,d0
		jsr	DR_GETUNITID(a6)

		
		cmp.l	#DRT_150RPM,d0
		beq.s	rDriveOk
		tst.l	d0		; cmp.l #DRT_AMIGA,d0
		bne.s	rGetUnitIDDBF
rDriveOk:

		;--	create eb_MountList entry for 3.5" drive
		exg	a4,a6		; use SysBase, save DiskBase
		move.l	#StrapFloppyNode_SIZEOF,d0
		moveq	#MEMF_PUBLIC,d1
		CALLLVO	AllocMem
		tst.l	d0
		beq.s	rAllocMemError

		move.l	d0,a3

		;------ initialize the mount node
		lea	rSFNTemplate(pc),a0
		move.l	a3,a1				; sfn_DevNode(a3) ea
		moveq	#StrapFloppyNode_SIZEOF/2-1,d0
rCopyTemplate:
		move.w	(a0)+,(a1)+
		dbf	d0,rCopyTemplate

		;------ patch BPTRs
		move.l	a3,d0		; get base BPTR
		lsr.l	#2,d0		;
		move.l	d0,a0		;

		lea	sfn_FSSM/4(a0),a1
		move.l	a1,sfn_DevNode+dn_Startup(a3)
		lea	sfn_DriveName/4(a0),a1
		move.l	a1,sfn_DevNode+dn_Name(a3)
		lea	sfn_DevName/4(a0),a1
		move.l	a1,sfn_FSSM+fssm_Device(a3)
		lea	sfn_Envec/4(a0),a1
		move.l	a1,sfn_FSSM+fssm_Environ(a3)

		;--	patch APTR
		move.l	a3,sfn_BootNode+bn_DeviceNode(a3) ; sfn_DevNode(a3) ea

		;--	patch unit number
		move.w	d2,sfn_FSSM+fssm_Unit+2(a3)
		move.w	d2,d0
		add.b	#'0',d0
		move.b	d0,sfn_DriveName+3(a3)

		;--	patch unit priority
		move.b	rFloppyPriorities(pc,d2.w),d0
		move.b	d0,sfn_BootNode+LN_PRI(a3)
		ext.w	d0
		ext.l	d0
		move.l	d0,sfn_Envec+de_BootPri(a3)

		;--	put this boot node on the eb_MountList
		lea	eb_MountList(a5),a0
		lea	sfn_BootNode(a3),a1
		CALLLVO	Enqueue

rAllocMemError:
		exg	a4,a6		; use DiskBase, save SysBase

rGetUnitIDDBF:
		dbf	d2,rGetUnitIDLoop


rError2:
		;------ release expansion library
		move.l	a5,a1		; ExpansionBase
		move.l	a4,a6		; SysBase
		CALLLVO	CloseLibrary

rError1:
		;------	restore stack, registers
		add.w	#CurrentBinding_SIZEOF,a7
		movem.l	(a7)+,d2/a2-a6
		rts


rFloppyPriorities:
		dc.b	5,-10,-20,-30	; for df0:-df3:

rSFNTemplate:
		dc.l	0,0,0,0		; dn_Next, dn_Type, dn_Task, dn_Lock
		dc.l	0,600,10	; dn_Handler, dn_StackSize, dn_Priority 
		dc.l	0,0,0		; dn_Startup, dn_SegList, dn_GlobalVec
		dc.l	0		; dn_Name
		dc.l	0,0,0		; fssm_Unit, fssm_Device, fssm_Environ
		dc.l	0		; fssm_Flags
		dc.l	19,128,0	; de_TableSize, de_SizeBlock, de_SecOrg
		dc.l	2,1		; de_Surfaces, de_SectorPerBlock
		dc.l	11,2		; de_BlocksPerTrack, de_Reserved
		dc.l	0,0,0		; de_PreAlloc, de_Interleave, de_LowCyl
		dc.l	79,5		; de_HighCyl, de_NumBuffers
		dc.l	MEMF_PUBLIC	; de_BufMemType
		dc.l	$200000		; de_MaxTransfer
		dc.l	$7ffffffe	; de_AddressMask
		dc.l	0		; de_BootPri
		dc.b	'DOS',0		; de_DosType
		dc.l	0,0		; de_Baud, de_Control
		dc.l	2		; de_BootBlocks
		dc.b	3,'DF0',0,0,0,0	; (dn_Name)
		dc.b	17,'trackdisk.device',0
					; (fssm_Device)
		dc.l	0,0		; ln_Succ, ln_Pred
		dc.b	NT_BOOTNODE,0	; ln_Type, ln_Pri
		dc.l	0		; ln_Name
		dc.w	0		; bn_Flags
		dc.l	0		; bn_DeviceNode
rsfntEnd:

	IFNE	(rsfntEnd-rSFNTemplate)-StrapFloppyNode_SIZEOF
	FAIL	'rSFNTemplate size not StrapFloppyNode_SIZEOF'
	ENDC

DRName		dc.b	'disk.resource',0
		ds.w	0

	END
@


36.11
log
@Use most recent V37 version of code (modified AFTER the V39
directory was made - for the A600 card stuff)
@
text
@d2 1
a2 1
**	$Id: config.asm,v 36.11 91/10/11 12:32:05 darren Exp $
d35 1
d52 1
a57 1
	XREF	ELName
d109 3
a111 3
					; d0 is zero from above
		lea	ELName(pc),a1
		CALLLVO	OpenLibrary
@


36.10
log
@Mark DRT_150RPM drives as ok on bootup.
@
text
@d2 1
a2 1
**	$Id: config.asm,v 36.9 90/04/13 11:37:15 kodiak Exp Locker: bryce $
d210 1
a210 1
		bne	rGetUnitIDDBF
d219 1
a219 1
		beq	rAllocMemError
@


36.9
log
@use Id instead of Header for 4.x rcs
@
text
@d2 1
a2 1
**	$Id$
d206 3
d211 1
@


36.8
log
@trackdisk no longer requires chip memory
@
text
@d2 1
a2 1
**	$Header: /usr2/kodiak/src/KS/strap/RCS/config.asm,v 36.7 90/04/02 11:13:46 kodiak Exp Locker: kodiak $
@


36.7
log
@for rcs 4.x header change
@
text
@d2 1
a2 1
**	$Header: /usr2/kodiak/src/KS/strap/RCS/config.asm,v 36.6 90/03/26 17:52:42 kodiak Exp Locker: kodiak $
d297 1
a297 2
		dc.l	MEMF_PUBLIC!MEMF_CHIP
					; de_BufMemType
d299 1
a299 1
		dc.l	$1ffffe		; de_AddressMask
@


36.6
log
@(new rcs causes new header)
@
text
@d2 1
a2 1
**	$Header: /usr2/kodiak/src/KS/strap/RCS/config.asm,v 36.5 90/01/09 14:03:06 kodiak Exp $
@


36.5
log
@fixed floppy boot for single floppy system
@
text
@d2 1
a2 1
**	$Header: config.asm,v 36.4 89/12/30 22:22:58 kodiak Exp $
@


36.4
log
@add entries to eb_MountList for all 3.5" floppies
-- this requires a dos change.
@
text
@d2 1
a2 1
**	$Header$
d92 1
a92 1
	FAIL	'sfn_DevNode not zero, recode here'
d221 1
a221 1
		move.l	a3,a1
d252 1
a252 1
		move.b	d0,LN_PRI(a3)
@


36.3
log
@have eb_BoardList equal eb_Private05 if necessary
@
text
@d2 1
a2 4
**	$Filename$
**	$Release: 1.4 $
**	$Revision: 36.2 $
**	$Date: 89/04/04 14:42:43 $
d6 1
a6 1
**	(C) Copyright 1985,1986,1987,1988 Commodore-Amiga, Inc.
d11 1
a11 1
**	Included Files
d33 2
a34 1
**	Imported Names
a35 4
	XLVO	AddHead
	XLVO	CloseLibrary
	XLVO	InitResident
	XLVO	OpenLibrary
d37 1
a37 3
	XLVO	ObtainConfigBinding
	XLVO	ReleaseConfigBinding
	XLVO	SetCurrentBinding
d39 17
d60 1
a60 1
**	Exported Names
d62 10
a71 1
	XDEF	RMInit
d74 1
a74 3
**********************************************************************
RMInit:
		movem.l	d2/a2-a3/a5-a6,-(a7)
a75 1
		;------ initialize CurrentBinding local variable invariants
d79 22
d107 1
a107 1
		;------ get expansion library
d112 1
a112 1
		bne.s	riExpLibOK
d115 1
a115 1
		bra	riError
d117 1
a117 1
riExpLibOK:
d121 1
a121 1
		;------ obtain expansion config binding semaphore
d124 1
a124 1
		;------ scan expansion board list for boards to config
d127 1
a127 1
riScanEBList:
d129 1
a129 1
		beq.s	riScanDone
d131 1
a131 1
		;------ check if this board is to be configured
d133 1
a133 1
		beq.s	riScanNext
d136 1
a136 1
		beq.s	riScanNext
d139 1
a139 1
		beq.s	riScanNext
a141 6
	IFNE	DAC_CONFIGTIME-$10
	FAIL	"DAC_CONFIGTIME not bit 4, recode"
	ENDC
	IFNE	da_Config
	FAIL	"da_Config not zero, recode"
	ENDC
d143 1
a143 1
		beq.s	riScanNext
d145 1
a145 1
		;------ find the resident tag in the diagnostic area
d149 1
a149 1
riResTagScan:
d151 3
a153 3
riResTagScanDBF:
		dbeq	d0,riResTagScan
		bne.s	riScanNext	; no resident tag
d157 1
a157 1
		bne.s	riResTagScanDBF
d159 1
a159 1
		;------ set the cb_ConfigDev field in the config binding
a161 3
	IFNE	cb_ConfigDev
	FAIL	"cb_ConfigDev not zero, recode here"
	ENDC
d167 1
a167 1
		;------ configure the board
d174 1
a174 1
riScanNext:
d176 1
a176 1
		bra.s	riScanEBList
d178 2
a179 2
riScanDone:
		;------ release expansion config binding semaphore
d182 88
a269 1
riDone:
d271 2
a272 2
		move.l	a6,a1		; ExpansionBase
		move.l	a5,a6		; SysBase
d275 1
a275 1
riError:
d278 1
a278 1
		movem.l	(a7)+,d2/a2-a3/a5-a6
d280 41
@


36.2
log
@fix booting from eb_MountList devices
@
text
@d4 2
a5 2
**	$Revision: 36.1 $
**	$Date: 89/01/06 17:27:50 $
d29 5
@


36.1
log
@remove unneeded XLVOs
@
text
@d4 2
a5 2
**	$Revision: 36.0 $
**	$Date: 88/12/20 14:27:19 $
d66 1
a66 1
		moveq	#0,d0
d93 1
a93 5
		move.l	cd_Rom(a2),a0
	IFNE	er_Type
	FAIL	"er_Type not zero, recode here"
	ENDC
		btst.b	#ERTB_DIAGVALID,(a0)	; ,er_Type(a0)
d96 1
a96 1
		move.l	er_Reserved0c(a0),d0
d101 1
a101 1
	FAIL	"DAC_CONFIGTIME not bit 4, recode here and later"
d103 4
a106 1
		btst.b	#4,da_Config(a3)	; DAC_CONFIGTIME
@


36.0
log
@split from main.asm
@
text
@d5 1
a5 1
**	$Date: 88/12/20 13:51:55 $
a33 2
	XLVO	Alert
	XLVO	AllocMem
a34 1
	XLVO	FreeMem
a35 1
	XLVO	Insert
a36 1
	XLVO	Remove
@

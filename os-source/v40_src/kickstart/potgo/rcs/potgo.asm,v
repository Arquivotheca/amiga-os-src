head     36.8;
branch   ;
access   ;
symbols  V37_5:36.8 V37_4:36.8 V37_2:36.5 V36_19:36.5 V36_18:36.3 V36_17:36.3 V36_16:36.2 V36_15:36.2;
locks    ; strict;
comment  @*   @;


36.8
date     91.02.12.11.28.01;  author darren;  state Exp;
branches ;
next     36.7;

36.7
date     91.02.08.22.08.51;  author darren;  state Exp;
branches ;
next     36.6;

36.6
date     91.01.28.18.05.59;  author darren;  state Exp;
branches ;
next     36.5;

36.5
date     90.04.13.11.23.25;  author kodiak;  state Exp;
branches ;
next     36.4;

36.4
date     90.04.09.09.54.30;  author kodiak;  state Exp;
branches ;
next     36.3;

36.3
date     90.04.02.11.08.49;  author kodiak;  state Exp;
branches ;
next     36.2;

36.2
date     89.06.06.15.09.25;  author kodiak;  state Exp;
branches ;
next     36.1;

36.1
date     89.03.31.13.05.47;  author kodiak;  state Exp;
branches ;
next     36.0;

36.0
date     89.02.24.15.51.38;  author kodiak;  state Exp;
branches ;
next     ;


desc
@coalesce of prdata.i residenttag.asm resource.asm endmodule.asm
use AUTOINIT flag
use DISABLE/ENABLE macros
declare HiByte as (correctly) a word
change unnecessary movem to move
@


36.8
log
@Weird - 'the' appears as 'thr' in potgo.doc - yet the source
is correct -- hmmm.
@
text
@**
**      $Filename potgo.asm $
**      $Release: 1.4 $
**      $Revision: 36.7 $
**      $Date: 91/02/08 22:08:51 $
**
**      potgo resource module
**
**      (C) Copyright 1985,1989 Commodore-Amiga, Inc.
**          All Rights Reserved
**
**	$Id: potgo.asm,v 36.7 91/02/08 22:08:51 darren Exp Locker: darren $

	SECTION	potgo,code

**	Included Files

	INCLUDE		"exec/types.i"
	INCLUDE		"exec/nodes.i"
	INCLUDE		"exec/libraries.i"
	INCLUDE		"exec/resident.i"
	INCLUDE		"exec/initializers.i"
	INCLUDE		"exec/ables.i"

	INCLUDE		"potgo_rev.i"


**	Local Macros

XLVO	MACRO
	XREF	_LVO\1
	ENDM

**	Imported

	XREF	_intena
	XREF	_potgo


 STRUCTURE	PotgoResource,LIB_SIZE
    APTR	pr_ExecBase		; SysBase
    UWORD	pr_Allocated		; the owned bits of the reg
    UWORD	pr_HiByte		; current register high byte
    LABEL	pr_SIZEOF

**********************************************************************

residentPR:
		dc.w	RTC_MATCHWORD
		dc.l	residentPR
		dc.l	prEndModule
		dc.b	RTF_AUTOINIT!RTF_COLDSTART
		dc.b	VERSION
		dc.b	NT_RESOURCE
		dc.b	100
		dc.l	prName
		dc.l	prID
		dc.l	prInitTable

prName:
		dc.b	'potgo.resource',0

prID:
		VSTRING


prInitTable:
		dc.l	pr_SIZEOF
		dc.l	prFuncInit
		dc.l	prStructInit
		dc.l	prInit



FUNCDEF	MACRO
		dc.w    pr\1-prFuncInit
	ENDM

prFuncInit:
		dc.w	-1
	INCLUDE	"potgo_lib.i"
		dc.w	-1

prStructInit:
*	;------ Initialize the library
		INITBYTE    LN_TYPE,NT_RESOURCE
		INITLONG    LN_NAME,prName
		INITBYTE    LIB_FLAGS,LIBF_SUMUSED!LIBF_CHANGED
		dc.w	    0


;------ potgo.resource/Init ------------------------------------------
;
;
prInit:
		move.l	d0,a0
		move.l	a6,pr_ExecBase(a0)
		rts


******* potgo.resource/AllocPotBits **********************************
*
*   NAME
*	AllocPotBits -- Allocate bits in the potgo register.
*
*   SYNOPSIS
*	allocated = AllocPotBits(bits)
*	D0                       D0
*
*	UWORD AllocPotBits( UWORD );
*
*   FUNCTION
*	The AllocPotBits routine allocates bits in the hardware potgo
*	register that the application wishes to manipulate via
*	WritePotgo.  The request may be for more than one bit.  A
*	user trying to allocate bits may find that they are
*	unavailable because they are already allocated, or because
*	the start bit itself (bit 0) has been allocated, or if
*	requesting the start bit, because input bits have been
*	allocated.  A user can block itself from allocation: i.e. 
*	it should FreePotgoBits the bits it has and re-AllocPotBits if
*	it is trying to change an allocation involving the start bit.
*
*   INPUTS
*	bits - a description of the hardware bits that the application
*		wishes to manipulate, loosely based on the register
*		description itself:
*	    START (bit 0) - set if you wish to use start (i.e. start
*		    the proportional controller counters) with the
*		    input ports you allocate (below).  You must
*		    allocate all the DATxx ports you want to apply
*		    START to in this same call, with the OUTxx bit
*		    clear.
*	    DATLX (bit 8) - set if you wish to use the port associated
*		    with the left (0) controller, pin 5.
*	    OUTLX (bit 9) - set if you promise to use the LX port in
*		    output mode only.  The port is not set to output
*		    for you at this time -- this bit set indicates
*		    that you don't mind if STARTs are initiated at any
*		    time by others, since ports that are enabled for
*		    output are unaffected by START.
*	    DATLY (bit 10) - as DATLX but for the left (0) controller,
*		    pin 9.
*	    OUTLY (bit 11) - as OUTLX but for LY.
*	    DATRX (bit 12) - the right (1) controller, pin 5.
*	    OUTRX (bit 13) - OUT for RX.
*	    DATRY (bit 14) - the right (1) controller, pin 9.
*	    OUTRY (bit 15) - OUT for RY.
*
*   RESULTS
*	allocated - the START and DATxx bits of those requested that
*		were granted.  The OUTxx bits are don't cares.
*
**********************************************************************
prAllocPotBits:
		move.l	d2,-(a7)
		move.l	pr_ExecBase(a6),a0
		DISABLE	a0,NOFETCH
		move.w	pr_Allocated(a6),d2
		move.w	d2,d1
		and.w	#$5500,d1
		lsl.w	#1,d1
		or.w	d2,d1
		not.w	d1
		and.w	d1,d0	; eliminate allocated DATxx & START
		btst	#0,d0	; trying to allocate START?
		beq.s	startOK	;   no
		move.w	d2,d1	; find if there are any input ports
		lsr.w	#1,d1	;   already allocated...
		not.w	d1	;
		and.w	#$5500,d1
		and.w	d2,d1	; if zero then no inputs allocated
		beq.s	startOK	;
		bclr	#0,d0	; START allocation fails

startOK:
		btst	#0,d2	; was START already set?
		beq.s	inputOK	;   if not, input allocations are OK
		move.w	d0,d1
		lsr.w	#1,d1	; get output allocations
		and.w	#$5500,d0
		and.w	d1,d0	;   only output allocations are OK
		move.w	d0,d1	;
		lsl.w	#1,d1	;
		or.w	d1,d0	;   (save that they are outputs)
inputOK:
		and.l	#$ff01,d0
		or.w	d0,pr_Allocated(a6)
		ENABLE	a0,NOFETCH
		move.l	(a7)+,d2
		rts


******* potgo.resource/FreePotBits ***********************************
*
*   NAME
*	FreePotBits -- Free allocated bits in the potgo register.
*
*   SYNOPSIS
*	FreePotBits(allocated)
*	            D0
*
*	void FreePotBits( UWORD );
*
*   FUNCTION
*	The FreePotBits routine frees previously allocated bits in the
*	hardware potgo register that the application had allocated via
*	AllocPotBits and no longer wishes to use.  It accepts the
*	return value from AllocPotBits as its argument.
*
**********************************************************************
prFreePotBits:
		and.w	#$5501,d0
		move.w	d0,d1
		lsl.w	#1,d1
		and.w	#$aa00,d1
		or.w	d1,d0
		not.w	d0
		and.w	d0,pr_Allocated(a6)
		rts


******* potgo.resource/WritePotgo ************************************
*
*   NAME
*	WritePotgo -- Write to the hardware potgo register.
*
*   SYNOPSIS
*	WritePotgo(word, mask)
*	           D0    D1
*
*	void WritePotgo( UWORD, UWORD );
*
*   FUNCTION
*	The WritePotgo routine sets and clears bits in the hardware
*	potgo register.  Only those bits specified by the mask are
*	affected -- it is improper to set bits in the mask that you
*	have not successfully allocated.  The bits in the high byte
*	are saved to be maintained when other users write to the
*	potgo register.  The START bit is not saved, it is written
*	only explicitly as the result of a call to this routine with
*	the START bit set: other users will not restart it.
*
*   INPUTS
*	word - the data to write to the hardware potgo register and
*	    save for further use, except the START bit, which is
*	    not saved.
*	mask - those bits in word that are to be written.  Other
*	    bits may have been provided by previous calls to
*	    this routine, and default to zero.
*
**********************************************************************
prWritePotgo:
		and.w	d1,d0
		not.w	d1
		move.l	pr_ExecBase(a6),a0
		DISABLE	a0,NOFETCH
		and.w	d1,pr_HiByte(a6)
		or.w	pr_HiByte(a6),d0
		move.w	d0,_potgo
		clr.b	d0			; do not cache starts
		move.w	d0,pr_HiByte(a6)
		ENABLE	a0,NOFETCH
		rts

prEndModule:

	END
@


36.7
log
@No changes.
@
text
@d4 2
a5 2
**      $Revision: 36.6 $
**      $Date: 91/01/28 18:05:59 $
d12 1
a12 1
**	$Id: potgo.asm,v 36.6 91/01/28 18:05:59 darren Exp Locker: darren $
@


36.6
log
@*** empty log message ***
@
text
@d4 2
a5 2
**      $Revision: 36.5 $
**      $Date: 90/04/13 11:23:25 $
d12 1
a12 1
**	$Id: potgo.asm,v 36.5 90/04/13 11:23:25 kodiak Exp Locker: darren $
@


36.5
log
@use Id instead of Header for 4.x rcs
@
text
@d4 2
a5 2
**      $Revision: 36.4 $
**      $Date: 90/04/09 09:54:30 $
d12 1
a12 1
**	$Id$
@


36.4
log
@Avoids implicit $4 reference by converting DISABLE/ENABLE to NOFETCH form.
@
text
@d4 2
a5 2
**      $Revision: 36.3 $
**      $Date: 90/04/02 11:08:49 $
d12 1
a12 1
**	$Header: /usr2/kodiak/src/KS/potgo/RCS/potgo.asm,v 36.3 90/04/02 11:08:49 kodiak Exp Locker: kodiak $
@


36.3
log
@for rcs 4.x header change
@
text
@d4 2
a5 2
**      $Revision: 36.2 $
**      $Date: 89/06/06 15:09:25 $
d12 1
a12 1
**	$Header: /usr2/kodiak/src/KS/potgo/RCS/potgo.asm,v 36.2 89/06/06 15:09:25 kodiak Exp Locker: kodiak $
d41 1
d71 1
a71 1
		dc.l	0
d92 9
d157 2
a158 1
		DISABLE	a0
d189 1
a189 1
		ENABLE	a0
d256 2
a257 1
		DISABLE	a0
d263 1
a263 1
		ENABLE	a0
@


36.2
log
@autodoc changes
@
text
@d4 2
a5 2
**      $Revision: 36.1 $
**      $Date: 89/03/31 13:05:47 $
d12 1
a12 1
**	$Header: potgo.asm,v 36.1 89/03/31 13:05:47 kodiak Exp $
@


36.1
log
@fix resident tag flags for autoinit
@
text
@d4 2
a5 2
**      $Revision: 36.0 $
**      $Date: 89/02/24 15:51:38 $
d12 1
a12 1
**	$Header: potgo.asm,v 36.0 89/02/24 15:51:38 kodiak Exp $
d94 1
a94 1
*	AllocPotBits - allocate bits in the potgo register
d96 3
a98 3
*   SYnopSIS
*	allocated = AllocPotBits(bits), potgoResource
*	d0			 d0	a6
d100 2
d119 1
a119 1
*		    thr proportional controller counters) with the
d176 1
a176 1
		and.w	#$ff01,d0
d186 1
a186 1
*	FreePotBits - free allocated bits in the potgo register
d188 3
a190 3
*   SYnopSIS
*	FreePotBits(allocated), potgoResource
*		    d0		a6
d192 2
d215 1
a215 1
*	WritePotgo - write to the hardware potgo register
d217 3
a219 3
*   SYnopSIS
*	WritePotgo(word, mask), potgoResource
*		   d0    d1	a6
d221 2
d235 2
a236 2
*		save for further use, except the START bit, which is
*		not saved.
d238 2
a239 2
*		bits may have been provided by previous calls to
*		this routine, and default to zero.
@


36.0
log
@(oops, see description of file for this first pass)
@
text
@d5 1
a5 1
**      $Date: 89/02/17 13:33:42 $
d12 1
a12 1
**	$Header: potgo.asm,v 36.0 89/02/17 13:33:42 kodiak Exp $
d51 1
a51 1
		dc.b	RTW_COLDSTART!RTF_COLDSTART
@

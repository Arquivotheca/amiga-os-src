head     39.7;
branch   ;
access   ;
symbols  ;
locks    ; strict;
comment  @* @;


39.7
date     92.05.29.15.38.14;  author vertex;  state Exp;
branches ;
next     39.6;

39.6
date     92.04.15.11.39.57;  author vertex;  state Exp;
branches ;
next     39.5;

39.5
date     92.04.13.14.08.22;  author vertex;  state Exp;
branches ;
next     39.4;

39.4
date     92.04.07.13.49.45;  author vertex;  state Exp;
branches ;
next     39.3;

39.3
date     92.03.24.15.14.51;  author peter;  state Exp;
branches ;
next     39.2;

39.2
date     92.03.20.12.47.26;  author peter;  state Exp;
branches ;
next     39.1;

39.1
date     92.02.03.13.58.36;  author peter;  state Exp;
branches ;
next     ;


desc
@New GadTools library stuff.
@


39.7
log
@*** empty log message ***
@
text
@	OPTIMON

;---------------------------------------------------------------------------

	NOLIST

	INCLUDE	"exec/types.i"
	INCLUDE	"exec/libraries.i"
	INCLUDE	"exec/execbase.i"
	INCLUDE	"exec/initializers.i"
	INCLUDE	"exec/resident.i"
	INCLUDE "internal/librarytags.i"

	INCLUDE	"gadtools_rev.i"
	INCLUDE	"gtinternal.i"

	LIST

;---------------------------------------------------------------------------

	XREF	_LIB_CreateGadgetA
	XREF	_LIB_FreeGadgets
	XREF	_LIB_GT_SetGadgetAttrsA
	XREF	_LIB_CreateMenusA
	XREF	_LIB_FreeMenus
	XREF	_LIB_LayoutMenuItemsA
	XREF	_LIB_LayoutMenusA
	XREF	_LIB_GT_GetIMsg
	XREF	_LIB_GT_ReplyIMsg
	XREF	_LIB_GT_RefreshWindow
	XREF	_LIB_GT_BeginRefresh
	XREF	_LIB_GT_EndRefresh
	XREF	_LIB_GT_FilterIMsg
	XREF	_LIB_GT_PostFilterIMsg
	XREF	_LIB_CreateContext
	XREF	_LIB_DrawBevelBoxA
	XREF	_LIB_GetVisualInfoA
	XREF	_LIB_FreeVisualInfo
	XREF	_LIB_GT_GetGadgetAttrsA

	XREF	_DispatchPalette
	XREF	@@initGTButtonIClass

	XREF	_LVOSDivMod32
	XREF	_LVOUDivMod32
	XREF	_LVOSMult32
	XREF	_LVOUMult32

	XREF	_LVOInitSemaphore
	XREF	_LVOTaggedOpenLibrary


	XREF	endtag

;---------------------------------------------------------------------------

	XDEF	LibInit
	XDEF	LibOpen
	XDEF	LibClose
	XDEF	LibExpunge
	XDEF	LibReserved
	XDEF	_callCHook

;---------------------------------------------------------------------------

ROMTAG:
	DC.W	RTC_MATCHWORD		; UWORD RT_MATCHWORD
	DC.L	ROMTAG			; APTR  RT_MATCHTAG
	DC.L	endtag			; APTR  RT_ENDSKIP
	DC.B	RTF_AUTOINIT		; UBYTE RT_FLAGS
	DC.B	VERSION			; UBYTE RT_VERSION
	DC.B	NT_LIBRARY		; UBYTE RT_TYPE
	DC.B	-120			; BYTE  RT_PRI
	DC.L	LibName			; APTR  RT_NAME
	DC.L	LibId			; APTR  RT_IDSTRING
	DC.L	LibInitTable		; APTR  RT_INIT

LibName	DC.B	'gadtools.library',0
LibId	VSTRING

	CNOP	0,2

LibInitTable:
	DC.L	GadToolsLib_SIZEOF
	DC.L	LibFuncTable
	DC.L	0
	DC.L	LibInit

V_DEF	MACRO
	DC.W	\1+(*-LibFuncTable)
	ENDM

LibFuncTable:
	DC.W	-1
	V_DEF	LibOpen
	V_DEF	LibClose
	V_DEF	LibExpunge
	V_DEF	LibReserved

	V_DEF	_LIB_CreateGadgetA
	V_DEF	_LIB_FreeGadgets
	V_DEF	_LIB_GT_SetGadgetAttrsA
	V_DEF	_LIB_CreateMenusA
	V_DEF	_LIB_FreeMenus
	V_DEF	_LIB_LayoutMenuItemsA
	V_DEF	_LIB_LayoutMenusA
	V_DEF	_LIB_GT_GetIMsg
	V_DEF	_LIB_GT_ReplyIMsg
	V_DEF	_LIB_GT_RefreshWindow
	V_DEF	_LIB_GT_BeginRefresh
	V_DEF	_LIB_GT_EndRefresh
	V_DEF	_LIB_GT_FilterIMsg
	V_DEF	_LIB_GT_PostFilterIMsg
	V_DEF	_LIB_CreateContext
	V_DEF	_LIB_DrawBevelBoxA
	V_DEF	_LIB_GetVisualInfoA
	V_DEF	_LIB_FreeVisualInfo
	V_DEF	LibReserved
	V_DEF	LibReserved
	V_DEF	LibReserved
	V_DEF	LibReserved
	V_DEF	LibReserved
	V_DEF	LibReserved

*
* V39 functions begin here
*

	V_DEF	_LIB_GT_GetGadgetAttrsA

	DC.W	-1

;-----------------------------------------------------------------------

; Library Init entry point called when library is first loaded in memory
; On entry, D0 points to library base, A0 has lib seglist, A6 has SysBase
; Returns 0 for failure or the library base for success.

; We're too lazy to check for failure, since you'd be hosed anyways.

LibInit:
	movem.l	d0/a3,-(sp)
	move.l	d0,a3
	move.l	a6,gtb_SysBase(a3)

	move.w	#REVISION,LIB_REVISION(a3)

	moveq	#OLTAG_UTILITY,d0
	bsr.s	OpenLib
	move.l	d0,a0
	move.l	a0,gtb_UtilityBase(a3)

	; a0 has UtilityBase
	; a3 has own library base (GadToolsBase in this case)
	; d0, a1 and a0 get modified

	lea	_LVOSMult32(a0),a0	; get address of first jump vector
	lea	gtb_SMult32(a3),a1	; load target address of first vector
	moveq	#3,d0
1$:
	move.l	a0,(a1)+
	subq.l	#6,a0
	dbra	d0,1$

	moveq	#OLTAG_GRAPHICS,d0
	bsr.s	OpenLib
	move.l	d0,gtb_GfxBase(a3)

	moveq	#OLTAG_LAYERS,d0
	bsr.s	OpenLib
	move.l	d0,gtb_LayersBase(a3)

	moveq	#OLTAG_INTUITION,d0
	bsr.s	OpenLib
	move.l	d0,gtb_IntuitionBase(a3)

	exg	a3,a6
	bsr	@@initGTButtonIClass
	move.l	d0,gtb_GTButtonIClass(a6)

	lea	_DispatchPalette,a0
	move.l	a0,gtb_PaletteGHook+h_SubEntry(a6)
	lea	_callCHook(pc),a0
	move.l	a0,gtb_PaletteGHook+h_Entry(a6)
	move.l	a6,gtb_PaletteGHook+h_Data(a6)

	exg	a3,a6

	movem.l	(sp)+,d0/a3
	rts

OpenLib:
	jmp	_LVOTaggedOpenLibrary(a6)

	CNOP	0,2

;-----------------------------------------------------------------------

; Library open entry point called every OpenLibrary()
; On entry, A6 has GadToolsBase, task switching is disabled
; Returns 0 for failure, or GadToolsBase for success.
LibOpen:
	addq.w	#1,LIB_OPENCNT(a6)
	move.l	a6,d0
	rts

;-----------------------------------------------------------------------

; Library close entry point called every CloseLibrary()
; On entry, A6 has GadToolsBase, task switching is disabled
; Returns 0 normally, or the library seglist when lib should be expunged
;   due to delayed expunge bit being set, which won't happen for a ROM
;   module
LibClose:

	subq.w	#1,LIB_OPENCNT(a6)

*** FALLS THROUGH !!!

;-----------------------------------------------------------------------

; Library expunge entry point called whenever system memory is lacking
; On entry, A6 has GadToolsBase, task switching is disabled
; Returns the library seglist if the library open count is 0, returns 0
; otherwise and sets the delayed expunge bit. Won't happen since this is
; a ROM module

LibExpunge:

*** FALLS THROUGH !!!

;-----------------------------------------------------------------------

LibReserved:
	moveq	#0,d0
	rts

;-----------------------------------------------------------------------

* Restores context and calls high-level language hook

_callCHook:
	movem.l	a5/a6,-(sp)
	movem.l	h_SubEntry(a0),a5/a6	; h_SubEntry into A5, h_Data into A6
	jsr	(a5)			; call HLL
	movem.l	(sp)+,a5/a6
	rts

;-----------------------------------------------------------------------

	END
@


39.6
log
@Now uses TaggedOpenLibrary()
@
text
@d41 3
d49 4
d62 1
a78 1

d86 1
a86 1
	DC.L	LibDataTable
a132 10
LibDataTable:
	INITBYTE	LN_TYPE,NT_LIBRARY
	INITLONG	LN_NAME,LibName
	INITBYTE	LIB_FLAGS,(LIBF_SUMUSED!LIBF_CHANGED)
	INITWORD	LIB_VERSION,VERSION
	INITWORD	LIB_REVISION,REVISION
	INITLONG	LIB_IDSTRING,LibId
	DC.W		0


d139 1
a139 5
; We're too lazy check for failure, since you'd be hosed anyways.

	XREF	_DispatchPalette
	XREF	@@initGTButtonIClass
	XREF	_LVOTaggedOpenLibrary
d146 2
d169 4
a239 2
		XDEF	_callCHook

d249 2
a251 2

;-----------------------------------------------------------------------
@


39.5
log
@*** empty log message ***
@
text
@d12 1
a71 5
UtilityName	DC.B	"utility.library",0
GfxName		DC.B	"graphics.library",0
IntuitionName	DC.B	"intuition.library",0


d146 1
a146 1
	XREF	_LVOOpenLibrary
d153 1
a153 1
	lea	UtilityName(pc),a1
d170 1
a170 1
	lea	GfxName(pc),a1
d174 1
a174 1
	lea	IntuitionName(pc),a1
d194 1
a194 2
	moveq	#36,d0
	jmp	_LVOOpenLibrary(a6)
d196 1
a196 1
	CNOP	0,4
@


39.4
log
@*** empty log message ***
@
text
@d148 1
a148 1
	XREF	_dispatchPaletteG
d186 1
a186 1
	lea	_dispatchPaletteG,a0
@


39.3
log
@Added GT_GetGadgetAttrs().
@
text
@d78 1
a78 1
	CNOP	0,4
d163 1
a163 1
	; a3 has own library base (LocaleBase in this case)
d198 2
a199 2
	MOVEQ	#36,d0
	JMP	_LVOOpenLibrary(a6)
@


39.2
log
@Init cached pointers for utility.library math LVOs.
@
text
@d38 1
d121 6
@


39.1
log
@Initial RCS check-in.
@
text
@d39 5
d152 14
a165 1
	move.l	d0,gtb_UtilityBase(a3)
@

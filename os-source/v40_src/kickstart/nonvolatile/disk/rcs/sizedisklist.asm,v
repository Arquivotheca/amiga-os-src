head     40.4;
branch   ;
access   ;
symbols  V40_35:40.4 V40_34:40.4 rel40_10:40.3 rel40_9:40.3 rel40_8:40.3 rel40_7:40.3 rel40_6:40.2 rel40_5:40.1 rel40_4:40.1 rel40_3:40.0 rel40_2:39.0 rel40_1:39.0 rel39_1:39.0;
locks    ; strict;
comment  @* @;


40.4
date     93.03.18.11.12.47;  author brummer;  state Exp;
branches ;
next     40.3;

40.3
date     93.02.25.08.53.30;  author Jim2;  state Exp;
branches ;
next     40.2;

40.2
date     93.02.23.13.23.43;  author Jim2;  state Exp;
branches ;
next     40.1;

40.1
date     93.02.18.10.59.29;  author Jim2;  state Exp;
branches ;
next     40.0;

40.0
date     93.02.16.13.22.39;  author Jim2;  state Exp;
branches ;
next     39.0;

39.0
date     93.02.03.11.30.52;  author Jim2;  state Exp;
branches ;
next     ;


desc
@Determines the number of bytes required for a list of files/directories
on the users nonvolatile device.
@


40.4
log
@Fix SizeDiskList to return zero when disk is not found.
@
text
@******************************************************************************
*
*	$Id: sizedisklist.asm,v 40.3 93/02/25 08:53:30 Jim2 Exp Locker: brummer $
*
******************************************************************************
*
*	$Log:	sizedisklist.asm,v $
* Revision 40.3  93/02/25  08:53:30  Jim2
* Documentation.
*
* Revision 40.2  93/02/23  13:23:43  Jim2
* Changed the name of the data type from NVItem to NVEntry.
*
* Revision 40.1  93/02/18  10:59:29  Jim2
* Changed sizing routines to use the label for allowing for the
* size of the array elements.  Thus the next time the size
* changes I won't have to change this file.
*
* Revision 40.0  93/02/16  13:22:39  Jim2
* Included the bytes requried for the new sturctures.
*
* Revision 39.0  93/02/03  11:30:52  Jim2
* Initial Release - Tested.
*
*
*	(C) Copyright 1992,1993 Commodore-Amiga, Inc.
*	    All Rights Reserved
*
******************************************************************************

		INCLUDE	'exec/macros.i'

		INCLUDE	'dos/dos.i'


		INCLUDE	'/nonvolatilebase.i'
		INCLUDE	'/nonvolatile.i'

		XDEF	SizeDiskList

*****i* nonvolatile.library/SizeDiskList *************************************
*
*   NAME
*	SizeDiskList - Byte required for a list of Disk items/applications.
*
*   SYNOPSIS
*	Size = SizeDiskList (AppName, NVBase)
*	d0		     a0	      a5
*
*	LONG SizeDiskList (STRPTR, struct NVBase *)
*
*   FUNCTION
*	Determines how large a block of memory is requried to store the
*	List of NVEntry structures and the name strings pointed to by the
*	structures.  The size is the size for the string plus the size for
*	an NVEntry structure.
*
*	If AppName is non NULL only report the size needed to create a
*	list of strings for the Items found for that AppName.
*
*	If AppName is NULL report the size needed for a list of all
*	AppNames and all ItemNames.
*
*
*   INPUTS
*	AppName - Pointer to the application name.  Or NULL if the entire
*		  contents is to be requested.
*
*   RESULT
*	Size - Number of bytes required to create a list.
*
*   SEE ALSO
*	SizeNVRAMList()
*
******************************************************************************
SizeDiskList
		moveq.l	#0,d0			; setup return value if lock fails
		move.l	nv_DiskLock(a5),d1	;Check to see if there is a nonvolatile disk out there.
		beq.s	NoDisk
				;Yes, there is a disk.
		movem.l	d2/d7/a2/a6,-(sp)
		move.l	nv_DOSBase(a5),a6
		moveq.l	#0,d7			;d7 will be used to accumulate the lengths.
		move.l	a0,-(sp)		;Temporarily save the AppName Pointer.
		JSRLIB	CurrentDir		;Change to the directory the user has picked for nonvolatile storage.
		move.l	d0,-(sp)		;Remember the orginal directory.
		move.l	4(sp),d1
		beq.s	EveryApp		;Is this for a single AppName, or all AppNames?
				;The pointer to the single AppName has been loaded into d1.
		bsr.s	SizeApp			;Determine the size needed for this AppName.
		bra.s	Cleanup			;And we are done.
				;Scan the nonvolatile directory for every AppName, size each of them.
EveryApp:	move.l	nv_DiskLock(a5),d1
		sub.l	#fib_SIZEOF+4,sp	;Need a FIB, but must be long word aligned.
		move.l	sp,d2
		addq.l	#3,d2
		and.b	#$FC,d2			;Forced long word alignment.
		move.l	d2,a2
		JSRLIB	Examine
Skip:		move.l	nv_DiskLock(a5),d1
		JSRLIB	ExNext			;Move to the next directory entry.
		tst.w	d0			;Is there another entry in this directory?
		beq.s	DoneDirs
				;Yep, have a new entry to check.
		tst.b	fib_DirEntryType(a2)
		bmi.s	Skip			;Is this another directory?
				;Yep, so we need to add the string length.
LoopingDir:	addq.w	#1,d0
		tst.b	fib_FileName(a2,d0.w)
		bne.s	LoopingDir		;Increment d0 until the NULL is found.

		add.w	#1+NVENTRY_SIZE,d0	;Size is one more than index of the NULL, plus one element to store the AppName.
		ext.l	d0
		add.l	d0,d7			;Add this to any previous value.
		movem.l	d2/a2,-(sp)		;Don't lose the pointers (SizeApp is non standard in register usage.)
		lea	fib_FileName(a2),a0
		move.l	a0,d1			;Place the AppName pointer in d1.
		bsr.s	SizeApp			;Size the AppName directory.
		movem.l	(sp)+,d2/a2		;Restore the pointers.

		bra.s	Skip			;This is a top testing loop.
				;All entries have been processed.
DoneDirs:	add.l	#fib_SIZEOF+4,sp	;Don't need the FIB anymore.
				;The sizing is complete.  Let's clean up and go home.
Cleanup:	move.l	(sp)+,d1
		JSRLIB	CurrentDir		;Restore the orginal directory.
		move.l	d7,d0			;Set up the return value.
		movem.l	(sp)+,d1/d2/d7/a2/a6	;Don't forget we pushed on one additional register.
NoDisk:		rts

*****l* a/SizeDiskList.asm/SizeApp *******************************************
*
*   NAME
*	SizeApp - Bytes required for a list of Disk items.
*
*   SYNOPSIS
*	Size = SizeApp (AppName, PreviousSize)
*	d7		d1	 d7
*
*	LONG SizeApp (STRPTR, ULONG)
*
*   FUNCTION
*	Determines how large a block of memory is requried to store the NULL
*	terminated list of NULL terminated string identifing the items
*	stored for the given AppName.
*
*
*   INPUTS
*	AppName - Pointer to the application name.
*	PrevousSize - Size required for any other AppNames.
*
*   RESULT
*	Size - Number of bytes required to save the list.
*
*   WARNING
*	Trashes a2,d2.
*
******************************************************************************
SizeApp
		move.l	#ACCESS_READ,d2
		JSRLIB	Lock			;Get a lock on the directory for the AppName.
		move.l	d0,d1
		beq.s	NotExist		;Were we sucessful?
				;Got lock now we can scan the directory.
		sub.l	#fib_SIZEOF+4,sp	;Need a FIB, but must be long word aligned.
		move.l	sp,d2
		addq.l	#3,d2
		and.b	#$FC,d2			;Forced long word alignment.
		move.l	d2,a2
		move.l	d1,-(sp)		;Don't want to lose the lock.
		JSRLIB	Examine			;Get the first entry.
ProcessFile:	move.l	(sp),d1			;Restore the lock.
		JSRLIB	ExNext			;Get next entry.
		tst.w	d0
		beq.s	Done			;Did we get a new entry?
				;Yep, so how long is the name?
Looping:	addq.w	#1,d0
		tst.b	fib_FileName(a2,d0.w)
		bne.s	Looping			;Wait for the NULL.

		add.w	#1+NVENTRY_SIZE,d0	;Add one to get the size, not the index number.  And the size of an element
		ext.l	d0
		add.l	d0,d7			;Sum.
		bra.s	ProcessFile		;This is a top testing loop.
				;All done.
Done:		move.l	(sp)+,d1		;Get the lock off the stack.
		JSRLIB	UnLock
		add.l	#fib_SIZEOF+4,sp	;Clear the FIB off the stack.
NotExist:	rts

		end
@


40.3
log
@Documentation.
@
text
@d3 1
a3 1
*	$Id: SizeDiskList.asm,v 40.2 93/02/23 13:23:43 Jim2 Exp Locker: Jim2 $
d7 4
a10 1
*	$Log:	SizeDiskList.asm,v $
d77 1
@


40.2
log
@Changed the name of the data type from NVItem to NVEntry.
@
text
@d3 1
a3 1
*	$Id: SizeDiskList.asm,v 40.1 93/02/18 10:59:29 Jim2 Exp Locker: Jim2 $
d8 3
d47 1
a47 1
*	LONG SizeDiskList (char *, struct NVBase *)
d50 4
a53 3
*	Determines how large a block of memory is requried to store the NULL
*	terminated list of NULL terminated string identifing the items
*	stored for the given AppName.
d55 2
a56 4
*	If AppName is a NULL pointer this will be taken another level deep.
*	ie the size for
*   App1<NULL>Item1<NULL>Item2<NULL><NULL>App2<NULL>Item47<NULL><NULL><NULL>
*	is 32 bytes.
d58 2
a59 2
*	If AppName pointed to App1. the size required is 13.
*		Item1<NULL>Item2<NULL><NULL>
d67 1
a67 1
*	Size - Number of bytes required to save the list.
d136 1
a136 1
*	LONG SizeApp (char *, ULONG)
@


40.1
log
@Changed sizing routines to use the label for allowing for the
size of the array elements.  Thus the next time the size
changes I won't have to change this file.
@
text
@d3 1
a3 1
*	$Id: SizeDiskList.asm,v 40.0 93/02/16 13:22:39 Jim2 Exp Locker: Jim2 $
d8 5
d73 1
a73 1
		beq	NoDisk
a84 3
		tst.l	d7
		beq.s	Cleanup
		add.l	#NVITEM_SIZE,d7			;Need an element for the AppName
d106 1
a106 1
		add.w	#1+NVITEM_SIZE,d0	;Size is one more than index of the NULL, plus one element to store the AppName.
d175 1
a175 1
		add.w	#1+NVITEM_SIZE,d0	;Add one to get the size, not the index number.  And the size of an element
@


40.0
log
@Included the bytes requried for the new sturctures.
@
text
@d3 1
a3 1
*	$Id: SizeDiskList.asm,v 39.0 93/02/03 11:30:52 Jim2 Exp Locker: Jim2 $
d8 3
d26 1
d68 1
a68 1
		beq.s	NoDisk
d82 1
a82 1
		add.l	#8,d7			;Need an element for the AppName
d104 1
a104 1
		add.w	#1+8,d0			;Size is one more than index of the NULL, plus one element to store the AppName.
d173 1
a173 1
		addq.w	#1,d0			;Add one to get the size, not the index number.
a175 1
		addq.l	#8,d7			;Need an element
@


39.0
log
@Initial Release - Tested.
@
text
@d3 1
a3 1
*	$Id: $
d7 3
a9 1
*	$Log: $
d11 1
d76 3
d100 1
a100 1
		addq.w	#1,d0			;Size is one more than index of the NULL.
d172 1
d175 1
a175 2
Done:		addq.l	#1,d7			;Add one for the extra NULL.
		move.l	(sp)+,d1		;Get the lock off the stack.
@

head     40.3;
branch   ;
access   ;
symbols  V40_35:40.3 V40_34:40.3 rel40_10:40.2 rel40_9:40.2 rel40_8:40.2 rel40_7:40.2 rel40_6:40.1 rel40_5:40.1 rel40_4:40.0;
locks    ; strict;
comment  @* @;


40.3
date     93.03.09.14.41.19;  author brummer;  state Exp;
branches ;
next     40.2;

40.2
date     93.02.25.08.52.10;  author Jim2;  state Exp;
branches ;
next     40.1;

40.1
date     93.02.19.14.53.18;  author Jim2;  state Exp;
branches ;
next     40.0;

40.0
date     93.02.18.10.38.45;  author Jim2;  state Exp;
branches ;
next     ;


desc
@
Sets or clears the delete protection bit.
@


40.3
log
@Check status after call to examine per Martin's request.
@
text
@******************************************************************************
*
*	$Id: SetDiskProtection.asm,v 40.2 93/02/25 08:52:10 Jim2 Exp Locker: brummer $
*
******************************************************************************
*
*	$Log:	SetDiskProtection.asm,v $
* Revision 40.2  93/02/25  08:52:10  Jim2
* Documentation change.
*
* Revision 40.1  93/02/19  14:53:18  Jim2
* Changed the labels.
*
* Revision 40.0  93/02/18  10:38:45  Jim2
* Initial release - tested.
*
*
*	(C) Copyright 1992,1993 Commodore-Amiga, Inc.
*	    All Rights Reserved
*
******************************************************************************

		INCLUDE	'exec/macros.i'

		INCLUDE	'dos/dos.i'


		INCLUDE	'/nonvolatilebase.i'

		XDEF	SetDiskProtection

*****i* nonvolatile.library/SetDiskProtection ********************************
*
*   NAME
*	SetDiskProtection - Attempts to change the protection of a file.
*
*   SYNOPSIS
*	SetDiskProtection (AppName, ItemName, NVBase, Protection, Success)
*			   a3	    a4	      a5      d2	   d3
*
*	SetDiskProtection (STRPTR, STRPTR, struct NVBase *, BOOL)
*
*   FUNCTION
*	Changes the delete bit for a file to the state indicated by the
*	corresponding bit it Protection.
*
*	Since there is no change protection call, this routine will read
*	the current protection and if there is a change in the delete
*	bit it will call the DOS routine to set the protection bit.  The
*	value returned by this DOS call is ORed into the Success flag.
*
*   INPUTS
*	AppName - Pointer to a NULL terminated string.  This is the
*		  directory that should contain the file.
*	ItemName - Pointer to a NULL terminated string.  This is the file
*		   file.
*	NVBase - Pointer the base of nonvolatile.library.
*	Protection - New protection status.  Only the least significant bit
*		     bit (delete) is used.
*	Success - Whether the protection was changed on a copy of this
*		  data on another device.
*
*   RESULT
*	Success - The return value from SetProtection ORed with the previous
*		  value of Success.
*
*   SEE ALSO
*	SetNVRAMProtection()
*
******************************************************************************
SetDiskProtection
		movem.l	d4-d5/a3/a6,-(sp)
		move.l	nv_DOSBase(a5),a6
		move.l	nv_DiskLock(a5),d1
		beq.s	sdp_Cleanup

		JSRLIB	CurrentDir		;Change to the directory the user has picked for nonvolatile storage.
		move.l	d0,-(sp)		;Remember the orginal directory.
		move.l	d2,-(sp)
		move.l	#ACCESS_READ,d2
		move.l	a3,d1
		JSRLIB	Lock
		move.l	d0,d5
		JSRLIB	CurrentDir
		move.l	a4,d1
		JSRLIB	Lock
		move.l	d0,d4
		move.l	d4,d1
		beq.s	sdp_Unlock

		sub.l	#fib_SIZEOF+4,sp	;Need a FIB, but must be long word aligned.
		move.l	sp,d2
		addq.l	#3,d2
		and.b	#$FC,d2			;Forced long word alignment.
		move.l	d2,a3
		JSRLIB	Examine
		tst.l	d0			; success ?
		beq.s	sdp_Unlock		; if no, j to exit
		move.l	fib_Protection(a3),d2
		add.l	#fib_SIZEOF+4,sp	;Don't need the FIB anymore.
		move.l	d4,d1
		JSRLIB	UnLock
		move.l	(sp),d0
		lsr.b	#1,d2
		lsr.b	#1,d0
		roxl.b	#1,d2
		move.l	a4,d1
		JSRLIB	SetProtection
		or.w	d0,d3			;Or the return value with the previous value of Success.
sdp_Unlock:	move.l	(sp)+,d2
		move.l	(sp)+,d1
		JSRLIB	CurrentDir
		move.l	d5,d1
		beq.s	sdp_Cleanup
		JSRLIB	UnLock
sdp_Cleanup:	movem.l	(sp)+,d4-d5/a3/a6
		rts

		end
@


40.2
log
@Documentation change.
@
text
@d3 1
a3 1
*	$Id: SetDiskProtection.asm,v 40.1 93/02/19 14:53:18 Jim2 Exp Locker: Jim2 $
d8 3
d97 2
@


40.1
log
@Changed the labels.
@
text
@d3 1
a3 1
*	$Id: SetDiskProtection.asm,v 40.0 93/02/18 10:38:45 Jim2 Exp Locker: Jim2 $
d8 3
d38 1
a38 1
*	BOOL SetDiskProtection (char *, char *, struct NVBase *, LONG)
@


40.0
log
@Initial release - tested.
@
text
@d3 1
a3 1
*	$Id:  $
d7 3
a9 1
*	$Log:	 $
d11 1
d69 1
a69 1
		beq.s	sid_Cleanup
d83 1
a83 1
		beq.s	sid_Unlock
d102 1
a102 1
sid_Unlock:	move.l	(sp)+,d2
d106 1
a106 1
		beq.s	sid_Cleanup
d108 1
a108 1
sid_Cleanup:	movem.l	(sp)+,d4-d5/a3/a6
@

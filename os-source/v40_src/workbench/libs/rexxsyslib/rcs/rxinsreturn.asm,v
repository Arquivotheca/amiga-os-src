head     1.2;
branch   ;
access   ;
symbols  LOCKDOWN_2_04:1.2 V36_23:1.2 V36_21:1.2;
locks    ; strict;
comment  @* @;


1.2
date     90.12.04.11.31.24;  author mks;  state Exp;
branches ;
next     1.1;

1.1
date     90.03.04.16.43.30;  author rsbx;  state Exp;
branches ;
next     ;


desc
@Instruction module, implements RETURN and other instructions
@


1.2
log
@Sync-up with Bill's latest changes
@
text
@* == cvs2d.asm =========================================================
*
* Copyright (c) 1986-1989 by William S. Hawes.  All Rights Reserved.
*
* ======================================================================
*
* ========================     rxInsRETURN     =========================
* Implements the 'EXIT' and 'RETURN' instructions.
* [instruction module routine]
rxInsRETURN
         clr.l    TSTRING(a0)          ; remove string from token

         ; Check whether an internal function is active.  The 'RETURN'
         ; instruction is equivalent to 'EXIT' in the base environment.

         moveq    #(EXB_EXIT-8),d2     ; 'EXIT' flag
         andi.w   #KTF_SECMASK,d5      ; 'EXIT' option?
         bne.s    1$                   ; yes ...
         tst.w    ev_Level(a4)         ; a function active?
         beq.s    1$                   ; no ...

         ; 'RETURN' being performed ... result may need to be copied.

         moveq    #(EXB_RETURN-8),d2   ; 'RETURN' flag

         ; Make a copy of the result string, if necessary.

         move.l   a1,d0                ; a result string?
         beq.s    1$                   ; no ...
         movea.l  a4,a0                ; storage environment
         bsr      MayCopyString        ; D0=A1=string

         ; Install the result in global name slot.

1$:      move.l   a1,EVNAME(a3)        ; install result

         ; Set the 'EXIT' or 'RETURN' flag as appropriate.

         bset     d2,(STATUS+2)(a4)    ; set flag
         rts

* ==========================     rxInsPUSH     =========================
* Implements the "PUSH" instruction.  The string is stacked to the STDIN
* stream.
rxInsPUSH
         move.w   #_LVOStackF,d4       ; output function
         bra.s    rQ001

* =========================     rxInsQUEUE     =========================
* Implements the "QUEUE" instruction.  Data is queued to the STDIN stream.
rxInsQUEUE
         move.w   #_LVOQueueF,d4       ; output function

rQ001    movea.l  rl_STDIN(a6),a2      ; logical name
         bra.s    rS001

* ==========================     rxInsSAY     ==========================
* Implements the 'SAY' instruction.
rxInsSAY
         move.w   #_LVOWriteF,d4       ; output function
         movea.l  rl_STDOUT(a6),a2     ; logical name

         ; Copy the string and append a 'newline' to it.

rS001    move.l   a1,d1                ; a result string?
         bne.s    1$                   ; yes ...
         movea.l  rl_NULL(a6),a1       ; ... use null string

1$:      movea.l  a4,a0                ; environment
         bsr      CopyString           ; D0=A1=string
         exg      a1,a2                ; logical=>A1, string=>A2
         moveq    #0,d2                ; clear length
         move.w   ns_Length(a2),d2     ; load length
         move.b   #NEWLINE,ns_Buff(a2,d2.l)
         addq.l   #1,d2

         ; Locate the IoBuff node corresponding to the desired stream.

         movea.l  a3,a0                ; global pointer
         bsr      FindLogical          ; D0=A0=node
         beq.s    2$                   ; not found?

         ; Send the string to the selected output function.

         lea      ns_Buff(a2),a1       ; buffer
         move.l   d2,d0                ; requested length
         jsr      0(a6,d4.w)           ; D0=actual count D1=error code

         ; Save error code and release the temporary string.

2$:      move.l   d0,d4                ; save count
         move.l   d1,d3                ; save error code
         movea.l  a4,a0                ; environment
         movea.l  a2,a1                ; string
         bsr      FreeString           ; release it

         ; Check whether the full length was written ...

         cmp.l    d2,d4                ; actual = request?
         beq.s    3$                   ; yes

         ; An error occurred ... save secondary code in 'RC' and check
         ; whether the IOERR interrupt is enabled.

         movea.l  a4,a0                ; environment
         move.l   d3,d0                ; load error code
         moveq    #INB_IOERR,d1        ; interrupt flag
         bsr      rxSignal             ; D0=error D1=boolean
         move.l   d0,d6                ; return error

3$:      rts

* =========================     rxInsINTERP     ========================
* Implements the 'INTERPRET' instruction.
* [instruction module routine]
rxInsINTERP
         btst     #CFB_NULLEXPR,c_Flags(a5)
         bne      rxErr45              ; error ... result required

         ; Call the "string parser" (in non-DEBUG mode).

         movea.l  a4,a0                ; environment
         move.l   c_SrcPos(a5),d0      ; source position
         bsr      ParseString          ; D0=error
         move.l   d0,d6
         rts
@


1.1
log
@Initial revision
@
text
@d1 1
a1 1
* == rxinsreturn.asm ===================================================
d13 2
a14 2
         ; Check whether an internal function is active.  'RETURN' is
         ; equivalent to 'EXIT' in the base environment.
d16 3
d20 1
a20 3
         beq.s    1$                   ; no
         andi.w   #KTF_SECMASK,d5      ; 'EXIT' option?
         bne.s    1$                   ; yes
d22 1
a22 1
         ; 'RETURN' being performed ... check if result needs to be copied.
d24 12
a35 15
         bset     #(EXB_RETURN-8),(STATUS+2)(a4)
         move.l   a1,d0                ; a result?
         beq.s    2$                   ; no
         btst     #NSB_KEEP,ns_Flags(a1)
         beq.s    2$                   ; ... not private

         ; Result string owned ... make a copy of it.

         movea.l  a4,a0                ; environment
         bsr      CopyString           ; D0=A1=string
         bra.s    2$

         ; Set the 'EXIT' flag.

1$:      bset     #(EXB_EXIT-8),(STATUS+2)(a4)
d37 1
a37 1
         ; Save the result in global name slot.
d39 1
a39 1
2$:      move.l   a1,EVNAME(a3)        ; install the result
@

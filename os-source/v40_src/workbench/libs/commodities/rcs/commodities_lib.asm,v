head     38.8;
branch   ;
access   ;
symbols  ;
locks    vertex:38.8; strict;
comment  @* @;


38.8
date     92.11.23.10.59.48;  author vertex;  state Exp;
branches ;
next     38.7;

38.7
date     92.10.16.18.02.21;  author vertex;  state Exp;
branches ;
next     38.6;

38.6
date     92.02.27.17.05.00;  author vertex;  state Exp;
branches ;
next     38.5;

38.5
date     91.10.24.10.20.47;  author vertex;  state Exp;
branches ;
next     38.4;

38.4
date     91.10.21.11.33.48;  author vertex;  state Exp;
branches ;
next     38.3;

38.3
date     91.10.21.11.04.32;  author vertex;  state Exp;
branches ;
next     38.2;

38.2
date     91.10.08.12.14.02;  author vertex;  state Exp;
branches ;
next     38.1;

38.1
date     91.10.07.15.26.12;  author vertex;  state Exp;
branches ;
next     ;


desc
@System library magic
@


38.8
log
@Removed some useless padding
@
text
@	OPTIMON

;---------------------------------------------------------------------------

	NOLIST

	INCLUDE	"exec/types.i"
	INCLUDE	"exec/libraries.i"
	INCLUDE	"exec/lists.i"
	INCLUDE	"exec/alerts.i"
	INCLUDE	"exec/initializers.i"
	INCLUDE	"exec/resident.i"
	INCLUDE	"dos/dos.i"

	INCLUDE	"commodities_rev.i"
	INCLUDE "commoditiesbase.i"
	INCLUDE "commodities.i"

	LIST

;---------------------------------------------------------------------------

	XREF	@@InitCommodities
	XREF	@@ShutdownCommodities

	XREF	_ActivateCxObjLVO
	XREF	_AddIEventsLVO
	XREF	_AttachCxObjLVO
	XREF	_BrokerCommandLVO
	XREF	_ClearCxObjErrorLVO
	XREF	_CopyBrokerListLVO
	XREF	_CreateCxObjLVO
	XREF	_CxBrokerLVO
	XREF	_CxMsgDataLVO
	XREF	_CxMsgIDLVO
	XREF	_CxMsgTypeLVO
	XREF	_CxObjErrorLVO
	XREF	_CxObjTypeLVO
	XREF	_DeleteCxObjLVO
	XREF	_DeleteCxObjAllLVO
	XREF	_DisposeCxMsgLVO
	XREF	_DivertCxMsgLVO
	XREF	_EnqueueCxObjLVO
	XREF	_FreeBrokerListLVO
	XREF	_InsertCxObjLVO
	XREF	_InvertKeyMapLVO
	XREF	_ParseIXLVO
	XREF	_RemoveCxObjLVO
	XREF	_RouteCxMsgLVO
	XREF	_SetCxObjPriLVO
	XREF	_SetFilterLVO
	XREF	_SetFilterIXLVO
	XREF	_SetTranslateLVO
	XREF	_MatchIXLVO
;	XREF	_SetCxObjAttrsALVO

	XREF	_LVOAlert
	XREF	_LVOCloseLibrary
	XREF	_LVOFreeMem
	XREF	_LVOInitSemaphore
	XREF	_LVOOpenLibrary

	XREF	@@FlushPools
	XREF    ENDCODE

;---------------------------------------------------------------------------

	XDEF	LibInit
	XDEF	LibOpen
	XDEF	LibClose
	XDEF	LibExpunge
	XDEF	LibReserved

;---------------------------------------------------------------------------

; First executable location, must return an error to the caller
Start:
	moveq   #-1,d0
	rts

;-----------------------------------------------------------------------

ROMTAG:
	DC.W    RTC_MATCHWORD		; UWORD RT_MATCHWORD
	DC.L    ROMTAG			; APTR  RT_MATCHTAG
	DC.L    ENDCODE			; APTR  RT_ENDSKIP
	DC.B    RTF_AUTOINIT		; UBYTE RT_FLAGS
	DC.B    VERSION			; UBYTE RT_VERSION
	DC.B    NT_LIBRARY		; UBYTE RT_TYPE
	DC.B    0			; BYTE  RT_PRI
	DC.L    LibName			; APTR  RT_NAME
	DC.L    LibId			; APTR  RT_IDSTRING
	DC.L    LibInitTable		; APTR  RT_INIT

LibName DC.B 'commodities.library',0
LibId   VSTRING

	CNOP	0,4

LibInitTable:
	DC.L	CxLib_SIZEOF
	DC.L	LibFuncTable
	DC.L	0
	DC.L	LibInit

V_DEF	MACRO
	DC.W	\1+(*-LibFuncTable)
	ENDM

LibFuncTable:
	DC.W	-1
	V_DEF	LibOpen
	V_DEF	LibClose
	V_DEF	LibExpunge
	V_DEF	LibReserved
	V_DEF	_CreateCxObjLVO
	V_DEF	_CxBrokerLVO
	V_DEF	_ActivateCxObjLVO
	V_DEF	_DeleteCxObjLVO
	V_DEF	_DeleteCxObjAllLVO
	V_DEF	_CxObjTypeLVO
	V_DEF	_CxObjErrorLVO
	V_DEF	_ClearCxObjErrorLVO
	V_DEF	_SetCxObjPriLVO
	V_DEF	_AttachCxObjLVO
	V_DEF	_EnqueueCxObjLVO
	V_DEF	_InsertCxObjLVO
	V_DEF	_RemoveCxObjLVO
	V_DEF	LibReserved
	V_DEF	_SetTranslateLVO
	V_DEF	_SetFilterLVO
	V_DEF	_SetFilterIXLVO
	V_DEF	_ParseIXLVO
	V_DEF	_CxMsgTypeLVO
	V_DEF	_CxMsgDataLVO
	V_DEF	_CxMsgIDLVO
	V_DEF	_DivertCxMsgLVO
	V_DEF	_RouteCxMsgLVO
	V_DEF	_DisposeCxMsgLVO
	V_DEF	_InvertKeyMapLVO
	V_DEF	_AddIEventsLVO
	V_DEF	_CopyBrokerListLVO
	V_DEF	_FreeBrokerListLVO
	V_DEF	_BrokerCommandLVO
	V_DEF	_MatchIXLVO
;	V_DEF	_SetCxObjAttrsALVO
	V_DEF	CxReserved0
	V_DEF	CxReserved1
	V_DEF	CxReserved2
	V_DEF	CxReserved3
	V_DEF	CxReserved4
	DC.W   -1

;-----------------------------------------------------------------------

; Library Init entry point called when library is first loaded in memory
; On entry, D0 points to library base, A0 has lib seglist, A6 has SysBase
; Returns 0 for failure or the library base for success.
LibInit:
	movem.l	a0/a5/d7,-(sp)
	move.l	d0,a5
	move.l	a6,cx_SysBase(a5)
	move.l	a0,cx_SegList(a5)

	move.w	#REVISION,LIB_REVISION(a5)

	move.l	#AG_OpenLib!AO_UtilityLib,d7
	lea	UtilityName(pc),a1
	moveq	#LIBVERSION,d0
	CALL	OpenLibrary
	move.l	d0,cx_UtilityBase(a5)
	beq.s   FailInit

;	move.l	#AG_OpenLib!AO_GraphicsLib,d7
;	lea	GfxName(pc),a1
;	moveq	#LIBVERSION,d0
;	CALL	OpenLibrary
;	move.l	d0,cx_GfxBase(a5)
;	beq.s   FailInit

	move.l	#AG_OpenLib,D7
	lea	KeymapName(pc),a1
	moveq	#LIBVERSION,d0
	CALL	OpenLibrary
	move.l	d0,cx_KeymapBase(a5)
	beq.s   FailInit

	move.l	a5,d0
	movem.l	(sp)+,a0/a5/d7
	rts

FailInit:
	bsr	CloseLibs
        CALL	Alert
	movem.l	(sp)+,a0/a5/d7
	moveq	#0,d0
	rts


LIBVERSION    EQU  37
UtilityName   DC.B "utility.library",0
KeymapName    DC.B "keymap.library",0
;GfxName	      DC.B "graphics.library",0

	CNOP 0,2

;-----------------------------------------------------------------------

; Library open entry point called every OpenLibrary()
; On entry, A6 has CxBase, task switching is disabled
; Returns 0 for failure, or CxBase for success.
LibOpen:
	addq.w	#1,cx_UsageCnt(a6)
	bclr	#LIBB_DELEXP,LIB_FLAGS(a6)

	cmp.w	#1,cx_UsageCnt(a6)
	bne.s	ExitOpen

	; if first open, then init lib base
	bsr	@@InitCommodities
	tst.w	d0
	bne.s	ExitOpen

	; couldn't init, so clean up lib base
	bsr	@@ShutdownCommodities
	moveq	#0,d0
	rts

ExitOpen:
	move.l	a6,d0
	rts

;-----------------------------------------------------------------------

; Library close entry point called every CloseLibrary()
; On entry, A6 has CxBase, task switching is disabled
; Returns 0 normally, or the library seglist when lib should be expunged
;   due to delayed expunge bit being set
LibClose:
	subq.w	#1,cx_UsageCnt(a6)
        bne.s	1$
        bsr	@@ShutdownCommodities

	; if delayed expunge bit set, then try to get rid of the library
1$:	btst	#LIBB_DELEXP,LIB_FLAGS(a6)
	bne.s	CloseExpunge

	; delayed expunge not set, so stick around
	moveq	#0,d0
	rts

CloseExpunge:
	; if no library users, then just remove the library
	tst.w	cx_UsageCnt(a6)
	beq.s	DoExpunge

	; still some library users, so just don't leave
	bsr	@@FlushPools
	bclr	#LIBB_DELEXP,LIB_FLAGS(a6)
	moveq	#0,d0
	rts

;-----------------------------------------------------------------------

; Library expunge entry point called whenever system memory is lacking
; On entry, A6 has CxBase, task switching is disabled
; Returns the library seglist if the library open count is 0, returns 0
; otherwise and sets the delayed expunge bit.
LibExpunge:
	bsr	@@FlushPools
	tst.w	cx_UsageCnt(a6)
	beq.s	DoExpunge

	bset	#LIBB_DELEXP,LIB_FLAGS(a6)
	moveq	#0,d0
	rts

DoExpunge:
	movem.l	d2/a5/a6,-(sp)
	move.l	a6,a5
	move.l	cx_SysBase(a5),a6
	move.l	cx_SegList(a5),d2

	move.l  a5,a1
	REMOVE

	bsr.s	CloseLibs

	move.l	a5,a1
	moveq	#0,d0
	move.w	LIB_NEGSIZE(a5),d0
	sub.l	d0,a1
	add.w	LIB_POSSIZE(a5),d0
	CALL	FreeMem

	move.l	d2,d0
	movem.l	(sp)+,d2/a5/a6
	rts

;-----------------------------------------------------------------------

CloseLibs:
	move.l	cx_UtilityBase(a5),a1
	CALL	CloseLibrary

;	move.l	cx_GfxBase(a5),a1
;	CALL	CloseLibrary

	move.l	cx_KeymapBase(a5),a1
	GO	CloseLibrary

;-----------------------------------------------------------------------

LibReserved:
CxReserved0:
CxReserved1:
CxReserved2:
CxReserved3:
CxReserved4:
	moveq	#0,d0
	rts

;-----------------------------------------------------------------------

	END
@


38.7
log
@*** empty log message ***
@
text
@a153 2
	CNOP	0,4

d205 1
a205 1
	CNOP 0,4
@


38.6
log
@Removed references to FindBroker()
@
text
@d103 1
a103 1
	DC.L	LibDataTable
a153 9
LibDataTable:
	INITBYTE   LN_TYPE,NT_LIBRARY
	INITLONG   LN_NAME,LibName
	INITBYTE   LIB_FLAGS,(LIBF_SUMUSED!LIBF_CHANGED)
	INITWORD   LIB_VERSION,VERSION
	INITWORD   LIB_REVISION,REVISION
	INITLONG   LIB_IDSTRING,LibId
	DC.W       0

d166 2
@


38.5
log
@*** empty log message ***
@
text
@a43 1
	XREF	_FindBrokerLVO
d129 1
a129 1
	V_DEF	_FindBrokerLVO
@


38.4
log
@Now calls FlushPool upon expunge
@
text
@d56 1
d147 1
@


38.3
log
@Added 5 reserved vectors
Added MatchIX() vector
@
text
@d63 1
d221 1
a221 1
	addq.w	#1,LIB_OPENCNT(a6)
d224 1
a224 1
	cmp.w	#1,LIB_OPENCNT(a6)
d248 1
a248 1
	subq.w	#1,LIB_OPENCNT(a6)
d262 1
a262 1
	tst.w	LIB_OPENCNT(a6)
d266 1
d278 2
a279 1
	tst.w	LIB_OPENCNT(a6)
@


38.2
log
@*** empty log message ***
@
text
@d55 1
d144 6
d320 5
@


38.1
log
@Initial V38 RCS checkin
@
text
@d104 4
d109 35
a143 34
	DC.L	LibOpen
	DC.L	LibClose
	DC.L	LibExpunge
	DC.L	LibReserved
	DC.L	_CreateCxObjLVO
	DC.L	_CxBrokerLVO
	DC.L	_ActivateCxObjLVO
	DC.L	_DeleteCxObjLVO
	DC.L	_DeleteCxObjAllLVO
	DC.L	_CxObjTypeLVO
	DC.L	_CxObjErrorLVO
	DC.L	_ClearCxObjErrorLVO
	DC.L	_SetCxObjPriLVO
	DC.L	_AttachCxObjLVO
	DC.L	_EnqueueCxObjLVO
	DC.L	_InsertCxObjLVO
	DC.L	_RemoveCxObjLVO
	DC.L	_FindBrokerLVO
	DC.L	_SetTranslateLVO
	DC.L	_SetFilterLVO
	DC.L	_SetFilterIXLVO
	DC.L	_ParseIXLVO
	DC.L	_CxMsgTypeLVO
	DC.L	_CxMsgDataLVO
	DC.L	_CxMsgIDLVO
	DC.L	_DivertCxMsgLVO
	DC.L	_RouteCxMsgLVO
	DC.L	_DisposeCxMsgLVO
	DC.L	_InvertKeyMapLVO
	DC.L	_AddIEventsLVO
	DC.L	_CopyBrokerListLVO
	DC.L	_FreeBrokerListLVO
	DC.L	_BrokerCommandLVO
	DC.L   -1
@

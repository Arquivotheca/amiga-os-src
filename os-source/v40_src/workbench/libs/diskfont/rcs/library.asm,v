head     38.4;
branch   ;
access   ;
symbols  V36_167:36.4 V36_162:36.4 V36_160:36.4 V36_157:36.4 V36_139:36.3 V36_137:36.3 V36_135:36.3 V36_125:36.2 V36_124:36.2 V36_122:36.2 V36_121:36.2 V36_116:36.2 V36_111:36.1 V36_97:36.0 V36_86:36.0 L36_84:36.0 L36_70:36.0;
locks    ; strict;
comment  @*   @;


38.4
date     92.01.29.13.54.20;  author davidj;  state Exp;
branches ;
next     38.3;

38.3
date     91.04.03.15.14.51;  author kodiak;  state Exp;
branches ;
next     37.1;

37.1
date     91.01.31.14.46.28;  author kodiak;  state Exp;
branches ;
next     37.0;

37.0
date     90.11.26.12.13.42;  author kodiak;  state Exp;
branches ;
next     36.6;

36.6
date     90.04.09.18.05.18;  author kodiak;  state Exp;
branches ;
next     36.5;

36.5
date     90.04.09.15.17.23;  author kodiak;  state Exp;
branches ;
next     36.4;

36.4
date     90.02.22.15.10.37;  author kodiak;  state Exp;
branches ;
next     36.3;

36.3
date     90.01.30.13.02.24;  author kodiak;  state Exp;
branches ;
next     36.2;

36.2
date     89.09.11.12.28.40;  author kodiak;  state Exp;
branches ;
next     36.1;

36.1
date     89.09.08.12.01.56;  author kodiak;  state Exp;
branches ;
next     36.0;

36.0
date     88.05.26.17.07.20;  author kodiak;  state Exp;
branches ;
next     35.1;

35.1
date     87.12.26.13.55.00;  author kodiak;  state Exp;
branches ;
next     35.0;

35.0
date     87.12.26.13.54.19;  author kodiak;  state Exp;
branches ;
next     ;


desc
@standard library routines, though Expunge is special here
@


38.4
log
@short jumps
@
text
@**
**	$Id: library.asm,v 38.3 91/04/03 15:14:51 kodiak Exp Locker: davidj $
**
**      diskfont.library initialization and access
**
**	(C) Copyright 1990 Robert R. Burns
**	    All Rights Reserved
**      (C) Copyright 1985,1989 Commodore-Amiga, Inc.
**          All Rights Reserved
**

;DEBUG	EQU	1

	SECTION		diskfont

*------ Included Files -----------------------------------------------

	INCLUDE		"exec/types.i"
	INCLUDE		"exec/nodes.i"
	INCLUDE		"exec/lists.i"
	INCLUDE		"exec/memory.i"
	INCLUDE		"exec/ports.i"
	INCLUDE		"exec/libraries.i"
	INCLUDE		"exec/devices.i"
	INCLUDE		"exec/tasks.i"
	INCLUDE		"exec/io.i"
	INCLUDE		"exec/strings.i"
	INCLUDE		"exec/initializers.i"
	INCLUDE		"exec/alerts.i"


	INCLUDE		"macros.i"
	INCLUDE		"dfdata.i"
	INCLUDE		"diskfont.i"
	INCLUDE		"debug.i"

*------ Imported Globals ---------------------------------------------

	XREF	DFName
	XREF	VERSION
	XREF	REVISION


*------ Imported Functions -------------------------------------------

	XLVO	Alert
	XLVO	AddLibrary
	XLVO	CloseLibrary
	XLVO	FreeMem
	XLVO	GetMsg
	XLVO	InitSemaphore
	XLVO	MakeLibrary
	XLVO	OpenLibrary

	XLVO	UnLoadSeg

	XLVO	StripFont

	XREF	_MyGetEnv

*------ Exported Functions -------------------------------------------

	XDEF	DFInit

	XDEF	_SysBase
	XDEF	_UtilityBase
	XDEF	_GfxBase
	XDEF	_DOSBase
	XDEF	_DiskfontBase


**********************************************************************
	DATA
_SysBase	ds.l	1
_UtilityBase	ds.l	1
_GfxBase	ds.l	1
_DOSBase	ds.l	1
_DiskfontBase	ds.l	1

**********************************************************************
	SECTION		diskfont

DFInit:
		dc.l	dfl_SIZEOF
		dc.l	dfFuncInit
		dc.l	dfStructInit
		dc.l	dfInit

dfInit:
		movem.l	a5/a6,-(a7)
		move.l	d0,a5
		move.l	a0,dfl_Segment(a5)
		move.l	a6,dfl_SysBase(a5)
		move.l	a6,_SysBase
		move.l	d0,dfl_DiskfontBase(a5)
		move.l	d0,_DiskfontBase

	    ;------ open the utility library
		lea	ULName(pc),a1
		moveq	#0,d0
		CALLLVO OpenLibrary
		move.l	d0,dfl_UtilityBase(a5)
		beq	dfiOpenULErr
		move.l	d0,_UtilityBase

	    ;------ open the graphics library
		lea	GLName(pc),a1
		moveq	#36,d0
		CALLLVO OpenLibrary
		move.l	d0,dfl_GfxBase(a5)
		beq	dfiOpenGLErr
		move.l	d0,_GfxBase

	    ;------ open the dos library
		lea	DLName(pc),a1
		moveq	#36,d0
		CALLLVO OpenLibrary
		move.l	d0,dfl_DOSBase(a5)
		beq.s	dfiOpenDLErr
		move.l	d0,_DOSBase

	    ;------ initialize the font list
		lea	dfl_DiskFonts(a5),a0
		NEWLIST	a0

	    ;------ initialize the bullet semaphore
		lea	dfl_BSemaphore(a5),a0
		CALLLVO	InitSemaphore

		move.l	a5,a0
		bsr	_MyGetEnv

		move.l	a5,d0
dfiRts:
		movem.l	(a7)+,a5/a6
		rts

dfiInitBLErr:
		ALERT	AN_DiskfontLib!AG_MakeLib
		bra.s	dfiCloseDOS

dfiOpenBLErr:
		ALERT	AN_DiskfontLib!AG_OpenLib
dfiCloseDOS:
		move.l	dfl_DOSBase(a5),a1
		CALLLVO	CloseLibrary
		bra.s	dfiCloseGfx

dfiOpenDLErr:
		ALERT	AN_DiskfontLib!AG_OpenLib!AO_DOSLib
dfiCloseGfx:
		move.l	dfl_GfxBase(a5),a1
		CALLLVO	CloseLibrary
		bra.s	dfiCloseUtil

dfiOpenGLErr:
		ALERT	AN_DiskfontLib!AG_OpenLib!AO_GraphicsLib

dfiCloseUtil:
		move.l	dfl_UtilityBase(a5),a1
		CALLLVO	CloseLibrary
		bra.s	dfiFreeMem

dfiOpenULErr:
		ALERT	AN_DiskfontLib!AG_OpenLib!AO_UtilityLib

dfiFreeMem:
		move.l	a5,a1
		move.w	LIB_NEGSIZE(a5),d0
		suba.w	d0,a1
		add.w	LIB_POSSIZE(a5),d0
		ext.l	d0
		CALLLVO FreeMem
		moveq	#0,d0
		bra	dfiRts

ULName:
		dc.b	'utility.library'
		dc.b	0
GLName:
		dc.b	'graphics.library'
		dc.b	0
DLName:
		dc.b	'dos.library'
		dc.b	0
		ds.w	0



*----------------------------------------------------------------------
*
* Definitions for Diskfont Library Initialization
*
*----------------------------------------------------------------------
dfStructInit:
*	;------ Initialize the library
		INITBYTE    LN_TYPE,NT_LIBRARY
		INITLONG    LN_NAME,DFName
		INITBYTE    LIB_FLAGS,LIBF_SUMUSED!LIBF_CHANGED
		INITWORD    LIB_VERSION,VERSION
		INITWORD    LIB_REVISION,REVISION

		INITBYTE    dfl_DiskFonts+LH_TYPE,NT_FONT
		INITWORD    dfl_XDPI,72
		INITWORD    dfl_YDPI,72
		INITWORD    dfl_DotSizeX,100		; 1.0
		INITWORD    dfl_DotSizeY,100		; 1.0
		dc.w	    0


FUNCDEF		MACRO
	XREF	_DF\1
		dc.l    _DF\1
		ENDM


dfFuncInit:
		dc.l	Open
		dc.l	Close
		dc.l	Expunge
		dc.l	ExtFunc		; currently an RTS

	INCLUDE		"diskfont_lib.i"

		dc.l	-1


*------ Expunge ------------------------------------------------------
*
*   NAME
*	Expunge - indicate a desire to remove this library
*
*   SYNOPSIS
*	segment = Expunge(), diskfontLib
*	d0		     a6
*
*   FUNCTION
*	The Expunge routine is called when a user issues a RemLibrary
*	call.  The existance of any other users of the library, as
*	determined by the library open count being non-zero, will
*	cause the Expunge to be deferred.  When the library is not
*	in use, or no longer in use, the Expunge is actually
*	performed, and the library removed from the system list.
*
*	This library keeps the library access count at zero so that it
*	can use the Expunge call to free unused fonts.
*
*---------------------------------------------------------------------
Expunge:
		movem.l	a2/a5/a6,-(a7)
		move.l	a6,a5
		move.l	dfl_DiskFonts(a5),a2
exFontLoop:
		tst.l	(a2)
		beq.s	tryExpunge
		kprintf	<'Expunge candidate accessors $%lx name "%s"',13,10>,dfh_TF+tf_BoldSmear(a2),dfh_TF+LN_NAME(a2)
		tst.w	dfh_TF+tf_Accessors(a2)
		bgt.s	exNextFont

		;--	remove from the DiskfontBase fonts list
		move.l	a2,a1
		REMOVE

		;--	remove from the GfxBase fonts list
		lea	dfh_TF(a2),a1
		move.l	(a1),a0		; check to ensure the font is still
		cmp.l	LN_PRED(a0),a1	;   on the graphics font list (in
		bne.s	exUncompile	;   case it's already been removed
		move.l	LN_PRED(a1),a0	;   as the workaround to a bug here
		cmp.l	(a0),a1		;   in V1.0 & V1.1
		bne.s	exUncompile	;
		REMOVE			; from the GfxBase fonts list

exUncompile:
		;--	un-compile the font
		lea	dfh_TF(a2),a0
		move.l	dfl_GfxBase(a5),a6
		CALLLVO	StripFont

		;--	unload the font memory
		move.l	dfh_Segment(a2),d1
		kprintf	<'  UnLoadSeg $%lx',13,10>,d1
		move.l	dfl_DOSBase(a5),a6
		CALLLVO	UnLoadSeg

		subq.w	#1,dfl_NumLFonts(a5)

exNextFont:
		move.l	(a2),a2
		bra	exFontLoop

tryExpunge:
	    ;-- see if any one is using the library
		tst.w	dfl_PrivOpenCnt(a5)
		bne.s	deferExpunge
		tst.w	dfl_NumLFonts(a5)
		bgt.s	deferExpunge

	    ;-- this is really it.  Free up all the resources
		move.l  dfl_DOSBase(a5),a1
		move.l	dfl_SysBase(a5),a6
		CALLLVO CloseLibrary

		move.l  dfl_GfxBase(a5),a1
		CALLLVO CloseLibrary

		move.l  dfl_UtilityBase(a5),a1
		CALLLVO CloseLibrary

		move.l	a5,a1
		REMOVE

	    ;------ deallocate library data
		move.l	dfl_Segment(a5),a2
		move.l	a5,a1
		move.w	LIB_NEGSIZE(a5),d0
		suba.w	d0,a1
		add.w	LIB_POSSIZE(a5),d0
		ext.l	d0
		CALLLVO FreeMem

		move.l	a2,d0

exDone:
		movem.l	(a7)+,a2/a5/a6
ExtFunc:
		rts

deferExpunge:
		bset	#LIBB_EXPUNGED,LIB_FLAGS(a5)
		moveq	#0,d0		    ;still in use
		bra.s	exDone


*------ Open ---------------------------------------------------------
*
*   NAME
*	Open - a request to open this library
*
*   SYNOPSIS
*	Open(), diskfontLib
*		a6
*
*   FUNCTION
*	The open routine grants access to the library.
*
*	The standard library open count is not incremented.  This
*	library maintains a private open count, and always appears to
*	be ready to be expunged so that it can free unused font memory
*	during the expunge call.
*
*---------------------------------------------------------------------
Open:
		bclr	#LIBB_EXPUNGED,LIB_FLAGS(a6)
		addq.w	#1,dfl_PrivOpenCnt(a6)
		move.l	a6,d0
		rts


*------ Close --------------------------------------------------------
*
*   NAME
*	Close - terminate access to a library
*
*   SYNOPSIS
*	Close(), diskfontLib
*		 a6
*
*   FUNCTION
*	The close routine notifies a library that it will no longer
*	be used.
*
*	The private open count on the library will be decremented, and
*	if it falls to zero, and there are no disk fonts still loaded,
*	and an Expunge is pending, the Expunge will take place.
*
*---------------------------------------------------------------------
Close:
		subq.w	#1,dfl_PrivOpenCnt(a6)

	    ;-- check if this should now be expunged
		bne.s	closeRts
		btst	#LIBB_EXPUNGED,LIB_FLAGS(a6)
		beq.s	closeRts
		jmp	LIB_EXPUNGE(a6)

*	    ;-- report not expunged
closeRts:
		moveq	#0,d0
		rts

	END
@


38.3
log
@don't init obsolete dfl_SymbolSet
@
text
@d2 1
a2 1
**	$Id: library.asm,v 37.1 91/01/31 14:46:28 kodiak Exp Locker: kodiak $
d119 1
a119 1
		beq	dfiOpenDLErr
d255 1
a255 1
		beq	tryExpunge
@


37.1
log
@final phase 1 bullet version
@
text
@d2 1
a2 1
**	$Id: library.asm,v 37.0 90/11/26 12:13:42 kodiak Exp Locker: kodiak $
a207 1
		INITWORD    dfl_SymbolSet,$4C31		; L1
@


37.0
log
@no longer romable, alpha 2 Bullet
@
text
@d2 1
a2 1
**	$Id$
d12 2
d35 1
a35 1

a58 4
	XREF	_CGBInit
	XREF	_CGBOpen
	XREF	_CGBClose

a121 8
	    ;------ initialize the Bullet library
		bsr	_CGBInit
		tst.l	d0
		beq	dfiInitBLErr
		bsr	_CGBOpen
		move.l	d0,dfl_BulletBase(a5)
		beq	dfiOpenBLErr

d208 1
a208 1
		INITWORD    dfl_SymbolSet,$4531		; E1
d256 2
a257 1
		beq.s	tryExpunge
d283 1
d291 1
a291 1
		bra.s	exFontLoop
d301 1
a301 1
		move.l  dfl_BulletBase(a5),a1
a302 3
		bsr	_CGBClose

		move.l  dfl_DOSBase(a5),a1
d331 1
a331 1
		bset	#LIBB_EXPUNGED,LIB_FLAGS(a6)
@


36.6
log
@port to native environment, use new rcs
initialize dl_Segment for use when expunging library
@
text
@d2 1
a2 1
**	$Header: Q:Src/diskfont/RCS/library.asm,v 36.4 90/02/22 15:10:37 kodiak Exp $
d6 2
d49 1
d57 3
d61 2
d67 5
a71 5
	XDEF	_SysBaseOffset
	XDEF	_UtilityBaseOffset
	XDEF	_GfxBaseOffset
	XDEF	_DOSBaseOffset
	XDEF	_DiskfontBaseOffset
d75 6
a80 5
_SysBaseOffset		EQU	dfl_SysBase
_UtilityBaseOffset	EQU	dfl_UtilityBase
_GfxBaseOffset		EQU	dfl_GfxBase
_DOSBaseOffset		EQU	dfl_DOSBase
_DiskfontBaseOffset	EQU	dfl_DiskfontBase
d83 2
d96 1
d98 1
d106 1
d114 1
d122 9
d136 7
d148 11
d161 1
d185 1
a185 1
		bra.s	dfiRts
d214 5
a218 1

d309 4
a313 1
		move.l	dfl_SysBase(a5),a6
@


36.5
log
@save dfl_Segment during init so it's valid during Expunge
@
text
@d2 1
a2 1
**	$Header: /usr2/kodiak/src/WB/diskfont/RCS/library.asm,v 36.4 90/02/22 15:10:37 kodiak Exp Locker: kodiak $
d176 2
a177 2
	XREF	DFA\1
		dc.l    DFA\1
@


36.4
log
@ensure dos.library is version 36
@
text
@d2 1
a2 1
**	$Header: library.asm,v 36.3 90/01/30 13:02:24 kodiak Exp $
d83 1
@


36.3
log
@support utility.library management, StripFont font before removing.
@
text
@d2 1
a2 1
**	$Header: library.asm,v 36.2 89/09/11 12:28:40 kodiak Exp $
d102 1
a102 1
		moveq	#0,d0
@


36.2
log
@use of AUTOINIT eliminates explicit MakeLibrary code
define Offset equates as part of making ROMable
restructure register use to keep DiskfontBase in a register
@
text
@d2 1
a2 1
**	$Header$
d52 1
d54 1
a58 1
	XDEF	_DiskfontBaseOffset
d60 2
d63 1
a63 1
	XDEF	_GfxBaseOffset
d68 1
d86 2
a87 2
	    ;------ open the dos library
		lea	DLName(pc),a1
d90 2
a91 2
		move.l	d0,dfl_DOSBase(a5)
		beq	initDLErr
d98 1
a98 1
		beq	initGLErr
d100 7
d116 7
a122 1
initGLErr:
d124 3
a126 1
		move.l	dfl_DOSBase(a5),a1
d128 1
d130 4
a133 1
initDLErr:
d143 2
a144 2
DLName:
		dc.b	'dos.library'
d149 3
a214 1
		move.l	dfl_DOSBase(a5),a6
d221 2
d224 3
a226 3
		REMOVE			; from the DiskfontBase fonts list
		move.l	dfh_Segment(a2),d1
		CALLLVO	UnLoadSeg
d230 1
a230 1
		bne.s	outOfGfxList	;   case it's already been removed
d233 1
a233 1
		bne.s	outOfGfxList	;
d235 12
a246 1
outOfGfxList:
d248 1
d261 1
a261 1
		move.l  dfl_GfxBase(a5),a1
d265 4
a268 1
		move.l  dfl_DOSBase(a5),a1
@


36.1
log
@require gfx 36 -- alert if not available
@
text
@d1 8
a8 37
	TTL    '$Header: library.asm,v 36.0 88/05/26 17:07:20 kodiak Exp $'
**********************************************************************
*								     *
*   Copyright 1985, Commodore-Amiga Inc.   All rights reserved.	     *
*   No part of this program may be reproduced, transmitted,	     *
*   transcribed, stored in retrieval system, or translated into	     *
*   any language or computer language, in any form or by any	     *
*   means, electronic, mechanical, magnetic, optical, chemical,	     *
*   manual or otherwise, without the prior written permission of     *
*   Commodore-Amiga Incorporated, 983 University Ave. Building #D,   *
*   Los Gatos, California, 95030				     *
*								     *
**********************************************************************
*
*   Source Control
*   --------------
*   $Header: library.asm,v 36.0 88/05/26 17:07:20 kodiak Exp $
*
*   $Locker: kodiak $
*
*   $Log:	library.asm,v $
*   Revision 36.0  88/05/26  17:07:20  kodiak
*   DL -> DiskfontBase
*   
*   Revision 35.1  87/12/26  13:55:00  kodiak
*   fix sense of Close count to zero before checking for expunge
*   
*   Revision 35.0  87/12/26  13:54:19  kodiak
*   initial for V35
*   
*   Revision 1.2  85/12/03  18:26:52  kodiak
*   remove expunged fonts from the graphics font list
*   
*   Revision 1.1  85/12/03  17:05:10  kodiak
*   added to rcs for updating in version 1.2
*   
**********************************************************************
d35 3
a37 3
	XREF		DFName
	XREF		VERSION
	XREF		REVISION
d42 7
a48 7
	XREF_EXE	Alert
	XREF_EXE	AddLibrary
	XREF_EXE	CloseLibrary
	XREF_EXE	FreeMem
	XREF_EXE	GetMsg
	XREF_EXE	MakeLibrary
	XREF_EXE	OpenLibrary
d50 1
a50 1
	XREF_DOS	UnLoadSeg
d55 1
a55 1
	XDEF		DFInit
d57 4
a60 4
	XDEF		_DiskfontBase
	XDEF		_SysBase
	XDEF		_DOSBase
	XDEF		_GfxBase
d64 4
a67 5
	SECTION		diskfont,DATA
_DiskfontBase	DC.L	0
_SysBase	DC.L	0
_DOSBase	DC.L	0
_GfxBase	DC.L	0
a69 1
	SECTION		diskfont,CODE
d71 4
a74 7
		movem.l	a0/a2,-(a7)
		lea	dfFuncInit(pc),a0
		lea	dfStructInit(pc),a1
		lea	dfInit(pc),a2
		move.l	#dfl_SIZEOF,d0
		CALLEXE	MakeLibrary
		movem.l	(a7)+,a0/a2
a75 12
		tst.l	d0
		beq.s	dfRts

		move.l	d0,a1
		move.l	a0,dfl_Segment(a1)
		CALLEXE	AddLibrary

		moveq.l	#1,d0

dfRts:
		rts

d77 4
a80 5
		move.l	a6,-(a7)
		move.l	d0,a0
		move.l	a6,_SysBase
		move.l	d0,a6			; library pointer
		move.l	d0,_DiskfontBase
d82 1
a82 1
*	    ;------ open the dos library
d85 2
a86 2
		LINKEXE OpenLibrary
		move.l	d0,_DOSBase
d89 1
a89 1
*	    ;------ open the graphics library
d92 2
a93 2
		LINKEXE OpenLibrary
		move.l	d0,_GfxBase
d97 1
a97 1
		lea	dfl_DiskFonts(a6),a0
d100 1
a100 1
		move.l	a6,d0
d102 1
a102 1
		move.l	(a7)+,a6
d107 2
a108 2
		move.l	_DOSBase,a1
		LINKEXE	CloseLibrary
d111 2
a112 2
		move.l	a6,a1
		move.w	LIB_NEGSIZE(a6),d0
d114 1
a114 1
		add.w	LIB_POSSIZE(a6),d0
d116 1
a116 1
		LINKEXE FreeMem
d149 2
a150 2
	XREF	DF\1
		dc.l    DF\1
d187 4
a190 2
		move.l	a2,-(a7)
		move.l	dfl_DiskFonts(a6),a2
d199 1
a199 1
		LINKDOS	UnLoadSeg
d209 1
a209 1
		subq.w	#1,dfl_NumLFonts(a6)
a214 1
		move.l	(a7)+,a2
d216 1
a216 1
		tst.w	dfl_PrivOpenCnt(a6)
d218 1
a218 1
		tst.w	dfl_NumLFonts(a6)
d222 3
a224 2
		move.l  _GfxBase,a1
		LINKEXE CloseLibrary
d226 2
a227 2
		move.l  _DOSBase,a1
		LINKEXE CloseLibrary
d229 3
d233 3
a235 3
		move.l	dfl_Segment(a6),-(a7)
		move.l	a6,a1
		move.w	LIB_NEGSIZE(a6),d0
d237 1
a237 1
		add.w	LIB_POSSIZE(a6),d0
d239 1
a239 1
		LINKEXE FreeMem
d241 1
a241 2
		move.l	a6,a1
		REMOVE
d243 3
a245 1
		move.l	(a7)+,d0
d251 1
a252 2
ExtFunc:
		rts
a253 1

d311 1
a311 1
		END
@


36.0
log
@DL -> DiskfontBase
@
text
@d1 1
a1 1
	TTL    '$Header: library.asm,v 35.1 87/12/26 13:55:00 kodiak Exp $'
d17 1
a17 1
*   $Header: library.asm,v 35.1 87/12/26 13:55:00 kodiak Exp $
d22 3
d54 1
d71 1
d102 5
a106 5
		MOVEM.L	A0/A2,-(A7)
		LEA	dfFuncInit(PC),A0
		LEA	dfStructInit(PC),A1
		LEA	dfInit(PC),A2
		MOVE.L	#dfl_SIZEOF,D0
d108 1
a108 1
		MOVEM.L	(A7)+,A0/A2
d110 2
a111 2
		TST.L	D0
		BEQ.S	dfRts
d113 2
a114 2
		MOVE.L	D0,A1
		MOVE.L	A0,dfl_Segment(A1)
d117 1
a117 1
		MOVEQ.L	#1,D0
d120 1
a120 1
		RTS
d123 5
a127 5
		MOVE.L	A6,-(A7)
		MOVE.L	D0,A0
		MOVE.L	A6,_SysBase
		MOVE.L	D0,A6			; library pointer
		MOVE.L	D0,_DiskfontBase
d130 2
a131 2
		LEA	DLName(PC),A1
		MOVEQ	#0,D0
d133 2
a134 2
		MOVE.L	D0,_DOSBase
		BEQ	initDLErr
d137 2
a138 2
		LEA	GLName(PC),A1
		MOVEQ	#0,D0
d140 2
a141 2
		MOVE.L	D0,_GfxBase
		BEQ	initGLErr
d144 2
a145 2
		LEA	dfl_DiskFonts(A6),A0
		NEWLIST	A0
d147 1
a147 1
		MOVE.L	A6,D0
d149 2
a150 2
		MOVE.L	(A7)+,A6
		RTS
d153 2
a154 1
		MOVE.L	_DOSBase,A1
d158 5
a162 5
		MOVE.L	A6,A1
		MOVE.W	LIB_NEGSIZE(A6),D0
		SUBA.W	D0,A1
		ADD.W	LIB_POSSIZE(A6),D0
		EXT.L	D0
d164 2
a165 2
		MOVEQ	#0,D0
		BRA.S	dfiRts
d168 2
a169 2
		DC.B	'dos.library'
		DC.B	0
d171 3
a173 3
		DC.B	'graphics.library'
		DC.B	0
		DS.W	0
d192 1
a192 1
		DC.W	    0
d197 1
a197 1
		DC.L    DF\1
d202 4
a205 4
		DC.L	Open
		DC.L	Close
		DC.L	Expunge
		DC.L	ExtFunc		; currently an RTS
d209 1
a209 1
		DC.L	-1
d219 1
a219 1
*	D0		     A6
d234 2
a235 2
		MOVE.L	A2,-(A7)
		MOVE.L	dfl_DiskFonts(A6),A2
d237 5
a241 5
		TST.L	(A2)
		BEQ.S	tryExpunge
		TST.W	dfh_TF+tf_Accessors(A2)
		BGT.S	exNextFont
		MOVE.L	A2,A1
d243 1
a243 1
		MOVE.L	dfh_Segment(A2),D1
d245 7
a251 7
		LEA	dfh_TF(A2),A1
		MOVE.L	(A1),A0		; check to ensure the font is still
		CMP.L	LN_PRED(A0),A1	;   on the graphics font list (in
		BNE.S	outOfGfxList	;   case it's already been removed
		MOVE.L	LN_PRED(A1),A0	;   as the workaround to a bug here
		CMP.L	(A0),A1		;   in V1.0 & V1.1
		BNE.S	outOfGfxList	;
d254 1
a254 1
		SUBQ.W	#1,dfl_NumLFonts(A6)
d256 2
a257 2
		MOVE.L	(A2),A2
		BRA.S	exFontLoop
d260 1
a260 1
		MOVE.L	(A7)+,A2
d262 4
a265 4
		TST.W	dfl_PrivOpenCnt(A6)
		BNE.S	deferExpunge
		TST.W	dfl_NumLFonts(A6)
		BGT.S	deferExpunge
d268 1
a268 1
		MOVE.L  _GfxBase,A1
d271 1
a271 1
		MOVE.L  _DOSBase,A1
d275 6
a280 6
		MOVE.L	dfl_Segment(A6),-(A7)
		MOVE.L	A6,A1
		MOVE.W	LIB_NEGSIZE(A6),D0
		SUBA.W	D0,A1
		ADD.W	LIB_POSSIZE(A6),D0
		EXT.L	D0
d283 1
a283 1
		MOVE.L	A6,A1
d286 2
a287 2
		MOVE.L	(A7)+,D0
		RTS
d290 2
a291 2
		BSET	#LIBB_EXPUNGED,LIB_FLAGS(A6)
		MOVEQ	#0,D0		    ;still in use
d294 1
a294 1
		RTS
d304 1
a304 1
*		A6
d316 4
a319 4
		BCLR	#LIBB_EXPUNGED,LIB_FLAGS(A6)
		ADDQ.W	#1,dfl_PrivOpenCnt(A6)
		MOVE.L	A6,D0
		RTS
d329 1
a329 1
*		 A6
d341 1
a341 1
		SUBQ.W	#1,dfl_PrivOpenCnt(A6)
d344 4
a347 4
		BNE.S	closeRts
		BTST	#LIBB_EXPUNGED,LIB_FLAGS(A6)
		BEQ.S	closeRts
		JMP	LIB_EXPUNGE(A6)
d351 2
a352 2
		MOVEQ	#0,D0
		RTS
@


35.1
log
@fix sense of Close count to zero before checking for expunge
@
text
@d1 1
a1 1
	TTL    '$Header: library.asm,v 35.0 87/12/26 13:54:19 kodiak Exp $'
d17 1
a17 1
*   $Header: library.asm,v 35.0 87/12/26 13:54:19 kodiak Exp $
d22 3
d81 1
a81 1
	XDEF		_DFL
d89 1
a89 1
_DFL		DC.L	0
d122 1
a122 1
		MOVE.L	D0,_DFL
@


35.0
log
@initial for V35
@
text
@d1 1
a1 1
	TTL    '$Header: library.asm,v 1.2 85/12/03 18:26:52 kodiak Exp $'
d17 1
a17 1
*   $Header: library.asm,v 1.2 85/12/03 18:26:52 kodiak Exp $
d19 1
a19 1
*   $Locker:  $
d22 3
d335 1
a335 1
		BEQ.S	closeRts
@

head     1.1;
branch   ;
access   ;
symbols  ;
locks    ; strict;
comment  @@;


1.1
date     91.12.10.09.51.23;  author darren;  state Exp;
branches ;
next     ;


desc
@Initial check-in of wfaust canon bj driver - candidate for 2.1.
@



1.1
log
@Initial revision
@
text
@	INCLUDE "exec/types.i"

	INCLUDE	"intuition/intuition.i"
	INCLUDE "devices/printer.i"
	INCLUDE "devices/prtbase.i"
	INCLUDE "devices/prtgfx.i"

	XREF	_PD
	XREF	_PED
;	XREF	_LVODebug
	XREF	_AbsExecBase

	XDEF	@@Transfer

	SECTION CODE

@@Transfer:
; Transfer(PInfo, y, ptr)
; struct PrtInfo *PInfo		4-7
; UWORD y;			8-11
; UBYTE *ptr;			12-15
; UWORD *colors;		16-19
;

	movem.l	d2-d6/a2-a4,-(sp)	;save regs used
	moveq.l	#3,d2
	and.w	d0,d2			;d2 = y & 3
	lsl.w	#2,d2			;d2 = (y & 3) << 2
	movea.l	pi_dmatrix(a0),a3	;a3 = dmatrix
	adda.l	d2,a3			;a3 = dmatrix + ((y & 3) << 2)

	movea.l	_PED,a4			;a4 = ptr to PED
	divu.w	ped_NumRows(a4),d0	;d0 = y / NumRows
	swap.w	d0			;d0 = y % NumRows
	move.w	d0,d6
	not.b	d6			;d6 = bit to set
	asr.w	#3,d0			;d0 = ymod / 8
	adda.w	d0,a1			;ptr += ymod / 8

not_color:
; a0 - PInfo
; a1 - ptr
; a2 - colors
; a3 - dmatrix ptr
; d0 - y
; d6 - bit to set
	
	move.w	pi_width(a0),d1		;d1 = width
	subq.w	#1,d1			;adjust for dbra

	move.w	pi_threshold(a0),d3	;d3 = threshold, thresholding?
	beq.s	grey_scale		;no, grey-scaling

threshold:
; a0 - PInfo
; a1 - ptr
; a3 - dmatrix ptr
; d1 - width-1
; d3 - threshold
; d6 - bit to set

	eori.b	#15,d3			;d3 = dvalue
	movea.l	pi_ColorInt(a0),a2	;a2 = ColorInt ptr
	move.w	pi_xpos(a0),d2		;d2 = x
	movea.l	pi_ScaleX(a0),a0	;a0 = ScaleX (sxptr)
	adda.w	d2,a1
	adda.w	d2,a1
	adda.w	d2,a1			;ptr += x * 3
	cmpi.w  #$30,ped_NumRows(a4)
	bne     twidth_loop
	adda.w	d2,a1
	adda.w	d2,a1
	adda.w	d2,a1			;ptr += x * 3

; a0 - sxptr
; a1 - ptr
; a2 - ColorInt ptr
; a3 - dmatrix ptr (NOT USED)
; d1 - width
; d3 - dvalue
; d4 - Black
; d5 - sx
; d6 - bit to set

twidth_loop:
	move.b	PCMBLACK(a2),d4		;d4 = Black
	addq.l	#ce_SIZEOF,a2		;advance to next entry

	move.w	(a0)+,d5		;d5 = # of times to use this pixel

	cmp.b	d3,d4			;render this pixel?
	ble.s	tsx_end			;no, skip to next pixel.
	subq.w	#1,d5			;adjust for dbra

tsx_render:				;yes, render this pixel sx times
	bset.b	d6,(a1)			;*(ptr) |= bit;

	adda.w	#3,a1			;ptr += 3
	cmpi.w  #$30,ped_NumRows(a4)
	bne     no360ydpi
	adda.w	#3,a1			;ptr += 3
no360ydpi:
	dbra	d5,tsx_render		;sx--
	dbra	d1,twidth_loop		;width--
	bra.s	exit			;all done
	
tsx_end:
	adda.w	d5,a1
	adda.w	d5,a1
	adda.w	d5,a1			;ptr += sx * 3
	cmpi.w  #$30,ped_NumRows(a4)
	bne     jump1
	adda.w	d5,a1
	adda.w	d5,a1
	adda.w	d5,a1			;ptr += sx * 3

jump1:
	dbra	d1,twidth_loop		;width--
	bra.s	exit

grey_scale:
; a0 - PInfo
; a1 - ptr
; a3 - dmatrix ptr
; d0 - y
; d1 - width-1
; d6 - bit to set

	movea.l	pi_ColorInt(a0),a2	;a2 = ColorInt ptr
	move.w	pi_xpos(a0),d2		;d2 = x
	movea.l	pi_ScaleX(a0),a0	;a0 = ScaleX (sxptr)
	adda.w	d2,a1
	adda.w	d2,a1
	adda.w	d2,a1			;ptr += x * 3
	cmpi.w  #$30,ped_NumRows(a4)
	bne     gwidth_loop
	adda.w	d2,a1
	adda.w	d2,a1
	adda.w	d2,a1			;ptr += x * 3

; a0 - sxptr
; a1 - ptr
; a2 - ColorInt ptr
; a3 - dmatrix ptr
; d1 - width
; d2 - x
; d3 - dvalue (dmatrix[x & 3])
; d4 - Black
; d5 - sx
; d6 - bit to set

gwidth_loop:
	move.b	PCMBLACK(a2),d4		;d4 = Black
	addq.l	#ce_SIZEOF,a2		;advance to next entry

	move.w	(a0)+,d5		;d5 = # of times to use this pixel
	subq.w	#1,d5			;adjust for dbra

gsx_loop:
	moveq.l	#3,d3
	and.w	d2,d3			;d3 = x & 3	
	move.b	0(a3,d3.w),d3		;d3 = dmatrix[x & 3]

	cmp.b	d3,d4			;render this pixel?
	ble.s	gsx_end			;no, skip to next pixel.

	bset.b	d6,(a1)			;*ptr |= bit

gsx_end
	adda.w	#3,a1			;ptr += 3
	cmpi.w  #$30,ped_NumRows(a4)
	bne     jumpydpi
	adda.w	#3,a1			;ptr += 3
jumpydpi:
	addq.w	#1,d2			;x++
	dbra	d5,gsx_loop		;sx--
	dbra	d1,gwidth_loop		;width--

exit:
	movem.l	(sp)+,d2-d6/a2-a4	;restore regs used
	moveq.l	#0,d0			;flag all ok
	rts				;goodbye

sx	dc.w	0
width	dc.w	0

	END
@

;kludge.s -- kludge for mfm.device Init code to execute
;TDPatch12/TDPatch13. Returns zero for success else error.

	include	"mfm.i"
	include	"exec/types.i"
	include	"exec/execbase.i"
	include	"exec/tasks.i"
	include	"dos/dos.i"

    ifnd CBM

SYS	macro
	XREF	_LVO\1
	jsr	_LVO\1(a6)
	endm
; Test code.  Go figure!!!
;	bsr.s	TDPatchKludge
;	move.l	d0,$100.w
;	rts
        XREF tdIO
        XDEF TDPatchKludge


TDPatchKludge:

;Check Kickstart version -- if not 1.2 or 1.3, exit immediately.
	moveq	#0,d0
	movem.l	d0-d7/a0-a6,-(sp)
	move.l	4.w,a4
	move.l	a4,a6
	lea	Patch(pc),a2
	cmp.w	#33,LIB_VERSION(a6)
	beq.s	DoKludge
	cmp.w	#34,LIB_VERSION(a6)
	bne	Exit
	moveq	#'3',d0
	move.b	d0,11(a2)
	move.b	d0,Ver
DoKludge:
	lea	DosName(pc),a1
	SYS	OldOpenLibrary
	move.l	d0,a5

	bsr	CheckTD
	bne.s	Success

;Attempt to invoke TDPatch12/TDPatch13
	move.l	a5,a6
	move.l	a2,d1
	SYS	LoadSeg
	bsr.s	RunCheck
	bne.s	Success
	move.l	a5,a6
	move.l	a2,d1
	addq.l	#1,d1
	move.b	#':',2(a2)
	SYS	LoadSeg
	bsr.s	RunCheck
	bne.s	Success

;Bring up an alert indicating that we were unable to run TDPatch
Alert:
	move.l	a4,a6
	lea	IntName(pc),a1
	SYS	OldOpenLibrary
	move.l	d0,a6
	sub.l	a0,a0	;window
	sub.l	a2,a2	;PosText
	moveq	#0,d0	;PosFlags
	moveq	#0,d1	;NegFlags
	move.l	#300,d2	;Width
	moveq	#80,d3	;Height
	lea	BodyText(pc),a1
	lea	NegText(pc),a3
	SYS	AutoRequest
	move.l	a6,a1
	move.l	a4,a6
	SYS	CloseLibrary
	st	(sp)
Success:
	move.l	a4,a6
	move.l	a5,a1
	SYS	CloseLibrary
Exit:	movem.l	(sp)+,d0-d7/a0-a6
	rts

RunCheck:
;Enter with BPTR (or 0) to seglist in d0
	move.l	d0,d5
	lsl.l	#2,d0
	beq.s	_RTS
	move.l	d0,a1
	movem.l	d0-d7/a0-a6,-(sp)
	lea	CR(pc),a0
	moveq	#1,d0
	jsr	4(a1)
	movem.l	(sp)+,d0-d7/a0-a6
	move.l	d5,d1
	SYS	UnLoadSeg

;Check whether TDPatch has already been installed.
CheckTD:
	move.l	a4,a6
	lea	tdIO,a1
	SYS	FindTask
	move.l	d0,a1
	tst.l	TC_Userdata(a1)
_RTS:
	rts

MyFont:
	dc.l	FontName
	dc.w	8	;height of font
	dc.b	0,0	;style, flags
BodyText:
	dc.b	0,1	;FrontPen, BackPen
	dc.b	0	;DrawMode
	dc.w	10,10	;LeftEdge, TopEdge
	dc.l	MyFont
	dc.l	BodyTextText
	dc.l	0	;next IntuiText
NegText:
	dc.b	0,1	;FrontPen, BackPen
	dc.b	1	;DrawMode
	dc.w	6,3	;LeftEdge, TopEdge
	dc.l	MyFont
	dc.l	NegTextText
	dc.l	0	;next IntuiText

FontName:
	dc.b	'topaz.font',0
BodyTextText:
	dc.b	'Unable to run TDPatch1'
Ver:
	dc.b	'2!',0
NegTextText:
	dc.b	'Ok',0
DosName:
	dc.b	'dos.library',0
IntName:
	dc.b	'intuition.library',0
CR:	dc.b	$a
Patch:
	dc.b	':c/tdpatch12',0
	cnop	0,2

    endc

	end
